script "DiscOfRepulsionCooldown" (void) //207
{
    if (CheckInventory("DiscOfRepulsionCooldown") > 0)
    {
        delay(35);
        TakeInventory("DiscOfRepulsionCooldown",1);
        restart;
    }
}

str HexenWeapons[8] = { "Hammer of Retribution", "Firestorm", "Timon's Axe", "Serpent Staff", "Frost Shards", "Wraithverge", "Sapphire Wand", "Mace of Contrition" };

Script "Hexen_SwitchPriority" (void)
{
	for(int a = 0; a < 8; a++)
	{
		if(CheckInventory(HexenWeapons[a]) && !CheckWeapon(HexenWeapons[a]))
		{
			SetWeapon(HexenWeapons[a]);
			break;
		}
	}
}

Script "DarkServantAutoRemoval" (void)
{
	int newtid = uniquetid();
	if(!Spawn("MinotaurFriend2",GetActorX(0),GetActorY(0),GetActorZ(0),newtid) || !SetActivator(0,AAPTR_Tracer))
	{
		SetResultValue(0);
	}
	else
	{
		Thing_remove(newtid);
		SetResultValue(1);
	}
}

Script "FlechetteCooldown" (void)
{
	if (CheckInventory("FlechetteCooldown") > 0)
    {
        delay(35);
        TakeInventory("FlechetteCooldown",1);
        restart;
    }
}

script "SamsaraHexenSerpentStaffMeleeHealCheck" (void)
{
    // initialize tid

    int pufftid = UniqueTID();

    Thing_ChangeTID(0, pufftid);

    // get owner's current team

    int curteam;

    if (isTeamGame())
    {
        SetActivator(pufftid, AAPTR_TARGET);

        curteam = CheckInventory("SamsaraCurrentTeam");
    }

    // check if the shootable is eligible to give health

    SetActivator(pufftid, AAPTR_TRACER);

    bool playercheck = IsPlayerOrBot(0) && (isFreeForAll() || (isTeamGame() && CheckInventory("SamsaraCurrentTeam") != curteam) || GetCVar("teamdamage") > 0);
    bool invulncheck = CheckFlag(0, "INVULNERABLE") ||
                       CheckInventory("SamsaraPowerInvulnerableNormal") || CheckInventory("SamsaraPowerInvulnerableDoom64") || CheckInventory("SamsaraPowerInvulnerableGoldMap") ||
                       CheckInventory("SamsaraPowerInvulnerableGrey") || CheckInventory("QuakePentagram") || CheckInventory("GodProtection") ||
                       CheckInventory("DogProtection") || CheckInventory("PainkillerDemonMorphInvulnerability") || CheckInventory("SamsaraPowerInvulnerableDXEnviro1") ||
                       CheckInventory("SamsaraPowerInvulnerableDXEnviro2") || CheckInventory("SamsaraPowerInvulnerableDXEnviro3") || CheckInventory("SamsaraPowerInvulnerableDXEnviro4");

    if ((playercheck || CheckFlag(0, "ISMONSTER")) && !CheckFlag(0, "DORMANT") && !invulncheck) { SetResultValue(1); }
    else { SetResultValue(0); }

    // clear tid just in case

    Thing_ChangeTID(pufftid, 0);
}

script "SamsaraHexenWeaponPaletteBlend" (int weapon) CLIENTSIDE
{
    while (true)
    {
        int curblend = CheckInventory("SamsaraHexenWeaponPaletteBlendCounter");

        if (CheckWeapon("Wraithverge") && !isDead(0) && curblend > 0)
        {
            bool flashmode = GetCVar("pf_hexenweaps");

            int blendr = 0;
            int blendg = 0;
            int blendb = 0;
            int blenda = 0.0;

            if (weapon >= 1) // Bloodscourge
            {
                if (flashmode)
                {
                    if (curblend >= 3)
                    {
                        blendr = 100;
                        blendg = 73;
                    } else if (curblend == 2) {
                        blendr = 125;
                        blendg = 92;
                    } else if (curblend == 1) {
                        blendr = 150;
                        blendg = 110;
                    }

                    blenda = 0.5;
                } else {
                    blendr = 151;
                    blendg = 110;

                    if (curblend >= 3) { blenda = 0.5; }
                    else if (curblend == 2) { blenda = 0.33; }
                    else if (curblend == 1) { blenda = 0.16; }
                }
            } else { // Wraithverge
                if (flashmode)
                {
                    if (curblend >= 3)
                    {
                        blendr = 70;
                        blendg = 70;
                        blendb = 70;
                    } else if (curblend == 2) {
                        blendr = 100;
                        blendg = 100;
                        blendb = 100;
                    } else if (curblend == 1) {
                        blendr = 130;
                        blendg = 130;
                        blendb = 130;
                    }

                    blenda = 0.5;
                } else {
                    blendr = 131;
                    blendg = 131;
                    blendb = 131;

                    if (curblend >= 3) { blenda = 0.5; }
                    else if (curblend == 2) { blenda = 0.33; }
                    else if (curblend == 1) { blenda = 0.16; }
                }
            }

            FadeTo(blendr, blendg, blendb, blenda, 0.0);
            Delay(1);
        } else {
            FadeTo(0, 0, 0, 0.0, 0.0);
            terminate;
        }
    }
}
