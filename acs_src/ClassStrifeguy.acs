Script "StrifeGuy_UPG" ENTER
{
	if ((PlayerClass(PlayerNumber())!=11) || (CheckInventory("StrifeguyStartingUpgradesHasBeenGiven"))) terminate; //Check if the class is strifeguy, otherwise terminate the script
    
    int sup=GetCvar("samsara_strifeupgrades");
	
	GiveInventory("AccuracyUpgrade1", sup * 10);
    GiveInventory("AccuracyUpgrade2", sup * 10);
    GiveInventory("StaminaUpgradeDamage", sup * 10);
	GiveInventory("StrifeUpgradeCount", sup);
	GiveInventory("StrifeguyStartingUpgradesHasBeenGiven", 1);
	SetActorProperty(0, APROP_SpawnHealth, 100 + (sup * 10));
	SetActorProperty(0,APROP_Health,GetActorProperty(0,APROP_SpawnHealth));
}

Script "Samsara_StrifeCooldown" (void)
{
	While(CheckInventory("StrifeBeaconCooldown") > 0)
	{
		Delay(35);
		Takeinventory("StrifeBeaconCooldown", 1);
	}
}

Script "Samsara_StrifeUpgradesMapSpot" (int set)
{
	Switch(set)
	{
		Case 1:
		TakeInventory(strparam(s:"StrifeUpgrade", d:CheckInventory("StrifeUpgradeCount")-1,s:"X"), 0x7FFFFFFF);
		TakeInventory(strparam(s:"StrifeUpgrade", d:CheckInventory("StrifeUpgradeCount")-1,s:"Y"), 0x7FFFFFFF);
		TakeInventory(strparam(s:"StrifeUpgrade", d:CheckInventory("StrifeUpgradeCount")-1,s:"Z"), 0x7FFFFFFF);
		GiveInventory(strparam(s:"StrifeUpgrade", d:CheckInventory("StrifeUpgradeCount")-1,s:"X"), (GetActorX(0)>>16)+65536);
		GiveInventory(strparam(s:"StrifeUpgrade", d:CheckInventory("StrifeUpgradeCount")-1,s:"Y"), (GetActorY(0)>>16)+65536);
		GiveInventory(strparam(s:"StrifeUpgrade", d:CheckInventory("StrifeUpgradeCount")-1,s:"Z"), (GetActorZ(0)>>16)+65536);
		break;
		Case 2:
		for(int a = 0; a < 10; a++)
		{
			if(abs((GetActorX(0)>>16)+65536 - CheckInventory(strparam(s:"StrifeUpgrade",d:a,s:"X"))) <= 36 && abs((GetActorY(0)>>16)+65536 - CheckInventory(strparam(s:"StrifeUpgrade",d:a,s:"Y"))) <= 36 && abs((GetActorZ(0)>>16)+65536 - CheckInventory(strparam(s:"StrifeUpgrade",d:a,s:"Z"))) <= 36)
			{
				SetResultValue(0);
				terminate;
			}
		}
		SetResultValue(1);
		break;
	}
}

script "BlackBirdTauntCooldown" (void)
{
    while (CheckInventory("BlackBirdTauntCooldown") > 0)
    {
        Delay(35);
        TakeInventory("BlackBirdTauntCooldown", 1);
    }
}

Script "StrifeBlackBirdQuote" (int type) CLIENTSIDE
{
	SetHudSize(640,480,0);
	int adjWidth = getaspectratio() * 480;

	int left = fround((640.0-adjWidth)/2);

    SetFont("CMPSSTRF");
	HudMessage(s:"Incoming message from Blackbird...";HUDMSG_FADEOUT|HUDMSG_LOG,5700,CR_UNTRANSLATED,left+0.1,0.1,1.0,1.0);
	switch(type)
	{
		case 0:
			LocalAmbientSound("strife/blackbird/enter", 127);
			break;
		case 1:
			LocalAmbientSound("strife/blackbird/roaming", 127);
			break;
		case 2:
			LocalAmbientSound("strife/blackbird/killtaunt", 127);
			break;
		case 3:
			LocalAmbientSound("strife/blackbird/xkilltaunt", 127);
			break;
		case 4:
			LocalAmbientSound("strife/blackbird/bkilltaunt", 127);
			break;
		case 5:
			LocalAmbientSound("strife/blackbird/death", 127);
			break;
	}
}

Script "StrifeSigilPreferred" (void)
{
	SetResultValue(CheckInventory("StrifeSigilPiece"));
}
