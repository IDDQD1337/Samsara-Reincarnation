script "SamsaraBStoneAmmoRefillFailMessage" (int messagetype) CLIENTSIDE
{
    str messagename = "Placeholder";

    switch (messagetype)
    {
      case 0:
        messagename = "BSTONE_AMMOREFILL_INCOMPATIBLE";
        break;

      case 1:
        messagename = "BSTONE_AMMOREFILL_AMMOFULL";
        break;

      default:
        terminate;
    }

    SetHudSize(320, 240, 0);
    HudMessage(l:messagename; 0, 578, CR_LIGHTBLUE, 160.0, 200.0, 1.0);
}

script "SamsaraBStoneAmmoRefill" (int ammotype)
{
    str ammoname = "Placeholder";
    int ammoamount = 0;

    int maxClip = GetCVar("samsara_max_clip");
    int maxShell = GetCVar("samsara_max_shell");
    int maxRocket = GetCVar("samsara_max_rockets");
    int maxCell = GetCVar("samsara_max_cell");
    int hasBackpack = 1 + (CheckInventory("Backpack") || CheckInventory("BagOfHolding") || CheckInventory("AmmoSatchel") || CheckInventory("Zorchpack"));

    switch (ammotype)
    {
      case 0:
        ammoname = "Clip";
        ammoamount = (maxClip * hasBackpack) / 4;
        break;

      case 1:
        ammoname = "Shell";
        ammoamount = (maxShell * hasBackpack) / 4;
        break;

      case 2:
        ammoname = "RocketAmmo";
        ammoamount = (maxRocket * hasBackpack) / 4;
        break;

      case 3:
        ammoname = "Cell";
        ammoamount = (maxCell * hasBackpack) / 4;
        break;

      default:
        terminate;
    }

    SetInventory(ammoname, CheckInventory(ammoname) + ammoamount);
}
