script "DiscOfRepulsionCooldown" (void) //207
{
    if (CheckInventory("SamsaraDiscOfRepulsionCooldown") > 0)
    {
        delay(35);
        TakeInventory("SamsaraDiscOfRepulsionCooldown",1);
        restart;
    }
}

script "DarkServantCooldown" (void)
{
    if (CheckInventory("SamsaraDarkServantCooldown") > 0)
    {
        delay(35);
        TakeInventory("SamsaraDarkServantCooldown",1);
        restart;
    }
}

str HexenWeapons[8] = { "Hammer of Retribution", "Firestorm", "Timon's Axe", "Serpent Staff", "Frost Shards", "Wraithverge", "Sapphire Wand", "Mace of Contrition" };

Script "Hexen_SwitchPriority" (void)
{
	for(int a = 0; a < 8; a++)
	{
		if(CheckInventory(HexenWeapons[a]) && !CheckWeapon(HexenWeapons[a]))
		{
			SetWeapon(HexenWeapons[a]);
			break;
		}
	}
}

script "SamsaraHexenDarkServantAutoRemoval" (void)
{
    int newtid = UniqueTID();

    if (!Spawn("SamsaraHexenMinotaurFriend", GetActorX(0), GetActorY(0), GetActorZ(0), newtid) || !SetActivator(0, AAPTR_TRACER)) { SetResultValue(0); }
    else
    {
        Thing_Remove(newtid);
        SetResultValue(1);
    }
}

Script "FlechetteCooldown" (void)
{
	if (CheckInventory("FlechetteCooldown") > 0)
    {
        delay(35);
        TakeInventory("FlechetteCooldown",1);
        restart;
    }
}

script "SamsaraHexenSerpentStaffMeleeHealCheck" (void)
{
    // initialize tid

    int pufftid = UniqueTID();

    Thing_ChangeTID(0, pufftid);

    // get owner's current team

    int curteam;

    if (isTeamGame())
    {
        SetActivator(pufftid, AAPTR_TARGET);

        curteam = CheckInventory("SamsaraCurrentTeam");
    }

    // check if the shootable is eligible to give health

    SetActivator(pufftid, AAPTR_TRACER);

    bool playercheck = IsPlayerOrBot(0) && (isFreeForAll() || (isTeamGame() && CheckInventory("SamsaraCurrentTeam") != curteam) || GetCVar("teamdamage") > 0);
    bool invulncheck = CheckFlag(0, "INVULNERABLE") ||
                       CheckInventory("SamsaraPowerInvulnerable") || CheckInventory("SamsaraPowerInvulnerableNormal") || CheckInventory("SamsaraPowerInvulnerableDoom64") ||
                       CheckInventory("SamsaraPowerInvulnerableGoldMap") || CheckInventory("SamsaraPowerInvulnerableGrey") || CheckInventory("SamsaraPowerInvulnerableGreyCleric") ||
                       CheckInventory("SamsaraPowerInvulnerableGreyMage") || CheckInventory("SamsaraPowerInvulnerableGreyFighter") || CheckInventory("QuakePentagram") ||
                       CheckInventory("GodProtection") || CheckInventory("DogProtection") || CheckInventory("PowerCatacomb_Invulnerability") ||
                       CheckInventory("Painkiller_IronWill_Protection") || CheckInventory("PainkillerDemonMorphInvulnerability") || CheckInventory("SamsaraPowerInvulnerableDXEnviro1") ||
                       CheckInventory("SamsaraPowerInvulnerableDXEnviro2") || CheckInventory("SamsaraPowerInvulnerableDXEnviro3") || CheckInventory("SamsaraPowerInvulnerableDXEnviro4");

    if ((playercheck || CheckFlag(0, "ISMONSTER")) && !CheckFlag(0, "DORMANT") && !invulncheck) { SetResultValue(1); }
    else { SetResultValue(0); }

    // clear tid just in case

    Thing_ChangeTID(pufftid, 0);
}

script "SamsaraHexenWeaponPaletteBlend" (int weapon, int blendcount) CLIENTSIDE
{
    SetInventory("SamsaraHexenWeaponPaletteBlendCounter", blendcount);

    while (CheckWeapon("Wraithverge") && !isDead(0) && CheckInventory("SamsaraHexenWeaponPaletteBlendCounter"))
    {
        int curblend = CheckInventory("SamsaraHexenWeaponPaletteBlendCounter");

        bool flashmode = GetCVar("pf_hexenweaps");

        int blendr = 0;
        int blendg = 0;
        int blendb = 0;
        int blenda = 0.0;

        if (weapon >= 1) // Bloodscourge
        {
            if (flashmode)
            {
                if (curblend >= 3)
                {
                    blendr = 100;
                    blendg = 73;
                } else if (curblend == 2) {
                    blendr = 125;
                    blendg = 92;
                } else if (curblend == 1) {
                    blendr = 150;
                    blendg = 110;
                }

                blenda = 0.50196;
            } else {
                blendr = 151;
                blendg = 110;

                if (curblend >= 3) { blenda = 0.50196; }
                else if (curblend == 2) { blenda = 0.33333; }
                else if (curblend == 1) { blenda = 0.16471; }
            }
        } else { // Wraithverge
            if (flashmode)
            {
                if (curblend >= 3)
                {
                    blendr = 70;
                    blendg = 70;
                    blendb = 70;
                } else if (curblend == 2) {
                    blendr = 100;
                    blendg = 100;
                    blendb = 100;
                } else if (curblend == 1) {
                    blendr = 130;
                    blendg = 130;
                    blendb = 130;
                }

                blenda = 0.50196;
            } else {
                blendr = 131;
                blendg = 131;
                blendb = 131;

                if (curblend >= 3) { blenda = 0.50196; }
                else if (curblend == 2) { blenda = 0.33333; }
                else if (curblend == 1) { blenda = 0.16471; }
            }
        }

        FadeTo(blendr, blendg, blendb, blenda, 0.0);
        Delay(1);
    }

    FadeTo(0, 0, 0, 0.0, 0.0);
}

script "SamsaraHexenAmmoRefill" (int ammotype)
{
    str ammoname = "Placeholder";
    int ammoamount = 0;

    int maxClip = GetCVar("samsara_max_clip");
    int maxCell = GetCVar("samsara_max_cell");
    int hasBackpack = 1 + (CheckInventory("Backpack") || CheckInventory("BagOfHolding") || CheckInventory("AmmoSatchel") || CheckInventory("Zorchpack"));

    switch (ammotype)
    {
      case 0:
        ammoname = "Clip";
        if (CheckInventory("Hexen2Class")) { ammoamount = (((maxClip * 5) / 4) * hasBackpack + CheckInventory("Hexen2BlueManaAdd")) / 2; }
        else { ammoamount = (maxClip * hasBackpack) / 2; }
        break;

      case 1:
        ammoname = "Cell";
        if (CheckInventory("Hexen2Class")) { ammoamount = (((maxCell * 5) / 6) * hasBackpack + CheckInventory("Hexen2GreenManaAdd")) / 2; }
        else { ammoamount = (maxCell * hasBackpack) / 2; }
        break;

      default:
        terminate;
    }

    SetInventory(ammoname, CheckInventory(ammoname) + ammoamount);
}

script "SamsaraHexenInvulnerabilityEffect" (int invulnmode)
{
    if (invulnmode >= 1) // Reflective Invulnerability
    {
        while (CheckInventory("SamsaraPowerInvulnerableGreyMage"))
        {
            if (!CheckFlag(0, "REFLECTIVE")) { GiveInventory("SamsaraHexenMageInvulnerabilityReflectiveOn", 1); }

            Delay(1);
        }

        if (CheckFlag(0, "REFLECTIVE")) { GiveInventory("SamsaraHexenMageInvulnerabilityReflectiveOff", 1); }
    } else { // Ghost Invulnerability
        // commented out values refer to the player alpha; can't have separate values for player and weapon
        int newalpha1 = 0.695312; //0.59375;
        int newalpha2 = 0.554688; //0.40625;
        int newalpha3 = 0.25; //0.0;

        while (CheckInventory("SamsaraPowerInvulnerableGreyCleric"))
        {
            if (!CheckFlag(0, "SHADOW")) // Don't mess with the translucency settings if an invisibility powerup is active.
            {
                int alpha = GetActorProperty(0, APROP_Alpha);

                if (!(Timer() & 7) && alpha > 0.0 && alpha < 1.0)
                {
                    if (alpha == newalpha1) { alpha = newalpha2; }
                    else
                    {
                        alpha = newalpha3;
                        if (!CheckFlag(0, "NONSHOOTABLE")) { GiveInventory("SamsaraHexenClericInvulnerabilityNonShootableOn", 1); }
                    }
                }

                if (!(Timer() & 31))
                {
                    if (alpha == newalpha3)
                    {
                        if (CheckFlag(0, "NONSHOOTABLE")) { GiveInventory("SamsaraHexenClericInvulnerabilityNonShootableOff", 1); }
                        alpha = newalpha2;
                    }
                    else { alpha = newalpha1; }
                }

                SetActorProperty(0, APROP_RenderStyle, STYLE_Translucent);
                SetActorProperty(0, APROP_Alpha, alpha);
            } else {
                if (CheckFlag(0, "NONSHOOTABLE")) { GiveInventory("SamsaraHexenClericInvulnerabilityNonShootableOff", 1); }
            }

            Delay(1);
        }

        if (CheckFlag(0, "NONSHOOTABLE")) { GiveInventory("SamsaraHexenClericInvulnerabilityNonShootableOff", 1); }

        if (!CheckFlag(0, "SHADOW")) // Don't mess with the translucency settings if an invisibility powerup is active.
        {
            SetActorProperty(0, APROP_RenderStyle, STYLE_Normal);
            SetActorProperty(0, APROP_Alpha, 1.0);
        }
    }
}

Script "HexenGreenFireDamage" (void)
{
	if(!SetActorState(0,"Burn",1))
		if(!SetActorState(0,"XDeath",1))
			SetActorState(0,"Death",1);
}
