script "SamsaraPDWTAK" DEATH //675 -- SAMSARA_PDWTAK
{
    if (GetCVar("samsara_pdwtak_distance") <= 0) { terminate; }

    int bombtimer, yourTID;
    int myTID = defaultTID(-1);
    int classNum = samsaraClassNum();
    int x, y, z, z2;
    int rVel, rAng, rPit, rTID;
    int i, j, k, l;

    x  = GetActorX(myTID);
    y  = GetActorY(myTID);
    z  = GetActorZ(myTID) + 32.0;
    z2 = GetActorZ(myTID);

    SetActivator(0, AAPTR_TARGET);

    if (PlayerNumber() == -1) { bombtimer = 35; }
    else
    {
        yourTID = ActivatorTID();

        if (yourTID == myTID) { bombtimer = 0; }
        else { bombtimer = 24; }

        if (yourTID == 0)
        {
            yourTID = unusedTID(15000, 16999);
            Thing_ChangeTID(0, yourTID);
        }
    }

    for (bombtimer += 0; bombtimer > 0; bombtimer--)
    {
        rTID = unusedTID(12000, 14999);

        rVel = random(0, 12.0);
        rAng = random(0, 1.0);
        rPit = random(-0.25, 0.25);

        if (bombtimer % 18 == 0)
        {
            if (classNum != -1) { j = PeopleDieStuff[classNum][1]; }
            else { j = "DoomguysDiePulse"; }

            SpawnForced(j, x, y, z, rTID);
        } else if (Timer() % 2) {
            if (classNum != -1) { j = PeopleDieStuff[classNum][0]; }
            else { j = "DoomguysDieParticle"; }

            SpawnForced(j,
                        x + FixedMul(FixedMul(rVel, cos(rAng)), cos(rPit)),
                        y + FixedMul(FixedMul(rVel, sin(rAng)), cos(rPit)),
                        z + FixedMul(rVel, sin(rPit)),
                        rTID);

            rVel = random(0, 1.0);
            rVel = FixedMul(rVel, rVel);
            rAng = random(0, 1.0);
            rPit = random(-0.25, 0.25);

            SetActorVelocity(rTID,
                              FixedMul(FixedMul(rVel, cos(rAng)), cos(rPit)) / 2,
                              FixedMul(FixedMul(rVel, sin(rAng)), cos(rPit)) / 2,
                              FixedMul(rVel, sin(rPit)) * 2,
                              false, false);
        }

        Delay(1);
    }

    i = GetActorX(yourTID);
    j = GetActorY(yourTID);
    k = GetActorZ(yourTID);

    SetActorPosition(yourTID, x, y, z2, false);

    if (GetCVar("samsara_pdwtak_damage") > 0)
    {
        if (classNum != -1) { GiveActorInventory(yourTID, PeopleDieStuff[classNum][3], 1); }
        else { GiveActorInventory(yourTID, "InvPeopleDie", 1); }
    }

    SetActorPosition(yourTID, i, j, k, false);

    if (classNum != -1) { SpawnForced(PeopleDieStuff[classNum][2], x, y, z); }
    else { SpawnForced("DoomguyDiesWhenHeIsKilled", x, y, z); }
}
