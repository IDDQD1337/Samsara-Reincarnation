actor HacxTerminatrix_Samsara : SamsaraMonsterRoboticLarge replaces BaronOfHell
{
	//game Hacx
	spawnid 3
	obituary "$OB_TERMIN"
	hitobituary "$OB_TERMINHIT"
	health 800
	speed 8
	radius 24
	height 96
	painchance 40
	mass 1000
	seesound "baron/sight"
	painsound "cyber/sight"
	deathsound "baron/death"
	activesound "caco/sight"
    DamageFactor "CorvusCrossbow", 1.15
    DamageFactor "DragonClaw", 1.6
	DamageFactor "MarathonFusion", 2.0
    PainChance "MightyBoot", 255
	Tag "Terminatrix"
	MONSTER
	+FLOORCLIP
    +DONTRIP
    +BOSSDEATH
    +NOTDMATCH
    +NOICEDEATH
	+NOBLOOD
	states
	{
		Spawn:
			BOSS AB 10 A_Look
			loop
		See:
			BOSS A 3 A_Chase
			BOSS A 3 A_VileChase
			BOSS B 3 A_Chase
			BOSS B 3 A_VileChase
			BOSS C 3 A_Chase
			BOSS C 3 A_VileChase
			BOSS D 3 A_Chase
			BOSS D 3 A_VileChase
			loop
		Melee:
			VILE N 8 A_FaceTarget
			VILE O 8 A_VileAttack
			VILE P 20
			FIRE B 0 A_BabyMetal
			goto See
		Missile:
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			BOSS EF 8 A_FaceTarget
			BOSS G 8 A_CustomMissile ("HacxTerminatrixBall_Samsara", 32.0, 0, 0)
			goto See
		Pain:
			BOSS H 7 bright A_Pain
			BOSS H 7 bright
			goto See
		Pain.MightyBoot:
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			BOSS H 2 A_FaceTarget
			TNT1 A 0 A_Recoil(10)
			BOSS H 16 A_Pain
			Goto See
		Death:
			TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			BOSS I 8
			BOSS J 8 A_Scream
			BOSS K 8
			BOSS L 8 A_NoBlocking
			BOSS MN 8
			BOSS O -1 A_BossDeath
			stop
	}
}

actor HacxTerminatrixBall_Samsara
{
	//game Hacx
	spawnid 154
	health 1000
	speed 20
	radius 6
	height 8
	damage 12
	mass 100
	seesound "fatso/attack"
	deathsound "vile/firestrt"
	PROJECTILE
	+RANDOMIZE
	+FIREDAMAGE
	states
	{
		Spawn:
			FATB AB 4 bright
			loop
		Death:
			APLS ABCDE 5 bright
			HEAD L 25
			stop
	}
}