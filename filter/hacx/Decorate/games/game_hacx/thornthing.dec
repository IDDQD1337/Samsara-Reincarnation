actor HacxThornThing_Samsara : SamsaraMonsterRobotic replaces Arachnotron
{
	//game Hacx
	spawnid 6
	obituary "$OB_THORN"
	health 600
	speed 16
	radius 64
	height 96
	painchance 20
	mass 600
	painsound "baby/sight"
	deathsound "baby/death"
	activesound "baby/active"
    DamageFactor "CorvusCrossbow", 1.1
    DamageFactor "DragonClaw", 1.5
    DamageFactor "MarathonFusion", 2.0
    PainChance "MightyBoot", 255
	MONSTER
	Tag "Thorn Thing"
	Bloodcolor "4A FF 56"
	+FLOORCLIP
    +DONTRIP
    +BOSSDEATH
    +NOICEDEATH
	states
	{
		Spawn:
			BSPI AB 10 A_Look
			loop
		See:
			BSPI A 10
			BSPI A 3 A_Metal
			BSPI ABBCC 3 A_Chase
			loop
		Missile:
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			BSPI A 8 bright A_FaceTarget
			BSPI G 3 bright A_CustomMissile ("HacxThornThingBall_Samsara", 32.0, 0, 0)
			BSPI H 3 bright
			BSPI G 6 bright A_FaceTarget
			goto See
		Pain:
			BSPI I 3
			BSPI I 3 A_Pain
			goto See
		Pain.MightyBoot:
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			BSPI I 2 A_FaceTarget
			TNT1 A 0 A_Recoil(8)
			BSPI I 16 A_Pain
			goto See
		Death:
			TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			TNT1 A 0 A_SpawnItemEx("UniversalBloodyHellGoreDeathEffect",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)
			BSPI J 7 A_Scream
			BSPI K 7 A_NoBlocking
			BSPI LMNO 7
			BSPI P -1 A_BossDeath
			stop
		Raise:
			BSPI A 0
			"####" "#" 0 A_JumpIfInventory("EnemyZorched",1,"ZorchRaise")
			BSPI PONMLKJ 5
			goto See
	}
}

actor ThornThingFrozen
{
  Health 10
  Radius 64
  Height 96
  Mass 600
  Monster
  +FLOORCLIP
  +NEVERRESPAWN
  +NOTELEPORT
  +NOTARGET
  States
  {
  Spawn:
    BSPI I 1
    BSPI I 0 A_GenericFreezeDeath
    BSPI I 70 A_UnsetSolid
  Death:
    BSPI I 0 A_IceGuyDie
    BSPI I 1 A_IceGuyDie
    TNT1 A -1
    stop
}
}

actor HacxThornThingBall_Samsara : RevenantTracer
{
	//game Hacx
	spawnid 53
	mass 100
	seesound "skeleton/attack"
	deathsound "skeleton/tracex"
	states
	{
		Spawn:
			BAL7 AB 1 bright A_Tracer
			loop
		Death:
			BAL7 C 8 bright
			BAL7 D 6 bright
			BAL7 E 4 bright
			stop
	}
}