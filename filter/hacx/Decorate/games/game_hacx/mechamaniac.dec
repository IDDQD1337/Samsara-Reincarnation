actor HacxMechaManiac_Samsara : SamsaraMonsterRobotic replaces HellKnight
{
	//game Hacx
	spawnid 113
	obituary "$OB_MANIAC"
	hitobituary "$OB_MANIAC"
	health 800
	speed 8
	radius 24
	height 96
	painchance 40
	mass 1000
	seesound "imp/sight1"
	painsound "wolfss/death"
	deathsound "imp/death1"
	activesound "keen/death"
    DamageFactor "CorvusCrossbow", 1.15
    DamageFactor "DragonClaw", 1.6
	DamageFactor "MarathonFusion", 2.0
    PainChance "MightyBoot", 255
	Tag "Mecha Maniac"
	MONSTER
	+FLOORCLIP
    +DONTRIP
    +BOSSDEATH
    +NOICEDEATH
	+NOBLOOD
	states
	{
		Spawn:
			BOS2 AB 10 A_Look
			loop
		See:
			BOS2 AABBCCDD 3 A_Chase
			loop
		Melee:
		Missile:
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			BOS2 EF 8 bright A_FaceTarget
			BOS2 G 8 bright A_CustomMissile ("HacxPhoton_Samsara", 32.0, 0, 0)
			goto See
		Pain:
			BOS2 H 2
			BOS2 H 2 A_Pain
			goto See
		Pain.MightyBoot:
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			BOS2 H 2 A_FaceTarget
			TNT1 A 0 A_Recoil(10)
			BOS2 H 16 A_Pain
			goto See
		Death:
			TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			BOS2 I 8
			BOS2 J 8 A_Scream
			BOS2 K 8
			BOS2 L 8 A_NoBlocking
			BOS2 MN 8
			BOS2 O -1
			stop
		Raise:
			BOS2 A 0
			"####" "#" 0 A_JumpIfInventory("EnemyZorched",1,"ZorchRaise")
			BOS2 NMLKJI 8
			goto See
	}
}

actor HacxPhoton_Samsara : Rocket
{
	//game Hacx
	spawnid 127
	obituary "$OB_MPZOOKA"
	DamageType "Explosive"
	-DEHEXPLOSION
	states
	{
		Death:
			BEXP A 3 bright
			BEXP B 3 bright A_Scream
			BEXP C 3 bright
			BEXP D 7 bright A_Explode(128, 128)
			BEXP E 10 bright
			HEAD L 25 bright
			stop
	}
}