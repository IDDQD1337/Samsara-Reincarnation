actor "RMR Grenade Launcher" : SamsaraRMRWeapon
{
    Inventory.PickupMessage "Grenade Launcher"
    Weapon.SelectionOrder 200
    Weapon.SlotNumber 5
    Weapon.AmmoType "RocketAmmo"
    Weapon.AmmoGive 9
    Weapon.AmmoUse 1
    Tag "Sukayev T-23 Grenade Launcher"
    +EXPLOSIVE
    //+NOAUTOFIRE
    States
    {
      Spawn:
        RM9Y E -1
        stop

      Ready:
        RM9Y A 1 A_WeaponReady
        loop

      Deselect:
        TNT1 A 0 A_Lower
        RM9Y A 1 A_Lower
        loop

      Select:
        TNT1 A 0 A_Raise
        RM9Y A 1 A_Raise
        loop

      Fire:
        TNT1 A 0 A_GiveInventory("SamsaraRMRGrenadeLauncherAttackHandler")
        RM9Y D 15 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        RM9Y B 16 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto Ready
    }
}

actor SamsaraRMRGrenadeLauncherAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,1)
        stop
        TNT1 A 0 A_TakeInventory("RocketAmmo",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupAttackRM")
      // Rebel Moon Rising sounds
      PickupAttackRMR:
        TNT1 A 0 A_PlaySound("RMR/PIGFIRE",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackRMRCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackRMRCoop")
      PickupAttackRMRDM:
        TNT1 A 0 A_FireCustomMissile("RMRDiscoBall",0,false)
        stop

      PickupAttackRMRCoop:
        TNT1 A 0 A_FireCustomMissile("RMRDiscoBallCoop",0,false)
        stop

      // Rebel Moon sounds
      PickupAttackRM:
        TNT1 A 0 A_PlaySound("RebelMoon/PIGFIRE",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackRMCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackRMCoop")
      PickupAttackRMDM:
        TNT1 A 0 A_FireCustomMissile("RMRDiscoBallSoundRM",0,false)
        stop

      PickupAttackRMCoop:
        TNT1 A 0 A_FireCustomMissile("RMRDiscoBallCoopSoundRM",0,false)
        stop
    }
}

actor RMRDiscoBall : FastProjectile
{
    Radius 5
    Height 5
    Speed 70
    Damage (100)
    DamageType "RMR-DiscoBall"
    Scale 0.4
    DeathSound "RMR/DiscoBoom"
    Decal "Scorch"
    Obituary "%k pummeled %o with the grenade launcher."
    +BLOODLESSIMPACT
    +FORCERADIUSDMG
    +FORCEXYBILLBOARD
    +THRUGHOST
    States
    {
      Spawn:
        RMZ2 NO 1 Bright
        loop

      Death:
        TNT1 A 0 A_Explode(100,64)
        TNT1 A 1 A_SpawnItemEx("RMRExplosion",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
        stop
    }
}

actor RMRDiscoBallCoop : RMRDiscoBall
{
    Speed 100
    Species "Player"
    +THRUSPECIES
}

actor RMRDiscoBallSoundRM : RMRDiscoBall { DeathSound "RebelMoon/DiscoBoom" }

actor RMRDiscoBallCoopSoundRM : RMRDiscoBallCoop { DeathSound "RebelMoon/DiscoBoom" }
