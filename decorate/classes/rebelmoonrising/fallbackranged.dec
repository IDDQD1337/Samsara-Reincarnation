actor " Laser Pistol " : SamsaraRMRWeapon
{
    Inventory.PickupMessage "Laser Pistol"
    Weapon.SlotNumber 1
    Weapon.SelectionOrder 1900
    Tag "Laser Pistol"
    +NOALERT
    +WIMPY_WEAPON
    States
    {
      Ready:
        RM2Z A 1 A_WeaponReady
        loop

      Deselect:
        TNT1 A 0 A_Lower
        RM2Z A 1 A_Lower
        loop

      Select:
        TNT1 A 0 A_Raise
        RM2Z A 1 A_Raise
        loop

      Fire:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto FireStart
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"FireStart")
        goto NoAmmoSwitch

      FireStart:
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_GiveInventory("SamsaraRMRLaserPistolAttackHandler")
        RM2Z B 7 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        RM2Z A 10 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto Ready

      // "No ammo" switch
      NoAmmoSwitch:
        TNT1 A 0 A_ClearReFire
        TNT1 A 0 A_SelectWeapon("Dirtshark")
        goto Deselect
    }
}

actor SamsaraRMRLaserPistolAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto PickupAttack
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupAttackRM")
      // Rebel Moon Rising sounds
      PickupAttackRMR:
        TNT1 A 0 A_PlaySound("RMR/LaserFire",CHAN_WEAPON)
        TNT1 A 0 A_FireBullets(0,0,1,0,"SamsaraBlankPuff",FBF_NORANDOM|FBF_NOFLASH)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackRMRCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackRMRCoop")
      PickupAttackRMRDM:
        TNT1 A 0 A_FireCustomMissile("RMRLaser",0,false)
        stop

      PickupAttackRMRCoop:
        TNT1 A 0 A_FireCustomMissile("RMRLaserCoop",0,false)
        stop

      // Rebel Moon sounds
      PickupAttackRM:
        TNT1 A 0 A_PlaySound("RebelMoon/LaserFire",CHAN_WEAPON)
        TNT1 A 0 A_FireBullets(0,0,1,0,"SamsaraBlankPuff",FBF_NORANDOM|FBF_NOFLASH)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackRMCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackRMCoop")
      PickupAttackRMDM:
        TNT1 A 0 A_FireCustomMissile("RMRLaserSoundRM",0,false)
        stop

      PickupAttackRMCoop:
        TNT1 A 0 A_FireCustomMissile("RMRLaserCoopSoundRM",0,false)
        stop
    }
}

actor RMRLaser : FastProjectile
{
    Radius 2
    Height 3
    Speed 140
    Damage (10)
    DamageType "RMR-GreenPlasma"
    RenderStyle "Add"
    Scale 0.5
    DeathSound "RMR/LaserPuff"
    Obituary "%k shot %o down with a laser pistol."
    +BLOODSPLATTER
    +FORCEXYBILLBOARD
    +NOEXTREMEDEATH
    States
    {
      Spawn:
        RMZ2 A -1 Bright Light("RMRGREENPLASMA")
        stop

      Death:
        RMZ2 B 1 Bright Light("RMRGREENPLASMAEXP")
        RMZ2 B 1 Bright Light("RMRGREENPLASMAEXP") A_FadeOut(0.04)
        wait

      XDeath:
      Crash:
        TNT1 A 1 A_SpawnItemEx("RMRPlasmaSplash_1",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
        stop
    }
}

actor RMRLaserCoop : RMRLaser
{
    Speed 175
    Damage (15)
    Species "Player"
    +THRUSPECIES
}

actor RMRLaserSoundRM : RMRLaser { DeathSound "RebelMoon/LaserPuff" }

actor RMRLaserCoopSoundRM : RMRLaserCoop { DeathSound "RebelMoon/LaserPuff" }
