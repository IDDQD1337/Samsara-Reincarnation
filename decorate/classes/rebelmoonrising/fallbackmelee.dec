actor "Dirtshark" : SamsaraRMRWeapon
{
    Inventory.PickupMessage "MolyCarbide Power Mining Drill"
    Weapon.SlotNumber 0
    Weapon.SelectionOrder 2200
    Weapon.Kickback 0
    Obituary "%k drilled %o a deep hole."
    Tag "\"Dirtshark\" MolyCarbide Power Mining Drill"
    +MELEEWEAPON
    +NOALERT
    States
    {
      Spawn:
        RMZ6 P -1
        stop

      Ready:
        RM0G A 1 A_WeaponReady
        loop

      Deselect:
        TNT1 A 0 A_Lower
        RM0G A 1 A_Lower
        loop

      Select:
        TNT1 A 0 A_Raise
        RM0G A 1 A_Raise
        loop

      Fire:
        TNT1 A 0 A_JumpIfInventory("SamsaraRMRDirtsharkUpgrade",1,"FireUpgraded")
      FireUnupgraded:
        TNT1 A 0 A_GiveInventory("SamsaraRMRDirtsharkAttackHandler")
        RM0G BCD 4 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto Ready

      FireUpgraded:
        TNT1 A 0 A_GiveInventory("SamsaraRMRDirtsharkAttackHandler")
        RM0G B 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        RM0G CD 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto Ready
    }
}

actor SamsaraRMRDirtsharkAttackHandler : Trigger
{
    States
    {
      // Attack stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraRMRDirtsharkUpgrade",1,"PickupUpgraded")
      // Unupgraded
      PickupUnupgraded:
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupUnupgradedRM")
      // Rebel Moon Rising sounds
      PickupUnupgradedRMR:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupUnupgradedRMRCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupUnupgradedRMRCoop")
      PickupUnupgradedRMRDM:
        TNT1 A 0 A_Saw("rmr/drillfull","rmr/drillfull",10,"RMRDirtsharkPuff",SF_NORANDOM|SF_NOUSEAMMO)
        stop

      PickupUnupgradedRMRCoop:
        TNT1 A 0 A_Saw("rmr/drillfull","rmr/drillfull",10,"RMRDirtsharkPuffCoop",SF_NORANDOM|SF_NOUSEAMMO)
        stop

      // Rebel Moon sounds
      PickupUnupgradedRM:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupUnupgradedRMCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupUnupgradedRMCoop")
      PickupUnupgradedRMDM:
        TNT1 A 0 A_Saw("rebelmoon/drillfull","rebelmoon/drillfull",10,"RMRDirtsharkPuffSoundRM",SF_NORANDOM|SF_NOUSEAMMO)
        stop

      PickupUnupgradedRMCoop:
        TNT1 A 0 A_Saw("rebelmoon/drillfull","rebelmoon/drillfull",10,"RMRDirtsharkPuffCoopSoundRM",SF_NORANDOM|SF_NOUSEAMMO)
        stop

      // Upgraded
      PickupUpgraded:
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupUpgradedRM")
      // Rebel Moon Rising sounds
      PickupUpgradedRMR:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupUpgradedRMRCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupUpgradedRMRCoop")
      PickupUpgradedRMRDM:
        TNT1 A 0 A_Saw("rmr/drillfull","rmr/drillfull",30,"RMRDirtsharkPuff",SF_NORANDOM|SF_NOUSEAMMO)
        stop

      PickupUpgradedRMRCoop:
        TNT1 A 0 A_Saw("rmr/drillfull","rmr/drillfull",30,"RMRDirtsharkPuffCoop",SF_NORANDOM|SF_NOUSEAMMO)
        stop

      // Rebel Moon sounds
      PickupUpgradedRM:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupUpgradedRMCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupUpgradedRMCoop")
      PickupUpgradedRMDM:
        TNT1 A 0 A_Saw("rebelmoon/drillfull","rebelmoon/drillfull",30,"RMRDirtsharkPuffSoundRM",SF_NORANDOM|SF_NOUSEAMMO)
        stop

      PickupUpgradedRMCoop:
        TNT1 A 0 A_Saw("rebelmoon/drillfull","rebelmoon/drillfull",30,"RMRDirtsharkPuffCoopSoundRM",SF_NORANDOM|SF_NOUSEAMMO)
        stop
    }
}

actor RMRDirtsharkPuff : RMRPuff
{
    +NOTIMEFREEZE
    +PUFFONACTORS
    States
    {
      Spawn:
      Crash:
        RMZ9 A 0 Bright Light("RMRPUFF4") NoDelay A_PlaySound("rmr/drillhit")
      SpawnStart:
        TNT1 A 0 A_ChangeFlag("NOTIMEFREEZE",false)
        goto Puff

      XDeath:
        TNT1 A 1 A_PlaySound("rmr/drillhit")
        stop
    }
}

actor RMRDirtsharkPuffCoop : RMRDirtsharkPuff { +MTHRUSPECIES }

actor RMRDirtsharkPuffSoundRM : RMRDirtsharkPuff
{
    States
    {
      Spawn:
      Crash:
        RMZ9 A 0 Bright Light("RMRPUFF4") NoDelay A_PlaySound("rebelmoon/drillhit")
        goto SpawnStart

      XDeath:
        TNT1 A 1 A_PlaySound("rebelmoon/drillhit")
        stop
    }
}

actor RMRDirtsharkPuffCoopSoundRM : RMRDirtsharkPuffCoop
{
    States
    {
      Spawn:
      Crash:
        RMZ9 A 0 Bright Light("RMRPUFF4") NoDelay A_PlaySound("rebelmoon/drillhit")
        goto SpawnStart

      XDeath:
        TNT1 A 1 A_PlaySound("rebelmoon/drillhit")
        stop
    }
}
