actor PSMagnum : SamsaraPowerSlaveWeapon
{
    Inventory.PickupMessage "A .357 Magnum!"
    Weapon.SlotNumber 1
    Weapon.SlotPriority 1
    Weapon.SelectionOrder 3700
    Weapon.AmmoType "PSMagnumReload"
    Weapon.AmmoUse 1
    Obituary "%k killed %o with an ancient magnum."
    Tag ".357 Magnum"
    +AMMO_OPTIONAL
    +NOALERT
    +WIMPY_WEAPON
    States
    {
      Spawn:
        WMAG A -1
        stop

      Ready:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",0,"ReadyAllowReload")
      ReadyDisallowReload:
        MGNM A 1 A_WeaponReady
        goto Ready

      ReadyAllowReload:
        TNT1 A 0 A_JumpIfInventory("PSMagnumReload",0,"ReadyDisallowReload")
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto ReadyAllowReloadSuccess
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReadyAllowReloadSuccess")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"ReadyAllowReloadSuccess")
        goto ReadyDisallowReload

      ReadyAllowReloadSuccess:
        MGNM A 1 A_WeaponReady(WRF_ALLOWRELOAD)
        goto Ready

      Deselect:
        MGNM A 1 A_Lower
        loop

      Select:
        MGNM A 1 A_Raise
        loop

      Fire:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"FireReloadable")
      FireNonReloadable:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto FireStart
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"FireStart")
        goto FireDry

      FireReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireStart")
        TNT1 A 0 A_JumpIfInventory("PSMagnumReload",1,"FireStart")
      FireReloadableDryCheck:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto Reload
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"Reload")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"Reload")
        goto FireDry

      FireDry:
        MGNM A 5 A_PlaySound("PS/MagnumE")
        goto Ready

      FireStart:
        TNT1 A 0 A_AlertMonsters
        MGNM B 1 Bright A_GiveInventory("SamsaraPowerSlaveMagnumAttackHandler")
        MGNM C 1
        MGNM D 1 Offset(2,41)
        MGNM D 1 Offset(3,47)
        MGNM D 1 Offset(5,55)
        MGNM D 1 Offset(4,56)
        MGNM D 1 Offset(3,57)
        MGNM C 1 Offset(-4,54)
        MGNM C 1 Offset(-3,53)
        MGNM C 1 Offset(-2,52)
        MGNM A 1 Offset(-1,51)
        MGNM A 1 Offset(0,50)
        MGNM A 1 Offset(0,47)
        MGNM A 1 Offset(0,43)
        MGNM A 1 Offset(0,37)
        MGNM A 1 A_ReFire
        goto Ready

      Reload:
        MGNM A 1 Offset(-6,39) A_PlaySound("PS/MagnumR")
        MGNM A 1 Offset(-10,74)
        MGNM A 1 Offset(-12,87)
        MGNM A 1 Offset(-16,93)
        MGNM A 1 Offset(-22,102)
        MGNM A 1 Offset(-34,120)
        MGNM A 4 Offset(-36,143)
        MGNM A 1 Offset(-32,137) A_PlaySound("PS/MagnumR")
        MGNM A 1 Offset(-30,124)
        MGNM A 1 Offset(-26,118)
        MGNM A 1 Offset(-20,102)
        MGNM A 1 Offset(-18,96)
        MGNM A 1 Offset(-14,89)
        MGNM A 1 Offset(-8,72)
        MGNM A 1 Offset(-6,38)
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"ReloadAmmo")
        goto Ready

      ReloadAmmo:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,"ReloadAmmoReserve")
        TNT1 A 0 A_JumpIfInventory("PSMagnumReload",0,"Ready")
        TNT1 A 0 A_GiveInventory("PSMagnumReload")
        loop

      ReloadAmmoReserve:
        TNT1 A 0 A_JumpIfInventory("PSMagnumReload",0,"Ready")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        goto Ready
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_TakeInventory("Clip",1)
        TNT1 A 0 A_GiveInventory("PSMagnumReload")
        goto ReloadAmmo
    }
}

actor SamsaraPowerSlaveMagnumAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"PickupReloadable")
      PickupNonReloadable:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto PickupAttack
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",1)
        goto PickupAttack

      PickupReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("PSMagnumReload",1,1)
        stop
        TNT1 A 0 A_TakeInventory("PSMagnumReload",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("PS/Magnum",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireBullets(2,2,-1,25,"PSBulletPuff",FBF_NORANDOM|FBF_NOFLASH)
        goto PickupFinish

      PickupAttackCoop:
        TNT1 A 0 A_FireBullets(2,2,-1,25,"PSBulletPuffCoop",FBF_NORANDOM|FBF_NOFLASH)
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 A_Quake(1,2,0,16,"nosoundforyoubitch")
        stop
    }
}

actor PSMagnumReload : Ammo
{
    Inventory.MaxAmount 6
    Tag ".357 Magnum Cylinder"
    +IGNORESKILL
}
