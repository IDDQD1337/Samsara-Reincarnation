actor PSSacredManacleCharges : Counter { Inventory.MaxAmount 8 }
actor SamsaraPowerSlaveSacredManacleAttackSuccess : Boolean {}

actor "Sacred Manacle" : SamsaraPowerSlaveWeapon
{
    Inventory.PickupMessage "The Sacred Manacle!"
    Weapon.UpSound "Jon/SacredRaise"
    Weapon.SlotNumber 6
    Weapon.SelectionOrder 300
    Weapon.AmmoType1 "Cell"
    Weapon.AmmoGive1 36
    Weapon.AmmoUse1 8
    Weapon.AmmoType2 "Cell"
    Weapon.AmmoUse2 2
    Obituary "%k makes a pancake out of %o."
    Tag "Sacred Manacle"
    +FLOATBOB
    +NOALERT
    +NOAUTOAIM
    States
    {
      Spawn:
        WTSM A -1
        stop

      Ready:
        TNT1 A 0 A_TakeInventory("PSSacredManacleCharges")
        C_D8 P 1 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
        C_D8 P 1 Offset(-1,38) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
        C_D8 P 1 Offset(-2,39) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
        C_D8 P 1 Offset(-3,37) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
        C_D8 P 1 Offset(-2,38) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
        C_D8 P 1 Offset(-3,39) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
        C_D8 P 1 Offset(-4,40) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
        C_D8 P 1 Offset(-5,41) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
        C_D8 P 1 Offset(-4,40) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
        C_D8 P 1 Offset(-3,39) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
        C_D8 P 1 Offset(-2,38) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
        C_D8 P 1 Offset(-1,37) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
        C_D8 P 1 Offset(0,38) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      ReadyLoop:
        TNT1 A 0 A_TakeInventory("PSSacredManacleCharges")
        C_D8 A 1 A_WeaponReady
        loop

      Deselect:
        TNT1 A 0 A_PlaySound("Jon/SacredLower",CHAN_WEAPON)
      DeselectLoop:
        TNT1 A 0 A_TakeInventory("PSSacredManacleCharges")
        C_D8 A 1 A_Lower
        loop

      Select:
        TNT1 A 0 A_TakeInventory("PSSacredManacleCharges")
        C_D8 A 1 A_Raise
        loop

      Fire: // Eye of Ra Gauntlet (PC) behavior
        C_D8 A 1 A_JumpIfTargetInLOS(1,0,JLOSF_PROJECTILE|JLOSF_DEADNOJUMP|JLOSF_ALLYNOJUMP,6584) // Aim first to keep going
        goto ReadyLoop
        C_D8 P 2 Bright A_PlaySound("Jon/CloudFire",CHAN_WEAPON)
        C_D8 B 2 Bright Offset(2,39)
        C_D8 B 2 Bright Offset(0,41)
        C_D8 B 2 Bright Offset(-1,38)
        C_D8 B 2 Bright Offset(0,37)
        C_D8 B 2 Bright Offset(1,38)
        C_D8 B 2 Bright Offset(0,40)
        C_D8 B 2 Bright Offset(-2,39)
        C_D8 B 2 Bright Offset(-1,38)
        C_D8 B 2 Bright Offset(2,39)
        C_D8 B 2 Bright Offset(0,41)
        C_D8 B 1 Bright Offset(-1,38)
        C_D8 B 1 Bright Offset(0,37)
        C_D8 B 1 Bright Offset(1,38)
        C_D8 B 1 Bright Offset(0,40)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,2)
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,1)
        goto FireStart
        C_D8 B 1 Bright A_JumpIfTargetInLOS("FireStart",0,JLOSF_PROJECTILE|JLOSF_DEADNOJUMP|JLOSF_ALLYNOJUMP,6584) //Are you still aiming at the target?
        C_D8 P 2 Bright
        C_D8 A 2
        goto ReadyLoop

      FireStart:
        C_D8 C 3 Bright Offset(3,39)
        TNT1 A 0 A_PlaySound("Jon/SacredIDLE",CHAN_AUTO)
      FireContinue:
        C_D8 D 1 Bright Offset(-3,41) A_GiveInventory("SamsaraPowerSlaveEyeOfRaGauntletAttackHandler")
        C_D8 D 2 Bright Offset(0,37)
        C_D8 C 1 Bright Offset(0,37)
        C_D8 C 2 Bright Offset(2,41)
        C_D8 D 3 Bright Offset(-1,40)
        C_D8 C 2 Bright Offset(0,38)
        TNT1 A 0 A_ReFire
      FireFinish:
        C_D8 C 1 Bright Offset(0,37)
        C_D8 B 2 Bright Offset(-3,38)
        C_D8 P 1 Bright Offset(-1,38)
        C_D8 A 1 Offset(0,37)
        goto ReadyLoop

      Hold:
        TNT1 A 0 A_JumpIfTargetInLOS("HoldStart",0,JLOSF_PROJECTILE|JLOSF_DEADNOJUMP|JLOSF_ALLYNOJUMP,6584) //Same as above
        TNT1 A 0 A_ClearReFire
        goto FireFinish

      HoldStart:
        C_D8 C 3 Bright Offset(3,39)
        goto FireContinue

      AltFire: // Sacred Manacle (Console) behavior
        C_D8 P 2 Bright A_PlaySound("Jon/CloudFire",CHAN_WEAPON)
      AltFireCharge:
        C_D8 B 1 Bright Offset(2,39)
        C_D8 B 1 Bright Offset(0,41)
        C_D8 B 1 Bright Offset(-1,38)
        C_D8 B 1 Bright Offset(0,37)
        C_D8 B 1 Bright Offset(1,38)
        C_D8 B 1 Bright Offset(0,40)
        C_D8 B 1 Bright Offset(-2,39)
        C_D8 B 1 Bright Offset(-1,38)
        C_D8 B 1 Bright Offset(2,39)
        C_D8 B 1 Bright Offset(0,41)
        C_D8 B 1 Bright Offset(-1,38)
        C_D8 B 1 Bright Offset(0,37)
        C_D8 B 1 Bright Offset(1,38)
        C_D8 B 1 Bright Offset(0,40)
        TNT1 A 0 A_GiveInventory("PSSacredManacleCharges")
        TNT1 A 0 A_ReFire("AltFireCharge")
      AltFireRelease:
        C_D8 C 3 Bright Offset(3,39)
        C_D8 D 1 Bright Offset(-3,41)
        TNT1 A 0 A_AlertMonsters
        C_D8 C 1 Bright Offset(0,74) A_GiveInventory("SamsaraPowerSlaveSacredManacleAttackHandler")
        C_D8 C 1 Bright Offset(8,79)
        C_D8 C 1 Bright Offset(16,84)
        C_D8 C 1 Bright Offset(24,89)
        C_D8 C 1 Bright Offset(32,94)
        C_D8 C 1 Bright Offset(40,99)
        C_D8 C 1 Bright Offset(48,104)
        C_D8 C 1 Bright Offset(56,109)
        C_D8 B 1 Offset(64,114)
        C_D8 B 1 Offset(72,119)
        C_D8 B 1 Offset(80,124)
        C_D8 B 1 Offset(72,119) A_PlaySound("Jon/CloudFire",CHAN_WEAPON)
        C_D8 A 1 Offset(64,114)
        C_D8 A 1 Offset(56,109)
        C_D8 A 1 Offset(48,104)
        C_D8 A 1 Offset(40,99)
        C_D8 A 1 Offset(32,94)
        C_D8 A 1 Offset(24,90)
        C_D8 A 1 Offset(16,84)
        C_D8 A 1 Offset(8,79)
        C_D8 A 1 Offset(0,74)
        C_D8 A 1 Offset(0,63)
        C_D8 A 1 Offset(0,53)
        C_D8 A 1 Offset(0,42)
        C_D8 A 1 Offset(0,32)
        goto ReadyLoop
    }
}

actor SamsaraPowerSlaveEyeOfRaGauntletAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Cell",8,1)
        stop
        TNT1 A 0 A_TakeInventory("Cell",8)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("PSCloudSeeker_3",0,false)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("PSCloudSeeker_2",0,false)
        stop
    }
}

actor SamsaraPowerSlaveSacredManacleAttackHandler : Trigger
{
    States
    {
      // Token clearing stuff
      Pickup:
        TNT1 A 0 A_TakeInventory("SamsaraPowerSlaveSacredManacleAttackSuccess")
        goto PickupChargeCheck

      // Charge checking stuff
      PickupChargeCheck:
        TNT1 A 0 A_JumpIfInventory("PSSacredManacleCharges",1,"PickupAmmoCheck")
        goto PickupFinish

      // Ammo checking/taking stuff
      PickupAmmoCheck:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Cell",2,1)
        goto PickupFinish
        TNT1 A 0 A_TakeInventory("Cell",2)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_GiveInventory("SamsaraPowerSlaveSacredManacleAttackSuccess")
        TNT1 A 0 A_PlaySound("Jon/ManacleBolt",CHAN_5)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("PSManacleBoltDM",0,false,8)
        goto PickupChargeDecrement

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("PSManacleBoltCoop",0,false,8)
        goto PickupChargeDecrement

      // Charge decrementing stuff
      PickupChargeDecrement:
        TNT1 A 0 A_TakeInventory("PSSacredManacleCharges",1)
        goto PickupChargeCheck

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 A_TakeInventory("PSSacredManacleCharges")
        TNT1 A 0 A_JumpIfInventory("SamsaraPowerSlaveSacredManacleAttackSuccess",1,1)
        stop
        TNT1 A 0 A_Recoil(1)
        TNT1 A 0 A_Quake(4,2,0,16,"nosoundforyoubitch")
        stop
    }
}

actor PSCloudSeeker : FastProjectile
{
    Projectile
    Radius 1
    Height 1
    Speed 360
    Damage (0)
    +BLOODLESSIMPACT
    +DONTSPLASH
    +FOILINVUL
    +FORCERADIUSDMG
    +NOEXTREMEDEATH
    +NOTIMEFREEZE
    +PAINLESS
    +SEEKERMISSILE
    States
    {
      Spawn:
        TNT1 A 1 A_SeekerMissile(90,90,SMF_LOOK,256)
        loop

      Death:
        TNT1 A 1
        stop

      XDeath:
      Crash:
        TNT1 A 1 A_CustomMissile("PSCloudStrike",0,0,0,0,CMF_TRACKOWNER|CMF_CHECKTARGETDEAD)
        stop
    }
}

actor PSCloudSeeker_2 : PSCloudSeeker
{
    Species "Player"
    +THRUSPECIES
    States
    {
      XDeath:
      Crash:
        TNT1 A 1 A_CustomMissile("PSCloudStrike_2",0,0,0,0,CMF_TRACKOWNER|CMF_CHECKTARGETDEAD)
        stop
    }
}

actor PSCloudSeeker_3 : PSCloudSeeker
{
    States
    {
      XDeath:
      Crash:
        TNT1 A 1 A_CustomMissile("PSCloudStrike_3",0,0,0,0,CMF_TRACKOWNER|CMF_CHECKTARGETDEAD)
        stop
    }
}

actor PSCloudStrike
{
    Projectile
    Radius 7
    Height 10
    Speed 3
    Damage (0)
    DamageType "PSRAPower"
    Scale 0.6
    +BLOODLESSIMPACT
    +DONTSPLASH
    +FORCERADIUSDMG
    +FRIENDLY
    +NOBLOCKMAP
    +NOCLIP
    //+RIPPER
    +SEEKERMISSILE
    States
    {
      Spawn:
        TNT1 A 0 NoDelay A_Warp(AAPTR_TRACER,0,0,0,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE)
        TNT1 A 0 A_PlaySound("Jon/CloudIDLE",CHAN_7,1,false,ATTN_NONE)
        C_D8 EFGHHIIJJKKLL 1 Bright A_SeekerMissile(90,90,SMF_LOOK|SMF_PRECISE,256,10)
        TNT1 A 0 A_PlaySound("Jon/CloudStrike")
        C_D8 MMM 1 Bright A_SeekerMissile(90,90,SMF_LOOK|SMF_PRECISE,256,10)
        TNT1 A 0 A_Explode(200,50,0,true)
        TNT1 A 0 A_Quake(8,6,0,384,"nosoundforyoubitch")
        C_D8 LLNNOOLL 1 Bright A_SeekerMissile(90,90,SMF_LOOK|SMF_PRECISE,256,10)
      Death:
        C_D8 L 1 Bright A_SeekerMissile(90,90,SMF_LOOK|SMF_PRECISE,256,10)
        C_D8 KJIH 2 Bright A_SeekerMissile(90,90,SMF_LOOK|SMF_PRECISE,256,10)
        C_D8 GF 2 Bright
        TNT1 A 0 A_PlaySound("Jon/CloudIDLE",CHAN_7)
        C_D8 E 2 Bright A_FadeOut(0.15)
        stop
    }
}

actor PSCloudStrike_2 : PSCloudStrike
{
    DamageType "PSRAPowerCoop"
    Species "Player"
    +THRUSPECIES
}

actor PSCloudStrike_3 : PSCloudStrike
{
    Speed 12
    States
    {
      Spawn:
        TNT1 A 0 NoDelay A_Warp(AAPTR_TRACER,0,0,0,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE)
        TNT1 A 0 A_PlaySound("Jon/CloudIDLE",CHAN_7,1,false,ATTN_NONE)
        C_D8 EFGHHIIJJKKLL 1 Bright A_SeekerMissile(90,90,SMF_LOOK|SMF_PRECISE,256,10)
        TNT1 A 0 A_PlaySound("Jon/CloudStrike")
        C_D8 MMM 1 Bright A_SeekerMissile(90,90,SMF_LOOK|SMF_PRECISE,256,10)
        TNT1 A 0 A_Explode(80,50,0,true)
        C_D8 LLNNOOLL 1 Bright A_SeekerMissile(90,90,SMF_LOOK|SMF_PRECISE,256,10)
        goto Death
    }
}

actor PSManacleBoltHitSomethingPositive : Boolean {}

actor PSManacleBolt
{
    Projectile
    Radius 8
    Height 16
    Damage (75)
    DamageType "PSRAPower"
    MaxTargetRange 8
    +SCREENSEEKER
    +SEEKERMISSILE
    States
    {
      Spawn:
        TNT1 A 0
        TNT1 A 1 ACS_NamedExecuteWithResult("PSManacleBoltPitch")
      SpawnLoop:
        //TNT1 A 0 A_GiveInventory("PSManacleBoltHitSomething2")
        //TNT1 A 0 A_JumpIfInventory("PSManacleBoltHitSomethingPositive",1,"Death")
        TNT1 A 0 A_SpawnItemEx("PSManacleBoltTrail",0,0,0,0,0,0,0,SXF_TRANSFERPITCH)
        TNT1 A 0 A_Warp(AAPTR_DEFAULT,20*cos(pitch),0,20*sin(pitch),0,WARPF_WARPINTERPOLATION,1)
        goto Death
        TNT1 A 1
        loop

      Death:
        TNT1 A 1
        stop
    }
}

actor PSManacleBoltDM : PSManacleBolt
{
    Damage (40)
    States
    {
      Spawn:
        TNT1 A 0
        TNT1 A 1 ACS_NamedExecuteWithResult("PSManacleBoltPitch")
      SpawnLoop:
        //TNT1 A 0 A_GiveInventory("PSManacleBoltHitSomething2")
        //TNT1 A 0 A_JumpIfInventory("PSManacleBoltHitSomethingPositive",1,"Death")
        TNT1 A 0 A_SpawnItemEx("PSManacleBoltTrail",0,0,0,0,0,0,0,SXF_TRANSFERPITCH)
        TNT1 A 0 A_Warp(AAPTR_DEFAULT,20*cos(pitch),0,20*sin(pitch),0,0,1)
        goto Death
        TNT1 A 1
        loop
    }
}

actor PSManacleBoltCoop : PSManacleBolt
{
    DamageType "PSRAPowerCoop"
    Species "Player"
    +THRUSPECIES
    States
    {
      Spawn:
        TNT1 A 0
        TNT1 A 1 ACS_NamedExecuteWithResult("PSManacleBoltPitch")
      SpawnLoop:
        //TNT1 A 0 A_GiveInventory("PSManacleBoltHitSomething2")
        //TNT1 A 0 A_JumpIfInventory("PSManacleBoltHitSomethingPositive",1,"Death")
        TNT1 A 0 A_SpawnItemEx("PSManacleBoltTrail",0,0,0,0,0,0,0,SXF_TRANSFERPITCH)
        TNT1 A 0 A_Warp(AAPTR_DEFAULT,20*cos(pitch),0,20*sin(pitch),0,0,1)
        goto Death
        TNT1 A 1
        loop
    }
}

actor PSManacleBoltTrail
{
    Radius 8
    Height 16
    +CLIENTSIDEONLY
    +NOINTERACTION
    States
    {
      Spawn:
        PS00 A 8 Bright
        stop
    }
}

actor PSManacleBoltHitSomething : Trigger
{
    States
    {
      Pickup:
        TNT1 A 0 A_RadiusGive("PSManacleBoltHitSomethingQuery",8,RGF_PLAYERS|RGF_MONSTERS|RGF_OBJECTS)
        stop
    }
}

actor PSManacleBoltHitSomething2 : Trigger
{
    States
    {
      Pickup:
        TNT1 A 0 A_RadiusGive("PSManacleBoltHitSomethingQuery",8,RGF_MONSTERS|RGF_OBJECTS)
        stop
    }
}

actor PSManacleBoltHitSomethingQuery : Trigger
{
    States
    {
      Pickup:
        TNT1 A 0 A_RadiusGive("PSManacleBoltHitSomethingPositive",8,RGF_MISSILES)
        stop
    }
}

actor PSManacleBoltSeeker : Trigger
{
    States
    {
      Pickup:
        TNT1 A 0 A_Jump(224,"PickupStop")
        TNT1 A 0 A_SeekerMissile(5,10,SMF_LOOK,256,8)
        TNT1 A 0 A_FaceTracer
        stop
    }
}
