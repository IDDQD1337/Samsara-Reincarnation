actor SamsaraPowerSlaveMacheteFireAnim : Boolean {}

actor PSMachete : SamsaraPowerSlaveWeapon
{
    Inventory.PickupMessage "A Machete!"
    Weapon.UpSound "PS/SlashUp"
    Weapon.SlotNumber 0
    Weapon.SelectionOrder 4900
    Weapon.Kickback 100
    Obituary "%o was stabbed by %k."
    Tag "Machete"
    +MELEEWEAPON
    +NOALERT
    +WIMPY_WEAPON
    States
    {
      Ready:
        KNF_ I 1 A_WeaponReady
        loop

      Deselect:
        TNT1 A 0 A_PlaySound("PS/SlashDown",CHAN_WEAPON)
        KNF_ I 1 A_Lower
        wait

      Select:
        KNF_ ABCDEFGH 1
        KNF_ I 1 A_Raise
        wait

      Fire:
        TNT1 A 0 A_JumpIfInventory("SamsaraPowerSlaveMacheteFireAnim",1,"Fire2")
      Fire1:
        TNT1 A 0 A_GiveInventory("SamsaraPowerSlaveMacheteFireAnim")
        TNT1 A 0 A_JumpIfInventory("SamsaraCurrentWaterLevel",0,"Fire1Underwater")
        KNFA A 1 Offset(96,102)
        //KNFA A 1 Offset(1,106)
        KNFA A 1 Offset(-10,109)
        KNFA B 1 Offset(0,71)
        TNT1 A 0 A_CheckLOF("Fire1Hit",CLOFF_JUMPFRIEND|CLOFF_SKIPTARGET|CLOFF_ALLOWNULL|CLOFF_BEYONDTARGET|CLOFF_JUMP_ON_MISS,65,0,0,0,0,0,AAPTR_NULL)
        TNT1 A 0 A_JumpIfTargetInLOS("Fire1Hit",0,JLOSF_DEADNOJUMP|JLOSF_NOAUTOAIM,65)
        KNFA B 1 Offset(-28,73) A_GiveInventory("SamsaraPowerSlaveMacheteAttackHandler")
        KNFA C 1 Offset(-1,80)
        //KNFA C 1 Offset(-13,82)
        KNFA C 1 Offset(-24,90)
        KNFA D 1 Offset(-10,63)
        KNFA D 1 Offset(-23,77)
        KNFA D 1 Offset(-36,85)
        KNFA D 1 Offset(-42,91)
        goto FireFinish

      Fire1Hit:
        KNFA B 1 Offset(-28,73) A_GiveInventory("SamsaraPowerSlaveMacheteAttackHandler")
        KNFA B 1 Offset(-26,104)
        KNFA B 1 Offset(-27,121)
        KNFA B 1 Offset(-30,144)
        KNFA B 1 Offset(-32,162)
        KNFA B 1 Offset(-41,179)
        KNFA B 1 Offset(-56,191)
        TNT1 A 5
        goto FireFinish

      Fire1Underwater:
        KNFA A 1 Offset(96,102)
        KNFA A 1 Offset(-10,109)
        KNFA B 1 Offset(0,71)
        KNFA B 1 Offset(-28,73) A_GiveInventory("SamsaraPowerSlaveMacheteUnderwaterAttackHandler")
        KNFA C 1 Offset(-1,80)
        KNFA C 1 Offset(-24,90)
        KNFA D 1 Offset(-10,63)
        KNFA D 1 Offset(-23,77)
        KNFA D 1 Offset(-36,85)
        KNFA D 1 Offset(-42,91)
        TNT1 A 5
        goto FireFinish

      Fire2:
        TNT1 A 0 A_TakeInventory("SamsaraPowerSlaveMacheteFireAnim")
        TNT1 A 0 A_JumpIfInventory("SamsaraCurrentWaterLevel",0,"Fire2Underwater")
        KNFB A 1 Offset(-9,70)
        KNFB B 1 Offset(-1,78)
        KNFB C 1 Offset(0,37)
        KNFB C 1 Offset(15,38)
        //KNFB C 1 Offset(86,39)
        KNFB C 1 Offset(121,43)
        //KNFB C 1 Offset(136,47)
        TNT1 A 0 A_CheckLOF("Fire2Hit",CLOFF_JUMPFRIEND|CLOFF_SKIPTARGET|CLOFF_ALLOWNULL|CLOFF_BEYONDTARGET|CLOFF_JUMP_ON_MISS,65,0,0,0,0,0,AAPTR_NULL)
        TNT1 A 0 A_JumpIfTargetInLOS("Fire2Hit",0,JLOSF_DEADNOJUMP|JLOSF_NOAUTOAIM,65)
        KNFB C 1 Offset(193,51) A_GiveInventory("SamsaraPowerSlaveMacheteAttackHandler")
        KNFB C 1 Offset(216,58)
        KNFB D 1 Offset(19,38)
        KNFB D 1 Offset(46,46)
        KNFB D 1 Offset(68,51)
        goto FireFinish

      Fire2Hit:
        KNFB C 1 Offset(146,51) A_GiveInventory("SamsaraPowerSlaveMacheteAttackHandler")
        KNFB C 1 Offset(131,73)
        KNFB C 1 Offset(144,89)
        KNFB C 1 Offset(138,102)
        KNFB C 1 Offset(112,121)
        KNFB C 1 Offset(96,139)
        KNFB C 1 Offset(84,144)
        KNFB C 1 Offset(74,153)
        TNT1 A 4
        goto FireFinish

      Fire2Underwater:
        KNFB A 1 Offset(-9,70)
        KNFB B 1 Offset(-1,78)
        KNFB C 1 Offset(0,37)
        KNFB C 1 Offset(15,38)
        KNFB C 1 Offset(121,43)
        KNFB C 1 Offset(193,51) A_GiveInventory("SamsaraPowerSlaveMacheteUnderwaterAttackHandler")
        KNFB C 1 Offset(216,58)
        KNFB D 1 Offset(19,38)
        KNFB D 1 Offset(46,46)
        KNFB D 1 Offset(68,51)
        TNT1 A 5
        goto FireFinish

      FireFinish:
        TNT1 A 0 A_ReFire
        goto Ready
    }
}

actor SamsaraPowerSlaveMacheteAttackHandler : Trigger
{
    States
    {
      // Attack stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupCoop")
      PickupDM:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupDMSpread")
        TNT1 A 0 A_CustomPunch(25,true,0,"PSMacheteSparkPuff")
        stop

      PickupDMSpread:
        TNT1 A 0 A_CustomPunch(25*3,true,0,"PSMacheteSparkPuff")
        stop

      PickupCoop:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupCoopSpread")
        TNT1 A 0 A_CustomPunch(25,true,0,"PSMacheteSparkPuffCoop")
        stop

      PickupCoopSpread:
        TNT1 A 0 A_CustomPunch(25*3,true,0,"PSMacheteSparkPuffCoop")
        stop
    }
}

actor SamsaraPowerSlaveMacheteUnderwaterAttackHandler : Trigger
{
    States
    {
      // Attack stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupCoop")
      PickupDM:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupDMSpread")
        TNT1 A 0 A_CustomPunch(25,true,0,"PSMacheteSparkPuff_UnderWater")
        stop

      PickupDMSpread:
        TNT1 A 0 A_CustomPunch(25*3,true,0,"PSMacheteSparkPuff_UnderWater")
        stop

      PickupCoop:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupCoopSpread")
        TNT1 A 0 A_CustomPunch(25,true,0,"PSMacheteSparkPuff_UnderWaterCoop")
        stop

      PickupCoopSpread:
        TNT1 A 0 A_CustomPunch(25*3,true,0,"PSMacheteSparkPuff_UnderWaterCoop")
        stop
    }
}
