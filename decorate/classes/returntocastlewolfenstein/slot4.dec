actor RTCW_FG42Spread : Counter { Inventory.MaxAmount 8 }

Actor RTCW_FG42 : RTCW_Weapon
{
	Weapon.SlotNumber 4
	Weapon.SlotPriority 0
	Weapon.SelectionOrder 750
	Weapon.AmmoType1 "RTCW_FG42Magazine"
	Weapon.AmmoUse1 1
	Weapon.AmmoType2 "Clip"
	Weapon.AmmoGive2 40
	Obituary "%o was killed (fg42) by %k."
	Inventory.PickupMessage "FG42 Paratroop Rifle"
    Inventory.Icon "CWCMPW06"
	Tag "FG-42"
	States
	{
		Spawn:
			RW98 A -1
			stop

		ReadyActual:
			RW00 A 0 A_TakeInventory("RTCW_FG42Spread")
			RW00 A 0 A_JumpIfInventory("RTCW_Kicking",1,"Kicking")
			RW00 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"ReadyActualReloadable")
		ReadyActualNonReloadable:
			RW00 A 0 A_JumpIfInventory("RTCW_Scoped",1,2)
			RW00 A 1 A_WeaponReady(WRF_ALLOWZOOM)
			goto ReadyActualAmmoCheck
			TNT1 A 1 A_WeaponReady(WRF_ALLOWZOOM)
			goto ReadyActualAmmoCheck

		ReadyActualReloadable:
			RW00 A 0 A_JumpIfInventory("RTCW_Scoped",1,2)
			RW00 A 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
			goto ReadyActualAmmoCheck
			TNT1 A 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
			goto ReadyActualAmmoCheck

		ReadyActualAmmoCheck:
			RW00 A 0 A_JumpIfInventory("SamsaraReloadMode",2,1)
			goto ReadyActual
			RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"ReadyActual")
			RW00 A 0 A_JumpIfInventory("RTCW_FG42Magazine",1,"ReadyActual")
			goto ReloadCheck

		Deselect:
			RW00 A 0 A_TakeInventory("RTCW_FG42Spread")
			RW00 A 0 A_JumpIfInventory("RTCW_Scoped",1,1)
			goto DeselectStart
			RW00 A 0 A_TakeInventory("RTCW_Scoped")
			RW00 A 0 A_ZoomFactor(1,ZOOM_INSTANT)
			goto DeselectStart

		DeselectStart:
			RW00 A 0 A_JumpIfHealthLower(1,"DeselectInstant")
			RW00 A 2 A_GiveInventory("SamsaraRTCWWeaponSwitchSoundPlayer")
			RW00 FGHIJKL 2
			goto DeselectInstant

		Ready: // our "Select" state
			RW00 A 0 A_TakeInventory("RTCW_WeaponToken",256)
			RW00 A 0 A_GiveInventory("RTCW_WeaponToken",12)
			RW00 A 0 A_TakeInventory("RTCW_FG42Spread")
			RW00 M 0 A_JumpIfInventory("RTCW_Scoped",1,"ReadyZoomOut1") // map change hack
		ReadyStart1:
			RW00 M 1 A_JumpIfInventory("SamsaraHasDoubleFiringSpeed",1,1)
			RW00 M 0 A_JumpIfInventory("RTCW_Scoped",1,"ReadyZoomOut2") // map change hack
		ReadyStart2:
			RW00 M 1
			RW00 LNOP 2
			goto ReadyActual

		ReadyZoomOut1:
			RW00 M 0 A_TakeInventory("RTCW_Scoped")
			RW00 M 0 A_ZoomFactor(1,ZOOM_INSTANT)
			goto ReadyStart1

		ReadyZoomOut2:
			RW00 M 0 A_TakeInventory("RTCW_Scoped")
			RW00 M 0 A_ZoomFactor(1,ZOOM_INSTANT)
			goto ReadyStart2

		Fire:
			RW00 B 0 A_JumpIfInventory("SamsaraReloadMode",2,"FireReloadable")
		FireNonReloadable:
			RW00 B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart")
			RW00 B 0 A_JumpIfInventory("Clip",2,"FireStart")
			goto FireDry

		FireReloadable:
			RW00 B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireStart")
			RW00 B 0 A_JumpIfInventory("RTCW_FG42Magazine",1,"FireStart")
		FireReloadableDryCheck:
			RW00 B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReloadScopeCheck")
			RW00 B 0 A_JumpIfInventory("Clip",2,"ReloadScopeCheck")
			goto FireDry

		FireDry:
			RW00 A 0 A_ClearReFire
			RW00 A 0 A_GiveInventory("SamsaraRTCWWeaponDrySoundPlayer")
			RW00 A 0 A_JumpIfInventory("RTCW_Scoped",1,2)
			RW00 A 17 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto FireDryFinish
			TNT1 A 17 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto FireDryFinish

		FireDryFinish:
			RW00 A 0 A_SelectWeapon("RTCW_Luger")
			goto Deselect

		FireUnderwater:
			RW00 A 0 A_ClearReFire
			RW00 A 0 A_GiveInventory("SamsaraRTCWWeaponUnderwaterSoundPlayer")
			RW00 A 0 A_JumpIfInventory("RTCW_Scoped",1,2)
			RW00 A 17 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto ReadyActual
			TNT1 A 17 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto ReadyActual

		FireStart:
			RW00 B 0 A_JumpIfInventory("SamsaraCanFireWeaponsUnderwater",1,2)
			RW00 B 0 A_JumpIfInventory("SamsaraCurrentWaterLevel",0,"FireUnderwater")
			RW00 B 0 A_GiveInventory("RTCW_FiringToken",1)
			RW00 B 0 A_GiveInventory("RTCW_BlazkoWeaponAlert",1)
			RW00 B 0 A_JumpIfInventory("RTCW_Scoped",1,"FireScoped")
			RW00 B 0 A_GunFlash("FireFlash")
			RW00 B 0 A_GiveInventory("SamsaraRTCWFG42AttackHandler")
			RW00 BCD 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW00 E 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW00 F 0 A_ReFire
			goto ReadyActual

		FireScoped:
			TNT1 A 0 A_GiveInventory("SamsaraRTCWFG42AttackHandler")
			TNT1 AAA 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			TNT1 A 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			TNT1 A 0 A_ReFire
			goto ReadyActual

		FireFlash:
			TNT1 A 0 A_Jump(256,1,2)
			RW03 B 2 Bright
			stop
			RW04 B 2 Bright
			stop

		AltFire:
		Zoom:
			RW00 A 0 A_GiveInventory("SamsaraRTCWWeaponSwitchSoundPlayer")
			RW00 A 0 A_JumpIfInventory("RTCW_Scoped",1,"ZoomUnscope")
		ZoomScope:
			TNT1 A 0 A_GiveInventory("RTCW_Scoped",1)
			TNT1 A 0 ACS_NamedExecuteWithResult("RTCW_Scope")
			TNT1 A 0 A_ZoomFactor(1.63,ZOOM_INSTANT)
			TNT1 A 10 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto ReadyActual

		ZoomUnscope:
			RW00 A 0 A_TakeInventory("RTCW_Scoped",1)
			RW00 A 0 A_ZoomFactor(1,ZOOM_INSTANT)
			RW00 M 0 A_JumpIfInventory("SamsaraHasDoubleFiringSpeed",1,2)
			RW00 MM 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW00 LNOP 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto ReadyActual

		Reload:
		ReloadCheck:
			RW00 A 0 A_JumpIfInventory("RTCW_FG42Magazine",0,"ReadyActual")
			RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
			RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReloadScopeCheck")
			RW00 A 0 A_JumpIfInventory("Clip",2,"ReloadScopeCheck")
			goto ReadyActual

		ReloadScopeCheck:
			RW00 A 0 A_JumpIfInventory("RTCW_Scoped",1,1)
			goto ReloadStart
			RW00 A 0 A_GiveInventory("SamsaraRTCWWeaponSwitchSoundPlayer")
			RW00 A 0 A_TakeInventory("RTCW_Scoped",1)
			RW00 A 0 A_ZoomFactor(1,ZOOM_INSTANT)
			RW00 L 0 A_JumpIfInventory("SamsaraHasDoubleFiringSpeed",1,2)
			RW00 LL 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW00 MNOP 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto ReloadCheck

		ReloadStart:
			RW00 Q 0 A_GiveInventory("SamsaraRTCWFG42ReloadSoundPlayer1")
			//RW00 Q 0 A_JumpIfInventory("RTCW_FG42Magazine",1,1)
			goto ReloadStartAmmoless
			RW00 "QRSTUVWXYZ[\]" 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW01 ABCDEFGHIJKLMN 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW02 DEFG 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW00 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"ReloadAmmo")
		ReloadFinish:
			RW00 A 0
			goto ReadyActual

		ReloadAmmo:
			RW00 A 0 A_JumpIfInventory("RTCW_FG42Magazine",0,"ReloadFinish")
			RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
			RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
			RW00 A 0 A_JumpIfInventory("Clip",2,1)
			goto ReloadFinish
			RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
			RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
			RW00 A 0 A_TakeInventory("Clip",2)
			RW00 A 0 A_GiveInventory("RTCW_FG42Magazine")
			loop

		ReloadStartAmmoless:
			RW00 "QRSTUVWXYZ[\]" 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW01 ABCDEFGHIJKLMNOPQR 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW01 S 0 A_GiveInventory("SamsaraRTCWFG42ReloadSoundPlayer2")
			RW01 STUVWX 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW01 Y 0 A_JumpIfInventory("SamsaraReloadMode",2,"ReloadAmmoAmmoless")
		ReloadFinishAmmoless:
			RW01 "YZ[\]" 2 A_WeaponReady(WRF_ALLOWZOOM)
			RW02 ABCDEFG 1 A_WeaponReady(WRF_ALLOWZOOM)
			goto ReadyActual

		ReloadAmmoAmmoless:
			RW01 Y 0 A_JumpIfInventory("RTCW_FG42Magazine",0,"ReloadFinishAmmoless")
			RW01 Y 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
			RW01 Y 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
			RW01 Y 0 A_JumpIfInventory("Clip",2,1)
			goto ReloadFinishAmmoless
			RW01 Y 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
			RW01 Y 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
			RW01 Y 0 A_TakeInventory("Clip",2)
			RW01 Y 0 A_GiveInventory("RTCW_FG42Magazine")
			loop

		Kicking:
			RW00 A 0 A_GunFlash("KickingFoot")
		KickingDelay:
			RW00 A 0 A_JumpIfInventory("RTCW_Scoped",1,2)
			RW00 A 18 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto KickingDelayFinish
			TNT1 A 18 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE) // not really supposed to see anything, even the kick
			goto KickingDelayFinish

		KickingDelayFinish:
			RW00 A 0 A_TakeInventory("RTCW_Kicking",1)
			goto ReadyActual
	}
}

actor SamsaraRTCWFG42AttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"PickupReloadable")
      PickupNonReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",2,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",2)
        goto PickupAttack

      PickupReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("RTCW_FG42Magazine",1,1)
        stop
        TNT1 A 0 A_TakeInventory("RTCW_FG42Magazine",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",2,"PickupAttackET")
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupAttackRTCWConsole")
      // Original sounds
      PickupAttackRTCWPC:
        TNT1 A 0 A_PlaySound("RTCW/FG42/Fire",CHAN_WEAPON)
        TNT1 A 0 A_SpawnItemEx("RTCW_Casing",4,-10*cos(-pitch),43+(10*sin(-pitch)),1,0,2*cos(-pitch),-random(85,95),SXF_TRANSFERPITCH)
        TNT1 A 0 A_JumpIfInventory("RTCW_Scoped",1,"PickupAttackRTCWPCScoped")
      // Unscoped
      PickupAttackRTCWPCUnscoped:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackRTCWPCUnscopedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackRTCWPCUnscopedCoop")
      PickupAttackRTCWPCUnscopedDM:
        TNT1 A 0 A_FireCustomMissile("RTCW_FG42Tracer",CallACS("RTCW_Decorate", 1, 1)*frandom(-1.0,1.0),false,4,9,0,CallACS("RTCW_Decorate", 1, 1)*frandom(-1.0,1.0))
        goto PickupFinishUnscoped

      PickupAttackRTCWPCUnscopedCoop:
        TNT1 A 0 A_FireCustomMissile("RTCW_FG42TracerCoop",CallACS("RTCW_Decorate", 1, 1)*frandom(-1.0,1.0),false,4,9,0,CallACS("RTCW_Decorate", 1, 1)*frandom(-1.0,1.0))
        goto PickupFinishUnscoped

      // Scoped
      PickupAttackRTCWPCScoped:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackRTCWPCScopedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackRTCWPCScopedCoop")
      PickupAttackRTCWPCScopedDM:
        TNT1 A 0 A_FireCustomMissile("RTCW_FG42Tracer",CallACS("RTCW_Decorate", 1, 1)*frandom(-0.25,0.25),false,0,9,0,CallACS("RTCW_Decorate", 1, 1)*frandom(-0.25,0.25))
        goto PickupFinishScoped

      PickupAttackRTCWPCScopedCoop:
        TNT1 A 0 A_FireCustomMissile("RTCW_FG42TracerCoop",CallACS("RTCW_Decorate", 1, 1)*frandom(-0.25,0.25),false,0,9,0,CallACS("RTCW_Decorate", 1, 1)*frandom(-0.25,0.25))
        goto PickupFinishScoped

      // Console sounds
      PickupAttackRTCWConsole:
        TNT1 A 0 A_PlaySound("RTCW/FG42/Fire",CHAN_WEAPON)
        TNT1 A 0 A_SpawnItemEx("RTCW_Casing",4,-10*cos(-pitch),43+(10*sin(-pitch)),1,0,2*cos(-pitch),-random(85,95),SXF_TRANSFERPITCH)
        TNT1 A 0 A_JumpIfInventory("RTCW_Scoped",1,"PickupAttackRTCWConsoleScoped")
      // Unscoped
      PickupAttackRTCWConsoleUnscoped:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackRTCWConsoleUnscopedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackRTCWConsoleUnscopedCoop")
      PickupAttackRTCWConsoleUnscopedDM:
        TNT1 A 0 A_FireCustomMissile("RTCW_FG42TracerSoundConsole",CallACS("RTCW_Decorate", 1, 1)*frandom(-1.0,1.0),false,4,9,0,CallACS("RTCW_Decorate", 1, 1)*frandom(-1.0,1.0))
        goto PickupFinishUnscoped

      PickupAttackRTCWConsoleUnscopedCoop:
        TNT1 A 0 A_FireCustomMissile("RTCW_FG42TracerCoopSoundConsole",CallACS("RTCW_Decorate", 1, 1)*frandom(-1.0,1.0),false,4,9,0,CallACS("RTCW_Decorate", 1, 1)*frandom(-1.0,1.0))
        goto PickupFinishUnscoped

      // Scoped
      PickupAttackRTCWConsoleScoped:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackRTCWConsoleScopedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackRTCWConsoleScopedCoop")
      PickupAttackRTCWConsoleScopedDM:
        TNT1 A 0 A_FireCustomMissile("RTCW_FG42TracerSoundConsole",CallACS("RTCW_Decorate", 1, 1)*frandom(-0.25,0.25),false,0,9,0,CallACS("RTCW_Decorate", 1, 1)*frandom(-0.25,0.25))
        goto PickupFinishScoped

      PickupAttackRTCWConsoleScopedCoop:
        TNT1 A 0 A_FireCustomMissile("RTCW_FG42TracerCoopSoundConsole",CallACS("RTCW_Decorate", 1, 1)*frandom(-0.25,0.25),false,0,9,0,CallACS("RTCW_Decorate", 1, 1)*frandom(-0.25,0.25))
        goto PickupFinishScoped

      // Enemy Territory sounds
      PickupAttackET:
        TNT1 A 0 A_PlaySound("RTCWET/FG42/Fire",CHAN_WEAPON)
        TNT1 A 0 A_SpawnItemEx("RTCW_Casing",4,-10*cos(-pitch),43+(10*sin(-pitch)),1,0,2*cos(-pitch),-random(85,95),SXF_TRANSFERPITCH)
        TNT1 A 0 A_JumpIfInventory("RTCW_Scoped",1,"PickupAttackETScoped")
      // Unscoped
      PickupAttackETUnscoped:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackETUnscopedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackETUnscopedCoop")
      PickupAttackETUnscopedDM:
        TNT1 A 0 A_FireCustomMissile("RTCW_FG42TracerSoundET",CallACS("RTCW_Decorate", 1, 1)*frandom(-1.0,1.0),false,4,9,0,CallACS("RTCW_Decorate", 1, 1)*frandom(-1.0,1.0))
        goto PickupFinishUnscoped

      PickupAttackETUnscopedCoop:
        TNT1 A 0 A_FireCustomMissile("RTCW_FG42TracerCoopSoundET",CallACS("RTCW_Decorate", 1, 1)*frandom(-1.0,1.0),false,4,9,0,CallACS("RTCW_Decorate", 1, 1)*frandom(-1.0,1.0))
        goto PickupFinishUnscoped

      // Scoped
      PickupAttackETScoped:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackETScopedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackETScopedCoop")
      PickupAttackETScopedDM:
        TNT1 A 0 A_FireCustomMissile("RTCW_FG42TracerSoundET",CallACS("RTCW_Decorate", 1, 1)*frandom(-0.25,0.25),false,0,9,0,CallACS("RTCW_Decorate", 1, 1)*frandom(-0.25,0.25))
        goto PickupFinishScoped

      PickupAttackETScopedCoop:
        TNT1 A 0 A_FireCustomMissile("RTCW_FG42TracerCoopSoundET",CallACS("RTCW_Decorate", 1, 1)*frandom(-0.25,0.25),false,0,9,0,CallACS("RTCW_Decorate", 1, 1)*frandom(-0.25,0.25))
        goto PickupFinishScoped

      // Finishing stuff
      // Unscoped
      PickupFinishUnscoped:
        TNT1 A 0 A_GiveInventory("RTCW_FG42Spread")
        TNT1 A 0 ACS_NamedExecuteWithResult("RTCW_Recoil", 1)
        stop

      // Scoped
      PickupFinishScoped:
        TNT1 A 0 A_GiveInventory("RTCW_FG42Spread")
        TNT1 A 0 A_GiveInventory("RTCW_AimSpread",1048576)
        TNT1 A 0 A_SetPitch(pitch-0.5)
        TNT1 A 0 A_SetAngle(angle+frandom(-1.0,1.0))
        stop
    }
}

actor SamsaraRTCWFG42ReloadSoundPlayer1 : Trigger
{
    States
    {
      // Sound playing stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",2,"PickupET")
      // Original/Console sounds
      PickupRTCW:
        TNT1 A 0 A_PlaySound("RTCW/FG42/Reload",CHAN_6)
        stop

      // Enemy Territory sounds
      PickupET:
        TNT1 A 0 A_PlaySound("RTCWET/FG42/Reload",CHAN_6)
        stop
    }
}

actor SamsaraRTCWFG42ReloadSoundPlayer2 : Trigger
{
    States
    {
      // Sound playing stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",2,"PickupET")
      // Original/Console sounds
      PickupRTCW:
        TNT1 A 0 A_PlaySound("RTCW/FG42/Reload2",CHAN_6)
        stop

      // Enemy Territory sounds
      PickupET:
        TNT1 A 0 A_PlaySound("RTCWET/FG42/Reload2",CHAN_6)
        stop
    }
}

actor RTCW_FG42Magazine : Ammo
{
	Inventory.MaxAmount 20
	Tag "FG-42 Magazine"
	+IGNORESKILL
}

//Was 15, now 40
Actor RTCW_FG42Tracer : RTCW_Tracer
{
	+THRUGHOST
	States
	{
		Crash:
		Death:
			TNT1 A 0 A_Explode(CallACS("RTCW_BulletDamage", 40, 0),8,!XF_HURTSOURCE,0,8)
			Goto Super::Death
		XDeath:
			TNT1 A 0 A_Explode(CallACS("RTCW_BulletDamage", 40, 0),8,!XF_HURTSOURCE,0,8)
			Goto Super::XDeath
	}
}

Actor RTCW_FG42TracerCoop : RTCW_FG42Tracer { +THRUSPECIES Species "Player" }

Actor RTCW_FG42TracerSoundConsole : RTCW_TracerSoundConsole
{
	+THRUGHOST
	States
	{
		Crash:
		Death:
			TNT1 A 0 A_Explode(CallACS("RTCW_BulletDamage", 40, 1),8,!XF_HURTSOURCE,0,8)
			Goto Super::Death
		XDeath:
			TNT1 A 0 A_Explode(CallACS("RTCW_BulletDamage", 40, 1),8,!XF_HURTSOURCE,0,8)
			Goto Super::XDeath
	}
}

Actor RTCW_FG42TracerCoopSoundConsole : RTCW_FG42TracerSoundConsole { +THRUSPECIES Species "Player" }

Actor RTCW_FG42TracerSoundET : RTCW_TracerSoundET
{
	+THRUGHOST
	States
	{
		Crash:
		Death:
			TNT1 A 0 A_Explode(CallACS("RTCW_BulletDamage", 40, 2),8,!XF_HURTSOURCE,0,8)
			Goto Super::Death
		XDeath:
			TNT1 A 0 A_Explode(CallACS("RTCW_BulletDamage", 40, 2),8,!XF_HURTSOURCE,0,8)
			Goto Super::XDeath
	}
}

Actor RTCW_FG42TracerCoopSoundET : RTCW_FG42TracerSoundET { +THRUSPECIES Species "Player" }
