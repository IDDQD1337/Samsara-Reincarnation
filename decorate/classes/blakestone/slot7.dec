actor "Anti-Plasma Cannon" : SamsaraBStoneWeapon
{
    Inventory.PickupMessage "You got an Anti-Plasma Cannon!"
    Inventory.PickupSound "wolfen/knifeget"
    Weapon.SlotNumber 7
    Weapon.SelectionOrder 1000
    Weapon.AmmoType "Cell"
    Weapon.AmmoGive 50
    Weapon.AmmoUse 8
    Obituary "%k obliterated %o with the Anti-Plasma Cannon."
    Tag "Anti-Plasma Cannon"
    +BFG
    +EXPLOSIVE
    States
    {
      Spawn:
        BWP7 A -1
        stop

      // Planet Strike
      Ready:
        TNT1 A 0 A_JumpIfInventory("BlakeAOGToken",1,"ReadyAOG")
      ReadyPS:
        DCAN A 1 A_WeaponReady
        goto Ready

      Fire:
        TNT1 A 0 A_JumpIfInventory("BlakeAOGToken",1,"FireAOG")
      FirePS:
        DCAN B 3
        goto HoldPS

      Hold:
        TNT1 A 0 A_JumpIfInventory("BlakeAOGToken",1,"HoldAOG")
      HoldPS:
		TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"HoldPSSmooth")
        DCAN C 3 Bright A_GiveInventory("SamsaraBStoneAntiPlasmaCannonAttackHandler")
        DCAN DE 2
        DCAN A 5 A_ReFire
        goto Ready
		
      HoldPSSmooth:
        DCAN C 1 Bright A_GiveInventory("SamsaraBStoneAntiPlasmaCannonAttackHandler")
		9381 Z 2
        DCAN DE 2
        DCAN A 5 A_ReFire
        goto Ready

      // Aliens Of Gold
      ReadyAOG:
        DCAN F 1 A_WeaponReady(WRF_NOBOB)
        goto Ready

      FireAOG:
        DCAN G 3
        goto HoldAOG

      HoldAOG:
		TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"HoldAOGSmooth")
        DCAN H 3 Bright A_GiveInventory("SamsaraBStoneAntiPlasmaCannonAttackHandler")
        DCAN IJ 2
        DCAN F 5 A_ReFire
        goto Ready

      HoldAOGSmooth:
        DCAN H 1 Bright A_GiveInventory("SamsaraBStoneAntiPlasmaCannonAttackHandler")
		9381 M 2
        DCAN IJ 2
        DCAN F 5 A_ReFire
        goto Ready	
    }
}

actor SamsaraBStoneAntiPlasmaCannonAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Cell",8,1)
        stop
        TNT1 A 0 A_TakeInventory("Cell",8)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("DRifle/Fire",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("DCannonProj",random(-5,5),false,0,4)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("DCannonProjCoop",random(-5,5),false,0,4)
        stop
    }
}

actor DCannonProj
{
    Projectile
    Radius 12
    Height 8
    Speed 16
    Damage (10)
    DamageType "DCannonProj"
    //ProjectileKickBack 0.8 // Damn u Zandronum
    Scale 1.25
    DeathSound "BCannon/Boom"
    Decal "Scorch"
    Obituary "%k obliterated %o with the Anti-Plasma Cannon."
    +BLOODLESSIMPACT
    +EXTREMEDEATH
    +FORCERADIUSDMG
    +FORCEXYBILLBOARD
    +RIPPER
    //+WINDTHRUST
    States
    {
      Spawn:
        9C0P A -1 Bright Light("ANTIPLASMA") //A_Explode(100,12,0,false,12)
        stop

      Death:
        TNT1 A 0 A_Explode(40*random(1,3),96)
        TNT1 A 0 A_BFGSpray("DCannonPuff",10,6)
      DeathFinish:
        DCAP BCDEFGHI 3 Bright Light("ANTIPLASMABOOM")
        stop
    }
}

actor DCannonProjCoop : DCannonProj
{
    Damage (20)
	Species "Player"
    +THRUSPECIES
    States
    {
      Death:
        TNT1 A 0 A_Explode(40*random(1,3),96)
        TNT1 A 0 A_BFGSpray("DCannonPuffCoop",25,6)
        goto DeathFinish
    }
}

actor DCannonPuff 
{
    DamageType "DCannonProj"    
    +NOGRAVITY
    +NOINTERACTION
    +PUFFGETSOWNER
    States
    {
      Spawn:
        DCAP BCDEFGHI 3 Bright
        stop
    }
}

actor DCannonPuffCoop : DCannonPuff
{
    +MTHRUSPECIES
    +NODAMAGETHRUST
}
