// Parias (Reincarnation/Classic)

actor "Mace of Contrition" : SamsaraHexenWeapon
{
    Inventory.PickupMessage "Mace of Contrition"
    Weapon.SlotNumber 0
    Weapon.SlotPriority 0
    Weapon.SelectionOrder 3500
    Weapon.YAdjust -8
    Obituary "$SAMSARA_HEXEN_OB_FALLBACKMELEE"
    Tag "Mace of Contrition"
    +BLOODSPLATTER
    +MELEEWEAPON
    +NOALERT
    +WIMPY_WEAPON
    States
    {
      Ready:
        CMCE A 1 A_WeaponReady
        loop

      Deselect:
        CMCE A 1 A_Lower
        loop

      Select:
        CMCE A 1 A_Raise
        loop

      Fire:
        CMCE B 2 Offset(60,20)
        CMCE B 1 Offset(30,33)
        CMCE B 2 Offset(8,45)
        CMCE CDE 1 Offset(8,45)
        CMCE E 1 Offset(-11,58) A_GiveInventory("SamsaraHexenMaceOfContritionAttackHandler")
        CMCE F 1 Offset(8,45)
        CMCE F 2 Offset(-8,74)
        CMCE F 1 Offset(-20,96)
        CMCE F 8 Offset(-33,160)
        CMCE A 2 Offset(8,75) A_ReFire
        CMCE A 1 Offset(8,65)
        CMCE A 2 Offset(8,60)
        CMCE A 1 Offset(8,55)
        CMCE A 2 Offset(8,50)
        CMCE A 1 Offset(8,45)
        goto Ready
    }
}

actor SamsaraHexenMaceOfContritionAttackHandler : Trigger
{
    States
    {
      // Attack stuff
      Pickup:
        TNT1 A 0 A_JumpIfCloser(129,"PickupHitActor")
      // Hit map geometry
      PickupHitOther:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupHitOtherCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupHitOtherCoop")
      PickupHitOtherDM:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupHitOtherDMSpread")
        TNT1 A 0 A_CustomPunch(random(25,40),true,0,"HammerPuff")
        stop

      PickupHitOtherDMSpread:
        TNT1 A 0 A_CustomPunch(random(25,40)*3,true,0,"HammerPuff")
        stop

      PickupHitOtherCoop:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupHitOtherCoopSpread")
        TNT1 A 0 A_CustomPunch(random(25,40),true,0,"HammerPuffCoop")
        stop

      PickupHitOtherCoopSpread:
        TNT1 A 0 A_CustomPunch(random(25,40)*3,true,0,"HammerPuffCoop")
        stop

      // Hit an actor
      PickupHitActor:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupHitActorCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupHitActorCoop")
      PickupHitActorDM:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupHitActorDMSpread")
        TNT1 A 0 A_CustomPunch(random(25,40),true,0,"HammerPuff",128)
        stop

      PickupHitActorDMSpread:
        TNT1 A 0 A_CustomPunch(random(25,40)*3,true,0,"HammerPuff",128)
        stop

      PickupHitActorCoop:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupHitActorCoopSpread")
        TNT1 A 0 A_CustomPunch(random(25,40),true,0,"HammerPuffCoop",128)
        stop

      PickupHitActorCoopSpread:
        TNT1 A 0 A_CustomPunch(random(25,40)*3,true,0,"HammerPuffCoop",128)
        stop
    }
}

actor HammerPuffCoop : HammerPuff { +MTHRUSPECIES }

// Baratus

actor SamsaraHexenSpikedGauntletsHitCounter : Counter { Inventory.MaxAmount 2 }

actor "Spiked Gauntlets" : SamsaraHexenWeapon
{
    Inventory.PickupMessage "Spiked Gauntlets"
    Weapon.SlotNumber 0
    Weapon.SlotPriority 0
    Weapon.SelectionOrder 3400
    Obituary "$SAMSARA_HEXEN_OB_FALLBACKMELEE"
    Tag "Spiked Gauntlets"
    +BLOODSPLATTER
    +MELEEWEAPON
    +NOALERT
    +WIMPY_WEAPON
    States
    {
      Spawn:
        WGNT A -1
        stop

      Ready:
        FPCH A 1 A_WeaponReady
        loop

      Deselect:
        FPCH A 1 A_Lower
        loop

      Select:
        TNT1 A 0 A_TakeInventory("SamsaraHexenSpikedGauntletsHitCounter") // map change ignores this
      SelectLoop:
        FPCH A 1 A_Raise
        loop

      Fire:
        FPCH B 5 Offset(5,40)
        FPCH C 4 Offset(5,40)
        TNT1 A 0 A_JumpIfInventory("SamsaraHexenSpikedGauntletsHitCounter",0,"FireStrong")
      FireNormal:
        TNT1 A 0 A_GiveInventory("SamsaraHexenSpikedGauntletsAttackHandler")
      FireNormalFinish:
        FPCH DC 4 Offset(5,40)
        FPCH B 5 Offset(5,40) A_ReFire
        goto Ready

      FireStrong:
        TNT1 A 0 A_JumpIfCloser(129,1)
        goto FireNormal
        TNT1 A 0 A_GiveInventory("SamsaraHexenSpikedGauntletsAttackHandler")
      FireStrongFinish:
        FPCH DE 4 Offset(5,40)
        FPCH E 1 Offset(15,50)
        FPCH E 1 Offset(25,60)
        FPCH E 1 Offset(35,70)
        FPCH E 1 Offset(45,80)
        FPCH E 1 Offset(55,90)
        FPCH E 1 Offset(65,100)
        FPCH E 10 Offset(0,150)
        goto Ready
    }
}

actor SamsaraHexenSpikedGauntletsAttackHandler : Trigger
{
    States
    {
      // Attack stuff
      Pickup:
        TNT1 A 0 A_JumpIfCloser(129,"PickupHitActor")
      // Hit map geometry
      PickupHitOther:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupHitOtherCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupHitOtherCoop")
      PickupHitOtherDM:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupHitOtherDMSpread")
        TNT1 A 0 A_CustomPunch(random(40,55),true,0,"PunchPuff")
        goto PickupFinishHitOther

      PickupHitOtherDMSpread:
        TNT1 A 0 A_CustomPunch(random(40,55)*3,true,0,"PunchPuff")
        goto PickupFinishHitOther

      PickupHitOtherCoop:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupHitOtherCoopSpread")
        TNT1 A 0 A_CustomPunch(random(40,55),true,0,"PunchPuffCoop")
        goto PickupFinishHitOther

      PickupHitOtherCoopSpread:
        TNT1 A 0 A_CustomPunch(random(40,55)*3,true,0,"PunchPuffCoop")
        goto PickupFinishHitOther

      // Hit an actor
      PickupHitActor:
        TNT1 A 0 A_JumpIfInventory("SamsaraHexenSpikedGauntletsHitCounter",0,"PickupHitActorStrong")
      // Normal
      PickupHitActorNormal:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupHitActorNormalCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupHitActorNormalCoop")
      PickupHitActorNormalDM:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupHitActorNormalDMSpread")
        TNT1 A 0 A_CustomPunch(random(40,55),true,0,"PunchPuff",128)
        goto PickupFinishHitActor

      PickupHitActorNormalDMSpread:
        TNT1 A 0 A_CustomPunch(random(40,55)*3,true,0,"PunchPuff",128)
        goto PickupFinishHitActor

      PickupHitActorNormalCoop:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupHitActorNormalCoopSpread")
        TNT1 A 0 A_CustomPunch(random(40,55),true,0,"PunchPuffCoop",128)
        goto PickupFinishHitActor

      PickupHitActorNormalCoopSpread:
        TNT1 A 0 A_CustomPunch(random(40,55)*3,true,0,"PunchPuffCoop",128)
        goto PickupFinishHitActor

      // Strong
      PickupHitActorStrong:
        TNT1 A 0 A_PlaySound("*fistgrunt",CHAN_VOICE)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupHitActorStrongCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupHitActorStrongCoop")
      PickupHitActorStrongDM:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupHitActorStrongDMSpread")
        TNT1 A 0 A_CustomPunch(random(40,55)*2,true,0,"HammerPuff",128)
        goto PickupFinishHitActor

      PickupHitActorStrongDMSpread:
        TNT1 A 0 A_CustomPunch((random(40,55)*2)*3,true,0,"HammerPuff",128)
        goto PickupFinishHitActor

      PickupHitActorStrongCoop:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupHitActorStrongCoopSpread")
        TNT1 A 0 A_CustomPunch(random(40,55)*2,true,0,"HammerPuffCoop",128)
        goto PickupFinishHitActor

      PickupHitActorStrongCoopSpread:
        TNT1 A 0 A_CustomPunch((random(40,55)*2)*3,true,0,"HammerPuffCoop",128)
        goto PickupFinishHitActor

      // Finishing stuff
      // Hit map geometry
      PickupFinishHitOther:
        TNT1 A 0 A_TakeInventory("SamsaraHexenSpikedGauntletsHitCounter")
        stop

      // Hit an actor
      PickupFinishHitActor:
        TNT1 A 0 A_JumpIfInventory("SamsaraHexenSpikedGauntletsHitCounter",0,"PickupFinishHitActorStrong")
      // Normal
      PickupFinishHitActorNormal:
        TNT1 A 0 A_GiveInventory("SamsaraHexenSpikedGauntletsHitCounter")
        stop

      // Strong
      PickupFinishHitActorStrong:
        TNT1 A 0 A_TakeInventory("SamsaraHexenSpikedGauntletsHitCounter")
        stop
    }
}

actor PunchPuffCoop : PunchPuff { +MTHRUSPECIES }
