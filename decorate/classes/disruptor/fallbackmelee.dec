actor "Disruptor Fist" : SamsaraDisruptorWeapon
{
    Weapon.SlotNumber 0
    Weapon.SelectionOrder 3500
    Weapon.Kickback 100
    Obituary "%k's gun was too hard for %o's head."
    Tag "Pistol Whip"
    +MELEEWEAPON
    +NOALERT
    +WIMPY_WEAPON
    States
    {
      Ready:
        SQ95 A 1 A_WeaponReady
        loop

      Deselect:
        TNT1 A 0 A_Lower
        SQ95 A 1 A_Lower
        loop

      Select:
        TNT1 A 0 A_Raise
        SQ95 A 1 A_Raise
        loop

      Fire:
        TNT1 A 0 A_GiveInventory("SamsaraDisruptorPistolWhipAttackHandler")
        SQ95 EDCB 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
        SQ95 A 12 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
        goto Ready
    }
}

actor SamsaraDisruptorPistolWhipAttackHandler : Trigger
{
    States
    {
      // Attack stuff
      Pickup:
        TNT1 A 0 A_PlaySound("Disruptor/Gun",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupCoop")
      PickupDM:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupDMSpread")
        TNT1 A 0 A_CustomPunch(random(3,9)*3,true,0,"DisruptorPuff2")
        stop

      PickupDMSpread:
        TNT1 A 0 A_CustomPunch((random(3,9)*3)*3,true,0,"DisruptorPuff2")
        stop

      PickupCoop:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupCoopSpread")
        TNT1 A 0 A_CustomPunch(random(3,9)*3,true,0,"DisruptorPuff2Coop")
        stop

      PickupCoopSpread:
        TNT1 A 0 A_CustomPunch((random(3,9)*3)*3,true,0,"DisruptorPuff2Coop")
        stop
    }
}

actor DisruptorPuff2 : DisruptorPuff
{
    AttackSound "Disruptor/Crash"
    Decal ""
    States
    {
      Melee:
        TNT1 A 1 A_PlaySound("Disruptor/Crash")
        stop

      Crash:
        SQ96 TUVWXYZ 4 Bright
        SQ97 ABCDEF 4 Bright
        stop
    }
}

actor DisruptorPuff2Coop : DisruptorPuff2 { +MTHRUSPECIES }
