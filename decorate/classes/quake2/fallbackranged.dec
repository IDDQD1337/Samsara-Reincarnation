actor Q2Blaster : Q2Weapon
{
    Inventory.PickupMessage "Blaster"
    Inventory.Icon "Q2CMPW01"
    Weapon.SlotNumber 1
    Weapon.SelectionOrder 900
    Tag "Blaster"
    +WIMPY_WEAPON
    States
    {
      Spawn:
        Q2WP A -1
        stop

      ReadyActual:
        BLS2 A 18 A_WeaponReady
        BLS2 A 0 A_Jump(64,1)
        loop
        BLS2 ABCDEFGHIJK 6 A_WeaponReady
      ReadyActual2:
        BLS2 L 18 A_WeaponReady
        BLS2 L 0 A_Jump(64,1)
        loop
        BLS2 LMNOPQRSTUV 6 A_WeaponReady
        goto ReadyActual

      Deselect:
        BLS2 A 3 A_JumpIfHealthLower(1,"DeselectInstant")
        BLS1 JKL 4
        goto DeselectInstant

      Ready: // our "Select" state
        TNT1 A 0 A_TakeInventory("Q2WeaponIndex") // have to use TNT1 A to get the weapon usable
        BLS1 AB 5
        BLS1 CDE 4
        goto ReadyActual

      Fire:
        BLS1 F 0 A_GiveInventory("SamsaraQuake2BlasterAttackHandler")
        BLS1 F 0 A_GunFlash // have to place it here for recoil
        BLS1 FGHI 4 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        BLS2 A 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto ReadyActual

      Flash:
        TNT1 A 0 A_Light2
        TNT1 A 0 A_JumpIfInventory("SamsaraRecoilIsDisabled",1,"FlashNoRecoil")
        goto FlashRecoil

      FlashNoRecoil:
        TNT1 AAAAA 1
        TNT1 A 1 A_Light1
        TNT1 AAAA 1
        goto LightDone

      FlashRecoil:
        TNT1 A 0 A_SetPitch(pitch-3)
        TNT1 AAAAA 1 A_SetPitch(pitch+0.2)
        TNT1 A 0 A_Light1
        TNT1 AAAAA 1 A_SetPitch(pitch+0.2)
        TNT1 A 0 A_Light0
        TNT1 AAAAA 1 A_SetPitch(pitch+0.2)
        stop
    }
}

actor SamsaraQuake2BlasterAttackHandler : Trigger
{
    States
    {
      // Sound stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("PowerSilencer",1,3)
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_PlaySound("Q2Weapons/BlasterFire",CHAN_WEAPON)
        goto PickupAttack
        TNT1 A 0 A_PlaySound("Q2Weapons/BlasterFire",CHAN_WEAPON,0.25,false,ATTN_STATIC)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_FireCustomMissile("Q2WeaponFlash",0,false)
        TNT1 A 0 A_FireBullets(0,0,1,0,"SamsaraBlankPuff",FBF_NORANDOM|FBF_NOFLASH)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("BlasterProjectile_DM",0,false,8,4)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("BlasterProjectile_Coop",0,false,8,4)
        stop
    }
}

actor BlasterProjectile : FastProjectile
{
    Radius 3
    Height 6
    Speed 32
    Damage (20)
    DamageType "BittermanDamage"
    MissileType "BlasterTrailSpawner"
    MissileHeight 8
    DeathSound "Q2Weapons/LaserHit"
    Decal "BlasterScorch"
    +BLOODSPLATTER
    States
    {
      Spawn:
        NULL A 1 Bright NoDelay Light("BlasterProjectile") A_PlaySound("Q2Weapons/LaserFly",CHAN_VOICE,0.5,true)
        loop

      Death:
        TNT1 A 3 Light("BlasterProjectile_X1") A_SpawnItemEx("BlasterProjectileImpact")
        TNT1 A 3 Light("BlasterProjectile_X2")
        TNT1 A 3 Light("BlasterProjectile_X3")
        TNT1 A 3 Light("BlasterProjectile_X4")
        stop

      XDeath:
        TNT1 A 1
        stop
    }
}

actor BlasterProjectile_Coop : BlasterProjectile
{
    Species "Player"
    +THRUSPECIES
}

actor BlasterProjectile_DM : BlasterProjectile { Damage (30) }

actor BlasterProjectileImpact : Q2EffectBase
{
    Height 6
    RenderStyle "Normal"
    //Translation "80:111=208:223"
    States
    {
      Spawn:
        TNT1 AAAAAAAAAAAAAAAAAA 0 NoDelay A_SpawnItemEx("BlasterImpactParticle",0,0,0,frandom(0,1),0,frandom(-0.25,3),random(0,359))
        TNT1 A 0 A_CheckFloor("SpawnFloor")
        TNT1 A 0 A_CheckCeiling("SpawnCeiling")
        goto SpawnAir

      SpawnFloor:
        NULL ABC 3 Bright
        NULL D 1 Bright A_FadeOut(0.25)
        wait

      SpawnCeiling:
        TNT1 A 0 A_SetScale(-1)
        goto SpawnFloor

      SpawnAir:
        NULL EFG 3 Bright
        NULL H 1 Bright A_FadeOut(0.25)
        wait
    }
}

actor BlasterTrailSpawner : Q2EffectBase
{
    Scale 0.66
    States
    {
      Spawn:
        TNT1 AA 0 NoDelay A_SpawnItemEx("BlasterTrail",frandom(0,2),0,frandom(-2,2),frandom(0,0.15),0,frandom(-0.15,0.15),random(0,359),SXF_CLIENTSIDE|SXF_TRANSFERSCALE)
        stop
    }
}

actor BlasterTrail : Q2BulletPuffParticle
{
    Scale 0.75
    //Translation "80:111=208:223"
    States
    {
      Spawn:
        P005 ABCDEFGH 0 Bright NoDelay A_Jump(64,"Fade")
        loop
    }
}

actor BlasterImpactParticle : BlasterTrail
{
    Projectile
    Gravity 0.125
    -NOGRAVITY
    -NOINTERACTION
    States
    {
      Fade:
        "----" A 10 Bright
        goto FadeContinue
    }
}
