actor Q2Shotgun : Q2Weapon
{
    Inventory.PickupMessage "Shotgun"
    Inventory.Icon "Q2CMPW03"
    Weapon.SlotNumber 2
    Weapon.AmmoType "Shell"
    Weapon.AmmoGive 8
    Weapon.AmmoUse 1
    Tag "Shotgun"
    States
    {
      Spawn:
        Q2WP D -1
        stop

      ReadyActual:
        W001 T 15 A_WeaponReady
        W001 T 0 A_Jump(32,1)
        loop
        W001 TUVW 15 A_WeaponReady
      ReadyActual2:
        W001 X 15 A_WeaponReady
        W001 X 0 A_Jump(32,1)
        loop
        W001 "XYZ[" 15 A_WeaponReady
      ReadyActual3:
        W001 "\" 15 A_WeaponReady
        W001 "\" 0 A_Jump(32,1)
        loop
        W001 "\]T" 15 A_WeaponReady
        goto ReadyActual

      Deselect:
        W001 T 2 A_JumpIfHealthLower(1,"DeselectInstant")
        W002 ABC 4
        goto DeselectInstant

      Ready: // our "Select" state
        TNT1 A 0 A_TakeInventory("Q2WeaponIndex")
        W001 A 4 A_GiveInventory("Q2WeaponIndex",2)
        W001 B 3
        W001 C 4
        W001 D 3
        W001 E 4
        W001 F 3
        W001 GH 4
        goto ReadyActual

      Fire:
        W001 I 0 A_TakeInventory("SamsaraQuake2WeaponEmptySwitchPending")
        W001 I 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart")
        W001 I 0 A_JumpIfInventory("Shell",1,"FireStart")
        goto FireDry

      FireDry:
        W001 T 0 A_ClearReFire
        W001 T 0 A_JumpIfInventory("SamsaraQuake2WeaponEmptySwitchPending",1,2)
        W001 T 0 A_PlaySound("Q2Weapons/NoAmmo",CHAN_5)
        W001 T 0 A_SelectWeapon("Q2Blaster")
        goto Deselect

      FireStart:
        W001 I 0 A_GiveInventory("SamsaraQuake2ShotgunAttackHandler")
        W001 I 0 A_GunFlash // have to place it here for recoil
        W001 IJ 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        W001 K 0 A_GiveInventory("SamsaraQuake2ShotgunLoadSoundPlayer")
        W001 KLMNOPQRS 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        W001 T 0 A_ReFire(1)
        goto FireFinish
        W001 T 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart")
        W001 T 0 A_JumpIfInventory("Shell",1,"FireStart")
        W001 T 0 A_JumpIfInventory("SamsaraQuake2WeaponEmptySwitchPending",1,"FireFinish")
        W001 T 0 A_PlaySound("Q2Weapons/NoAmmo",CHAN_5)
        W001 T 0 A_GiveInventory("SamsaraQuake2WeaponEmptySwitchPending")
        goto FireFinish

      FireFinish:
        W001 T 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        W001 T 0 A_JumpIfInventory("SamsaraQuake2WeaponEmptySwitchPending",1,"FireDry")
        goto ReadyActual

      Flash:
        TNT1 A 1 A_Light2
        TNT1 A 0 A_JumpIfInventory("SamsaraRecoilIsDisabled",1,"FlashNoRecoil")
        goto FlashRecoil

      FlashNoRecoil:
        TNT1 AAAAA 1
        TNT1 A 1 A_Light1
        TNT1 AAAA 1
        goto LightDone

      FlashRecoil:
        TNT1 A 0 A_SetPitch(pitch-3)
        TNT1 AAAAA 1 A_SetPitch(pitch+0.3)
        TNT1 A 0 A_Light1
        TNT1 AAAAA 1 A_SetPitch(pitch+0.3)
        goto LightDone
    }
}

actor SamsaraQuake2ShotgunAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupPlaySound")
        TNT1 A 0 A_JumpIfInventory("Shell",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Shell",1)
        goto PickupPlaySound

      // Sound stuff
      PickupPlaySound:
        TNT1 A 0 A_JumpIfInventory("PowerSilencer",1,4)
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_PlaySound("Q2Weapons/ShotgunFire",CHAN_WEAPON)
        TNT1 A 0 A_TakeInventory("SamsaraQuake2SilentFired")
        goto PickupAttack
        TNT1 A 0 A_PlaySound("Q2Weapons/ShotgunFire",CHAN_WEAPON,0.25,false,ATTN_STATIC)
        TNT1 A 0 A_GiveInventory("SamsaraQuake2SilentFired")
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_FireCustomMissile("Q2WeaponFlash",0,false)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireBullets(6,3,12,6,"SamsaraQuake2ShotgunPuff",FBF_NORANDOM|FBF_NOFLASH)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireBullets(6,3,12,6,"SamsaraQuake2ShotgunPuffCoop",FBF_NORANDOM|FBF_NOFLASH)
        stop
    }
}

actor SamsaraQuake2ShotgunLoadSoundPlayer : Trigger
{
    States
    {
      // Sound playing stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraQuake2SilentFired",1,2)
        TNT1 A 0 A_PlaySound("Q2Weapons/ShotgunPump")
        stop
        TNT1 A 0 A_PlaySound("Q2Weapons/ShotgunPump",CHAN_BODY,0.25,false,ATTN_STATIC)
        stop
    }
}

actor SamsaraQuake2ShotgunPuff : Q2BulletPuff
{
    States
    {
      Crash:
        TNT1 AAAAAAAAAAAAA 0 A_SpawnItemEx("Q2BulletPuffParticle",frandom(0,12),0,frandom(-8,15),frandom(0,0.5),0,frandom(-1,-0.1),random(0,359))
        TNT1 A 0 A_SpawnItemEx("Q2BulletPuffFlash")
        goto SpawnFinish
    }
}

actor SamsaraQuake2ShotgunPuffCoop : SamsaraQuake2ShotgunPuff { +MTHRUSPECIES }
