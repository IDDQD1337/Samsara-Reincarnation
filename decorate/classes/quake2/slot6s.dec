actor SamsaraQuake2PlasmaBeamLoopSoundCheck : Boolean {}
actor SamsaraQuake2PlasmaBeamFiring : Boolean {}
actor SamsaraQuake2PlasmaBeamRingOffset : Counter { Inventory.MaxAmount 64 }
actor SamsaraQuake2PlasmaBeamFiredCount: Counter { Inventory.MaxAmount 2 }

actor Q2PlasmaBeam : Q2Weapon
{
    Inventory.PickupMessage "Plasma Beam"
    Inventory.Icon "Q2CMPW07" // placeholder
    Weapon.SlotNumber 6
    Weapon.SlotPriority 1
    Weapon.SelectionOrder 500
    Weapon.AmmoType "Cell"
    Weapon.AmmoGive 20
    Weapon.AmmoUse 1
    Tag "Plasma Beam"
    States
    {
      Spawn:
        Q2WP A -1
        stop

      ReadyActual:
        PLB2 A 0 A_TakeInventory("SamsaraQuake2PlasmaBeamLoopSoundCheck")
        PLB2 A 0 A_TakeInventory("SamsaraQuake2PlasmaBeamFiring")
        PLB2 A 1 A_WeaponReady
        PLB2 A 0 A_Jump(32,1)
        loop
        PLB2 "ABCDEFGHIJKLMNOPQRSTUVWXYZ[\]" 4 A_WeaponReady
        PLB3 A 4 A_WeaponReady
        loop

      Deselect:
        PLB2 A 0 A_GiveInventory("SamsaraQuake2PlasmaBeamLoopSoundStopper")
        PLB2 A 0 A_TakeInventory("SamsaraQuake2PlasmaBeamFiring")
        PLB2 A 3 A_JumpIfHealthLower(1,"DeselectInstant")
        PLB4 ABCDE 3
        goto DeselectInstant

      Ready: // our "Select" state
        TNT1 A 0 A_TakeInventory("Q2WeaponIndex")
        TNT1 A 0 A_GiveInventory("Q2WeaponIndex",14)
        PLB0 A 0 A_TakeInventory("SamsaraQuake2PlasmaBeamLoopSoundCheck")
        PLB0 A 1 A_TakeInventory("SamsaraQuake2PlasmaBeamFiring")
        PLB0 A 0 A_TakeInventory("SamsaraQuake2PlasmaBeamLoopSoundCheck") // map change hack
        PLB0 A 2 A_TakeInventory("SamsaraQuake2PlasmaBeamFiring")
        PLB0 BCDEFGHI 3
        goto ReadyActual

      Fire:
        PLB1 A 0 A_TakeInventory("SamsaraQuake2WeaponEmptySwitchPending")
        PLB1 A 0 A_TakeInventory("SamsaraQuake2PlasmaBeamRingOffset")
        PLB1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireInit")
        PLB1 A 0 A_JumpIfInventory("Cell",1,"FireInit")
        goto FireDry

      FireDry:
        PLB2 A 0 A_ClearReFire
        PLB2 A 0 A_JumpIfInventory("SamsaraQuake2WeaponEmptySwitchPending",1,2)
        PLB2 A 0 A_PlaySound("Q2Weapons/NoAmmo",CHAN_5)
        PLB2 A 0 A_SelectWeapon("Q2Blaster")
        goto Deselect

      FireInit:
        PLB1 A 0 ACS_NamedExecuteWithResult("SamsaraQuake2PlasmaBeamEffect")
        PLB1 A 0 A_JumpIfInventory("PowerSilencer",1,2)
        PLB1 A 0 A_TakeInventory("SamsaraQuake2SilentLoopSound")
        goto FireStart1
        PLB1 A 0 A_GiveInventory("SamsaraQuake2SilentLoopSound")
        goto FireStart1

      FireStart1:
        PLB1 A 0 A_GunFlash
        PLB1 A 0 A_GiveInventory("SamsaraQuake2PlasmaBeamLoopSoundPlayer") PLB1 A 0 A_GiveInventory("SamsaraQuake2PlasmaBeamAttackHandler")
        PLB1 A 4 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        PLB1 B 0 A_ReFire(1)
        goto FireFinish
        PLB1 B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart2")
        PLB1 B 0 A_JumpIfInventory("Cell",1,"FireStart2")
        PLB1 B 0 A_JumpIfInventory("SamsaraQuake2WeaponEmptySwitchPending",1,"FireFinish")
        PLB1 B 0 A_PlaySound("Q2Weapons/NoAmmo",CHAN_5)
        PLB1 B 0 A_GiveInventory("SamsaraQuake2WeaponEmptySwitchPending")
        goto FireFinish

      FireStart2:
        PLB1 B 0 A_GunFlash("Flash2")
        PLB1 B 0 A_GiveInventory("SamsaraQuake2PlasmaBeamLoopSoundPlayer") PLB1 B 0 A_GiveInventory("SamsaraQuake2PlasmaBeamAttackHandler")
        PLB1 B 3 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        PLB1 C 0 A_ReFire(1)
        goto FireFinish
        PLB1 C 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart3")
        PLB1 C 0 A_JumpIfInventory("Cell",1,"FireStart3")
        PLB1 C 0 A_JumpIfInventory("SamsaraQuake2WeaponEmptySwitchPending",1,"FireFinish")
        PLB1 C 0 A_PlaySound("Q2Weapons/NoAmmo",CHAN_5)
        PLB1 C 0 A_GiveInventory("SamsaraQuake2WeaponEmptySwitchPending")
        goto FireFinish

      FireStart3:
        PLB1 C 0 A_GunFlash
        PLB1 C 0 A_GiveInventory("SamsaraQuake2PlasmaBeamLoopSoundPlayer") PLB1 C 0 A_GiveInventory("SamsaraQuake2PlasmaBeamAttackHandler")
        PLB1 C 4 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        PLB1 D 0 A_ReFire(1)
        goto FireFinish
        PLB1 D 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart4")
        PLB1 D 0 A_JumpIfInventory("Cell",1,"FireStart4")
        PLB1 D 0 A_JumpIfInventory("SamsaraQuake2WeaponEmptySwitchPending",1,"FireFinish")
        PLB1 D 0 A_PlaySound("Q2Weapons/NoAmmo",CHAN_5)
        PLB1 D 0 A_GiveInventory("SamsaraQuake2WeaponEmptySwitchPending")
        goto FireFinish

      FireStart4:
        PLB1 D 0 A_GunFlash("Flash2")
        PLB1 D 0 A_GiveInventory("SamsaraQuake2PlasmaBeamLoopSoundPlayer") PLB1 D 0 A_GiveInventory("SamsaraQuake2PlasmaBeamAttackHandler")
        PLB1 D 3 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        PLB1 A 0 A_ReFire(1)
        goto FireFinish
        PLB1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart1")
        PLB1 A 0 A_JumpIfInventory("Cell",1,"FireStart1")
        PLB1 A 0 A_JumpIfInventory("SamsaraQuake2WeaponEmptySwitchPending",1,"FireFinish")
        PLB1 A 0 A_PlaySound("Q2Weapons/NoAmmo",CHAN_5)
        PLB1 A 0 A_GiveInventory("SamsaraQuake2WeaponEmptySwitchPending")
        goto FireFinish

      FireFinish:
        PLB2 A 0 A_GiveInventory("SamsaraQuake2PlasmaBeamLoopSoundStopper")
        PLB2 A 0 A_TakeInventory("SamsaraQuake2PlasmaBeamFiring")
        PLB2 A 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        PLB2 A 0 A_JumpIfInventory("SamsaraQuake2WeaponEmptySwitchPending",1,"FireDry")
        goto ReadyActual

      Flash:
        TNT1 A 4 A_Light1
        goto LightDone

      Flash2:
        TNT1 A 3 A_Light1
        goto LightDone
    }
}

actor SamsaraQuake2PlasmaBeamAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupSilentCheck")
        TNT1 A 0 A_JumpIfInventory("Cell",1,1)
        stop
        TNT1 A 0 A_JumpIfInventory("SamsaraQuake2PlasmaBeamFiredCount",0,2)
        TNT1 A 0 A_GiveInventory("SamsaraQuake2PlasmaBeamFiredCount")
        goto PickupSilentCheck
        TNT1 A 0 A_TakeInventory("Cell",1)
        TNT1 A 0 A_TakeInventory("SamsaraQuake2PlasmaBeamFiredCount")
        goto PickupSilentCheck

      // Silencer checking stuff
      PickupSilentCheck:
        TNT1 A 0 A_JumpIfInventory("PowerSilencer",1,"PickupAttack")
        TNT1 A 0 A_AlertMonsters
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireBullets(0,0,-1,15,"Q2PlasmaBeamPuff",FBF_NORANDOM|FBF_NOFLASH|FBF_NORANDOMPUFFZ)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireBullets(0,0,-1,15,"Q2PlasmaBeamPuffCoop",FBF_NORANDOM|FBF_NOFLASH|FBF_NORANDOMPUFFZ)
        stop
    }
}

actor SamsaraQuake2PlasmaBeamEffectHandler : Trigger
{
    States
    {
      // Ring offset adjusting stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraQuake2PlasmaBeamRingOffset",0,2)
        TNT1 A 0 A_GiveInventory("SamsaraQuake2PlasmaBeamRingOffset")
        goto PickupBeam
        TNT1 A 0 A_TakeInventory("SamsaraQuake2PlasmaBeamRingOffset")
        goto PickupBeam

      // Beam stuff
      PickupBeam:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupBeamCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupBeamCoop")
      PickupBeamDM:
        TNT1 A 0 A_FireBullets(0,0,-1,0,"Q2PlasmaBeamEffectPuff",FBF_NORANDOM|FBF_NOFLASH|FBF_NORANDOMPUFFZ)
        stop

      PickupBeamCoop:
        TNT1 A 0 A_FireBullets(0,0,-1,0,"Q2PlasmaBeamEffectPuffCoop",FBF_NORANDOM|FBF_NOFLASH|FBF_NORANDOMPUFFZ)
        stop
    }
}

actor SamsaraQuake2PlasmaBeamLoopSoundPlayer : Trigger
{
    States
    {
      // Sound starting stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("PowerSilencer",1,"PickupPlayLoopSoundInitSilent")
        goto PickupPlayLoopSoundInitNormal

      PickupPlayLoopSoundInitNormal:
        TNT1 A 0 A_JumpIfInventory("SamsaraQuake2SilentLoopSound",1,1)
        goto PickupPlayLoopSound
        TNT1 A 0 A_PlaySound("silence",CHAN_WEAPON)
        TNT1 A 0 A_StopSound(CHAN_WEAPON)
        TNT1 A 0 A_TakeInventory("SamsaraQuake2SilentLoopSound")
        goto PickupPlayLoopSound

      PickupPlayLoopSoundInitSilent:
        TNT1 A 0 A_JumpIfInventory("SamsaraQuake2SilentLoopSound",1,"PickupPlayLoopSound")
        TNT1 A 0 A_PlaySound("silence",CHAN_WEAPON)
        TNT1 A 0 A_StopSound(CHAN_WEAPON)
        TNT1 A 0 A_GiveInventory("SamsaraQuake2SilentLoopSound")
        goto PickupPlayLoopSound

      PickupPlayLoopSound:
        TNT1 A 0 A_JumpIfInventory("PowerSilencer",1,2)
        TNT1 A 0 A_PlaySound("Q2Weapons/BFG10KFly",CHAN_WEAPON,1.0,true)
        goto PickupPlayLoopSoundFinish
        TNT1 A 0 A_PlaySound("Q2Weapons/BFG10KFly",CHAN_WEAPON,0.25,true,ATTN_STATIC)
        goto PickupPlayLoopSoundFinish

      PickupPlayLoopSoundFinish:
        TNT1 A 0 A_GiveInventory("SamsaraQuake2PlasmaBeamLoopSoundCheck")
        stop
    }
}

actor SamsaraQuake2PlasmaBeamLoopSoundStopper : Trigger
{
    States
    {
      // Sound stopping stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraQuake2PlasmaBeamLoopSoundCheck",1,1)
        stop
        TNT1 A 0 A_PlaySound("silence",CHAN_WEAPON)
        TNT1 A 0 A_StopSound(CHAN_WEAPON)
        TNT1 A 0 A_TakeInventory("SamsaraQuake2PlasmaBeamLoopSoundCheck")
        stop
    }
}

actor Q2PlasmaBeamPuff : BulletPuff
{
    Radius 4
    Height 2
    VSpeed 0
    DamageType "BittermanFire"
    -ALLOWPARTICLES
    +ALWAYSPUFF
    +PUFFONACTORS
    States
    {
      Spawn:
        TNT1 A 0 NoDelay Light("BlasterProjectile_X1") A_PlaySound("Q2Weapons/LaserHit")
        TNT1 AAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("BlasterImpactParticle",0,0,0,frandom(0,1),0,frandom(-0.25,3),random(0,359))
        TNT1 A 3 Light("BlasterProjectile_X1")
        TNT1 A 3 Light("BlasterProjectile_X2")
        TNT1 A 3 Light("BlasterProjectile_X3")
        TNT1 A 3 Light("BlasterProjectile_X4")
        stop

      XDeath:
        TNT1 A 1 A_PlaySound("Q2Weapons/LaserHit")
        stop
    }
}

actor Q2PlasmaBeamPuffCoop : Q2PlasmaBeamPuff { +MTHRUSPECIES }

actor Q2PlasmaBeamEffectPuff : BulletPuff
{
    Radius 4
    Height 2
    VSpeed 0
    -ALLOWPARTICLES
    +ALWAYSPUFF
    +BLOODLESSIMPACT
    +CLIENTSIDEONLY
    +DONTSPLASH
    +NOINTERACTION
    +NOTIMEFREEZE
    +NOTRIGGER
    +PUFFGETSOWNER
    +PUFFONACTORS
    +SKYEXPLODE
    States
    {
      Spawn:
        TNT1 A 1 NoDelay ACS_NamedExecuteWithResult("Q2DrawLaser")
        stop
    }
}

actor Q2PlasmaBeamEffectPuffCoop : Q2PlasmaBeamEffectPuff { +MTHRUSPECIES }

actor Q2PlasmaBeamProjectileSegment : Q2EffectBase
{
    RenderStyle "Translucent"
    Alpha 0.33
    +CLIENTSIDEONLY
    +NOTIMEFREEZE
    States
    {
      Spawn:
        NULL A 2 Bright
        stop
    }
}

actor Q2PlasmaBeamProjectileRing
{
    Projectile
    Gravity 0.125
    RenderStyle "Translucent"
    Alpha 0.33
    +BLOODLESSIMPACT
    +CLIENTSIDEONLY
    +DONTBLAST
    +DONTREFLECT
    +DONTSPLASH
    +NOTIMEFREEZE
    +NOTRIGGER
    States
    {
      Spawn:
        NULL A 2 Bright
        stop
    }
}
