actor Painkiller_Stakegun : PainkillerWeapon
{
	Inventory.PickupMessage "Stakegun"
	Inventory.PickupSound "Painkiller/Stakegun/Pickup"
    Inventory.Icon "PKCMPW03"
	Weapon.SlotNumber 1
	Weapon.SelectionOrder 800
	Weapon.BobRangeX 0
	Weapon.BobRangeY 1.2
	Weapon.AmmoType1 "Shell"
	Weapon.AmmoGive1 20
	Weapon.AmmoUse1 1
	Weapon.AmmoType2 "RocketAmmo"
	Weapon.AmmoGive2 5
	Weapon.AmmoUse2 1
	Obituary "%o was impaled by %k's stakegun."
	Tag "Stakegun/Grenade Launcher"
	States
	{
		Spawn:
			PK99 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("Painkiller_Stake_Icon",0,0,40)
			PK99 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("Painkiller_Stake_Icon2",0,0,40)
			loop

		Ready:
			PK00 ABCDEFGHI 10 A_WeaponReady
			loop

		Select:
			TNT1 A 0 A_GiveInventory("Painkiller_HoldingStake")
			TNT1 A 0 A_TakeInventory("Painkiller_HoldingShotgun")
			TNT1 A 0 A_TakeInventory("Painkiller_HoldingBolt")
			TNT1 A 0 A_TakeInventory("Painkiller_HoldingRifle")
			TNT1 A 0 A_TakeInventory("Painkiller_HoldingChaingun")
			TNT1 A 0 A_TakeInventory("Painkiller_HoldingElectro")
			TNT1 A 0 A_TakeInventory("Painkiller_HoldingDemon")
			goto Super::Select

		Fire:
			PK01 A 2 A_GiveInventory("SamsaraPainkillerStakegunPrimaryAttackHandler")
			PK01 BC 2
			PK01 DEFGHJKMN 4
			PK01 PQR 3
			Pk01 STU 3 A_WeaponReady
			goto Ready

		AltFire:
			PK02 A 2 A_GiveInventory("SamsaraPainkillerStakegunSecondaryAttackHandler")
			PK02 BCDEFGHJL 2
			PK02 NP 2 A_WeaponReady
			goto Ready
	}
}

actor SamsaraPainkillerStakegunPrimaryAttackHandler : Trigger
{
	States
	{
	// Ammo checking/taking stuff
	Pickup:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
		TNT1 A 0 A_JumpIfInventory("Shell",1,1)
		stop
		TNT1 A 0 A_TakeInventory("Shell",1)
		goto PickupAttack

	// Attack stuff
	PickupAttack:
		TNT1 A 0 A_PlaySound("Painkiller/Stakegun/Shoot",CHAN_WEAPON)
		TNT1 A 0 A_JumpIf(CallACS("SamsaraDecorate", 48) == 1, "PickupAttackPainkiller")
	// PainkillerStake damage type
	PickupAttackPainkillerStake:
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackPainkillerStakeCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackPainkillerStakeCoop")
	PickupAttackPainkillerStakeDM:
		TNT1 A 0 A_FireCustomMissile("Painkiller_StakeProjectile",0,false)
		stop
	PickupAttackPainkillerStakeCoop:
		TNT1 A 0 A_FireCustomMissile("Painkiller_StakeProjectileCoop",0,false)
		stop

	// Painkiller (regular) damage type
	PickupAttackPainkiller:
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackPainkillerCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackPainkillerCoop")
	PickupAttackPainkillerDM:
		TNT1 A 0 A_FireCustomMissile("Painkiller_StakeProjectile_RegularDamageType",0,false)
		stop
	PickupAttackPainkillerCoop:
		TNT1 A 0 A_FireCustomMissile("Painkiller_StakeProjectileCoop_RegularDamageType",0,false)
		stop
	}
}

actor SamsaraPainkillerStakegunSecondaryAttackHandler : Trigger
{
	States
	{
	// Ammo checking/taking stuff
	Pickup:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
		TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,1)
		stop
		TNT1 A 0 A_TakeInventory("RocketAmmo",1)
		goto PickupAttack

	// Attack stuff
	PickupAttack:
		TNT1 A 0 A_PlaySound("Painkiller/Grenade/Shoot",CHAN_WEAPON)
		TNT1 A 0 A_PlaySound("Painkiller/Grenade/Reload",CHAN_5)
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
	PickupAttackDM:
		TNT1 A 0 A_FireCustomMissile("Painkiller_StakeGrenade",0,false,0,0,0,10)
		stop
	PickupAttackCoop:
		TNT1 A 0 A_FireCustomMissile("Painkiller_StakeGrenadeCoop",0,false,0,0,0,10)
		stop
	}
}

actor Painkiller_Stake_Icon : Painkiller_Weapon_Icon
{
	States
	{
		Spawn:
			PK07 T 2
			stop
	}
}

actor Painkiller_Stake_Icon2 : Painkiller_Weapon_Icon
{
	States
	{
		Spawn:
			PK07 W 2
			stop
	}
}

Actor Painkiller_StakeProjectile : PainkillerProjectile
{
	-NOGRAVITY
	+HITTRACER
	+NODAMAGETHRUST
	+FORCERADIUSDMG
	+NOEXTREMEDEATH
	+THRUGHOST
	//+THRUACTORS
	Decal "PainkillerStakeHole"
	DamageType "PainkillerStake"
	Gravity 0.40
	Speed 56
	Scale 0.5
	Damage (130)
	Height 4
	Radius 2
	var int user_timer;
	States
	{
		Spawn:
			PK00 A 0
			TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,2,AAPTR_TARGET)
			TNT1 A 0 A_JumpIfInventory("Painkiller_WeaponModifier_Token",1,1,AAPTR_TARGET)
			goto SpawnLoop
			TNT1 A 0 A_ChangeFlag("NOGRAVITY",true)
			TNT1 A 0 A_SetUserVar(user_timer,user_timer+5)
			goto SpawnLoop

		SpawnLoop:
			TNT1 A 0 A_GiveInventory("Painkiller_StakeFlew")
			TNT1 A 0 ACS_NamedExecuteWithResult("Painkiller_ProjectilePitch")
			TNT1 A 0 A_SetUserVar(user_timer,user_timer+1)
			TNT1 A 0 A_JumpIf(user_timer < 8, 3)
			TNT1 A 0 A_GiveInventory("Painkiller_StakeFlaming")
			TNT1 A 0 A_SpawnItemEx("Painkiller_StakeFire",0,0,16,frandom(-0.25,0.25),frandom(-0.25,0.25),frandom(0.25,0.5),0,SXF_NOCHECKPOSITION)
			PK00 A 2 A_SpawnItemEx("Painkiller_StakeTrail",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)
			loop

		Death:
			TNT1 A 0 A_PlaySound("Painkiller/Stake/Default",CHAN_VOICE,0.5)
			TNT1 A 0 A_ChangeFlag("NOGRAVITY",true)
			TNT1 A 0 A_JumpIfInventory("Painkiller_StakeFlew",1,2)
			TNT1 A 0 A_SetPitch(-CallACS("Painkiller_ShooterPitch")/182)
			TNT1 A 0 A_SpawnItemEx("Painkiller_StakeHitWallEffect1",0,0,0,0,0,-0.15,0,SXF_NOCHECKPOSITION)
			TNT1 A 0 A_SpawnItemEx("Painkiller_StakeHitWallEffect2",0,0,0,0,0,frandom(0.15,0.5),0,SXF_NOCHECKPOSITION)
			PK00 B 105 A_GiveInventory("Painkiller_DamageCredit",1,AAPTR_TRACER)
			PK00 BBBBBBBBB 2 A_FadeOut(0.1)
			stop

		XDeath:
			TNT1 A 0 A_JumpIfInventory("Painkiller_StakeFlaming",1,1)
			goto XDeathStart
			TNT1 A 0 A_Explode(70,6,0,false,6)
		XDeathStart:
			TNT1 A 0 A_JumpIf(CallACS("Painkiller_StakeShatter") == 2, "Remove")
			TNT1 A 0 A_PlaySound("Painkiller/Stake/Body",CHAN_VOICE,0.5)
			TNT1 A 0 A_JumpIf(CallACS("Painkiller_StakeShatter") == 1, "SpawnAnother")
			TNT1 A 0 A_JumpIfInventory("Painkiller_StakeFlew",1,2)
			TNT1 A 0 A_SetPitch(-CallACS("Painkiller_ShooterPitch")/182)
			TNT1 A 0 A_SpawnItemEx("Painkiller_StakeDebris1",0,0,0,frandom(-2,2),frandom(-2,2),frandom(-2,2),0,SXF_NOCHECKPOSITION)
			TNT1 A 0 A_SpawnItemEx("Painkiller_StakeDebris2",0,0,0,frandom(-2,2),frandom(-2,2),frandom(-2,2),0,SXF_NOCHECKPOSITION)
			TNT1 A 0 A_SpawnItemEx("Painkiller_StakeDebris3",0,0,0,frandom(-2,2),frandom(-2,2),frandom(-2,2),0,SXF_NOCHECKPOSITION)
			TNT1 A 0 A_SpawnItemEx("Painkiller_StakeDebris4",0,0,0,frandom(-2,2),frandom(-2,2),frandom(-2,2),0,SXF_NOCHECKPOSITION)
			TNT1 A 0 A_SpawnItemEx("Painkiller_StakeDebris5",0,0,0,frandom(-2,2),frandom(-2,2),frandom(-2,2),0,SXF_NOCHECKPOSITION)
			TNT1 A 2 A_GiveInventory("Painkiller_DamageCredit",1,AAPTR_TRACER)
			stop

		SpawnAnother:
			TNT1 A 1 ACS_NamedExecuteWithResult("Painkiller_ForceStakeBodyRemoval")
			TNT1 A 5 A_CustomMissile("Painkiller_StakeIntermediate",0,0,0,CMF_AIMDIRECTION|CMF_SAVEPITCH,-pitch)
			stop

		Remove:
			TNT1 A 5
			stop
	}
}

Actor Painkiller_StakeProjectileCoop : Painkiller_StakeProjectile { +THRUSPECIES Species "Player" }

Actor Painkiller_StakeIntermediate
{
	-COUNTKILL
	-SOLID
	+THRUACTORS
	-SHOOTABLE
	Speed 0
	Height 0
	Radius 0
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_Stop
			TNT1 A 0 A_SetPitch(-CallACS("Painkiller_ShooterPitch")/182)
			TNT1 A 3 A_CustomMissile("Painkiller_StakeProjectilePenetrate",0,0,0,CMF_AIMDIRECTION|CMF_TRACKOWNER,-pitch)
			stop
	}
}

Actor Painkiller_StakeProjectilePenetrate : Painkiller_StakeProjectile
{
	+THRUACTORS
	States
	{
		Spawn:
			PK00 A 0
			TNT1 A 0 A_TransferPointer(AAPTR_TARGET,AAPTR_DEFAULT,AAPTR_TARGET,AAPTR_TARGET)
			TNT1 A 0 A_TransferPointer(AAPTR_TARGET,AAPTR_DEFAULT,AAPTR_TRACER,AAPTR_TRACER)
			TNT1 A 0 A_TransferPointer(AAPTR_TARGET,AAPTR_DEFAULT,AAPTR_TARGET,AAPTR_TARGET)
			TNT1 A 0 ACS_NamedExecuteWithResult("Painkiller_StakeRetrieveTracer")
			TNT1 A 0 A_GiveInventory("Painkiller_StakeFlew")
			TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,2,AAPTR_TARGET)
			TNT1 A 0 A_JumpIfInventory("Painkiller_WeaponModifier_Token",1,1,AAPTR_TARGET)
			goto SpawnLoop
			TNT1 A 0 A_ChangeFlag("NOGRAVITY",true)
			goto SpawnLoop

		SpawnLoop:
			TNT1 A 0 A_GiveInventory("Painkiller_StakeFlew")
			TNT1 A 0 ACS_NamedExecuteWithResult("Painkiller_ProjectilePitch")
			PK00 A 2 A_SpawnItemEx("Painkiller_StakeTrail",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)
			loop

		Death:
			TNT1 A 0 A_PlaySound("Painkiller/Stake/Default",CHAN_VOICE,0.5)
			TNT1 A 0 A_ChangeFlag("NOGRAVITY",true)
			TNT1 A 0 A_JumpIfInventory("Painkiller_StakeFlew",1,6)
			TNT1 A 0 A_TransferPointer(AAPTR_TARGET,AAPTR_DEFAULT,AAPTR_TARGET,AAPTR_TARGET)
			TNT1 A 0 A_SetPitch(CallACS("Painkiller_ShooterPitch")/182)
			TNT1 A 0 A_TransferPointer(AAPTR_TARGET,AAPTR_DEFAULT,AAPTR_TRACER,AAPTR_TRACER)
			TNT1 A 0 A_TransferPointer(AAPTR_TARGET,AAPTR_DEFAULT,AAPTR_TARGET,AAPTR_TARGET)
			TNT1 A 0 ACS_NamedExecuteWithResult("Painkiller_StakeRetrieveTracer")
			TNT1 A 0 A_SpawnItemEx("Painkiller_StakeHitWallEffect1",0,0,0,0,0,-0.15,0,SXF_NOCHECKPOSITION)
			TNT1 A 0 A_SpawnItemEx("Painkiller_StakeHitWallEffect2",0,0,0,0,0,frandom(0.15,0.5),0,SXF_NOCHECKPOSITION)
			PK00 B 105 A_GiveInventory("Painkiller_DamageCredit",1,AAPTR_TRACER)
			PK00 BBBBBBBBB 2 A_FadeOut(0.1)
			stop

		XDeath:
			TNT1 A 0 A_JumpIfInventory("Painkiller_StakeFlew",1,2)
			TNT1 A 0 A_SetPitch(-CallACS("Painkiller_ShooterPitch")/182)
			PK00 B 105 A_GiveInventory("Painkiller_DamageCredit",1,AAPTR_TRACER)
			PK00 BBBBBBBBB 2 A_FadeOut(0.1)
			stop

		SpawnAnother:
			PK00 B 105
			PK00 BBBBBBBBB 2 A_FadeOut(0.1)
			stop
	}
}

Actor Painkiller_StakeProjectileLaunch : PainkillerProjectile
{
	-NOGRAVITY
	+HITTRACER
	+NODAMAGETHRUST
	+NOEXTREMEDEATH
	//+THRUACTORS
	DamageType "PainkillerExplosive"
	Gravity 0.5
	Speed 50
	Scale 0.5
	Damage (0)
	Height 4
	Radius 2
	States
	{
		Spawn:
			PK00 A 0
			//PK00 A 1
			//TNT1 A 0 A_ChangeFlag("THRUACTORS",false)
			goto SpawnLoop

		SpawnLoop:
			TNT1 A 0 A_GiveInventory("Painkiller_StakeFlew")
			TNT1 A 0 ACS_NamedExecuteWithResult("Painkiller_ProjectilePitch")
			TNT1 A 0 A_SpawnItemEx("Painkiller_FlamethrowerFire",0,0,16,frandom(-0.25,0.25),frandom(-0.25,0.25),frandom(0.25,0.5),0,SXF_NOCHECKPOSITION)
			PK00 A 2 A_SpawnItemEx("Painkiller_StakeTrail",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)
			loop

		Death:
			TNT1 A 0 A_PlaySound("Painkiller/Grenade/Explode",CHAN_VOICE)
			TNT1 A 0 A_Explode(200,96)
			TNT1 A 0 A_RadiusGive("Painkiller_DamageCredit",96,RGF_MONSTERS|RGF_NOSIGHT,1)
			TNT1 A 0 A_SpawnItemEx("Painkiller_StakeDebris1",0,0,0,frandom(-2.0,2.0),frandom(-2.0,2.0),frandom(-2.0,2.0),0,SXF_NOCHECKPOSITION)
			TNT1 A 0 A_SpawnItemEx("Painkiller_StakeDebris2",0,0,0,frandom(-2.0,2.0),frandom(-2.0,2.0),frandom(-2.0,2.0),0,SXF_NOCHECKPOSITION)
			TNT1 A 0 A_SpawnItemEx("Painkiller_StakeDebris3",0,0,0,frandom(-2.0,2.0),frandom(-2.0,2.0),frandom(-2.0,2.0),0,SXF_NOCHECKPOSITION)
			TNT1 A 0 A_SpawnItemEx("Painkiller_StakeDebris4",0,0,0,frandom(-2.0,2.0),frandom(-2.0,2.0),frandom(-2.0,2.0),0,SXF_NOCHECKPOSITION)
			TNT1 A 0 A_SpawnItemEx("Painkiller_StakeDebris5",0,0,0,frandom(-2.0,2.0),frandom(-2.0,2.0),frandom(-2.0,2.0),0,SXF_NOCHECKPOSITION)
			TNT1 A 0 A_GiveInventory("Painkiller_DamageCredit",1,AAPTR_TRACER)
			TNT1 A 1 A_SpawnItemEx("Painkiller_GrenadeSmokeSpawner",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
			stop
	}
}

Actor Painkiller_StakeProjectileLaunchCoop : Painkiller_StakeProjectileLaunch { +THRUSPECIES Species "Player" }

Actor Painkiller_StakeFlew : TemporaryBoolean {}
Actor Painkiller_StakeFlaming : TemporaryBoolean {}

Actor Painkiller_StakeDebris1 : Painkiller_Debris
{
	Scale 3.0
	Gravity 0.5
	States
	{
		Spawn:
			PK00 B 0
		SpawnLoop:
			PK00 B 70 A_SetAngle(random(0,360))
			PK00 BBBBBBBBBBBBBBBB 3 A_FadeOut(0.05)
			Stop
	}
}

Actor Painkiller_StakeDebris2 : Painkiller_StakeDebris1 {}
Actor Painkiller_StakeDebris3 : Painkiller_StakeDebris1 {}
Actor Painkiller_StakeDebris4 : Painkiller_StakeDebris1 {}
Actor Painkiller_StakeDebris5 : Painkiller_StakeDebris1 {}

Actor Painkiller_StakeTrail : Painkiller_Trail {}

Actor Painkiller_StakeProjectile_RegularDamageType : Painkiller_StakeProjectile { DamageType "Painkiller" }
Actor Painkiller_StakeProjectileCoop_RegularDamageType : Painkiller_StakeProjectileCoop { DamageType "Painkiller" }

Actor Painkiller_StakeGrenade : PainkillerProjectile
{
	-NOGRAVITY
	+NODAMAGETHRUST
	+THRUSPECIES
	+FORCERADIUSDMG
	+HITTRACER
	-SKYEXPLODE
	DamageType "PainkillerExplosive"
	BounceType "Doom"
	BounceCount 30
	BounceFactor 0.6
	WallBounceFactor 0.5
	BounceSound "Painkiller/Grenade/Bounce"
	Species "PainkillerGrenade"
	//+THRUACTORS
	Scale 10.0
	Gravity 0.35
	Speed 16
	Damage (0)
	Height 4
	Radius 2
	var int user_pitch;
	States
	{
		Spawn:
			PK00 B 0
			TNT1 A 0 A_SetUserVar("user_pitch",-CallACS("Painkiller_ShooterPitch")/182)
			//TNT1 A 0 A_JumpIf(CallACS("Painkiller_GrenadeTidToken") == 1, 1)
			PK00 B 1 ACS_NamedExecuteWithResult("Painkiller_SpawnGrenadeHitBox", 1)
			//TNT1 A 0 A_SpawnItemEx("Painkiller_GrenadeHitBox",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
			//PK00 A 1
			//TNT1 A 0 A_ChangeFlag("THRUACTORS",false)
			goto SpawnLoop

		SpawnLoop:
			//TNT1 A 0 A_SetPitch(pitch+5)
			TNT1 A 0 ACS_NamedExecuteWithResult("Painkiller_ProjectilePitch")
			TNT1 A 0 A_SpawnItemEx("Painkiller_Smoke",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
			PK00 B 2 A_SpawnItemEx("Painkiller_GrenadeTrail",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)
			loop

		Launch:
			TNT1 A 0 A_SetUserVar("user_pitch",-CallACS("Painkiller_ShooterPitch")/182)
			TNT1 A 2 A_CustomMissile("Painkiller_StakeProjectileLaunch",0,0,CallACS("Painkiller_ShooterAngle")/182,CMF_AIMDIRECTION|CMF_ABSOLUTEANGLE,user_pitch)
			stop

		Death:
			TNT1 A 0 A_PlaySound("Painkiller/Grenade/Explode",CHAN_VOICE)
			TNT1 A 0 A_RadiusGive("Painkiller_DamageCredit",96,RGF_MONSTERS|RGF_NOSIGHT,1)
			TNT1 A 0 A_Explode(110,96)
			TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"FireExplode",AAPTR_TARGET)
			TNT1 A 0 A_JumpIfInventory("Painkiller_WeaponModifier_Token",1,"FireExplode",AAPTR_TARGET)
			goto DeathEnd

		FireExplode:
			TNT1 A 0 A_RadiusGive("Painkiller_RadiusRoaster",96,RGF_MONSTERS|RGF_CORPSES,1)
			PK00 BBB 0 A_SpawnItemEx("Painkiller_GasTankExplosionFire",0,0,16,frandom(-0.25,0.25),frandom(-0.25,0.25),frandom(0.25,0.5),0,SXF_NOCHECKPOSITION)
			PK00 BB 0 A_SpawnItemEx("Painkiller_FlamethrowerFireStarterMod",random(-16,16),random(-16,16),random(-16,16),0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
			goto DeathEnd

		DeathEnd:
			TNT1 A 0 A_GiveInventory("Painkiller_DamageCredit",1,AAPTR_TRACER)
			TNT1 A 1 A_SpawnItemEx("Painkiller_GrenadeSmokeSpawner",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
			stop
	}
}

Actor Painkiller_StakeGrenadeCoop : Painkiller_StakeGrenade
{
	+MTHRUSPECIES
	States
	{
		Launch:
			TNT1 A 0 A_SetUserVar("user_pitch",-CallACS("Painkiller_ShooterPitch")/182)
			TNT1 A 2 A_CustomMissile("Painkiller_StakeProjectileLaunchCoop",0,0,CallACS("Painkiller_ShooterAngle")/182,CMF_AIMDIRECTION|CMF_ABSOLUTEANGLE,user_pitch)
			stop
	}
}

Actor Painkiller_GrenadeSmokeSpawner : Painkiller_VisualEffect
{
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 AAAAAAAAAAAAAAAA 0 A_SpawnItemEx("Painkiller_GrenadeSmoke",random(-16,16),random(-16,16),random(-16,16),random(-1,1),random(-1,1),random(4,5),0,SXF_NOCHECKPOSITION)
			TNT1 AAAAAAAAAAAAAAAA 0 A_SpawnItemEx("Painkiller_Smoke",random(-16,16),random(-16,16),random(-16,48),random(-1,1),random(-1,1),3,0,SXF_CLIENTSIDE|SXF_NOCHECKPOSITION)
			TNT1 A 0 A_SpawnItemEx("Painkiller_GrenadeWhiteSmoke",0,0,0,0,0,3,0,SXF_NOCHECKPOSITION)
			TNT1 A 0 A_SpawnItemEx("Painkiller_GrenadeSmokeDart",0,0,0,-12,0,12,0,SXF_CLIENTSIDE|SXF_NOCHECKPOSITION)
			TNT1 A 0 A_SpawnItemEx("Painkiller_GrenadeSmokeDart",0,0,0,-12,12,12,0,SXF_CLIENTSIDE|SXF_NOCHECKPOSITION)
			TNT1 A 0 A_SpawnItemEx("Painkiller_GrenadeSmokeDart",0,0,0,0,12,12,0,SXF_CLIENTSIDE|SXF_NOCHECKPOSITION)
			TNT1 A 0 A_SpawnItemEx("Painkiller_GrenadeSmokeDart",0,0,0,12,12,12,0,SXF_CLIENTSIDE|SXF_NOCHECKPOSITION)
			TNT1 A 0 A_SpawnItemEx("Painkiller_GrenadeSmokeDart",0,0,0,12,0,12,0,SXF_CLIENTSIDE|SXF_NOCHECKPOSITION)
			TNT1 A 3 A_SpawnItemEx("Painkiller_GrenadeSmokeDart",0,0,0,12,-12,12,0,SXF_CLIENTSIDE|SXF_NOCHECKPOSITION)
			TNT1 BCDEF 3
			stop
	}
}

Actor Painkiller_GrenadeSmokeDart
{
	//Projectile
	Height 4
	Radius 2
	//Damage (0)
	Speed 20
	Gravity 0.75
	Scale 0.5
	+THRUACTORS
	+BLOODLESSIMPACT
	-NOGRAVITY
	+NOINTERACTION
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_SpawnItemEx("Painkiller_GrenadeFire",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)	TNT1 A 2 A_SpawnItemEx("Painkiller_Smoke",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE|SXF_TRANSFERSCALE)
			//TNT1 A 0 A_ChangeVelocity(0,0,-8,CVF_RELATIVE)
			TNT1 A 0 A_SpawnItemEx("Painkiller_GrenadeFire",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)	TNT1 A 2 A_SpawnItemEx("Painkiller_Smoke",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE|SXF_TRANSFERSCALE)
			TNT1 A 0 A_SpawnItemEx("Painkiller_GrenadeFire",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)	TNT1 A 2 A_SpawnItemEx("Painkiller_Smoke",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE|SXF_TRANSFERSCALE)
			TNT1 A 0 A_SpawnItemEx("Painkiller_GrenadeFire",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)	TNT1 A 2 A_SpawnItemEx("Painkiller_Smoke",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE|SXF_TRANSFERSCALE)
			TNT1 A 0 A_SpawnItemEx("Painkiller_GrenadeFire",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)	TNT1 A 2 A_SpawnItemEx("Painkiller_Smoke",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE|SXF_TRANSFERSCALE)
			TNT1 A 0 A_SpawnItemEx("Painkiller_GrenadeFire",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)	TNT1 A 2 A_SpawnItemEx("Painkiller_Smoke",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE|SXF_TRANSFERSCALE)
			TNT1 A 0 A_SpawnItemEx("Painkiller_GrenadeFire",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)	TNT1 A 2 A_SpawnItemEx("Painkiller_Smoke",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE|SXF_TRANSFERSCALE)
			TNT1 A 0 A_SpawnItemEx("Painkiller_GrenadeFire",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)	TNT1 A 2 A_SpawnItemEx("Painkiller_Smoke",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE|SXF_TRANSFERSCALE)
			stop
	}
}

Actor Painkiller_GrenadeTrail : Painkiller_Trail {}

Actor Painkiller_GrenadeSmoke : Painkiller_Smoke
{
	Scale 0.4
	RenderStyle Add
	Alpha 1.0
	+BRIGHT
	States
	{
		Spawn:
			PK00 M 0
			PK00 MMMMMMMM 2 A_FadeOut(0.04)
			PK00 LLLLLLLLL 2 A_FadeOut(0.04)
			PK00 KKKKKKKK 2 A_FadeOut(0.04)
			stop
	}
}

Actor Painkiller_GrenadeFire : Painkiller_Smoke
{
	Scale 0.1
	+BRIGHT
	States
	{
		Spawn:
			PK00 K 0
			PK00 KKKKKKKKKKKKKKKK 3 A_FadeOut(0.05)
			stop
	}
}

Actor Painkiller_GrenadeWhiteSmoke : Painkiller_Smoke
{
	Scale 0.67
	States
	{
		Spawn:
			PK00 C 0
			PK00 CCCCCCCCCCCCCCCC 3 A_FadeOut(0.05)
			stop
	}
}

Actor Painkiller_GrenadeHitBox
{
	Height 8
	Radius 4
	Health 1
	Mass 50000
    Tag "Grenade"
	-COUNTKILL
	//+THRUACTORS
	-SOLID
	+SHOOTABLE
	+NOBLOOD
	+NOGRAVITY
	DamageFactor "PainkillerStake", 1
	DamageFactor "Normal", 0
	Species "PainkillerGrenade"
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0
		SpawnLoop:
			TNT1 A 0 ACS_NamedExecuteWithResult("Painkiller_HitBoxRemover")
			TNT1 A 2 A_Warp(AAPTR_TRACER,0,0,0,0,WARPF_NOCHECKPOSITION)
			loop

		Death:
			TNT1 A 2
			stop

		Death.PainkillerStake:
			TNT1 A 2 ACS_NamedExecuteWithResult("Painkiller_StakeGrenadeComboLaunch")
			stop
	}
}

Actor Painkiller_StakeFire
{
	Height 16
	Radius 8
	RenderStyle Add
	Scale 0.33
	Alpha 0.75
	+NOINTERACTION
	+CLIENTSIDEONLY
	+FORCEXYBILLBOARD
	+BRIGHT
	States
	{
		Spawn:
			PK02 A 0
			TNT1 A 0 A_SpawnItemEx("Painkiller_FlamethrowerSmoke",0,0,0,frandom(-1.0,1.0),frandom(-1.0,1.0),frandom(0.5,1.0),0,SXF_NOCHECKPOSITION)
			PK02 ABCDE 1 A_FadeOut(0.15)
			stop
	}
}

Actor Painkiller_StakeHitWallEffect1 : Painkiller_Smoke
{
	Scale 0.1
	RenderStyle Translucent
	Alpha 0.1
	States
	{
		Spawn:
			PK07 K 0
			TNT1 A 0 A_SetScale(frandom(0.08,0.1))
			TNT1 A 0 A_SetScale(scalex+0.02)	PK07 K 1 A_FadeOut(0.01)
			TNT1 A 0 A_SetScale(scalex+0.02)	PK07 K 1 A_FadeOut(0.01)
			TNT1 A 0 A_SetScale(scalex+0.02)	PK07 K 1 A_FadeOut(0.01)
			TNT1 A 0 A_SetScale(scalex+0.02)	PK07 K 1 A_FadeOut(0.01)
			TNT1 A 0 A_SetScale(scalex+0.02)	PK07 K 1 A_FadeOut(0.01)
			TNT1 A 0 A_SetScale(scalex+0.02)	PK07 K 1 A_FadeOut(0.01)
			TNT1 A 0 A_SetScale(scalex+0.02)	PK07 K 1 A_FadeOut(0.01)
			TNT1 A 0 A_SetScale(scalex+0.02)	PK07 K 1 A_FadeOut(0.01)
			TNT1 A 0 A_SetScale(scalex+0.02)	PK07 K 1 A_FadeOut(0.01)
			stop
	}
}

Actor Painkiller_StakeHitWallEffect2 : Painkiller_Smoke
{
	Scale 0.1
	RenderStyle Translucent
	Alpha 0.3
	States
	{
		Spawn:
			PK07 K 0
			TNT1 A 0 A_SetScale(frandom(0.0,0.25))
			TNT1 A 0 A_SetScale(scalex+0.05)	PK07 K 1 A_FadeOut(0.03)
			TNT1 A 0 A_SetScale(scalex+0.05)	PK07 K 1 A_FadeOut(0.03)
			TNT1 A 0 A_SetScale(scalex+0.05)	PK07 K 1 A_FadeOut(0.03)
			TNT1 A 0 A_SetScale(scalex+0.05)	PK07 K 1 A_FadeOut(0.03)
			TNT1 A 0 A_SetScale(scalex+0.05)	PK07 K 1 A_FadeOut(0.03)
			TNT1 A 0 A_SetScale(scalex+0.05)	PK07 K 1 A_FadeOut(0.03)
			TNT1 A 0 A_SetScale(scalex+0.05)	PK07 K 1 A_FadeOut(0.03)
			TNT1 A 0 A_SetScale(scalex+0.05)	PK07 K 1 A_FadeOut(0.03)
			TNT1 A 0 A_SetScale(scalex+0.05)	PK07 K 1 A_FadeOut(0.03)
			stop
	}
}
