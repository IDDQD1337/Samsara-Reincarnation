// Token

actor Wolfen_IHaveSlot1 : Boolean {}

// Original/Lost Episodes

actor BJSuperKnife : Boolean {}

// Castle Totenkopf SDL

actor Totenkopf_IHaveDualMausers : Boolean {}
actor SamsaraTotenkopfMauserAttackAmount : Counter { Inventory.MaxAmount 5 }

actor Totenkopf_Mauser : InstantSwitchWeapon
{
	Tag "M712 Rapid-Fire Pistol (Single)"
    Weapon.SelectionOrder 800
    Weapon.SlotNumber 1
	Weapon.SlotPriority 2
    Inventory.PickUpSound "Totenkopf/getweapon"
    Weapon.AmmoUse 1
    //Weapon.AmmoGive 5
    Weapon.AmmoType "Clip"
    +DONTBOB
    +INVENTORY.UNDROPPABLE
    Inventory.PickupMessage "M712 Rapid-Fire Pistol"
	Inventory.RestrictedTo "WolfenPlayer"
    Weapon.YAdjust 16
	+WEAPON.NOALERT
    States
    {
      Spawn:
        TK10 K -1
        stop

      Ready:
        TNT1 A 0 A_JumpIfInventory("Totenkopf_MauserDual",1,2)
        TK10 A 1 A_WeaponReady(WRF_NOSECONDARY)
        loop
        TK10 A 1 A_WeaponReady
        loop

      Fire:
        TK10 B 3 A_AlertMonsters
        goto HoldStart

      Hold:
        TNT1 A 0 A_AlertMonsters
      HoldStart:
        TNT1 A 0 A_TakeInventory("SamsaraTotenkopfMauserAttackAmount") // reset
		TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"HoldStartLoopSmooth")
      HoldStartLoop:
        TNT1 A 0 A_GiveInventory("SamsaraTotenkopfMauserAttackAmount")
        TK10 C 1 Bright A_GiveInventory("SamsaraTotenkopfMauserSingleAttackHandler")
        TK10 D 2
        TNT1 A 0 A_CheckReload
        TNT1 A 0 A_JumpIfInventory("SamsaraTotenkopfMauserAttackAmount",0,"HoldFinish")
        loop
		
      HoldStartLoopSmooth:
        TNT1 A 0 A_GiveInventory("SamsaraTotenkopfMauserAttackAmount")
        TK10 C 1 Bright A_GiveInventory("SamsaraTotenkopfMauserSingleAttackHandler")
        TK10 N 1
		TK10 D 1
        TNT1 A 0 A_CheckReload
        TNT1 A 0 A_JumpIfInventory("SamsaraTotenkopfMauserAttackAmount",0,"HoldFinish")
        loop

      HoldFinish:
        TK10 D 9
        TK10 E 3 A_ReFire
        goto Ready

      AltFire:
        TK10 A 8
        TNT1 A 0 A_SelectWeapon("Totenkopf_MauserDual")
        goto Deselect
    }
} 

actor Totenkopf_MauserDual : Totenkopf_Mauser
{
	Tag "M712 Rapid-Fire Pistol (Dual)"
    Weapon.SelectionOrder 350
	Weapon.SlotPriority 3
	//Weapon.AmmoGive 0
    States
    {
      Ready:
        TNT1 A 0 A_JumpIfInventory("Totenkopf_Mauser",1,2)
        TK10 F 1 A_WeaponReady(WRF_NOSECONDARY)
        loop
        TK10 F 1 A_WeaponReady
        loop

      Fire:
        TK10 G 3 A_AlertMonsters
        goto HoldStart

      Hold:
        TNT1 A 0 A_AlertMonsters
      HoldStart:
        TNT1 A 0 A_TakeInventory("SamsaraTotenkopfMauserAttackAmount") // reset
		TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"HoldStartLoopSmooth")
      HoldStartLoop:
        TNT1 A 0 A_GiveInventory("SamsaraTotenkopfMauserAttackAmount")
        TK10 H 1 Bright A_GiveInventory("SamsaraTotenkopfMauserDualAttackHandler")
        TNT1 A 0 A_CheckReload
        TK10 I 2 Bright A_GiveInventory("SamsaraTotenkopfMauserDualAttackHandler")
        TNT1 A 0 A_CheckReload
        TNT1 A 0 A_JumpIfInventory("SamsaraTotenkopfMauserAttackAmount",0,"HoldFinish")
        loop
		
      HoldStartLoopSmooth:
        TNT1 A 0 A_GiveInventory("SamsaraTotenkopfMauserAttackAmount")
        TK10 H 1 Bright A_GiveInventory("SamsaraTotenkopfMauserDualAttackHandler")
        TNT1 A 0 A_CheckReload
        TK10 O 1 Bright A_GiveInventory("SamsaraTotenkopfMauserDualAttackHandler")
		TK10 P 1 Bright
        TNT1 A 0 A_CheckReload
        TNT1 A 0 A_JumpIfInventory("SamsaraTotenkopfMauserAttackAmount",0,"HoldFinish")
        loop

      HoldFinish:
        TK10 J 9
        TK10 J 3 A_ReFire
        goto Ready

      AltFire:
        TK10 F 8
        TNT1 A 0 A_SelectWeapon("Totenkopf_Mauser")
        goto Deselect
    }
}

actor SamsaraTotenkopfMauserSingleAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("Totenkopf/atkmauser",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("Totenkopf_Tracer_MauserDM",random(-8,8),false)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("Totenkopf_Tracer_MauserCoop",random(-8,8),false)
        stop
    }
}

actor SamsaraTotenkopfMauserDualAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("Totenkopf/atkmauser",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("Totenkopf_Tracer_MauserDM",random(-10,10),false)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("Totenkopf_Tracer_MauserCoop",random(-10,10),false)
        stop
    }
}

Actor Totenkopf_MauserPickup : CustomInventory
{
	+INVENTORY.AUTOACTIVATE
	Inventory.PickUpSound "Totenkopf/getweapon"
	States
	{
		Spawn:
			TK10 K -1
			stop
		Pickup:
			TNT1 A 0 A_JumpIfInventory("Totenkopf_MauserDual",1,1)
			Goto Pickup+3
			TNT1 A 0 A_JumpIfInventory("Totenkopf_Mauser",1,1)
			Goto Pickup+3
			TNT1 A 0 A_JumpIfInventory("Clip",0,"PickupFailure")
			Goto Pickup+3
			TNT1 A 0 A_JumpIfInventory("Totenkopf_Mauser",1,"PickupDual")
			TNT1 A 0 A_Log("M712 Rapid-Fire Pistol")
			TNT1 A 0 A_GiveInventory("Totenkopf_Mauser",1)
			Stop
		PickupDual:
			TNT1 A 0 A_Log("M712 Rapid-Fire Pistol")
			TNT1 A 0 A_GiveInventory("Totenkopf_MauserDual",1)
			Stop
		PickupFailure:
			TNT1 A 0 A_Railwait
			fail
	}
}
 
actor Totenkopf_Tracer_Mauser : Totenkopf_Tracer
{
    Damage (6 + (random(0,20)))
    +THRUGHOST
}

actor Totenkopf_Tracer_MauserDM : Totenkopf_Tracer_Mauser { Damage (6 + (random(0,10))) }
actor Totenkopf_Tracer_MauserCoop : Totenkopf_Tracer_Mauser { Species "Player" +THRUSPECIES }

Actor Totenkopf_MauserPickedUpX : MapCoordinate {}
Actor Totenkopf_MauserPickedUpY : MapCoordinate {}
Actor Totenkopf_MauserPickedUpZ : MapCoordinate {}
actor Totenkopf_MauserLastPickupTime : Counter { Inventory.InterHubAmount 0 }
actor Totenkopf_MauserLevelLimiter : Boolean {}
