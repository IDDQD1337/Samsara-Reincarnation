Actor Unreal_QuadShotLoaded : Inventory { Inventory.MaxAmount 3	Inventory.InterHubAmount 0 +Inventory.UNDROPPABLE  -INVBAR }

Actor Unreal_QuadShot : UnrealWeapon
{
	Tag "QuadShot"
	Weapon.SlotNumber 8
	Weapon.AmmoType "Shell"
	Weapon.AmmoUse1 2
	Weapon.AmmoGive 24
	Weapon.AmmoType2 "Shell"
	Weapon.AmmoUse2 2
	Weapon.UpSound ""
	+WEAPON.BFG
	Weapon.UpSound "Unreal/QuadShot/Reload"
	Inventory.PickupMessage "You got the QuadShot."
	Obituary "%o was turned into ground beef by %k's QuadShot!"
	States
	{
		Spawn:
			UN00 A 10 BRIGHT
			UN00 A 10
			Loop
		Select:
			TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_Raise
			TNT1 A 1 A_Raise
			loop
		Ready:
			UN03 DCBA 2
			Goto Widle
		WIdle:
			UN01 A 1 A_WeaponReady
			Loop
		Deselect:
			UN03 ABCD 2
			Goto InstantDeselect
		InstantDeselect: // I have to copy paste this everytime. Thanks, Zandronum.
			TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
			TNT1 A 1 A_Lower
			Wait
		Fire:
			UN01 B 0 BRIGHT A_JumpIfInventory("CoopModeOn",1,2)
			UN01 B 3 BRIGHT A_FireBullets(4,4, 9, 25, "UnrealPuff", FBF_NORANDOM|FBF_USEAMMO)
			Goto FireEnd
			UN01 B 3 BRIGHT A_FireBullets(4,4, 9, 25, "UnrealPuffCoop", FBF_NORANDOM|FBF_USEAMMO)
			Goto FireEnd
		FireEnd:
			UN01 C 0 BRIGHT A_PlaySound("Unreal/QuadShot/Fire", CHAN_WEAPON)
			UN01 C 0 ACS_NamedExecuteWithResult("Unreal_Recoil")
			UN01 CD 3 BRIGHT
			UN01 E 3
			Goto ShotgunPump
		ShotgunPump:
			UN02 ABCD 2
			UN02 E 2 A_PlaySound("Unreal/QuadShot/Reload", CHAN_WEAPON)
			UN02 F 1
			UN02 G 2
			UN02 H 1
			UN02 I 2
			UN02 J 1
			UN02 K 2
			UN02 L 1
			UN02 M 0
			Goto WIdle
		AltFire:
			UN02 A 0 A_JumpIfInventory("Unreal_QuadShotLoaded", 3, "Release")
			UN02 AB 2
			UN02 C 0 A_JumpIfInventory("Shell", 2, 1)
			Goto Release
			UN02 CD 2
			UN02 E 2 A_PlaySound("Unreal/QuadShot/Reload", CHAN_WEAPON)
			UN02 F 1 A_GiveInventory("Unreal_QuadShotLoaded", 1)
			UN02 G 2 A_TakeInventory("Shell", 2, TIF_NOTAKEINFINITE)
			UN02 H 1
			UN02 I 2
			UN02 J 1
			UN02 K 2
			UN02 L 1
			UN02 M 0 A_Refire
			Goto Release
		Release:
			UN01 A 0 A_JumpIfInventory("Unreal_QuadShotLoaded", 3, "FourBarrels")
			UN01 A 0 A_JumpIfInventory("Unreal_QuadShotLoaded", 2, "ThreeBarrels")
			UN01 A 0 A_JumpIfInventory("Unreal_QuadShotLoaded", 1, "TwoBarrels")
			Goto OneBarrel
		OneBarrel:
			UN01 B 0 BRIGHT A_JumpIfInventory("CoopModeOn",1,2)
			UN01 B 3 BRIGHT A_FireBullets(4,4, 9, 25, "UnrealPuff", FBF_NORANDOM|FBF_USEAMMO)
			Goto BarrelEnd
			UN01 B 3 BRIGHT A_FireBullets(4,4, 9, 25, "UnrealPuffCoop", FBF_NORANDOM|FBF_USEAMMO)
			Goto BarrelEnd
		TwoBarrels:
			UN01 B 0 BRIGHT A_JumpIfInventory("CoopModeOn",1,2)
			UN01 B 3 BRIGHT A_FireBullets(6,6, 18, 25, "UnrealPuff", FBF_NORANDOM|FBF_USEAMMO)
			Goto BarrelEnd
			UN01 B 3 BRIGHT A_FireBullets(6,6, 18, 25, "UnrealPuffCoop", FBF_NORANDOM|FBF_USEAMMO)
			Goto BarrelEnd
		ThreeBarrels:
			UN01 B 0 BRIGHT A_JumpIfInventory("CoopModeOn",1,2)
			UN01 B 3 BRIGHT A_FireBullets(8,8, 27, 25, "UnrealPuff", FBF_NORANDOM|FBF_USEAMMO)
			Goto BarrelEnd
			UN01 B 3 BRIGHT A_FireBullets(8,8, 27, 25, "UnrealPuffCoop", FBF_NORANDOM|FBF_USEAMMO)
			Goto BarrelEnd
		FourBarrels:
			UN01 B 0 BRIGHT A_JumpIfInventory("CoopModeOn",1,2)
			UN01 B 3 BRIGHT A_FireBullets(10,10, 36, 25, "UnrealExtremePuff", FBF_NORANDOM|FBF_USEAMMO)
			Goto BarrelEnd
			UN01 B 3 BRIGHT A_FireBullets(10,10, 36, 25, "UnrealExtremePuffCoop", FBF_NORANDOM|FBF_USEAMMO)
			Goto BarrelEnd
		BarrelEnd:
			UN01 C 0 BRIGHT A_PlaySound("Unreal/QuadShot/Fire", CHAN_WEAPON)
			UN01 C 0 ACS_NamedExecuteWithResult("Unreal_Recoil")
			UN01 C 3 BRIGHT A_TakeInventory("Unreal_QuadShotLoaded", 4)
			UN01 D 3 BRIGHT
			UN01 E 9
			Goto ShotgunPump
	}
}