/*Actor Unreal_BFGReplacer replaces BFG9000
{
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_SpawnItemEx("Unreal_Redeemer",0,0,0,frandom(-0.25,0.25),frandom(-0.25,0.25))
			TNT1 A 0 A_SpawnItemEx("Unreal_QuadShot",0,0,0,frandom(-0.25,0.25),frandom(-0.25,0.25))
			Stop
	}
}
*/

Actor Unreal_RedeemerLoad : Boolean { }
Actor Unreal_Redeemer : UnrealWeapon
{
	Tag "Redeemer"
	Weapon.SlotNumber 7
	Weapon.AmmoType1 "Cell"
	Weapon.AmmoUse1 6
	Weapon.AmmoGive1 60
	Weapon.AmmoType2 "Cell"
	Weapon.AmmoUse2 60
	Weapon.AmmoGive2 60
	+WEAPON.BFG
	+WEAPON.EXPLOSIVE
	Weapon.UpSound "Unreal/RocketL/Select"
	Inventory.PickupMessage "You got the Redeemer."
	Obituary "%o was vaporized by %k's Redeemer!!"
	States
	{
		Spawn:
			UN00 A 10 BRIGHT
			UN00 A 10
			Loop
		Ready:
			UN05 A 0 A_GiveInventory("Unreal_RedeemerSelected",1)
			UN05 ABCDEFG 2 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
			Goto WIdle
		WIdle:
			//UN01 O 0 A_GunFlash("ReadyFlash",GFF_NOEXTCHANGE)
			UN01 O 0 A_Jump(6, "PigeonThrust", "Sway")
			UN01 O 1 A_WeaponReady
			Loop
		ReadyFlash:
			TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,2)
			TNT1 A 1 A_FireBullets(1,1,1,0, "Unreal_RazorBlankPuff", FBF_NORANDOM|FBF_NOFLASH|FBF_EXPLICITANGLE)
			Stop
			TNT1 A 1 A_FireBullets(1,1,1,0, "Unreal_RazorBlankPuffCoop", FBF_NORANDOM|FBF_NOFLASH|FBF_EXPLICITANGLE)
			Stop
		Deselect:
			UN02 A 0 A_TakeInventory("Unreal_RedeemerSelected",1)
			UN02 ABCDEFGHIJ 1
			Goto InstantDeselect
		InstantDeselect: // I have to copy paste this everytime. Thanks, Zandronum.
			TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
			TNT1 A 1 A_Lower
			Wait
		PigeonThrust:
			//UN06 A 0 A_GunFlash("PigeonFlash",GFF_NOEXTCHANGE)
			UN06 ABCABC 12 A_WeaponReady
			Goto WIdle
		PigeonFlash:
			TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PigeonFlashCoop")
			TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 2 A_FireBullets(1,1,1,0, "Unreal_RazorBlankPuff", FBF_NORANDOM|FBF_NOFLASH|FBF_EXPLICITANGLE)
			Stop
		PigeonFlashCoop:
			TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 2 A_FireBullets(1,1,1,0, "Unreal_RazorBlankPuffCoop", FBF_NORANDOM|FBF_NOFLASH|FBF_EXPLICITANGLE)
			Stop
		Sway:
			//UN07 A 0 A_GunFlash("PigeonFlash",GFF_NOEXTCHANGE)
			UN07 ABABAB 12 A_WeaponReady
			Goto WIdle
		Fire:
			UN03 A 0 ACS_NamedExecuteWithResult("Unreal_Recoil")
			UN03 A 0 BRIGHT A_JumpIfInventory("CoopModeOn",1,2)
			UN03 A 5 BRIGHT A_FireCustomMissile("Unreal_UMSRocket", 0, 1, 13)
			Goto FireEnd
			UN03 A 5 BRIGHT A_FireCustomMissile("Unreal_UMSRocketCoop", 0, 1, 13)
			Goto FireEnd
		FireEnd:
			UN03 B 5 BRIGHT A_PlaySound("Unreal/RocketL/Fire", CHAN_WEAPON)
			UN03 CDEFGHIJKLM 2 BRIGHT
			Goto WIdle
		AltFire:
			UN03 A 0 ACS_NamedExecuteWithResult("Unreal_Recoil")
			UN03 A 0 BRIGHT A_JumpIfInventory("CoopModeOn",1,2)
			UN03 A 5 BRIGHT A_FireCustomMissile("Unreal_UMSRocket2", 0, 1, 13)
			Goto AltFireEnd
			UN03 A 5 BRIGHT A_FireCustomMissile("Unreal_UMSRocket2Coop", 0, 1, 13)
			Goto AltFireEnd
		AltFireEnd:
			UN03 A 0 BRIGHT A_PlaySound("Unreal/RocketL/Fire", CHAN_WEAPON)
			UN03 B 5 BRIGHT //A_FireBullets(1,1,1,0, "Unreal_RazorBlankPuff", FBF_NORANDOM|FBF_NOFLASH|FBF_EXPLICITANGLE)
			UN03 CDEFGHIJKLM 2 BRIGHT //A_FireBullets(1,1,1,0, "Unreal_RazorBlankPuff", FBF_NORANDOM|FBF_NOFLASH|FBF_EXPLICITANGLE)
		AltFireLoop:
			UN03 A 2 A_JumpIfInventory("Unreal_RedeemerLoad",1,"Load")
			Loop
		Load:
			UN01 A 0 A_TakeInventory("Unreal_RedeemerLoad",1)
			UN01 ABCDEFGHIJKLMN 2 //A_FireBullets(1,1,1,0, "Unreal_RazorBlankPuff", FBF_NORANDOM|FBF_NOFLASH|FBF_EXPLICITANGLE)
			Goto WIdle
	}
}

Actor Unreal_UMSRocket
{
	Projectile
	Speed 55
	Radius 5
	Height 5
	Scale 2.0
	Damage (300)
	+FORCERADIUSDMG
	DamageType "UnrealExplosive"
	DeathSound "Unreal/Flak/Explode2"
	States
	{
		Spawn:
			UN00 A 0 NoDelay ACS_NamedExecuteWithResult("Unreal_RedeemerPitch")
			UN00 A 1 A_PlaySound("Unreal/RocketL/Loop1", CHAN_BODY, 1.0, True)
			UN00 BC 1
		SpawnLoop:
			UN00 D 0 A_SpawnItemEx("UnrealSmoke",-32*cos(pitch)-32*sin(-pitch),0,32*sin(-pitch)-32*cos(pitch),0,0,0)
			UN00 D 1 A_SpawnItemEx("UnrealSmoke",-64*cos(pitch)-32*sin(-pitch),0,64*sin(-pitch)-32*cos(pitch),0,0,0)
			Loop
		Death:
			TNT1 A 0 A_StopSound(CHAN_BODY)
			TNT1 A 0 A_Explode(300, 180)
			TNT1 AAA 0 A_SpawnItemEX("Unreal_RedeemerExplosionEffect", Random(0,4),Random(0,4),Random(0,4),0,0,0,0, SXF_NOCHECKPOSITION)
			TNT1 A 0 A_SpawnItemEX("Unreal_RedeemerExplosionEffect2",0,0,0,0,0,0,0, SXF_NOCHECKPOSITION)
			TNT1 A 5 A_SpawnItemEx("Unreal_YellowShockwave", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION)
			Stop
	}
}

Actor Unreal_UMSRocketCoop : Unreal_UMSRocket { +THRUSPECIES Species "Player" }

Actor Unreal_UMSRocket2 : Unreal_UMSRocket
{
	Speed 16
	Damage (1500)
	States
	{
		Spawn:
			UN00 A 0 NoDelay ACS_NamedExecuteWithResult("Unreal_RedeemerRocket")
			UN00 A 1 A_PlaySound("Unreal/RocketL/Loop1", CHAN_BODY, 1.0, True)
			UN00 BC 1
		SpawnLoop:
			UN00 D 0 A_SpawnItemEx("UnrealSmoke",-32*cos(pitch)-32*sin(-pitch),0,32*sin(-pitch)-32*cos(pitch),0,0,0)
			UN00 D 1 A_SpawnItemEx("UnrealSmoke",-64*cos(pitch)-32*sin(-pitch),0,64*sin(-pitch)-32*cos(pitch),0,0,0)
			Loop
		Death:
			TNT1 A 0 A_GiveInventory("Unreal_RedeemerLoad",1,AAPTR_Target)
			TNT1 A 0 A_StopSound(CHAN_BODY)
			TNT1 A 0 A_Explode(1500, 360)
			TNT1 AAA 0 A_SpawnItemEX("Unreal_EightballExplosiveEffect", Random(0,4),Random(0,4),Random(0,4),0,0,0,0, SXF_NOCHECKPOSITION)
			TNT1 A 0 A_SpawnItemEX("Unreal_RedeemerExplosionEffect2",0,0,0,0,0,0,0, SXF_NOCHECKPOSITION)
			TNT1 A 5 A_SpawnItemEx("Unreal_YellowShockwave2", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION)
			Stop
	}
}

Actor Unreal_UMSRocket2Coop : Unreal_UMSRocket2 { +THRUSPECIES Species "Player" }

Actor Unreal_RedeemerExplosionEffect : Unreal_EightballExplosiveEffect
{
	States
	{
		Spawn:
			UN18 ABCDEFG 2 BRIGHT A_SetScale(scalex+0.5)
			Stop
	}
}

Actor Unreal_RedeemerExplosionEffect2 : Unreal_RedeemerExplosionEffect
{
	Scale 1.0
	States
	{
		Spawn:
			UN27 AAABBBCCCDDDEEEFFFGGGHHH 1 BRIGHT A_SetScale(scalex+0.333)
			Stop
	}
}

Actor Unreal_YellowShockwave
{
	+CLIENTSIDEONLY
	+NOGRAVITY
	RenderStyle Add
	Alpha 0.5
	Scale 3.5
	States
	{
		Spawn:
			UN00 ABCDEF 1 BRIGHT
			Stop
	}
}

Actor Unreal_YellowShockwave2 : Unreal_YellowShockwave
{ 
	Scale 7.0 
	States
	{
		Spawn:
			UN00 ABCDEF 1 BRIGHT
			Stop
	}
}