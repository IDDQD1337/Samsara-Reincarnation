Actor Unreal_RazorJack : UnrealWeapon
{
	Tag "RazorJack"
	Weapon.SlotNumber 1
	Weapon.SlotPriority 0
	Weapon.AmmoType1 "Cell"
	Weapon.AmmoUse1 1
	Weapon.AmmoGive1 15
	Weapon.AmmoType2 "Cell"
	Weapon.AmmoUse2 1
	Weapon.AmmoGive2 15
	Weapon.UpSound "Unreal/RazorJack/Select"
	Inventory.PickupMessage "You got the RazorJack."
	Obituary "%k took a bloody chunk out of %o with the RazorJack."
	States
	{
		Spawn:
			UN00 A 10
			UN00 A 10 BRIGHT
			Loop
		Select:
			TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_Raise
			TNT1 A 1 A_Raise
			loop
		Ready:
			UN01 A 0 A_GiveInventory("Unreal_RazorJackSelected",1)
			UN01 ABCDEFGHIJKLMNOP 2 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
			UN03 ABCDE 1 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
			Goto WIdle
		WIdle:
			UN04 A 0 A_GunFlash("ReadyFlash",GFF_NOEXTCHANGE)
			UN04 ABCDEFGHIJKLMNOP 2 A_WeaponReady
			Loop
		ReadyFlash:
			TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"ReadyFlashCoop")
			TNT1 AAAAAAAAAAAAAAAA 2 A_FireBullets(1,1,1,0, "Unreal_RazorBlankPuff", FBF_NORANDOM|FBF_NOFLASH|FBF_EXPLICITANGLE)
			Stop
		ReadyFlashCoop:
			TNT1 AAAAAAAAAAAAAAAA 2 A_FireBullets(1,1,1,0, "Unreal_RazorBlankPuffCoop", FBF_NORANDOM|FBF_NOFLASH|FBF_EXPLICITANGLE)
			Stop
		Deselect:
			UN08 A 0 A_TakeInventory("Unreal_RazorJackSelected",1)
			UN08 ABCD 2
			Goto InstantDeselect
		InstantDeselect: // I have to copy paste this everytime. Thanks, Zandronum.
			TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
			TNT1 A 1 A_Lower
			Wait
		Fire:
			UN02 A 0 BRIGHT ACS_NamedExecuteWithResult("Unreal_Recoil")
			UN02 A 0 BRIGHT A_SetBlend("9f 2a e3", 0.15, 10)
			UN02 A 0 BRIGHT A_PlaySound("Unreal/RazorJack/Fire", CHAN_BODY)
			UN02 A 0 BRIGHT A_JumpIfInventory("CoopModeOn",1,2)
			UN02 A 2 BRIGHT A_FireCustomMissile("Unreal_Razor1", 0,1,0,8)
			Goto FireEnd
			UN02 A 2 BRIGHT A_FireCustomMissile("Unreal_Razor1Coop", 0,1,0,8)
			Goto FireEnd
		FireEnd:
			UN02 CE 2 BRIGHT
			UN02 CDEF 1 BRIGHT
			UN03 ABCD 1 BRIGHT
			UN03 E 2 BRIGHT A_Refire
			Goto WIdle
		Altfire:
			UN05 ABCDEFGHI 2
			Goto AltHold
		AltHold:
			UN05 J 0 BRIGHT ACS_NamedExecuteWithResult("Unreal_Recoil")
			UN05 J 0 BRIGHT A_SetBlend("9f 2a e3", 0.15, 10)
			UN05 J 0 BRIGHT A_PlaySound("Unreal/RazorJack/Fire", CHAN_BODY)
			UN05 J 0 BRIGHT A_JumpIfInventory("CoopModeOn",1,"AltHoldCoop")
			UN05 J 2 BRIGHT A_FireCustomMissile("Unreal_Razor2", 0,1,-16,8)
			UN05 KL 2 BRIGHT A_FireBullets(1,1,1,0, "Unreal_RazorBlankPuff", FBF_NORANDOM|FBF_NOFLASH|FBF_EXPLICITANGLE)
			UN06 ABC 2 BRIGHT A_FireBullets(1,1,1,0, "Unreal_RazorBlankPuff", FBF_NORANDOM|FBF_NOFLASH|FBF_EXPLICITANGLE)
			Goto AltEnd
		AltHoldCoop:
			UN05 J 2 BRIGHT A_FireCustomMissile("Unreal_Razor2Coop", 0,1,-16,8)	
			UN05 KL 2 BRIGHT A_FireBullets(1,1,1,0, "Unreal_RazorBlankPuffCoop", FBF_NORANDOM|FBF_NOFLASH|FBF_EXPLICITANGLE)
			UN06 ABC 2 BRIGHT A_FireBullets(1,1,1,0, "Unreal_RazorBlankPuffCoop", FBF_NORANDOM|FBF_NOFLASH|FBF_EXPLICITANGLE)
			Goto AltEnd
		AltEnd:
			UN06 D 2 BRIGHT A_Refire
			UN07 A 0 A_GunFlash("AltEndFlash")
			UN07 ABCDE 2 A_WeaponReady(WRF_NOFIRE)
			Goto WIdle
		AltEndFlash:
			TNT1 AAAAA 2 A_FireBullets(1,1,1,0, "Unreal_RazorBlankPuff", FBF_NORANDOM|FBF_NOFLASH|FBF_EXPLICITANGLE)
			Stop
	}
}

Actor Unreal_Razor1
{
	Projectile
	Radius 5
	Height 3
	Damage (0)
	Speed 26
	BounceCount 3
	BounceType Doom
	BounceSound "Unreal/RazorJack/Hitwall"
	DamageType "UnrealHeadshot"
	var int user_damage;
	ExplosionDamage 0
	+BOUNCEONFLOORS
	+BOUNCEONCEILINGS
	+BOUNCEONWALLS
	+CANBOUNCEWATER
	+NOEXTREMEDEATH
	+HITTRACER
	+FORCERADIUSDMG
	+NODAMAGETHRUST
	States
	{
		Spawn:
			UN00 A 0 NODELAY //A_SetUserVar("user_damage",45)
		SpawnLoop:
			UN00 A 0 BRIGHT A_PlaySound("Unreal/RazorJack/BladeLoop", CHAN_BODY, 1.0, True)
			UN00 ABCDEFGH 1 BRIGHT
			Loop
		Death:
		Crash:
			TNT1 A 0 A_PlaySound("Unreal/RazorJack/Hitwall", CHAN_BODY)
			TNT1 A 2 A_Explode(CallACS("UnrealHitBox",30, 105),12,0,0,12)
			Stop
		XDeath:
			TNT1 A 0
			TNT1 A 1 ACS_NamedExecuteWithResult("Unreal_BloodGenerator_Offsets")
			TNT1 A 0 A_GiveInventory("UnrealBloodyPuffGenerator",1,AAPTR_TRACER)
			TNT1 A 0 A_PlaySound("Unreal/RazorJack/HitFlesh", CHAN_BODY)
			TNT1 A 2 A_Explode(CallACS("UnrealHitBox",30, 105),12,0,0,12)
			Stop
	}
}

Actor Unreal_Razor1Coop : Unreal_Razor1 { +THRUSPECIES Species "Player" }

Actor Unreal_Razor2 : Unreal_Razor1
{
	Speed 24
	States
	{
		Spawn:
			UN00 A 0 NODELAY //A_SetUserVar("user_damage",45)
			UN00 A 0 ACS_NamedExecuteAlways("rocketFollow",0,0,0,0)
		SpawnLoop:
			UN00 A 0 BRIGHT A_PlaySound("Unreal/RazorJack/BladeLoop", CHAN_BODY, 1.0, True)
			UN00 ABCDEFGH 1 BRIGHT
			Loop
		Death:
		Crash:
			TNT1 A 0 A_PlaySound("Unreal/RazorJack/Hitwall", CHAN_BODY)
			TNT1 A 2 A_Explode(CallACS("UnrealHitBox",30, 105),12,0,0,12)
			Stop
		XDeath:
			TNT1 A 0
			TNT1 A 1 ACS_NamedExecuteWithResult("Unreal_BloodGenerator_Offsets")
			TNT1 A 0 A_GiveInventory("UnrealBloodyPuffGenerator",1,AAPTR_TRACER)
			TNT1 A 0 A_PlaySound("Unreal/RazorJack/HitFlesh", CHAN_BODY)
			TNT1 A 2 A_Explode(CallACS("UnrealHitBox",30, 105),12,0,0,12)
			Stop
	}
}

Actor Unreal_Razor2Coop : Unreal_Razor2 { +THRUSPECIES Species "Player" }

Actor Unreal_RazorBlankPuff
{
	Scale 0.5
	+BLOODLESSIMPACT
	+NOBLOCKMAP
    +NOGRAVITY
	-SKYEXPLODE
	+FORCEXYBILLBOARD
	+CLIENTSIDEONLY
	+ALWAYSPUFF
    +PUFFONACTORS
    +DONTSPLASH
	+FIXMAPTHINGPOS
	+FORCEXYBILLBOARD
	+NOTRIGGER
	renderstyle add
	alpha 1.0
	Mass 0
    Radius 1
    Height 1
	states
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 ACS_NamedExecute("getLaserCoords",0,x,y,z)
		TNT1 A 1
		stop
	}
}

Actor Unreal_RazorBlankPuffCoop : Unreal_RazorBlankPuff { Species "Player" +THRUSPECIES +MTHRUSPECIES }