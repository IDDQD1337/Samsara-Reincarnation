actor StrifeSigilPieceCheck1 : Boolean {}
actor StrifeSigilPieceCheck2 : Boolean {}
actor StrifeSigilPieceCheck3 : Boolean {}
actor StrifeSigilPieceCheck4 : Boolean {}
actor StrifeSigilPieceCheck5 : Boolean {}
actor StrifeSigilPiece : Counter { Inventory.MaxAmount 5 }
actor StrifeSigilPiecePreferred : Counter { Inventory.MaxAmount 5 }
actor SamsaraStrifeSigilMode3AngleIncrement : Counter { Inventory.MaxAmount 20 }

actor " Sigil " : SamsaraStrifeguyWeapon
{
    Inventory.PickupMessage "You picked up the SIGIL."
    Inventory.PickupSound "strife/sigilcharge"
    Weapon.SlotNumber 8
    Weapon.SelectionOrder 4000
    Obituary "%k \cisent\c- %o \cito the heaven."
    Tag "Sigil"
    +BFG
    +FLOORCLIP
    +NOALERT
    States
    {
      Spawn:
        TNT1 A 0
        TNT1 A 1 A_SpawnItemEx("SamsaraSlotSevenPickup")
        stop

      Ready:
        S1GL E 0 A_JumpIfInventory("StrifeSigilPiece",0,"ReadyStart")
        S1GL D 0 A_JumpIfInventory("StrifeSigilPiece",4,"ReadyStart")
        S1GL C 0 A_JumpIfInventory("StrifeSigilPiece",3,"ReadyStart")
        S1GL B 0 A_JumpIfInventory("StrifeSigilPiece",2,"ReadyStart")
        S1GL A 0
      ReadyStart:
        "####" "#" 0 A_JumpIfInventory("StrifeSigilPiece",2,2)
        "####" "#" 1 A_WeaponReady(WRF_NOSECONDARY)
        goto Ready
        "####" "#" 1 A_WeaponReady
        goto Ready

      Deselect:
        S1GL E 0 A_JumpIfInventory("StrifeSigilPiece",0,"DeselectStart")
        S1GL D 0 A_JumpIfInventory("StrifeSigilPiece",4,"DeselectStart")
        S1GL C 0 A_JumpIfInventory("StrifeSigilPiece",3,"DeselectStart")
        S1GL B 0 A_JumpIfInventory("StrifeSigilPiece",2,"DeselectStart")
        S1GL A 0
      DeselectStart:
        "####" "#" 1 A_Lower
        goto Deselect

      Select:
        S1GL E 0 A_JumpIfInventory("StrifeSigilPiece",0,"SelectStart")
        S1GL D 0 A_JumpIfInventory("StrifeSigilPiece",4,"SelectStart")
        S1GL C 0 A_JumpIfInventory("StrifeSigilPiece",3,"SelectStart")
        S1GL B 0 A_JumpIfInventory("StrifeSigilPiece",2,"SelectStart")
        S1GL A 0
      SelectStart:
        "####" "#" 1 A_Raise
        goto Select

      Fire:
        S1GL E 0 A_JumpIfInventory("StrifeSigilPiece",0,"FireStart")
        S1GL D 0 A_JumpIfInventory("StrifeSigilPiece",4,"FireStart")
        S1GL C 0 A_JumpIfInventory("StrifeSigilPiece",3,"FireStart")
        S1GL B 0 A_JumpIfInventory("StrifeSigilPiece",2,"FireStart")
        S1GL A 0
      FireStart:
        "####" "#" 0 A_AlertMonsters
        "####" "#" 0 A_PlaySound("strife/sigilcharge",CHAN_WEAPON)
        "####" "#" 18 Bright A_Light2
        "####" "#" 3 Bright A_GunFlash
        "####" "#" 15 A_GiveInventory("SamsaraStrifeSigilAttackHandler")
        goto Ready

      Flash:
        SIGF A 4 Bright A_Light2
        SIGF B 6 Bright A_LightInverse
        SIGF C 4 Bright A_Light1
        goto LightDone

      AltFire:
        S1GL E 0 A_JumpIfInventory("StrifeSigilPiece",0,"AltFireStart")
        S1GL D 0 A_JumpIfInventory("StrifeSigilPiece",4,"AltFireStart")
        S1GL C 0 A_JumpIfInventory("StrifeSigilPiece",3,"AltFireStart")
        S1GL B 0 A_JumpIfInventory("StrifeSigilPiece",2,"AltFireStart")
        S1GL A 0
      AltFireStart:
        "####" "#" 0 A_JumpIfInventory("StrifeSigilPiecePreferred",0,"AltFireTo1")
        "####" "#" 0 A_JumpIfInventory("StrifeSigilPiecePreferred",4,"AltFireTo5")
        "####" "#" 0 A_JumpIfInventory("StrifeSigilPiecePreferred",3,"AltFireTo4")
        "####" "#" 0 A_JumpIfInventory("StrifeSigilPiecePreferred",2,"AltFireTo3")
      AltFireTo2:
        "####" "#" 0 A_JumpIfInventory("StrifeSigilPiece",2,1)
        goto AltFireTo1
        "####" "#" 0 A_GiveInventory("StrifeSigilPiecePreferred")
        "####" "#" 0 A_Print("Sigil Mode: Lightning Bolt",1)
        goto AltFireFinish

      AltFireTo3:
        "####" "#" 0 A_JumpIfInventory("StrifeSigilPiece",3,1)
        goto AltFireTo1
        "####" "#" 0 A_GiveInventory("StrifeSigilPiecePreferred")
        "####" "#" 0 A_Print("Sigil Mode: Lightning Wall",1)
        goto AltFireFinish

      AltFireTo4:
        "####" "#" 0 A_JumpIfInventory("StrifeSigilPiece",4,1)
        goto AltFireTo1
        "####" "#" 0 A_GiveInventory("StrifeSigilPiecePreferred")
        "####" "#" 0 A_Print("Sigil Mode: Homing Lightning",1)
        goto AltFireFinish

      AltFireTo5:
        "####" "#" 0 A_JumpIfInventory("StrifeSigilPiece",0,1)
        goto AltFireTo1
        "####" "#" 0 A_GiveInventory("StrifeSigilPiecePreferred")
        "####" "#" 0 A_Print("Sigil Mode: Lightning Storm",1)
        goto AltFireFinish

      AltFireTo1:
        "####" "#" 0 A_TakeInventory("StrifeSigilPiecePreferred")
        "####" "#" 0 A_GiveInventory("StrifeSigilPiecePreferred")
        "####" "#" 0 A_Print("Sigil Mode: Lightning Strike",1)
        goto AltFireFinish

      AltFireFinish:
        "####" "#" 15
        goto Ready
    }
}

actor SamsaraStrifeSigilAttackHandler : Trigger
{
    States
    {
      // Attack stuff
      Pickup:
        TNT1 A 0 A_PlaySound("strife/sigilcharge",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("StrifeSigilPiecePreferred",0,"PickupMode5")
        TNT1 A 0 A_JumpIfInventory("StrifeSigilPiecePreferred",4,"PickupMode4")
        TNT1 A 0 A_JumpIfInventory("StrifeSigilPiecePreferred",3,"PickupMode3")
        TNT1 A 0 A_JumpIfInventory("StrifeSigilPiecePreferred",2,"PickupMode2")
      // Lightning Strike
      PickupMode1:
        TNT1 A 0 Thing_Damage(0,2,0)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupMode1Coop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupMode1Coop")
      PickupMode1DM:
        TNT1 A 0 A_FireCustomMissile("StrifeSigilLightningSpot",0,false)
        stop

      PickupMode1Coop:
        TNT1 A 0 A_FireCustomMissile("StrifeSigilLightningSpot_C",0,false)
        stop

      // Lightning Bolt
      PickupMode2:
        TNT1 A 0 Thing_Damage(0,4,0)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupMode2Coop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupMode2Coop")
      PickupMode2DM:
        TNT1 A 0 A_FireCustomMissile("StrifeSpectralLightningH1",0,false)
        stop

      PickupMode2Coop:
        TNT1 A 0 A_FireCustomMissile("StrifeSpectralLightningH1_C",0,false)
        stop

      // Lightning Wall
      PickupMode3:
        TNT1 A 0 Thing_Damage(0,6,0)
        TNT1 A 0 A_TakeInventory("SamsaraStrifeSigilMode3AngleIncrement") // reset
        TNT1 A 0 A_SetAngle(angle-90)
      PickupMode3Loop:
        TNT1 A 0 A_GiveInventory("SamsaraStrifeSigilMode3AngleIncrement")
        TNT1 A 0 A_SetAngle(angle+9)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupMode3LoopCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupMode3LoopCoop")
      PickupMode3LoopDM:
        TNT1 A 0 A_FireCustomMissile("StrifeSpectralLightningBall1",0,false)
        goto PickupFinishMode3

      PickupMode3LoopCoop:
        TNT1 A 0 A_FireCustomMissile("StrifeSpectralLightningBall1_C",0,false)
        goto PickupFinishMode3

      // Homing Lightning
      PickupMode4:
        TNT1 A 0 Thing_Damage(0,8,0)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupMode4Coop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupMode4Coop")
      PickupMode4DM:
        TNT1 A 0 A_FireCustomMissile("StrifeSigilLightningBigV1",-15,false)
        TNT1 A 0 A_FireCustomMissile("StrifeSigilLightningBigV1",-7.5,false)
        TNT1 A 0 A_FireCustomMissile("StrifeSigilLightningBigV1",0,false)
        TNT1 A 0 A_FireCustomMissile("StrifeSigilLightningBigV1",7.5,false)
        TNT1 A 0 A_FireCustomMissile("StrifeSigilLightningBigV1",15,false)
        stop

      PickupMode4Coop:
        TNT1 A 0 A_FireCustomMissile("StrifeSigilLightningBigV1_C",-15,false)
        TNT1 A 0 A_FireCustomMissile("StrifeSigilLightningBigV1_C",-7.5,false)
        TNT1 A 0 A_FireCustomMissile("StrifeSigilLightningBigV1_C",0,false)
        TNT1 A 0 A_FireCustomMissile("StrifeSigilLightningBigV1_C",7.5,false)
        TNT1 A 0 A_FireCustomMissile("StrifeSigilLightningBigV1_C",15,false)
        stop

      // Lightning Storm
      PickupMode5:
        TNT1 A 0 Thing_Damage(0,10,0)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupMode5Coop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupMode5Coop")
      PickupMode5DM:
        TNT1 A 0 A_FireCustomMissile("StrifeSpectralLightningBigBall",0,false)
        stop

      PickupMode5Coop:
        TNT1 A 0 A_FireCustomMissile("StrifeSpectralLightningBigBall_C",0,false)
        stop

      // Finishing stuff
      // Lightning Wall
      PickupFinishMode3:
        TNT1 A 0 A_JumpIfInventory("SamsaraStrifeSigilMode3AngleIncrement",0,1)
        goto PickupMode3Loop
        TNT1 A 0 A_SetAngle(angle-90)
        stop
    }
}

// Mode 1

actor StrifeSigilLightningSpot : SpectralLightningSpot
{
    DamageType "StrifeSigil"
    SeeSound "strife/sigil"
    DeathSound "strife/sigilhit"
    States
    {
      Spawn:
        ZAP5 A 4 Bright A_Countdown
        TNT1 A 0 A_SpawnItemEx("StrifeSpectralLightningV1Spawner",random(-3,3)*50,random(-3,3)*50,0,0,0,0,0,SXF_TRANSFERPOINTERS)
        TNT1 A 0 A_SpawnItemEx("StrifeSpectralLightningV2",0,0,ceilingz-z-24,0,0,-18,0,SXF_TRANSFERPOINTERS)
        ZAP5 B 4 Bright A_ChangeVelocity(random(-3,3),random(-3,3))
        ZAP5 CD 4 Bright A_Countdown
        loop
    }
}

actor StrifeSigilLightningSpot_C : StrifeSigilLightningSpot
{
    Species "Player"
    +THRUSPECIES
    States
    {
      Spawn:
        ZAP5 A 4 Bright A_Countdown
        TNT1 A 0 A_SpawnItemEx("StrifeSpectralLightningV1_CSpawner",random(-3,3)*50,random(-3,3)*50,0,0,0,0,0,SXF_TRANSFERPOINTERS)
        TNT1 A 0 A_SpawnItemEx("StrifeSpectralLightningV2_C",0,0,ceilingz-z-24,0,0,-18,0,SXF_TRANSFERPOINTERS)
        ZAP5 B 4 Bright A_ChangeVelocity(random(-3,3),random(-3,3))
        ZAP5 CD 4 Bright A_Countdown
        loop
    }
}

actor StrifeSpectralLightningV1Spawner
{
    +NOINTERACTION
    States
    {
      Spawn:
        TNT1 A 0
        TNT1 A 0 A_SpawnItemEx("StrifeSpectralLightningV1",0,0,ceilingz-z-24,0,0,-18,0,SXF_TRANSFERPOINTERS)
        stop
    }
}

actor StrifeSpectralLightningV1_CSpawner : StrifeSpectralLightningV1Spawner
{
    States
    {
      Spawn:
        TNT1 A 0
        TNT1 A 0 A_SpawnItemEx("StrifeSpectralLightningV1_C",0,0,ceilingz-z-24,0,0,-18,0,SXF_TRANSFERPOINTERS)
        stop
    }
}

actor StrifeSpectralLightningV1 : SpectralLightningV1
{
    DamageType "StrifeSigil"
    SeeSound "strife/sigil"
    DeathSound "strife/sigilhit"
    Decal "BaronScorch"
    Obituary "%k \cisent\c- %o \cito the heaven."
    -BLOODSPLATTER
}

actor StrifeSpectralLightningV1_C : StrifeSpectralLightningV1
{
    Species "Player"
    +THRUSPECIES
}

actor StrifeSpectralLightningV2 : StrifeSpectralLightningV1 { Damage 50 }

actor StrifeSpectralLightningV2_C : StrifeSpectralLightningV2
{
    Species "Player"
    +THRUSPECIES
}

// Mode 2

actor StrifeSpectralLightningH1 : SpectralLightningH1
{
    Damage 20
    DamageType "StrifeSigil"
    SeeSound "strife/sigil"
    DeathSound "strife/sigilhit"
    Decal "BaronScorch"
    Obituary "%k \cisent\c- %o \cito the heaven."
    -BLOODSPLATTER
    States
    {
      Spawn:
        ZAP6 A 4 Bright
        ZAP6 BC 4 Bright A_SpawnItemEx("StrifeSpectralLightningHTail",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
        loop
    }
}

actor StrifeSpectralLightningH1_C : StrifeSpectralLightningH1
{
    Species "Player"
    +THRUSPECIES
}

actor StrifeSpectralLightningHTail : SpectralLightningHTail
{
    +CLIENTSIDEONLY
    +FORCEXYBILLBOARD
}

// Mode 3

actor StrifeSpectralLightningBall1 : SpectralLightningBall1
{
    Damage 5
    DamageType "StrifeSigil"
    SeeSound "strife/sigil"
    DeathSound "strife/sigilhit"
    Decal "BaronScorch"
    -BLOODSPLATTER
}

actor StrifeSpectralLightningBall1_C : StrifeSpectralLightningBall1
{
    Species "Player"
    +THRUSPECIES
}

// Mode 4

actor StrifeSigilLightningBigV1 : SpectralLightningBigV1
{
    Damage 25
    DamageType "StrifeSigil"
    SeeSound "strife/sigil" 
    DeathSound "strife/sigilhit"
    Decal "BaronScorch"
    -BLOODSPLATTER
    States
    {
      Spawn:
        ZOT2 A 0 Bright
        TNT1 A 0 A_TransferPointer(AAPTR_TARGET,AAPTR_DEFAULT,AAPTR_PLAYER_GETTARGET,AAPTR_TRACER)
        TNT1 A 0 A_CheckFlag("SHOOTABLE",2,AAPTR_TRACER)
        ZOT2 A 4 Bright A_ChangeVelocity(28,0,0,CVF_RELATIVE)
        goto Super::Spawn+1
        ZOT2 A 4 Bright
        goto Super::Spawn+1
    }
}

actor StrifeSigilLightningBigV1_C : StrifeSigilLightningBigV1
{
    Species "Player"
    +THRUSPECIES
}

// Mode 5

actor StrifeSpectralLightningBigBall : SpectralLightningBigBall1
{
    Radius 16
    Height 32
    Damage (random(1,4)*100)
    DamageType "StrifeSigil"
    Decal "BaronScorch"
    -BLOODSPLATTER
    +FORCEXYBILLBOARD
    -STRIFEDAMAGE
    States
    {
      Spawn:
        ZAP7 A 4 Bright
        TNT1 A 0 A_CustomMissile("StrifeSpectralLightningH3",8,0,-90,CMF_AIMDIRECTION|CMF_TRACKOWNER)
        TNT1 A 0 A_CustomMissile("StrifeSpectralLightningH3",8,0,90,CMF_AIMDIRECTION|CMF_TRACKOWNER)
        ZAP7 B 4 Bright A_CustomMissile("StrifeSpectralLightningH3",8,0,0,CMF_AIMDIRECTION|CMF_TRACKOWNER)
        TNT1 A 0 A_CustomMissile("StrifeSpectralLightningH3",8,0,-90,CMF_AIMDIRECTION|CMF_TRACKOWNER)
        TNT1 A 0 A_CustomMissile("StrifeSpectralLightningH3",8,0,90,CMF_AIMDIRECTION|CMF_TRACKOWNER)
        ZAP7 C 4 Bright A_CustomMissile("StrifeSpectralLightningH3",8,0,0,CMF_AIMDIRECTION|CMF_TRACKOWNER)
        TNT1 A 0 A_CustomMissile("StrifeSpectralLightningH3",8,0,-90,CMF_AIMDIRECTION|CMF_TRACKOWNER)
        TNT1 A 0 A_CustomMissile("StrifeSpectralLightningH3",8,0,90,CMF_AIMDIRECTION|CMF_TRACKOWNER)
        ZAP7 D 4 Bright A_CustomMissile("StrifeSpectralLightningH3",8,0,0,CMF_AIMDIRECTION|CMF_TRACKOWNER)
        TNT1 A 0 A_CustomMissile("StrifeSpectralLightningH3",8,0,-90,CMF_AIMDIRECTION|CMF_TRACKOWNER)
        TNT1 A 0 A_CustomMissile("StrifeSpectralLightningH3",8,0,90,CMF_AIMDIRECTION|CMF_TRACKOWNER)
        ZAP7 E 4 Bright A_CustomMissile("StrifeSpectralLightningH3",8,0,0,CMF_AIMDIRECTION|CMF_TRACKOWNER)
        TNT1 A 0 A_CustomMissile("StrifeSpectralLightningH3",8,0,-90,CMF_AIMDIRECTION|CMF_TRACKOWNER)
        TNT1 A 0 A_CustomMissile("StrifeSpectralLightningH3",8,0,90,CMF_AIMDIRECTION|CMF_TRACKOWNER)
        TNT1 A 0 A_CustomMissile("StrifeSpectralLightningH3",8,0,0,CMF_AIMDIRECTION|CMF_TRACKOWNER)
        loop
    }
}

actor StrifeSpectralLightningBigBall_C : StrifeSpectralLightningBigBall
{
    Species "Player"
    +THRUSPECIES
    States
    {
      Spawn:
        ZAP7 A 4 Bright
        TNT1 A 0 A_CustomMissile("StrifeSpectralLightningH3_C",8,0,-90,CMF_AIMDIRECTION|CMF_TRACKOWNER)
        TNT1 A 0 A_CustomMissile("StrifeSpectralLightningH3_C",8,0,90,CMF_AIMDIRECTION|CMF_TRACKOWNER)
        ZAP7 B 4 Bright A_CustomMissile("StrifeSpectralLightningH3_C",8,0,0,CMF_AIMDIRECTION|CMF_TRACKOWNER)
        TNT1 A 0 A_CustomMissile("StrifeSpectralLightningH3_C",8,0,-90,CMF_AIMDIRECTION|CMF_TRACKOWNER)
        TNT1 A 0 A_CustomMissile("StrifeSpectralLightningH3_C",8,0,90,CMF_AIMDIRECTION|CMF_TRACKOWNER)
        ZAP7 C 4 Bright A_CustomMissile("StrifeSpectralLightningH3_C",8,0,0,CMF_AIMDIRECTION|CMF_TRACKOWNER)
        TNT1 A 0 A_CustomMissile("StrifeSpectralLightningH3_C",8,0,-90,CMF_AIMDIRECTION|CMF_TRACKOWNER)
        TNT1 A 0 A_CustomMissile("StrifeSpectralLightningH3_C",8,0,90,CMF_AIMDIRECTION|CMF_TRACKOWNER)
        ZAP7 D 4 Bright A_CustomMissile("StrifeSpectralLightningH3_C",8,0,0,CMF_AIMDIRECTION|CMF_TRACKOWNER)
        TNT1 A 0 A_CustomMissile("StrifeSpectralLightningH3_C",8,0,-90,CMF_AIMDIRECTION|CMF_TRACKOWNER)
        TNT1 A 0 A_CustomMissile("StrifeSpectralLightningH3_C",8,0,90,CMF_AIMDIRECTION|CMF_TRACKOWNER)
        ZAP7 E 4 Bright A_CustomMissile("StrifeSpectralLightningH3_C",8,0,0,CMF_AIMDIRECTION|CMF_TRACKOWNER)
        TNT1 A 0 A_CustomMissile("StrifeSpectralLightningH3_C",8,0,-90,CMF_AIMDIRECTION|CMF_TRACKOWNER)
        TNT1 A 0 A_CustomMissile("StrifeSpectralLightningH3_C",8,0,90,CMF_AIMDIRECTION|CMF_TRACKOWNER)
        TNT1 A 0 A_CustomMissile("StrifeSpectralLightningH3_C",8,0,0,CMF_AIMDIRECTION|CMF_TRACKOWNER)
        loop
    }
}

actor StrifeSpectralLightningH3 : StrifeSpectralLightningH1
{
    Damage (random(1,4)*35)
    SeeSound ""
    +FORCERADIUSDMG
    +FORCEXYBILLBOARD
    -STRIFEDAMAGE
}

actor StrifeSpectralLightningH3_C : StrifeSpectralLightningH3
{
    Species "Player"
    +THRUSPECIES
}
