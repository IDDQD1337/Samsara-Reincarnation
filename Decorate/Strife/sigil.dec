Actor " Sigil " : Weapon
{
	Weapon.Kickback 100
	+WEAPON.NOALERT
	+WEAPON.BFG
	+INVENTORY.UNDROPPABLE
	Inventory.PickupSound "strife/sigilcharge"
	Inventory.PickupMessage "" //"You picked up the Sigil."
	Weapon.SelectionOrder 100
	Weapon.SlotNumber 8
	Obituary "%k \cisent\c- %o \cito the heaven."
	Inventory.RestrictedTo "StrifeguyPlayer"
	Tag "Sigil"
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 1 A_SpawnItemEx("SamsaraSlotSevenPickup")
		Stop
	Ready:
		TNT1 A 0 A_JumpIfInventory("StrifeSigilPiece", 5, "Ready5")
		TNT1 A 0 A_JumpIfInventory("StrifeSigilPiece", 4, "Ready4")
		TNT1 A 0 A_JumpIfInventory("StrifeSigilPiece", 3, "Ready3")
		TNT1 A 0 A_JumpIfInventory("StrifeSigilPiece", 2, "Ready2")
		S1GL A 1 A_WeaponReady
		Loop
	Ready2:
		TNT1 A 0 A_JumpIfInventory("StrifeSigilPiece", 5, "Ready5")
		TNT1 A 0 A_JumpIfInventory("StrifeSigilPiece", 4, "Ready4")
		TNT1 A 0 A_JumpIfInventory("StrifeSigilPiece", 3, "Ready3")
		S1GL B 1 A_WeaponReady
		Loop
	Ready3:
		TNT1 A 0 A_JumpIfInventory("StrifeSigilPiece", 5, "Ready5")
		TNT1 A 0 A_JumpIfInventory("StrifeSigilPiece", 4, "Ready4")
		S1GL C 1 A_WeaponReady
		Loop
	Ready4:
		TNT1 A 0 A_JumpIfInventory("StrifeSigilPiece", 5, "Ready5")
		S1GL D 1 A_WeaponReady
		Loop
	Ready5:
		S1GL E 1 A_WeaponReady
		Loop
	Deselect:
		TNT1 A 0 A_JumpIfInventory("StrifeSigilPiece", 5, "Deselect5")
		TNT1 A 0 A_JumpIfInventory("StrifeSigilPiece", 4, "Deselect4")
		TNT1 A 0 A_JumpIfInventory("StrifeSigilPiece", 3, "Deselect3")
		TNT1 A 0 A_JumpIfInventory("StrifeSigilPiece", 2, "Deselect2")
		TNT1 A 0 A_JumpIfInventory("StrifeSigilPiece", 1, "Deselect1")
	Deselect1:
		S1GL A 1 A_Lower
		Loop
	Deselect2:
		S1GL B 1 A_Lower
		Loop
	Deselect3:
		S1GL C 1 A_Lower
		Loop
	Deselect4:
		S1GL D 1 A_Lower
		Loop
	Deselect5:
		S1GL E 1 A_Lower
		Loop
	Select:
		TNT1 A 0 A_JumpIfInventory("StrifeSigilPiece", 5, "Select5")
		TNT1 A 0 A_JumpIfInventory("StrifeSigilPiece", 4, "Select4")
		TNT1 A 0 A_JumpIfInventory("StrifeSigilPiece", 3, "Select3")
		TNT1 A 0 A_JumpIfInventory("StrifeSigilPiece", 2, "Select2")
		TNT1 A 0 A_JumpIfInventory("StrifeSigilPiece", 1, "Select1")
	Select1:
		S1GL A 1 A_Raise
		Loop
	Select2:
		S1GL B 1 A_Raise
		Loop
	Select3:
		S1GL C 1 A_Raise
		Loop
	Select4:
		S1GL D 1 A_Raise
		Loop
	Select5:
		S1GL E 1 A_Raise
		Loop
	Fire:
		"####" "#" 0 A_PlaySound("strife/sigilcharge", CHAN_WEAPON)
		"####" "#" 18 Bright A_Light2
		"####" "#" 3 Bright A_GunFlash
		"####" "#" 0 A_AlertMonsters
		"####" "#" 0 A_PlaySound("strife/sigilcharge", CHAN_WEAPON)
		"####" "#" 0 A_JumpIfInventory("StrifeSigilPiecePreferred", 5, "Fire5")
		"####" "#" 0 A_JumpIfInventory("StrifeSigilPiecePreferred", 4, "Fire4")
		"####" "#" 0 A_JumpIfInventory("StrifeSigilPiecePreferred", 3, "Fire3")
		"####" "#" 0 A_JumpIfInventory("StrifeSigilPiecePreferred", 2, "Fire2")
		Goto Fire1
	Fire1:
		"####" "#" 0 DamageThing(2)
		"####" "#" 0 A_JumpIfInventory("CoopModeOn",1,2)
		"####" "#" 0 A_FireCustomMissile("StrifeSigilLightningSpot", 0, 0)
		Goto ContinueFire1
		"####" "#" 0 A_FireCustomMissile("StrifeSigilLightningSpot_C", 0, 0)
		Goto ContinueFire1
	ContinueFire1:
		"####" "#" 15
		Goto Ready
	Fire2:
		"####" "#" 0 DamageThing(4)
		"####" "#" 0 A_JumpIfInventory("CoopModeOn",1,2)
		"####" "#" 0 A_FireCustomMissile("StrifeSpectralLightningH1", 0, 0)
		Goto ContinueFire1
		"####" "#" 0 A_FireCustomMissile("StrifeSpectralLightningH1_C", 0, 0)
		Goto ContinueFire1
	Fire3:
		"####" "#" 0 DamageThing(6)
		"####" "#" 0 A_GiveInventory("Sigil3AttackCounter", 20)
		"####" "#" 0 A_SetAngle(angle-90)
		Goto Fire3Loop
	Fire3Loop:
		"####" "#" 0 A_SetAngle(angle+9)
		"####" "#" 0 A_JumpIfInventory("CoopModeOn",1,2)
		"####" "#" 0 A_FireCustomMissile("StrifeSpectralLightningBall1", 0, 0)
		Goto ContinueFireLoop
		"####" "#" 0 A_FireCustomMissile("StrifeSpectralLightningBall1_C", 0, 0)
		Goto ContinueFireLoop
	ContinueFireLoop:
		"####" "#" 0 A_TakeInventory("Sigil3AttackCounter", 1)
		"####" "#" 0 A_JumpIfInventory("Sigil3AttackCounter", 1, "Fire3Loop")
		"####" "#" 0 A_SetAngle(angle-90)
		Goto ContinueFire1
	Fire4:
		"####" "#" 0 DamageThing(8)
		"####" "#" 0 A_JumpIfInventory("CoopModeOn",1,"Fire4Coop")
		"####" "#" 0 A_FireCustomMissile("StrifeSigilLightningBigV1", -15, 0)
		"####" "#" 0 A_FireCustomMissile("StrifeSigilLightningBigV1", 15, 0)
		"####" "#" 0 A_FireCustomMissile("StrifeSigilLightningBigV1", -7.5, 0)
		"####" "#" 0 A_FireCustomMissile("StrifeSigilLightningBigV1", 7.5, 0)
		"####" "#" 0 A_FireCustomMissile("StrifeSigilLightningBigV1", 0, 0)
		Goto ContinueFire1
	Fire4Coop:
		"####" "#" 0 A_FireCustomMissile("StrifeSigilLightningBigV1_C", -15, 0)
		"####" "#" 0 A_FireCustomMissile("StrifeSigilLightningBigV1_C", 15, 0)
		"####" "#" 0 A_FireCustomMissile("StrifeSigilLightningBigV1_C", -7.5, 0)
		"####" "#" 0 A_FireCustomMissile("StrifeSigilLightningBigV1_C", 7.5, 0)
		"####" "#" 0 A_FireCustomMissile("StrifeSigilLightningBigV1_C", 0, 0)
		Goto ContinueFire1
	Fire5:
		"####" "#" 0 DamageThing(10)
		"####" "#" 0 Bright A_JumpIfInventory("CoopModeOn",1,2)
		"####" "#" 10 Bright A_FireCustomMissile("StrifeSpectralLightningBigBall")
		Goto ContinueFire5
		"####" "#" 10 Bright A_FireCustomMissile("StrifeSpectralLightningBigBall_C")
		Goto ContinueFire5
	ContinueFire5:
		"####" "#" 5
		Goto Ready
	AltFire:
		"####" "#" 0 A_JumpIfInventory("StrifeSigilPiecePreferred",0,"AltFire1")
		"####" "#" 0 A_JumpIfInventory("StrifeSigilPiecePreferred",4,"AltFire5")
		"####" "#" 0 A_JumpIfInventory("StrifeSigilPiecePreferred",3,"AltFire4")
		"####" "#" 0 A_JumpIfInventory("StrifeSigilPiecePreferred",2,"AltFire3")
		"####" "#" 0 A_JumpIfInventory("StrifeSigilPiecePreferred",1,"AltFire2")
		Goto Ready
	AltFire1:
		"####" "#" 0 A_TakeInventory("StrifeSigilPiecePreferred",5)
		"####" "#" 0 A_GiveInventory("StrifeSigilPiecePreferred",1)
		"####" "#" 15 A_Print("Sigil Mode: Lightning Strike",1.0)
		Goto Ready
	AltFire2:
		"####" "#" 0 A_JumpIfInventory("StrifeSigilPiece",2,1)
		Goto AltFire1
		"####" "#" 0 A_GiveInventory("StrifeSigilPiecePreferred",1)
		"####" "#" 15 A_Print("Sigil Mode: Lightning Bolt",1.0)
		Goto Ready
	AltFire3:
		"####" "#" 0 A_JumpIfInventory("StrifeSigilPiece",3,1)
		Goto AltFire1
		"####" "#" 0 A_GiveInventory("StrifeSigilPiecePreferred",1)
		"####" "#" 15 A_Print("Sigil Mode: Lightning Wall",1.0)
		Goto Ready
	AltFire4:
		"####" "#" 0 A_JumpIfInventory("StrifeSigilPiece",4,1)
		Goto AltFire1
		"####" "#" 0 A_GiveInventory("StrifeSigilPiecePreferred",1)
		"####" "#" 15 A_Print("Sigil Mode: Homing Lightning",1.0)
		Goto Ready
	AltFire5:
		"####" "#" 0 A_JumpIfInventory("StrifeSigilPiece",5,1)
		Goto AltFire1
		"####" "#" 0 A_GiveInventory("StrifeSigilPiecePreferred",1)
		"####" "#" 15 A_Print("Sigil Mode: Lightning Storm",1.0)
		Goto Ready
	Flash:
		SIGF A 4 Bright A_Light2
		SIGF B 6 Bright A_LightInverse
		SIGF C 4 Bright A_Light1
		SIGF C 0 Bright A_Light0
		Stop
	}
}

Actor StrifeSigilPiece : Inventory { Inventory.Amount 1 Inventory.MaxAmount 5 Inventory.InterHubAmount 5 }
Actor StrifeSigilPiecePreferred : Inventory { Inventory.Amount 1 Inventory.MaxAmount 5 Inventory.InterHubAmount 5 }
Actor Sigil3AttackCounter : Inventory { Inventory.Amount 1 Inventory.MaxAmount 20 }

Actor StrifeSigilLightningSpot : SpectralLightningSpot
{
	SeeSound "strife/sigil"
	DeathSound "strife/sigilhit"
	DamageType "StrifeSigil"
	+SPECTRAL
	States
	{
	Spawn:
		ZAP5 A 4 Bright A_Countdown
		ZAP5 B 0 Bright A_SpawnItemEx("StrifeSpectralLightningV1Spawner",random(-3,3) * 50,random(-3,3) * 50,0,0,0,0,0,SXF_TRANSFERPOINTERS)
		ZAP5 B 0 Bright A_SpawnItemEx("StrifeSpectralLightningV2",0,0,ceilingz - z - 24,0,0,-18,0,SXF_TRANSFERPOINTERS)
		ZAP5 B 4 Bright A_ChangeVelocity(random(-3,3), random(-3,3))
		ZAP5 CD 4 Bright A_Countdown
		Loop
	}
}

Actor StrifeSigilLightningSpot_C : StrifeSigilLightningSpot
{
	+THRUSPECIES
	+DONTHARMSPECIES
	+DONTHARMCLASS
	+MTHRUSPECIES
	Species "Player"
	States
	{
	Spawn:
		ZAP5 A 4 Bright A_Countdown
		ZAP5 B 0 Bright A_SpawnItemEx("StrifeSpectralLightningV1_CSpawner",random(-3,3) * 50,random(-3,3) * 50,0,0,0,0,0,SXF_TRANSFERPOINTERS)
		ZAP5 B 0 Bright A_SpawnItemEx("StrifeSpectralLightningV2_C",0,0,ceilingz - z - 24,0,0,-18,0,SXF_TRANSFERPOINTERS)
		ZAP5 B 4 Bright A_ChangeVelocity(random(-3,3), random(-3,3))
		ZAP5 CD 4 Bright A_Countdown
		Loop
	}
}

Actor StrifeSpectralLightningV1Spawner
{
	+NOINTERACTION
	DamageType "StrifeSigil"
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_SpawnItemEx("StrifeSpectralLightningV1",0,0,ceilingz - z - 24,0,0,-18,0,SXF_TRANSFERPOINTERS)
		Stop
	}
}

Actor StrifeSpectralLightningV1_CSpawner : StrifeSpectralLightningV1Spawner
{
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_SpawnItemEx("StrifeSpectralLightningV1_C",0,0,ceilingz - z - 24,0,0,-18,0,SXF_TRANSFERPOINTERS)
		Stop
	}
}

Actor StrifeSpectralLightningV1 : SpectralLightningV1
{
	SeeSound "strife/sigil"
	DeathSound "strife/sigilhit"
	Decal "BaronScorch"
	DamageType "StrifeSigil"
	Obituary "%k \cisent\c- %o \cito the heaven."
    -BLOODSPLATTER
}
Actor StrifeSpectralLightningV1_C : StrifeSpectralLightningV1 { Species "Player" +THRUSPECIES +DONTHARMSPECIES +DONTHARMCLASS +MTHRUSPECIES }

Actor StrifeSpectralLightningV2 : StrifeSpectralLightningV1 { Damage 50 }
Actor StrifeSpectralLightningV2_C : StrifeSpectralLightningV2 { Species "Player" +THRUSPECIES +DONTHARMSPECIES +DONTHARMCLASS +MTHRUSPECIES }

Actor StrifeSpectralLightningH1 : SpectralLightningH1
{
	Damage 20
	SeeSound "strife/sigil"
	DeathSound "strife/sigilhit"
	Decal "BaronScorch"
	DamageType "StrifeSigil"
    -BLOODSPLATTER
	States
	{
	Spawn:
		ZAP6 A 4 Bright
		ZAP6 BC 4 Bright A_SpawnItemEx("StrifeSpectralLightningHTail",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
		Loop
	}
}
Actor StrifeSpectralLightningH1_C : StrifeSpectralLightningH1 { Species "Player" +THRUSPECIES +DONTHARMSPECIES +DONTHARMCLASS +MTHRUSPECIES }

Actor StrifeSpectralLightningBall1 : SpectralLightningBall1
{
	Damage 5
	SeeSound "strife/sigil"
	DeathSound "strife/sigilhit"
	Decal "BaronScorch"
	DamageType "StrifeSigil"
    -BLOODSPLATTER
}
Actor StrifeSpectralLightningBall1_C : StrifeSpectralLightningBall1 { Species "Player" +THRUSPECIES +DONTHARMSPECIES +DONTHARMCLASS +MTHRUSPECIES }

Actor StrifeSigilLightningBigV1 : SpectralLightningBigV1
{
	SeeSound "strife/sigil" 
	DeathSound "strife/sigilhit"
	Damage 25
	Decal "BaronScorch"
	DamageType "StrifeSigil"
    -BLOODSPLATTER
	+SPECTRAL
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_TransferPointer(AAPTR_TARGET, AAPTR_DEFAULT, AAPTR_PLAYER_GETTARGET, AAPTR_TRACER)
		TNT1 A 0 A_CheckFlag("SHOOTABLE", 2, AAPTR_TRACER)
		ZOT2 A 4 Bright A_ChangeVelocity(28,0,0, CVF_RELATIVE)
		Goto Super::Spawn+1
		ZOT2 A 4 Bright
		Goto Super::Spawn+1
	}
}
Actor StrifeSigilLightningBigV1_C : StrifeSigilLightningBigV1 { +THRUSPECIES +DONTHARMSPECIES +DONTHARMCLASS +MTHRUSPECIES Species "Player" }

Actor StrifeSpectralLightningH3 : StrifeSpectralLightningH1
{
	+FORCEXYBILLBOARD
	+FORCERADIUSDMG
	Damage (random(1,4)*35)
	SeeSound ""
	Obituary "%k \cisent\c- %o \cito the heaven."
	DamageType "StrifeSigil"
}
Actor StrifeSpectralLightningH3_C : StrifeSpectralLightningH3 { +THRUSPECIES Species "Player" +DONTHARMCLASS +DONTHARMSPECIES +MTHRUSPECIES }

Actor StrifeSpectralLightningHTail : SpectralLightningHTail { +FORCEXYBILLBOARD +CLIENTSIDEONLY }

Actor StrifeSpectralLightningBigBall : SpectralLightningBigBall1
{
    -BLOODSPLATTER
	+FORCEXYBILLBOARD
	Radius 16
	Height 32
	Damage (random(1,4)*100)
	Decal "BaronScorch"
	DamageType "StrifeSigil"
	States
	{
	Spawn:
		ZAP7 A 0 Bright A_CustomMissile("StrifeSpectralLightningH3",8,0,-90,CMF_TRACKOWNER|CMF_AIMDIRECTION)
		ZAP7 A 0 Bright A_CustomMissile("StrifeSpectralLightningH3",8,0,90,CMF_TRACKOWNER|CMF_AIMDIRECTION)
		ZAP7 A 4 Bright A_CustomMissile("StrifeSpectralLightningH3",8,0,0,CMF_TRACKOWNER|CMF_AIMDIRECTION)
		ZAP7 B 0 Bright A_CustomMissile("StrifeSpectralLightningH3",8,0,-90,CMF_TRACKOWNER|CMF_AIMDIRECTION)
		ZAP7 B 0 Bright A_CustomMissile("StrifeSpectralLightningH3",8,0,90,CMF_TRACKOWNER|CMF_AIMDIRECTION)
		ZAP7 B 4 Bright A_CustomMissile("StrifeSpectralLightningH3",8,0,0,CMF_TRACKOWNER|CMF_AIMDIRECTION)
		ZAP7 C 0 Bright A_CustomMissile("StrifeSpectralLightningH3",8,0,-90,CMF_TRACKOWNER|CMF_AIMDIRECTION)
		ZAP7 C 0 Bright A_CustomMissile("StrifeSpectralLightningH3",8,0,90,CMF_TRACKOWNER|CMF_AIMDIRECTION)
		ZAP7 C 4 Bright A_CustomMissile("StrifeSpectralLightningH3",8,0,0,CMF_TRACKOWNER|CMF_AIMDIRECTION)
		ZAP7 D 0 Bright A_CustomMissile("StrifeSpectralLightningH3",8,0,-90,CMF_TRACKOWNER|CMF_AIMDIRECTION)
		ZAP7 D 0 Bright A_CustomMissile("StrifeSpectralLightningH3",8,0,90,CMF_TRACKOWNER|CMF_AIMDIRECTION)
		ZAP7 D 4 Bright A_CustomMissile("StrifeSpectralLightningH3",8,0,0,CMF_TRACKOWNER|CMF_AIMDIRECTION)
		ZAP7 E 0 Bright A_CustomMissile("StrifeSpectralLightningH3",8,0,-90,CMF_TRACKOWNER|CMF_AIMDIRECTION)
		ZAP7 E 0 Bright A_CustomMissile("StrifeSpectralLightningH3",8,0,90,CMF_TRACKOWNER|CMF_AIMDIRECTION)
		ZAP7 E 4 Bright A_CustomMissile("StrifeSpectralLightningH3",8,0,0,CMF_TRACKOWNER|CMF_AIMDIRECTION)
		Loop
	}
}

Actor StrifeSpectralLightningBigBall_C : StrifeSpectralLightningBigBall
{
	+THRUSPECIES
	+DONTHARMCLASS
	+DONTHARMSPECIES
	+MTHRUSPECIES
	Species "Player"
	States
	{
	Spawn:
		ZAP7 A 0 Bright A_CustomMissile("StrifeSpectralLightningH3_C",8,0,-90,CMF_TRACKOWNER|CMF_AIMDIRECTION)
		ZAP7 A 0 Bright A_CustomMissile("StrifeSpectralLightningH3_C",8,0,90,CMF_TRACKOWNER|CMF_AIMDIRECTION)
		ZAP7 A 4 Bright A_CustomMissile("StrifeSpectralLightningH3_C",8,0,0,CMF_TRACKOWNER|CMF_AIMDIRECTION)
		ZAP7 B 0 Bright A_CustomMissile("StrifeSpectralLightningH3_C",8,0,-90,CMF_TRACKOWNER|CMF_AIMDIRECTION)
		ZAP7 B 0 Bright A_CustomMissile("StrifeSpectralLightningH3_C",8,0,90,CMF_TRACKOWNER|CMF_AIMDIRECTION)
		ZAP7 B 4 Bright A_CustomMissile("StrifeSpectralLightningH3_C",8,0,0,CMF_TRACKOWNER|CMF_AIMDIRECTION)
		ZAP7 C 0 Bright A_CustomMissile("StrifeSpectralLightningH3_C",8,0,-90,CMF_TRACKOWNER|CMF_AIMDIRECTION)
		ZAP7 C 0 Bright A_CustomMissile("StrifeSpectralLightningH3_C",8,0,90,CMF_TRACKOWNER|CMF_AIMDIRECTION)
		ZAP7 C 4 Bright A_CustomMissile("StrifeSpectralLightningH3_C",8,0,0,CMF_TRACKOWNER|CMF_AIMDIRECTION)
		ZAP7 D 0 Bright A_CustomMissile("StrifeSpectralLightningH3_C",8,0,-90,CMF_TRACKOWNER|CMF_AIMDIRECTION)
		ZAP7 D 0 Bright A_CustomMissile("StrifeSpectralLightningH3_C",8,0,90,CMF_TRACKOWNER|CMF_AIMDIRECTION)
		ZAP7 D 4 Bright A_CustomMissile("StrifeSpectralLightningH3_C",8,0,0,CMF_TRACKOWNER|CMF_AIMDIRECTION)
		ZAP7 E 0 Bright A_CustomMissile("StrifeSpectralLightningH3_C",8,0,-90,CMF_TRACKOWNER|CMF_AIMDIRECTION)
		ZAP7 E 0 Bright A_CustomMissile("StrifeSpectralLightningH3_C",8,0,90,CMF_TRACKOWNER|CMF_AIMDIRECTION)
		ZAP7 E 4 Bright A_CustomMissile("StrifeSpectralLightningH3_C",8,0,0,CMF_TRACKOWNER|CMF_AIMDIRECTION)
		Loop
	}
}

Actor StrifeSigilPieceCheck1 : Inventory { Inventory.Amount 1 Inventory.MaxAmount 1 }
Actor StrifeSigilPieceCheck2 : Inventory { Inventory.Amount 1 Inventory.MaxAmount 1 }
Actor StrifeSigilPieceCheck3 : Inventory { Inventory.Amount 1 Inventory.MaxAmount 1 }
Actor StrifeSigilPieceCheck4 : Inventory { Inventory.Amount 1 Inventory.MaxAmount 1 }
Actor StrifeSigilPieceCheck5 : Inventory { Inventory.Amount 1 Inventory.MaxAmount 1 }