actor "Flame Thrower" : SamsaraStrifeguyWeapon
{
    Inventory.PickupMessage "You picked up the flame thrower."
    Weapon.UpSound "strife/flameidle"
    Weapon.ReadySound "strife/flameidle"
    Weapon.SlotNumber 6
    Weapon.SelectionOrder 1500
    Weapon.AmmoType "Cell"
    Weapon.AmmoGive 100
    Weapon.AmmoUse 1
    Obituary "%k \cibarbecued\c- %o\ci."
    Tag "Flame Thrower"
    +FLOORCLIP
    States
    {
      Spawn:
        SFLM A -1
        stop

      Ready:
        SFLT AB 3 A_WeaponReady
        loop

      Deselect:
        SFLT A 1 A_Lower
        loop

      Select:
        SFLT A 1 A_Raise
        loop

      Fire:
        SFLT D 2 Bright A_GiveInventory("SamsaraStrifeFlameThrowerAttackHandler")
        SFLT E 3 bright A_ReFire
        goto Ready
    }
}

actor SamsaraStrifeFlameThrowerAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Cell",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Cell",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_SetAngle(angle+(random(0,128)-random(0,128))*90.0/4096.0)
        TNT1 A 0 A_JumpIfInventory("AccuracyUpgrade2",0,"PickupAttack100")
        TNT1 A 0 A_JumpIfInventory("AccuracyUpgrade2",80,"PickupAttack80")
        TNT1 A 0 A_JumpIfInventory("AccuracyUpgrade2",60,"PickupAttack60")
        TNT1 A 0 A_JumpIfInventory("AccuracyUpgrade2",40,"PickupAttack40")
        TNT1 A 0 A_JumpIfInventory("AccuracyUpgrade2",20,"PickupAttack20")
      // 0 accuracy upgrades
      PickupAttack0:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttack0Coop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttack0Coop")
      PickupAttack0DM:
        TNT1 A 0 A_FireCustomMissile("StrifeFlameMissile",0,false,0,0,0,12)
        stop

      PickupAttack0Coop:
        TNT1 A 0 A_FireCustomMissile("StrifeFlameMissileCoop",0,false,0,0,0,12)
        stop

      // 2 accuracy upgrades
      PickupAttack20:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttack20Coop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttack20Coop")
      PickupAttack20DM:
        TNT1 A 0 A_FireCustomMissile("StrifeFlameMissile20",0,false,0,0,0,10)
        stop

      PickupAttack20Coop:
        TNT1 A 0 A_FireCustomMissile("StrifeFlameMissileCoop20",0,false,0,0,0,10)
        stop

      // 4 accuracy upgrades
      PickupAttack40:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttack40Coop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttack40Coop")
      PickupAttack40DM:
        TNT1 A 0 A_FireCustomMissile("StrifeFlameMissile40",0,false,0,0,0,8)
        stop

      PickupAttack40Coop:
        TNT1 A 0 A_FireCustomMissile("StrifeFlameMissileCoop40",0,false,0,0,0,8)
        stop

      // 6 accuracy upgrades
      PickupAttack60:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttack60Coop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttack60Coop")
      PickupAttack60DM:
        TNT1 A 0 A_FireCustomMissile("StrifeFlameMissile60",0,false,0,0,0,6)
        stop

      PickupAttack60Coop:
        TNT1 A 0 A_FireCustomMissile("StrifeFlameMissileCoop60",0,false,0,0,0,6)
        stop

      // 8 accuracy upgrades
      PickupAttack80:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttack80Coop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttack80Coop")
      PickupAttack80DM:
        TNT1 A 0 A_FireCustomMissile("StrifeFlameMissile80",0,false,0,0,0,4)
        stop

      PickupAttack80Coop:
        TNT1 A 0 A_FireCustomMissile("StrifeFlameMissileCoop80",0,false,0,0,0,4)
        stop

      // 10 accuracy upgrades
      PickupAttack100:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttack100Coop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttack100Coop")
      PickupAttack100DM:
        TNT1 A 0 A_FireCustomMissile("StrifeFlameMissile100",0,false,0,0,0,2)
        stop

      PickupAttack100Coop:
        TNT1 A 0 A_FireCustomMissile("StrifeFlameMissileCoop100",0,false,0,0,0,2)
        stop
    }
}

actor StrifeFlameMissile : FlameMissile
{
    Speed 21 // Originally 15
    Damage (random(1,4)*8)
    DamageType "PhosFire"
    SeeSound "strife/flamefire"
    Decal "CacoScorch"
    Obituary "%k \cibarbecued\c- %o\ci."
    -BLOODSPLATTER
    -STRIFEDAMAGE
    +WINDTHRUST
}

actor StrifeFlameMissile20 : StrifeFlameMissile { Speed 26 }
actor StrifeFlameMissile40 : StrifeFlameMissile { Speed 31 }
actor StrifeFlameMissile60 : StrifeFlameMissile { Speed 36 }
actor StrifeFlameMissile80 : StrifeFlameMissile { Speed 41 }
actor StrifeFlameMissile100 : StrifeFlameMissile { Speed 46 }

actor RebelFlameMissile : StrifeFlameMissile
{
    Damage (random(1,4)*6)
    Obituary "%o was burned to death by rebel reinforcements."
}

actor StrifeFlameMissileCoop : StrifeFlameMissile
{
    Species "Player"
    +THRUSPECIES
}

actor StrifeFlameMissileCoop20 : StrifeFlameMissileCoop { Speed 26 }
actor StrifeFlameMissileCoop40 : StrifeFlameMissileCoop { Speed 31 }
actor StrifeFlameMissileCoop60 : StrifeFlameMissileCoop { Speed 36 }
actor StrifeFlameMissileCoop80 : StrifeFlameMissileCoop { Speed 41 }
actor StrifeFlameMissileCoop100 : StrifeFlameMissileCoop { Speed 46 }

actor RebelFlameMissile_2 : StrifeFlameMissileCoop
{
    Damage (random(1,4)*6)
    Obituary "%o was burned to death by rebel reinforcements."
}
