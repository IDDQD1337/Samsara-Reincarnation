actor "   Pistol   " : SamsaraStrifeguyWeapon
{
    Inventory.PickupMessage "You picked up the pistol."
    Weapon.SlotNumber 1
    Weapon.SelectionOrder 1700
    Obituary "%k \cipoked\c- %o \ciwith %p pop-gun."
    Tag "Pistol"
    +FLOORCLIP
    +NOALERT
    +WIMPY_WEAPON
    States
    {
      Spawn:
        SGPI A -1
        stop

      Ready:
        SGPI B 1 A_WeaponReady
        loop

      Deselect:
        SGPI B 1 A_Lower
        loop

      Select:
        SGPI B 1 A_Raise
        loop

      Fire:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto FireStart
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"FireStart")
        goto NoAmmoSwitch

      FireStart:
        SGPI C 2 A_AlertMonsters
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto FireAttack
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"FireAttack")
        goto FireContinue

      FireAttack:
        TNT1 A 0 A_GunFlash
      FireContinue:
        SGPI D 3 Bright A_GiveInventory("SamsaraStrifePistolAttackHandler")
        SGPI E 4
      FireFinish:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto FireFinishReFire
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireFinishReFire")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"FireFinishReFire")
        goto NoAmmoSwitch

      FireFinishReFire:
        SGPI C 5 A_ReFire
        goto Ready

      Flash:
        TNT1 A 4 Bright A_Light1
        goto LightDone

      // "No ammo" switch
      NoAmmoSwitch:
        TNT1 A 0 A_ClearReFire
        TNT1 A 0 A_SelectWeapon("Punch Dagger")
        goto Deselect
    }
}

actor SamsaraStrifePistolAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto PickupAttack
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("strife/pistolfire",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("AccuracyUpgrade2",0,"PickupAttack100")
        TNT1 A 0 A_JumpIfInventory("AccuracyUpgrade2",80,"PickupAttack80")
        TNT1 A 0 A_JumpIfInventory("AccuracyUpgrade2",60,"PickupAttack60")
        TNT1 A 0 A_JumpIfInventory("AccuracyUpgrade2",40,"PickupAttack40")
        TNT1 A 0 A_JumpIfInventory("AccuracyUpgrade2",20,"PickupAttack20")
      // 0 accuracy upgrades
      PickupAttack0:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttack0Coop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttack0Coop")
      PickupAttack0DM:
        TNT1 A 0 A_FireBullets(5.6,0,1,random(1,3)*4,"StrifeBulletPuff2",FBF_NORANDOM|FBF_NOFLASH)
        stop

      PickupAttack0Coop:
        TNT1 A 0 A_FireBullets(5.6,0,1,random(1,3)*4,"StrifeBulletPuff2Coop",FBF_NORANDOM|FBF_NOFLASH)
        stop

      // 2 accuracy upgrades
      PickupAttack20:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttack20Coop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttack20Coop")
      PickupAttack20DM:
        TNT1 A 0 A_FireBullets(2.8,0,1,random(1,3)*4,"StrifeBulletPuff2",FBF_NORANDOM|FBF_NOFLASH)
        stop

      PickupAttack20Coop:
        TNT1 A 0 A_FireBullets(2.8,0,1,random(1,3)*4,"StrifeBulletPuff2Coop",FBF_NORANDOM|FBF_NOFLASH)
        stop

      // 4 accuracy upgrades
      PickupAttack40:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttack40Coop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttack40Coop")
      PickupAttack40DM:
        TNT1 A 0 A_FireBullets(1.4,0,1,random(1,3)*4,"StrifeBulletPuff2",FBF_NORANDOM|FBF_NOFLASH)
        stop

      PickupAttack40Coop:
        TNT1 A 0 A_FireBullets(1.4,0,1,random(1,3)*4,"StrifeBulletPuff2Coop",FBF_NORANDOM|FBF_NOFLASH)
        stop

      // 6 accuracy upgrades
      PickupAttack60:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttack60Coop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttack60Coop")
      PickupAttack60DM:
        TNT1 A 0 A_FireBullets(0.7,0,1,random(1,3)*4,"StrifeBulletPuff2",FBF_NORANDOM|FBF_NOFLASH)
        stop

      PickupAttack60Coop:
        TNT1 A 0 A_FireBullets(0.7,0,1,random(1,3)*4,"StrifeBulletPuff2Coop",FBF_NORANDOM|FBF_NOFLASH)
        stop

      // 8 accuracy upgrades
      PickupAttack80:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttack80Coop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttack80Coop")
      PickupAttack80DM:
        TNT1 A 0 A_FireBullets(0.35,0,1,random(1,3)*4,"StrifeBulletPuff2",FBF_NORANDOM|FBF_NOFLASH)
        stop

      PickupAttack80Coop:
        TNT1 A 0 A_FireBullets(0.35,0,1,random(1,3)*4,"StrifeBulletPuff2Coop",FBF_NORANDOM|FBF_NOFLASH)
        stop

      // 10 accuracy upgrades
      PickupAttack100:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttack100Coop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttack100Coop")
      PickupAttack100DM:
        TNT1 A 0 A_FireBullets(0,0,1,random(1,3)*4,"StrifeBulletPuff2",FBF_NORANDOM|FBF_NOFLASH)
        stop

      PickupAttack100Coop:
        TNT1 A 0 A_FireBullets(0,0,1,random(1,3)*4,"StrifeBulletPuff2Coop",FBF_NORANDOM|FBF_NOFLASH)
        stop
    }
}

actor StrifeBulletPuff2 : StrifeBulletPuff
{
    DamageType "StrifePistol"
    Scale 0.5
}

actor StrifeBulletPuff2Coop : StrifeBulletPuff2 { +MTHRUSPECIES }
