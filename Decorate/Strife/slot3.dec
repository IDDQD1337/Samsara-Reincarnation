actor "Mini Missile Launcher" : MiniMissileLauncher
{
	Weapon.Kickback 100
	+INVENTORY.UNDROPPABLE
	Inventory.PickUpSound "strife/weaponget"
	Weapon.SelectionOrder 1800
	Weapon.AmmoUse 1
	Weapon.AmmoGive 10
	Weapon.AmmoType "RocketAmmo"
	Weapon.SlotNumber 3
	+WEAPON.EXPLOSIVE
	Inventory.PickupMessage "You picked up the Mini-missile Launcher."
	Obituary "%k \cislammed\c- %o \ciwith a mini-missile."
	Inventory.RestrictedTo "StrifeguyPlayer"
	Tag "Mini Missile Launcher"
	States
	{
	Spawn:
		SMRL A -1
		Stop
	Ready:
		SMML A 1 A_WeaponReady
		Loop
	Deselect:
		SMML A 1 A_Lower
		Loop
	Select:
		SMML A 1 A_Raise
		Loop
	Fire:
		SMML A 0 A_JumpIfInventory("AccuracyUpgrade2",10,"Fire.Accuracy100")
		SMML A 0 A_JumpIfInventory("AccuracyUpgrade2",8,"Fire.Accuracy80")
		SMML A 0 A_JumpIfInventory("AccuracyUpgrade2",6,"Fire.Accuracy60")
		SMML A 0 A_JumpIfInventory("AccuracyUpgrade2",4,"Fire.Accuracy40")
		SMML A 0 A_JumpIfInventory("AccuracyUpgrade2",2,"Fire.Accuracy20")
		SMML A 0 A_JumpIfInventory("CoopModeOn", 1, 2)
		SMML A 4 A_FireCustomMissile("StrifeMiniMissile",random(-560,560)/100.00,1,0,0,0)
		Goto FireEnd
		SMML A 4 A_FireCustomMissile("StrifeMiniMissile2",random(-560,560)/100.00,1,0,0,0)
		Goto FireEnd
	Fire.Accuracy20:
		SMML A 0 A_JumpIfInventory("CoopModeOn", 1, 2)
		SMML A 4 A_FireCustomMissile("StrifeMiniMissile",random(-280,280)/100.00,1,0,0,0)
		Goto FireEnd
		SMML A 4 A_FireCustomMissile("StrifeMiniMissile2",random(-280,280)/100.00,1,0,0,0)
		Goto FireEnd
	Fire.Accuracy40:
		SMML A 0 A_JumpIfInventory("CoopModeOn", 1, 2)
		SMML A 4 A_FireCustomMissile("StrifeMiniMissile",random(-140,140)/100.00,1,0,0,0)
		Goto FireEnd
		SMML A 4 A_FireCustomMissile("StrifeMiniMissile2",random(-140,140)/100.00,1,0,0,0)
		Goto FireEnd
	Fire.Accuracy60:
		SMML A 0 A_JumpIfInventory("CoopModeOn", 1, 2)
		SMML A 4 A_FireCustomMissile("StrifeMiniMissile",random(-70,70)/100.00,1,0,0,0)
		Goto FireEnd
		SMML A 4 A_FireCustomMissile("StrifeMiniMissile2",random(-70,70)/100.00,1,0,0,0)
		Goto FireEnd
	Fire.Accuracy80:
		SMML A 0 A_JumpIfInventory("CoopModeOn", 1, 2)
		SMML A 4 A_FireCustomMissile("StrifeMiniMissile",random(-35,35)/100.00,1,0,0,0)
		Goto FireEnd
		SMML A 4 A_FireCustomMissile("StrifeMiniMissile2",random(-35,35)/100.00,1,0,0,0)
		Goto FireEnd
	Fire.Accuracy100:
		SMML A 0 A_JumpIfInventory("CoopModeOn", 1, 2)
		SMML A 4 A_FireCustomMissile("StrifeMiniMissile",0,1,0,0,0)
		Goto FireEnd
		SMML A 4 A_FireCustomMissile("StrifeMiniMissile2",0,1,0,0,0)
		Goto FireEnd
	FireEnd:
		SMML B 4 A_Light1
		SMML C 4 Bright
		SMML D 2 Bright A_Light2
		SMML E 2 Bright
		SMML F 2 Bright A_Light0
		SMML F 0 A_ReFire
		Goto Ready
	}
}

actor StrifeMiniMissile : MiniMissile
{
	Speed 22
	Damage (40)
	Projectile
	SeeSound "strife/minifire"
	DeathSound ""
	Obituary "%k \cislammed\c- %o \ciwith a mini-missile."
	+BRIGHT
	-STRIFEDAMAGE
	DamageType "StrifeExplosive"
	Decal "Scorch"
	States
	{
	Spawn:
		MICR A 6 A_PlaySound("strife/minifly", CHAN_VOICE)
		MICR A 0 A_SpawnItemEx("StrifeRocketTrail", 0, 0, 0, 0, 0, 1, SXF_CLIENTSIDE)
		MICR A 0 A_SpawnItemEx("StrifeMiniMissilePuff", 0, 0, 0, 0, 0, 0, 180, SXF_CLIENTSIDE)
		Loop
	Death:
		SMIS A 0 A_SetTranslucent(1, 1)
		SMIS A 0 A_PlaySound("strife/miniexplode",CHAN_VOICE)
		SMIS A 5 A_Explode(96, 64, 1, 1) // was 64
		SMIS B 5
		SMIS C 4
		SMIS DEFG 2
		Stop
	}
}

actor StrifeMiniMissile2 : StrifeMiniMissile { +THRUSPECIES Species "Player" }
Actor RebelMiniMissile_2 : StrifeMiniMissile2
{
	Damage (random(1,4)*30)
	DamageType "RebelMissile"
	Obituary "%o was bombarded by rebel reinforcements."
		+DONTHARMSPECIES
}
Actor RebelMiniMissile : StrifeMiniMissile
{
	Damage (random(1,4)*30)
	Obituary "%o was bombarded by rebel reinforcements."
	States
	{
	Death:
		SMIS A 0 A_SetTranslucent(1, 1)
		SMIS A 0 A_PlaySound("strife/miniexplode",CHAN_VOICE)
		SMIS A 5 A_Explode(64, 64, 0, 1)
		Goto Super::Death+3
	}
}

actor StrifeMiniMissilePuff : StrifeBulletPuff
{
	-ALLOWPARTICLES
	+CLIENTSIDEONLY
	States
	{
	Spawn:
		Goto Crash
	}
}

ACTOR StrifeRocketTrail
{
	+NOBLOCKMAP
	+NOGRAVITY
	+CLIENTSIDEONLY
	RenderStyle Translucent
	Alpha 0.25
	SeeSound "strife/minifly"
	States
	{
	Spawn:
		SPFF BCBCD 4
		Stop
	}
}