actor "Mini Missile Launcher" : SamsaraStrifeguyWeapon
{
    Inventory.PickupMessage "You picked up the mini missile launcher."
    Weapon.SlotNumber 3
    Weapon.SelectionOrder 1800
    Weapon.AmmoType "RocketAmmo"
    Weapon.AmmoGive 10
    Weapon.AmmoUse 1
    Obituary "%k \cislammed\c- %o \ciwith a mini-missile."
    Tag "Mini Missile Launcher"
    +EXPLOSIVE
    +FLOORCLIP
    States
    {
      Spawn:
        SMRL A -1
        stop

      Ready:
        SMML A 1 A_WeaponReady
        loop

      Deselect:
        SMML A 1 A_Lower
        loop

      Select:
        SMML A 1 A_Raise
        loop

      Fire:
        TNT1 A 0 A_GunFlash
        SMML A 4 A_GiveInventory("SamsaraStrifeMiniMissileLauncherAttackHandler")
        SMML B 4 A_Light1
        SMML C 4 Bright
        SMML D 2 Bright A_Light2
        SMML E 2 Bright
        SMML F 2 Bright A_Light0
        TNT1 A 0 A_ReFire
        goto Ready
    }
}

actor SamsaraStrifeMiniMissileLauncherAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,1)
        stop
        TNT1 A 0 A_TakeInventory("RocketAmmo",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("AccuracyUpgrade2",0,"PickupAttack100")
        TNT1 A 0 A_JumpIfInventory("AccuracyUpgrade2",80,"PickupAttack80")
        TNT1 A 0 A_JumpIfInventory("AccuracyUpgrade2",60,"PickupAttack60")
        TNT1 A 0 A_JumpIfInventory("AccuracyUpgrade2",40,"PickupAttack40")
        TNT1 A 0 A_JumpIfInventory("AccuracyUpgrade2",20,"PickupAttack20")
      // 0 accuracy upgrades
      PickupAttack0:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttack0Coop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttack0Coop")
      PickupAttack0DM:
        TNT1 A 0 A_FireCustomMissile("StrifeMiniMissile",frandom(-5.6,5.6),false)
        stop

      PickupAttack0Coop:
        TNT1 A 0 A_FireCustomMissile("StrifeMiniMissile2",frandom(-5.6,5.6),false)
        stop

      // 2 accuracy upgrades
      PickupAttack20:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttack20Coop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttack20Coop")
      PickupAttack20DM:
        TNT1 A 0 A_FireCustomMissile("StrifeMiniMissile",frandom(-2.8,2.8),false)
        stop

      PickupAttack20Coop:
        TNT1 A 0 A_FireCustomMissile("StrifeMiniMissile2",frandom(-2.8,2.8),false)
        stop

      // 4 accuracy upgrades
      PickupAttack40:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttack40Coop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttack40Coop")
      PickupAttack40DM:
        TNT1 A 0 A_FireCustomMissile("StrifeMiniMissile",frandom(-1.4,1.4),false)
        stop

      PickupAttack40Coop:
        TNT1 A 0 A_FireCustomMissile("StrifeMiniMissile2",frandom(-1.4,1.4),false)
        stop

      // 6 accuracy upgrades
      PickupAttack60:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttack60Coop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttack60Coop")
      PickupAttack60DM:
        TNT1 A 0 A_FireCustomMissile("StrifeMiniMissile",frandom(-0.7,0.7),false)
        stop

      PickupAttack60Coop:
        TNT1 A 0 A_FireCustomMissile("StrifeMiniMissile2",frandom(-0.7,0.7),false)
        stop

      // 8 accuracy upgrades
      PickupAttack80:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttack80Coop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttack80Coop")
      PickupAttack80DM:
        TNT1 A 0 A_FireCustomMissile("StrifeMiniMissile",frandom(-0.35,0.35),false)
        stop

      PickupAttack80Coop:
        TNT1 A 0 A_FireCustomMissile("StrifeMiniMissile2",frandom(-0.35,0.35),false)
        stop

      // 10 accuracy upgrades
      PickupAttack100:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttack100Coop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttack100Coop")
      PickupAttack100DM:
        TNT1 A 0 A_FireCustomMissile("StrifeMiniMissile",0,false)
        stop

      PickupAttack100Coop:
        TNT1 A 0 A_FireCustomMissile("StrifeMiniMissile2",0,false)
        stop
    }
}

actor StrifeMiniMissile : MiniMissile
{
    Speed 22
    Damage (40)
    DamageType "StrifeExplosive"
    SeeSound "strife/minifire"
    DeathSound "strife/miniexplode"
    Decal "Scorch"
    Obituary "%k \cislammed\c- %o \ciwith a mini-missile."
    -BLOODSPLATTER
    -STRIFEDAMAGE
    States
    {
      Spawn:
        MICR A 6 Bright
        TNT1 A 0 A_PlaySound("strife/minifly",CHAN_VOICE)
        TNT1 A 0 A_SpawnItemEx("StrifeMiniMissilePuff",0,0,0,0,0,0,180,SXF_CLIENTSIDE)
        TNT1 A 0 A_SpawnItemEx("StrifeRocketTrail",-20,0,0,0,0,1,0,SXF_CLIENTSIDE) // kinda hacky values, but eh
        loop

      Death:
        TNT1 A 0 A_SetTranslucent(1,1)
        TNT1 A 0 A_Explode(96,64,XF_HURTSOURCE,true) // damage was 64
      DeathFinish:
        SMIS AB 5 Bright
        SMIS C 4 Bright
        SMIS DEFG 2 Bright
        stop
    }
}

actor StrifeMiniMissile2 : StrifeMiniMissile
{
    Species "Player"
    +THRUSPECIES
}

actor RebelMiniMissile : StrifeMiniMissile
{
    Damage (random(1,4)*30)
    Obituary "%o was bombarded by rebel reinforcements."
    States
    {
      Death:
        TNT1 A 0 A_SetTranslucent(1,1)
        TNT1 A 0 A_Explode(64,64,0,true)
        goto DeathFinish
    }
}

actor RebelMiniMissile_2 : StrifeMiniMissile2
{
    Damage (random(1,4)*30)
    DamageType "RebelMissile"
    Obituary "%o was bombarded by rebel reinforcements."
}

actor StrifeMiniMissilePuff : StrifeBulletPuff
{
    -ALLOWPARTICLES
    +CLIENTSIDEONLY
    States
    {
      Spawn:
        goto Crash
    }
}

actor StrifeRocketTrail : RocketTrail
{
    SeeSound "strife/minifly"
    +CLIENTSIDEONLY
    States
    {
      Spawn:
        SPFF BCBCD 4
        stop
    }
}
