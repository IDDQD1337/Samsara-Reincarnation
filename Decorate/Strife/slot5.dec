actor " Grenade Launcher " : StrifeGrenadeLauncher
{
	Weapon.Kickback 100
	+INVENTORY.UNDROPPABLE
	Inventory.PickUpSound "strife/weaponget"
	Weapon.SelectionOrder 2400
	Weapon.AmmoUse1 0
	Weapon.AmmoUse2 0
	Weapon.AmmoGive1 16
	Weapon.AmmoGive2 0
	Weapon.AmmoType1 "RocketAmmo"
	Weapon.AmmoType2 "WhitePhosGrenade"
	Weapon.SlotNumber 5
	Weapon.SisterWeapon ""
	+NOALERT
	+WEAPON.EXPLOSIVE
	Inventory.PickupMessage "You picked up the Grenade Launcher."
	//Obituary "%k \ciinverted\c- %o \ciwith a H-E grenade."
	Inventory.RestrictedTo "StrifeguyPlayer"
	Tag "Grenade Launcher"
	States
	{
	Spawn:
		SGRL A -1
		stop

	// High explosive
	Ready:
		GRN1 A 0 A_JumpIfInventory("GrenadeMode",0,"ReadyGas")
		GRN1 A 0 A_JumpIfInventory("GrenadeMode",1,"ReadyPhos")
		GRN1 A 1 A_WeaponReady
		loop

	Deselect:
		GRN1 A 0 A_JumpIfInventory("GrenadeMode",0,"DeselectGas")
		GRN1 A 0 A_JumpIfInventory("GrenadeMode",1,"DeselectPhos")
		GRN1 A 1 A_Lower
		loop

	Select:
		GRN1 A 0 A_JumpIfInventory("GrenadeMode",0,"SelectGas")
		GRN1 A 0 A_JumpIfInventory("GrenadeMode",1,"SelectPhos")
		GRN1 A 1 A_Raise
		loop

	Fire:
		GRN1 A 0 A_JumpIfInventory("GrenadeMode",0,"FireGas")
		GRN1 A 0 A_JumpIfInventory("GrenadeMode",1,"FirePhos")
		TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,"FireHE")
		goto Ready

	FireHE:
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_TakeInventory("RocketAmmo",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_GunFlash // netplay hack
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"FireHECoop")
		TNT1 A 0 A_FireGrenade("StrifeHEGrenade",-90,"Flash")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"FireHESpread")
	FireHE2:
		GRN1 A 5
		GRN1 B 10
		TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,1)
		goto FireHEFinish
		TNT1 A 0 A_TakeInventory("RocketAmmo",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_GunFlash("Flash2") // netplay hack
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"FireHE2Coop")
		TNT1 A 0 A_FireGrenade("StrifeHEGrenade",90,"Flash2")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"FireHE2Spread")
	FireHEFinish:
		GRN1 A 5
		GRN1 C 10
	FireFinishReFire:
		TNT1 A 0 A_ReFire
		goto Ready

	FireHESpread:
		TNT1 A 0 A_FireGrenade("StrifeHEGrenade",-180,"Flash")
		TNT1 A 0 A_FireGrenade("StrifeHEGrenade",0,"Flash")
		goto FireHE2

	FireHE2Spread:
		TNT1 A 0 A_FireGrenade("StrifeHEGrenade",0,"Flash2")
		TNT1 A 0 A_FireGrenade("StrifeHEGrenade",180,"Flash2")
		goto FireHEFinish

	FireHECoop:
		TNT1 A 0 A_FireGrenade("StrifeHEGrenade2",-90,"Flash")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"FireHESpreadCoop")
		goto FireHE2

	FireHE2Coop:
		TNT1 A 0 A_FireGrenade("StrifeHEGrenade2",90,"Flash2")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"FireHE2SpreadCoop")
		goto FireHEFinish

	FireHESpreadCoop:
		TNT1 A 0 A_FireGrenade("StrifeHEGrenade2",-180,"Flash")
		TNT1 A 0 A_FireGrenade("StrifeHEGrenade2",0,"Flash")
		goto FireHE2

	FireHE2SpreadCoop:
		TNT1 A 0 A_FireGrenade("StrifeHEGrenade2",0,"Flash2")
		TNT1 A 0 A_FireGrenade("StrifeHEGrenade2",180,"Flash2")
		goto FireHEFinish

	Flash:
		GRN1 D 5 Bright A_Light1
		goto LightDone

	Flash2:
		GRN1 E 5 Bright A_Light2
		goto LightDone

	// White phosphorus
	ReadyPhos:
		GRN2 A 1 A_WeaponReady
		loop

	DeselectPhos:
		GRN2 A 1 A_Lower
		loop

	SelectPhos:
		GRN2 A 1 A_Raise
		loop

	FirePhos:
		TNT1 A 0 A_JumpIfInventory("WhitePhosGrenade",1,1)
		goto AltFirePhos
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_TakeInventory("WhitePhosGrenade",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_GunFlash("Flash3") // netplay hack
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"FirePhosSP")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"FirePhosCoop")
		TNT1 A 0 A_FireGrenade("StrifePhosGrenade",-90,"Flash3")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"FirePhosSpread")
	FirePhos2:
		GRN2 A 5
		GRN2 B 10
		TNT1 A 0 A_JumpIfInventory("WhitePhosGrenade",1,1)
		goto FirePhosFinish
		TNT1 A 0 A_TakeInventory("WhitePhosGrenade",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_GunFlash("Flash4") // netplay hack
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"FirePhos2SP")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"FirePhos2Coop")
		TNT1 A 0 A_FireGrenade("StrifePhosGrenade",90,"Flash4")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"FirePhos2Spread")
	FirePhosFinish:
		GRN2 A 5
		GRN2 C 10
		goto FireFinishReFire

	FirePhosSpread:
		TNT1 A 0 A_FireGrenade("StrifePhosGrenade",-180,"Flash3")
		TNT1 A 0 A_FireGrenade("StrifePhosGrenade",0,"Flash3")
		goto FirePhos2

	FirePhos2Spread:
		TNT1 A 0 A_FireGrenade("StrifePhosGrenade",0,"Flash4")
		TNT1 A 0 A_FireGrenade("StrifePhosGrenade",180,"Flash4")
		goto FirePhosFinish

	FirePhosSP:
		TNT1 A 0 A_FireGrenade("StrifePhosGrenade3",-90,"Flash3")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"FirePhosSpreadSP")
		goto FirePhos2

	FirePhos2SP:
		TNT1 A 0 A_FireGrenade("StrifePhosGrenade3",90,"Flash4")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"FirePhos2SpreadSP")
		goto FirePhosFinish
	
	FirePhosCoop:
		TNT1 A 0 A_FireGrenade("StrifePhosGrenade2",-90,"Flash3")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"FirePhosSpreadCoop")
		goto FirePhos2

	FirePhos2Coop:
		TNT1 A 0 A_FireGrenade("StrifePhosGrenade2",90,"Flash4")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"FirePhos2SpreadCoop")
		goto FirePhosFinish

	FirePhosSpreadSP:
		TNT1 A 0 A_FireGrenade("StrifePhosGrenade3",-180,"Flash3")
		TNT1 A 0 A_FireGrenade("StrifePhosGrenade3",0,"Flash3")
		goto FirePhos2

	FirePhos2SpreadSP:
		TNT1 A 0 A_FireGrenade("StrifePhosGrenade3",0,"Flash4")
		TNT1 A 0 A_FireGrenade("StrifePhosGrenade3",180,"Flash4")
		goto FirePhosFinish
	
	FirePhosSpreadCoop:
		TNT1 A 0 A_FireGrenade("StrifePhosGrenade2",-180,"Flash3")
		TNT1 A 0 A_FireGrenade("StrifePhosGrenade2",0,"Flash3")
		goto FirePhos2

	FirePhos2SpreadCoop:
		TNT1 A 0 A_FireGrenade("StrifePhosGrenade2",0,"Flash4")
		TNT1 A 0 A_FireGrenade("StrifePhosGrenade2",180,"Flash4")
		goto FirePhosFinish

	Flash3:
		GRN2 D 5 Bright A_Light1
		goto LightDone

	Flash4:
		GRN2 E 5 Bright A_Light2
		goto LightDone

	// Gas
	ReadyGas:
		GRN3 A 1 A_WeaponReady
		loop

	DeselectGas:
		GRN3 A 1 A_Lower
		loop

	SelectGas:
		GRN3 A 1 A_Raise
		loop

	FireGas:
		TNT1 A 0 A_JumpIfInventory("StrifeGasGrenadeAmmo",1,1)
		goto AltFireGas
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_TakeInventory("StrifeGasGrenadeAmmo",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_GunFlash("Flash5") // netplay hack
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"FireGasCoop")
		TNT1 A 0 A_FireGrenade("StrifeGasGrenade",-90,"Flash5")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"FireGasSpread")
	FireGas2:
		GRN3 A 5
		GRN3 B 10
		TNT1 A 0 A_JumpIfInventory("StrifeGasGrenadeAmmo",1,1)
		goto FireGasFinish
		TNT1 A 0 A_TakeInventory("StrifeGasGrenadeAmmo",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_GunFlash("Flash6") // netplay hack
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"FireGas2Coop")
		TNT1 A 0 A_FireGrenade("StrifeGasGrenade",90,"Flash6")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"FireGas2Spread")
	FireGasFinish:
		GRN3 A 5
		GRN3 C 10
		goto FireFinishReFire

	FireGasSpread:
		TNT1 A 0 A_FireGrenade("StrifeGasGrenade",-180,"Flash5")
		TNT1 A 0 A_FireGrenade("StrifeGasGrenade",0,"Flash5")
		goto FireGas2

	FireGas2Spread:
		TNT1 A 0 A_FireGrenade("StrifeGasGrenade",0,"Flash6")
		TNT1 A 0 A_FireGrenade("StrifeGasGrenade",180,"Flash6")
		goto FireGasFinish

	FireGasCoop:
		TNT1 A 0 A_FireGrenade("StrifeGasGrenade2",-90,"Flash5")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"FireGasSpreadCoop")
		goto FireGas2

	FireGas2Coop:
		TNT1 A 0 A_FireGrenade("StrifeGasGrenade2",90,"Flash6")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"FireGas2SpreadCoop")
		goto FireGasFinish

	FireGasSpreadCoop:
		TNT1 A 0 A_FireGrenade("StrifeGasGrenade2",-180,"Flash5")
		TNT1 A 0 A_FireGrenade("StrifeGasGrenade2",0,"Flash5")
		goto FireGas2

	FireGas2SpreadCoop:
		TNT1 A 0 A_FireGrenade("StrifeGasGrenade2",0,"Flash6")
		TNT1 A 0 A_FireGrenade("StrifeGasGrenade2",180,"Flash6")
		goto FireGasFinish

	Flash5:
		GRN3 D 5 Bright A_Light1
		goto LightDone

	Flash6:
		GRN3 E 5 Bright A_Light2
		goto LightDone

	AltFire:
		TNT1 A 0 A_JumpIfInventory("GrenadeMode",0,"AltFireGas")
		TNT1 A 0 A_JumpIfInventory("GrenadeMode",1,"AltFirePhos")
	AltFireHE:
		GRN1 AAAAAAAAAAAA 1 A_Lower
		TNT1 A 0 A_GiveInventory("GrenadeMode")
		GRN2 AAAAAAAAAAAA 1 A_Raise
		goto Ready

	AltFirePhos:
		GRN2 AAAAAAAAAAAA 1 A_Lower
		TNT1 A 0 A_GiveInventory("GrenadeMode")
		GRN3 AAAAAAAAAAAA 1 A_Raise
		goto Ready

	AltFireGas:
		GRN3 AAAAAAAAAAAA 1 A_Lower
		TNT1 A 0 A_TakeInventory("GrenadeMode")
		GRN1 AAAAAAAAAAAA 1 A_Raise
		goto Ready
	}
}

actor WhitePhosGrenade : Ammo
{
    Inventory.MaxAmount 10
    Ammo.BackpackMaxAmount 20
    Inventory.Icon "AMSML0"
    Tag "Phosphorus-Grenade Rounds"
}

actor StrifeGasGrenadeAmmo : Ammo
{
    Inventory.MaxAmount 10
    Ammo.BackpackMaxAmount 20
    Inventory.Icon "AMSMR0"
    Tag "Gas Grenade Rounds"
}

actor StrifeHEGrenade
{
	Speed 15
	Radius 13
	Height 13
	Mass 20
	Damage (random(1,4)*1)
	ReactionTime 30
	Projectile
    -BLOODSPLATTER
	-NOGRAVITY
	+BOUNCEONACTORS
	+EXPLODEONWATER
	+FORCEXYBILLBOARD
	+WINDTHRUST
	+FORCERADIUSDMG
	MaxStepHeight 4
	BounceType "Doom"
	BounceFactor 0.5
	BounceCount 2
	DamageType "StrifeExplosive"
	SeeSound "strife/grenadeshoot"
	DeathSound "strife/grenadebang"
	Obituary "%k \ciinverted\c- %o \ciwith a H-E grenade."
	States
	{
	Spawn:
		HEGR AB 3 A_Countdown
		Loop
	Death:
		SBNG A 0 Bright A_Explode(192, 192, 1, 1)
		Goto DeathEnd
	DeathEnd:
		SBNG A 0 Bright A_SetTranslucent(1, 1)
		SBNG A 2 Bright A_NoGravity
		SBNG BCDEFGHIJKLMN 3 Bright
		Stop
	}
}

actor StrifeHEGrenade2 : StrifeHEGrenade { +THRUSPECIES Species "Player" +DONTHARMCLASS +DONTHARMSPECIES +MTHRUSPECIES }
actor RebelHEGrenade_2 : StrifeHEGrenade2
{
	DamageType "RebelMissile"
	States
	{
	Death:
		SBNG A 0 Bright A_Explode(80, 192, 1, 1)
		Goto Super::DeathEnd
	}
}
Actor RebelHEGrenade : StrifeHEGrenade
{
	States
	{
	Death:
		SBNG A 0 Bright A_Explode(80, 192, 0, 1)
		Goto Super::DeathEnd
	}
}

actor StrifePhosGrenade
{
	Speed 15
	Radius 13
	Height 13
	Mass 20
	Damage (random(1,4)*1)
	ReactionTime 40
	Projectile
    -BLOODSPLATTER
	+FRIENDLY
	-NOGRAVITY
	+BOUNCEONACTORS
	+EXPLODEONWATER
	+WINDTHRUST
	+FORCEXYBILLBOARD
	BounceType "Doom"
	MaxStepHeight 4
	BounceFactor 0.5
	BounceCount 2
	DamageType "StrifeExplosive"
	SeeSound "strife/grenadeshoot"
	DeathSound "strife/grenadebang"
	Obituary "%k \cimade\c- %o \citake a flame bath."
	States
	{
	Spawn:
		PHGR AB 3 A_Countdown
		Loop
	Death:
		SBN2 A 2 A_CustomMissile("DMPhosFire",0,0,0,CMF_TRACKOWNER|CMF_AIMDIRECTION)
		Stop
	}
}

actor StrifePhosGrenade2 : StrifePhosGrenade
{ 
	+THRUSPECIES
	Species "Player"
	+DONTHARMCLASS
	+DONTHARMSPECIES
	+MTHRUSPECIES
	States
	{
	Death:
		SBN2 A 2 A_CustomMissile("CoopPhosFire",0,0,0,CMF_TRACKOWNER|CMF_AIMDIRECTION)
		Stop
	}
}

actor StrifePhosGrenade3 : StrifePhosGrenade
{ 
	States
	{
	Death:
		SBN2 A 2 A_CustomMissile("SPPhosFire",0,0,0,CMF_TRACKOWNER|CMF_AIMDIRECTION)
		Stop
	}
}

actor DMPhosFire : PhosphorousFire
{
	ReactionTime 17
	Radius 1
	Height 1
	Obituary "%k \cimade\c- %o \citake a flame bath."
	DamageType "PhosFire"
	+BRIGHT
	+FORCERADIUSDMG
	-EXTREMEDEATH
	+NOEXTREMEDEATH
	+MOVEWITHSECTOR
	states
	{
	Spawn:
		SBN2 B 2 A_Explode (20, 96)
		SBN2 C 2 A_Countdown
		SGFW A 0 A_PlaySound("strife/largefire", CHAN_VOICE)
		SGFW A 2 A_SpawnItemEx("DMPhosFire2", random(-7,7), random(-7,7), 0, random(-3,3), random(-3,3), 0, random(-180, 180))
		SGFW B 2 A_Countdown
		SGFW C 2 A_Explode (20, 96)
		SGFW D 3 A_Countdown
		SGFW E 3 A_Explode (20, 96)
		SGFW F 0 A_SetAngle(angle + random(15, 90))
		SGFW F 0 A_Recoil(random(-6, 6))
		SGFW F 3 A_Countdown
		SGFW G 0 A_PlaySound("strife/largefire", CHAN_VOICE)
		SGFW G 3 A_SpawnItemEx("DMPhosFire2", random(-7,7), random(-7,7), 0, random(-3,3), random(-3,3), 0, random(-180, 180))
		Goto Spawn+6
	Death:
		SGFW H 2
		SGFW I 0 A_PlaySound("strife/largefire", CHAN_VOICE)
		SGFW I 2 A_SpawnItemEx("DMPhosFire2", random(-7,7), random(-7,7), 0, random(-3,3), random(-3,3), 0, random(-180, 180))
		SGFW JK 2
		Stop
	}
}

actor DMPhosFire2 : DMPhosFire
{
	ReactionTime 1
	Speed 2
	states
	{
	Spawn:
		SBN2 B 2 A_Explode (20, 96)	//was 64
		SBN2 C 2
		SGFW A 2 A_PlaySound("strife/largefire", CHAN_VOICE)
		SGFW B 2
		SGFW C 2 A_Explode (20, 96)	//was 64
		SGFW D 3
		SGFW E 3 A_Explode (20, 96)	//was 64
		SGFW F 3
		SGFW G 3 A_PlaySound("strife/largefire", CHAN_VOICE)
		SGFW G 1 A_Countdown
		Wait
	Death:
		SGFW H 2
		SGFW I 2 A_PlaySound("strife/largefire", CHAN_VOICE)
		SGFW JK 2
		Stop
	}
}

actor SPPhosFire : PhosphorousFire
{
	ReactionTime 60
	DamageType "PhosFireCoop"
	+FRIENDLY
	-EXTREMEDEATH
	+NOEXTREMEDEATH
	+FORCERADIUSDMG
	+MOVEWITHSECTOR
	states
	{
	Spawn:
		SBN2 B 2 A_Explode (48, 96)
		SBN2 C 2 A_Countdown
		SGFW A 0 A_PlaySound("strife/largefire", CHAN_VOICE)
		SGFW A 2 A_SpawnItemEx("SPPhosFire2", random(-7,7), random(-7,7), 0, random(-3,3), random(-3,3), 0, random(-180, 180))
		SGFW B 2 A_Countdown
		SGFW C 2 A_Explode (48, 96)
		SGFW D 3 A_Countdown
		SGFW E 3 A_Explode (48, 96)
		SGFW F 0 A_SetAngle(angle + random(15, 90))
		SGFW F 0 A_Recoil(random(-6, 6))
		SGFW F 3 A_Countdown
		SGFW G 0 A_PlaySound("strife/largefire", CHAN_VOICE)
		SGFW G 3 A_SpawnItemEx("SPPhosFire2", random(-7,7), random(-7,7), 0, random(-3,3), random(-3,3), 0, random(-180, 180))
		Goto Spawn+6
	Death:
		SGFW H 2
		SGFW I 0 A_PlaySound("strife/largefire", CHAN_VOICE)
		SGFW I 2 A_SpawnItemEx("SPPhosFire2", random(-7,7), random(-7,7), 0, random(-3,3), random(-3,3), 0, random(-180, 180))
		SGFW JK 2
		Stop
	}
}

actor SPPhosFire2 : SPPhosFire
{
	ReactionTime 1
	states
	{
	Spawn:
		SBN2 B 2 A_Explode (48, 96)
		SBN2 C 2
		SGFW A 2 A_PlaySound("strife/largefire", CHAN_VOICE)
		SGFW B 2
		SGFW C 2 A_Explode (48, 96)
		SGFW D 3
		SGFW E 3 A_Explode (48, 96)
		SGFW F 3
		SGFW G 3 A_PlaySound("strife/largefire", CHAN_VOICE)
		SGFW G 1 A_Countdown
		Wait
	Death:
		SGFW H 2
		SGFW I 2 A_PlaySound("strife/largefire", CHAN_VOICE)
		SGFW JK 2
		Stop
	}
}

actor CoopPhosFire : SPPhosFire
{
	Species "Player"
	+THRUSPECIES
	+DONTHARMCLASS
	+DONTHARMSPECIES
	+MTHRUSPECIES
	states
	{
	Spawn:
		SBN2 B 2 A_Explode (48, 96)
		SBN2 C 2 A_Countdown
		SGFW A 0 A_PlaySound("strife/largefire", CHAN_VOICE)
		SGFW A 2 A_SpawnItemEx("CoopPhosFire2", random(-7,7), random(-7,7), 0, random(-3,3), random(-3,3), 0, random(-180, 180))
		SGFW B 2 A_Countdown
		SGFW C 2 A_Explode (48, 96)
		SGFW D 3 A_Countdown
		SGFW E 3 A_Explode (48, 96)
		SGFW F 0 A_SetAngle(angle + random(15, 90))
		SGFW F 0 A_Recoil(random(-6, 6))
		SGFW F 3 A_Countdown
		SGFW G 0 A_PlaySound("strife/largefire", CHAN_VOICE)
		SGFW G 3 A_SpawnItemEx("CoopPhosFire2", random(-7,7), random(-7,7), 0, random(-3,3), random(-3,3), 0, random(-180, 180))
		Goto Spawn+6
	Death:
		SGFW H 2
		SGFW I 0 A_PlaySound("strife/largefire", CHAN_VOICE)
		SGFW I 2 A_SpawnItemEx("CoopPhosFire2", random(-7,7), random(-7,7), 0, random(-3,3), random(-3,3), 0, random(-180, 180))
		SGFW JK 2
		Stop
	}
}

actor CoopPhosFire2 : SPPhosFire2
{
	Species "Player"
	+THRUSPECIES
	+DONTHARMCLASS
	+DONTHARMSPECIES
	+MTHRUSPECIES
}

actor StrifeGasGrenade
{
	Speed 15
	Radius 13
	Height 13
	Mass 20
	Damage (random(1,4)*1)
	ReactionTime 40
	Projectile
    -BLOODSPLATTER
	+FRIENDLY
	-NOGRAVITY
	+BOUNCEONACTORS
	+EXPLODEONWATER
	+WINDTHRUST
	+FORCEXYBILLBOARD
	BounceType "Doom"
	MaxStepHeight 4
	BounceFactor 0.5
	BounceCount 2
	DamageType "StrifeExplosive"
	SeeSound "strife/grenadeshoot"
	DeathSound "strife/grenadebang"
	Obituary "%k \cimade\c- %o \citake an acid trip."
	States
	{
	Spawn:
		GASG AB 3 A_Countdown
		Loop
	Death:
		GASC AAAA 2 A_CustomMissile("StrifeGasCloud",0,0,0,CMF_TRACKOWNER|CMF_AIMDIRECTION)
		Stop
	}
}

actor StrifeGasGrenade2 : StrifeGasGrenade
{
	+THRUSPECIES
	Species "Player"
	+DONTHARMCLASS
	+DONTHARMSPECIES
	+MTHRUSPECIES
	States
	{
	Death:
		GASC AAAA 2 A_CustomMissile("CoopStrifeGasCloud",0,0,0,CMF_TRACKOWNER|CMF_AIMDIRECTION)
		Stop
	}
}

actor StrifeGasCloud
{
	Obituary "%k \cimade\c- %o \citake an acid trip."
	DamageType "PoisonBolt"
	Renderstyle Translucent
	Speed 2
	Height 2
	Radius 1
	ReactionTime 1
	Projectile
    -BLOODSPLATTER
	+THRUACTORS
	-EXTREMEDEATH
	+NOEXTREMEDEATH
	+FORCERADIUSDMG
	+NODAMAGETHRUST
	+MOVEWITHSECTOR
	-NOGRAVITY
	states
	{
	Spawn:
		TNT1 A 1 A_Countdown
		Wait
	Death:
		GASC A 5 ThrustThing(random(0,256),8,0,0)
		GASC B 5 A_Stop
		GASC CD 5
		GASC A 0 A_PlaySound("strife/gascloud", CHAN_VOICE)
		GASC E 5 A_Explode (16, 128)
		TNT1 AAAAAAAAAAA 0 A_CustomMissile("StrifePoisonProjectile", 32, 0, Random(0,359), CMF_AIMOFFSET|CMF_TRACKOWNER)
		GASC F 5 A_RadiusGive("StrifeGasGreen",128,RGF_PLAYERS,1)
		GASC G 5 A_PlaySound("strife/gascloud", CHAN_VOICE)
		GASC H 5 A_RadiusGive("StrifeGasGreen",128,RGF_PLAYERS,1)
		GASC G 5 A_Explode (16, 128)
		TNT1 AAAAAAAAAAA 0 A_CustomMissile("StrifePoisonProjectile", 32, 0, Random(0,359), CMF_AIMOFFSET|CMF_TRACKOWNER)
		GASC F 5 A_RadiusGive("StrifeGasGreen",128,RGF_PLAYERS,1)
		GASC E 5 A_Explode (16, 128)
		TNT1 AAAAAAAAAAA 0 A_CustomMissile("StrifePoisonProjectile", 32, 0, Random(0,359), CMF_AIMOFFSET|CMF_TRACKOWNER)
		GASC F 5 A_RadiusGive("StrifeGasGreen",128,RGF_PLAYERS,1)
		GASC G 5 A_PlaySound("strife/gascloud", CHAN_VOICE)
		GASC H 5 A_RadiusGive("StrifeGasGreen",128,RGF_PLAYERS,1)
		GASC G 5 A_Explode (16, 128)
		TNT1 AAAAAAAAAAA 0 A_CustomMissile("StrifePoisonProjectile", 32, 0, Random(0,359), CMF_AIMOFFSET|CMF_TRACKOWNER)
		GASC F 5 A_RadiusGive("StrifeGasGreen",128,RGF_PLAYERS,1)
		GASC E 5 A_Explode (16, 128)
		TNT1 AAAAAAAAAAA 0 A_CustomMissile("StrifePoisonProjectile", 32, 0, Random(0,359), CMF_AIMOFFSET|CMF_TRACKOWNER)
		GASC F 5 A_RadiusGive("StrifeGasGreen",128,RGF_PLAYERS,1)
		GASC G 5 A_PlaySound("strife/gascloud", CHAN_VOICE)
		GASC H 5 A_RadiusGive("StrifeGasGreen",128,RGF_PLAYERS,1)
		GASC G 5 A_Explode (16, 128)
		TNT1 AAAAAAAAAAA 0 A_CustomMissile("StrifePoisonProjectile", 32, 0, Random(0,359), CMF_AIMOFFSET|CMF_TRACKOWNER)
		GASC F 5 A_RadiusGive("StrifeGasGreen",128,RGF_PLAYERS,1)
		GASC E 5 A_Explode (16, 128)
		TNT1 AAAAAAAAAAA 0 A_CustomMissile("StrifePoisonProjectile", 32, 0, Random(0,359), CMF_AIMOFFSET|CMF_TRACKOWNER)
		GASC F 5 A_RadiusGive("StrifeGasGreen",128,RGF_PLAYERS,1)
		GASC G 5 A_PlaySound("strife/gascloud", CHAN_VOICE)
		GASC H 5 A_RadiusGive("StrifeGasGreen",128,RGF_PLAYERS,1)
		GASC G 5 A_Explode (16, 128)
		TNT1 AAAAAAAAAAA 0 A_CustomMissile("StrifePoisonProjectile", 32, 0, Random(0,359), CMF_AIMOFFSET|CMF_TRACKOWNER)
		GASC F 5 A_RadiusGive("StrifeGasGreen",128,RGF_PLAYERS,1)
		GASC E 5 A_Explode (16, 128)
		TNT1 AAAAAAAAAAA 0 A_CustomMissile("StrifePoisonProjectile", 32, 0, Random(0,359), CMF_AIMOFFSET|CMF_TRACKOWNER)
		GASC DCBA 5
		Stop
	}
}

Actor StrifePoisonProjectile : FastProjectile
{
	Projectile
	Speed 64
	Damage (0)
	Height 4
	Radius 2
	PoisonDamage 8, 8, 16
	DamageType "PoisonBolt"
	Obituary "%k \cimade\c- %o \citake an acid trip."
	+NOEXTREMEDEATH
	+NODAMAGETHRUST
	-BLOODSPLATTER
	+BLOODLESSIMPACT
	+NODAMAGETHRUST
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 1 
			Stop
		Crash:
		Death:
		XDeath:
			TNT1 A 1
			Stop
	}
}

Actor StrifePoisonProjectile_2 : StrifePoisonProjectile { Species "Player" +THRUSPECIES +MTHRUSPECIES }

Actor StrifeGasGreen : Trigger
{
	States
	{
		Pickup:
			TNT1 A 0 A_SetBlend("OliveDrab",0.25,105)
			Stop
	}
}

Actor BlankPuffPoison : BlankPuff { DamageType "PoisonBolt" PoisonDamage 8, 8, 16 +BLOODLESSIMPACT }

actor CoopStrifeGasCloud : StrifeGasCloud
{
	Obituary "%o killed %hself."
	DamageType "PoisonBolt"
	Species "Player"
	+FRIENDLY
	-EXTREMEDEATH
	+NOEXTREMEDEATH
	+MOVEWITHSECTOR
	+FORCERADIUSDMG
	+THRUSPECIES
	+DONTHARMCLASS
	+DONTHARMSPECIES
	+MTHRUSPECIES
	states
	{
	Death:
		GASC A 5 ThrustThing(random(0,256),8,0,0)
		GASC B 5 A_Stop
		GASC CD 5
		GASC A 0 A_PlaySound("strife/gascloud", CHAN_VOICE)
		GASC E 5 A_Explode (16, 128)
		TNT1 AAAAAAAAAAA 0 A_CustomMissile("StrifePoisonProjectile_2", 32, 0, Random(0,359), CMF_AIMOFFSET|CMF_TRACKOWNER)
		GASC F 5 A_RadiusGive("StrifeGasGreen",128,RGF_PLAYERS,1)
		GASC G 5 A_PlaySound("strife/gascloud", CHAN_VOICE)
		GASC H 5 A_RadiusGive("StrifeGasGreen",128,RGF_PLAYERS,1)
		GASC G 5 A_Explode (16, 128)
		TNT1 AAAAAAAAAAA 0 A_CustomMissile("StrifePoisonProjectile_2", 32, 0, Random(0,359), CMF_AIMOFFSET|CMF_TRACKOWNER)
		GASC F 5 A_RadiusGive("StrifeGasGreen",128,RGF_PLAYERS,1)
		GASC E 5 A_Explode (16, 128)
		TNT1 AAAAAAAAAAA 0 A_CustomMissile("StrifePoisonProjectile_2", 32, 0, Random(0,359), CMF_AIMOFFSET|CMF_TRACKOWNER)
		GASC F 5 A_RadiusGive("StrifeGasGreen",128,RGF_PLAYERS,1)
		GASC G 5 A_PlaySound("strife/gascloud", CHAN_VOICE)
		GASC H 5 A_RadiusGive("StrifeGasGreen",128,RGF_PLAYERS,1)
		GASC G 5 A_Explode (16, 128)
		TNT1 AAAAAAAAAAA 0 A_CustomMissile("StrifePoisonProjectile_2", 32, 0, Random(0,359), CMF_AIMOFFSET|CMF_TRACKOWNER)
		GASC F 5 A_RadiusGive("StrifeGasGreen",128,RGF_PLAYERS,1)
		GASC E 5 A_Explode (16, 128)
		TNT1 AAAAAAAAAAA 0 A_CustomMissile("StrifePoisonProjectile_2", 32, 0, Random(0,359), CMF_AIMOFFSET|CMF_TRACKOWNER)
		GASC F 5 A_RadiusGive("StrifeGasGreen",128,RGF_PLAYERS,1)
		GASC G 5 A_PlaySound("strife/gascloud", CHAN_VOICE)
		GASC H 5 A_RadiusGive("StrifeGasGreen",128,RGF_PLAYERS,1)
		GASC G 5 A_Explode (16, 128)
		TNT1 AAAAAAAAAAA 0 A_CustomMissile("StrifePoisonProjectile_2", 32, 0, Random(0,359), CMF_AIMOFFSET|CMF_TRACKOWNER)
		GASC F 5 A_RadiusGive("StrifeGasGreen",128,RGF_PLAYERS,1)
		GASC E 5 A_Explode (16, 128)
		TNT1 AAAAAAAAAAA 0 A_CustomMissile("StrifePoisonProjectile_2", 32, 0, Random(0,359), CMF_AIMOFFSET|CMF_TRACKOWNER)
		GASC F 5 A_RadiusGive("StrifeGasGreen",128,RGF_PLAYERS,1)
		GASC G 5 A_PlaySound("strife/gascloud", CHAN_VOICE)
		GASC H 5 A_RadiusGive("StrifeGasGreen",128,RGF_PLAYERS,1)
		GASC G 5 A_Explode (16, 128)
		TNT1 AAAAAAAAAAA 0 A_CustomMissile("StrifePoisonProjectile_2", 32, 0, Random(0,359), CMF_AIMOFFSET|CMF_TRACKOWNER)
		GASC F 5 A_RadiusGive("StrifeGasGreen",128,RGF_PLAYERS,1)
		GASC E 5 A_Explode (16, 128)
		TNT1 AAAAAAAAAAA 0 A_CustomMissile("StrifePoisonProjectile_2", 32, 0, Random(0,359), CMF_AIMOFFSET|CMF_TRACKOWNER)
		GASC DCBA 5
		Stop
	}
}

actor GrenadeMode : Counter { Inventory.MaxAmount 2 }
