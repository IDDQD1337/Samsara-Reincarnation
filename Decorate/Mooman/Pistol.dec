Actor " Aldus Pistol " : Weapon
{
	Weapon.Slotnumber 1
	Weapon.SelectionOrder 3750
	Weapon.BobStyle Inverse
	Weapon.BobSpeed 0.58
	Weapon.BobRangeX 1.1
	Weapon.BobRangeY 0.1
	Inventory.Pickupmessage "You have stolen Cyborg's Pistol! He's going to be mad at you..."
	Inventory.Pickupsound "greed/pickup"
	DamageType "PGBullet"
	+INVENTORY.UNDROPPABLE
	Tag "Pistol"	
	Obituary "$SAMSARA_GREED_OB_SLOT_PISTOL"
    Inventory.RestrictedTo "POGreedPlayer"	 		
	states
	{
	Ready:
		TNT1 A 0 A_JumpIfInventory("IPoGMode",4,"ReadyCyborg")
		TNT1 A 0 A_JumpIfInventory("IPoGMode",3,"ReadySpecimen")
		TNT1 A 0 A_JumpIfInventory("IPoGMode",2,2)
		TNT1 A 0 A_JumpIfInventory("IPoGMode",1,"ReadyCyborg")
		GP11 A 1 A_WeaponReady
		Goto Ready
	ReadyCyborg:
		GWP0 A 1 A_WeaponReady
		Goto Ready
	ReadySpecimen:
		GW80 A 1 A_WeaponReady
		Goto Ready
	Fire:
		GWP0 A 0 A_JumpIfInventory("PistolModeOn",1,"CheckAmmo")
		TNT1 A 0 A_JumpIfInventory("IPoGMode",4,"FireCyborg")
		TNT1 A 0 A_JumpIfInventory("IPoGMode",3,"FireSpecimen")
		TNT1 A 0 A_JumpIfInventory("IPoGMode",2,"FireDo")
		TNT1 A 0 A_JumpIfInventory("IPoGMode",1,"FireCyborg")
	FireDo:
		GP11 B 0 A_JumpIfInventory("CoopModeOn",1,"CoopFire")	
		GP11 B 0 A_PlayWeaponSound("greed/pistol")		
		GP11 B 4 bright A_FireBullets(1,1,-1,15,"PGBulletPuff",FBF_NORANDOM)
		GP11 C 4
		GP11 A 5
		goto Ready
	CoopFire:
		GP11 B 0 A_PlayWeaponSound("greed/pistol")	
		GP11 B 4 bright A_FireCustomMissile("PGPistolBulletCoop",frandom(-1.00,1.00),0,0,0,0,frandom(-1.00,1.00))		
		GP11 C 4
		GP11 A 5
		goto Ready
	FireCyborg:
		GWP0 B 0 A_JumpIfInventory("CoopModeOn",1,"CoopFireCyborg")	
		GWP0 B 0 A_PlayWeaponSound("greed/pistol")		
		GWP0 B 3 bright A_FireBullets(1,1,-1,15,"PGBulletPuff",FBF_NORANDOM)
		GWP0 C 3
		GWP0 A 4
		goto Ready
	CoopFireCyborg:
		GWP0 B 0 A_PlayWeaponSound("greed/pistol")	
		GWP0 B 3 bright A_FireCustomMissile("PGPistolBulletCoop",frandom(-1.00,1.00),0,0,0,0,frandom(-1.00,1.00))		
		GWP0 C 3
		GWP0 A 4
		goto Ready
	FireSpecimen:
		GW80 C 2 bright A_PlayWeaponSound("greed/mutant")
		GW80 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopFireSpecimen")
		GW80 D 3 bright A_FireCustomMissile("PGPoisonBall",0,1,0,0)
		GW80 C 3 bright
		GW80 B 3 bright
		GW80 A 4 A_Refire
		Goto Ready
	CoopFireSpecimen:
		GW80 D 3 bright A_FireCustomMissile("PGPoisonBallCoop",0,1,0,0)
		GW80 C 3 bright
		GW80 B 3 bright
		GW80 A 4 A_Refire
		Goto Ready
	Select:
		TNT1 A 0 A_JumpIfInventory("IPoGMode",4,"SelectCyborg")
		TNT1 A 0 A_JumpIfInventory("IPoGMode",3,"SelectSpecimen")
		TNT1 A 0 A_JumpIfInventory("IPoGMode",2,2)
		TNT1 A 0 A_JumpIfInventory("IPoGMode",1,"SelectCyborg")
		TNT1 AAA 0 A_Raise
		GP11 A 1 A_Raise
		Loop
	SelectCyborg:
		TNT1 AAA 0 A_Raise
		GWP0 A 1 A_Raise
		Goto Select
	SelectSpecimen:
		TNT1 AAA 0 A_Raise
		GW80 A 1 A_Raise
		Goto Select
	Deselect:
		TNT1 A 0 A_JumpIfInventory("IPoGMode",4,"DeselectCyborg")
		TNT1 A 0 A_JumpIfInventory("IPoGMode",3,"DeselectSpecimen")
		TNT1 A 0 A_JumpIfInventory("IPoGMode",2,2)
		TNT1 A 0 A_JumpIfInventory("IPoGMode",1,"DeselectCyborg")
		TNT1 AAA 0 A_Lower
		GP11 A 1 A_Lower
		Loop
	DeselectCyborg:
		TNT1 AAA 0 A_Lower
		GWP0 A 1 A_Lower
		Goto Deselect
	DeselectSpecimen:
		TNT1 AAA 0 A_Lower
		GW80 A 1 A_Lower
		Goto Deselect
	CheckAmmo:
		GWP0 A 0 A_JumpIfInventory("Clip",1,2)
		GWP0 A 0 A_SelectWeapon(" Tazer ")
		goto Deselect
		GWP0 A 0 A_TakeInventory("Clip",1)
		goto FireDo
	}
}

Actor PGPistolBulletCoop : PGBulletCoop { Damage (15) }