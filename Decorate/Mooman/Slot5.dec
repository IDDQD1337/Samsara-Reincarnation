Actor " Fast Rocket Launcher " : Weapon
{
	Weapon.Slotnumber 5
	Weapon.SelectionOrder 2000
	Weapon.BobStyle Inverse
	Weapon.BobSpeed 0.58
	Weapon.BobRangeX 1.1
	Weapon.BobRangeY 0.1	
	Inventory.Pickupmessage "You got a not-so-fast Rocket Launcher!"
	Inventory.Pickupsound "greed/pickup"
	Weapon.AmmoType "RocketAmmo"
    Weapon.AmmoUse 1
    Weapon.AmmoGive 5
	+WEAPON.NOAUTOFIRE
	scale 0.9
	Tag "Rocket Launcher"	
	Obituary "$SAMSARA_GREED_OB_SLOT_V"
    Inventory.RestrictedTo "POGreedPlayer"	 		
	states
	{
	Spawn:
		GW50 Z -1
		stop
	Ready:
		GW50 A 1 A_WeaponReady
		loop
	Select:
		TNT1 AAA 0 A_Raise
		GW50 A 1 A_Raise
		Loop
	Deselect:
		TNT1 AAA 0 A_Lower
		GW50 A 1 A_Lower
		Loop
	Fire:
		GW50 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopFire")
		GW50 B 0 bright A_PlayWeaponSound("greed/rocket")
		GW50 B 3 bright A_FireCustomMissile("PGRocket",0,1,0,0)
		GW50 C 4
		GW50 B 4
		GW50 A 12
		GW50 A 0 A_Refire
		goto Ready
	CoopFire:
		GW50 B 0 bright A_PlayWeaponSound("greed/rocket")
		GW50 B 3 bright A_FireCustomMissile("PGRocketCoop",0,1,0,0)
		GW50 C 4
		GW50 B 4
		GW50 A 12
		GW50 A 0 A_Refire
		Goto Ready
	}
}

Actor PGRocket
{
	Radius 6
	Height 10
	Speed 35
	PROJECTILE
	Damage (100)
	DamageType "PGExplosive"
	scale 0.375
	states
	{
	Spawn:
		GP50 A 3 bright A_SpawnItemEx("PGBulletPuffRocket",0,0,0,0,0,1.75,0,SXF_CLIENTSIDE)
		loop
	Death:
		TNT1 A 0 A_Explode(156,136,1)
		TNT1 A 0 A_SpawnItemEx("PGRocketPuff",0,0,0,0,0,0,4,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
		TNT1 AAAAAA 2 A_SpawnItemEx("PGRocketSmallPuff",random(-32,32),random(-32,32),random(-24,24),0,0,random(1,3),0,SXF_CLIENTSIDE)
		stop
	}
}

ACTOR PGBulletPuffRocket : PGBulletPuff
{
	States
	{
	Spawn:
	Crash:
		TNT1 A 2
		GP00 ABCD 4 bright
		stop
	Melee:
		TNT1 A 1
		Stop
	}
}

Actor PGRocketPuff
{
	+CLIENTSIDEONLY
	+NOGRAVITY
	+NOBLOCKMAP
	+NOINTERACTION
	-SOLID
	Renderstyle translucent
	Alpha 0.75
	scale 0.8
	states
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_PlaySound("greed/rockboom",CHAN_BODY)
	Death:
		GP30 ABCD 4 bright
		stop
	}
}

Actor PGRocketSmallPuff
{
	+CLIENTSIDEONLY
	+NOGRAVITY
	+NOBLOCKMAP
	+NOINTERACTION
	-SOLID
	Renderstyle translucent
	Alpha 0.75
	scale 0.7
	states
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_Jump(128,"Death2")
		TNT1 A 0 A_PlaySound("greed/rockboom",CHAN_BODY)
	Death:
		GP30 ABCD 5 bright
		stop
	Death2:
		TNT1 A 0 A_PlaySound("greed/flamehit",CHAN_BODY)
		GP31 ABCD 5 bright
		stop
	}
}

Actor PGRocketCoop : PGRocket {	
	+THRUSPECIES 
	Species "Player" 
	+DONTHARMCLASS 
	+DONTHARMSPECIES 
	+MTHRUSPECIES	
}