actor "Double Shotgun" : SamsaraQuakeWeapon
{
    Inventory.PickupMessage "You got the Double-Barreled Shotgun."
    Weapon.SelectionOrder 6
    Weapon.Kickback 100
    Weapon.AmmoType "Shell"
    Weapon.AmmoGive 10
    Weapon.AmmoUse 1
    Obituary "$SAMSARA_RANGER_OB_SLOT2"
    Tag "Double Shotgun"
    States
    {
      Spawn:
        QDBS Z -1
        stop

	  Ready:
        QDBS A 1 A_WeaponReady
        loop

      Select:
        TNT1 A 0 A_TakeInventory("Ranger_AxeSelected")
        TNT1 A 0 A_GiveInventory("Ranger_GunSelected")
        TNT1 A 0 A_TakeInventory("Ranger_ExplosiveSelected")
        TNT1 A 0 A_TakeInventory("Ranger_NailSelected")
        TNT1 A 0 A_TakeInventory("Ranger_PlasmaSelected")
        goto Super::Select

      // 25 tics
      Fire:
        QDBS B 0 A_GiveInventory("SamsaraQuakeWeaponAlert")
        QDBS B 0 A_GiveInventory("SamsaraQuakeDoubleShotgunAttackHandler")
        QDBS BC 1 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        QDBS D 2 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        QDBS EF 7 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        QDBS G 5 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        QDBS A 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        QDBS A 0 A_ReFire
        goto Ready
    }
}

actor SamsaraQuakeDoubleShotgunAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupPlaySound")
        TNT1 A 0 A_JumpIfInventory("Shell",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Shell",1)
        goto PickupPlaySound

      // Sound stuff
      PickupPlaySound:
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",2,"PickupAttack") // defined in attack state
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupPlaySoundRerelease")
      // Quake I (Original) sounds
      PickupPlaySoundOriginal:
        TNT1 A 0 A_PlaySound("quakeweps/ssgfire",CHAN_WEAPON)
        goto PickupAttack

      // Quake I (Rerelease) sounds
      PickupPlaySoundRerelease:
        TNT1 A 0 A_PlaySound("quakeweps/rerelease/ssgfire",CHAN_WEAPON)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",2,"PickupAttackDMC")
      // Quake I sounds (of which there are not actually any)
      PickupAttackQuakeI:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackQuakeICoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackQuakeICoop")
      PickupAttackQuakeIDM:
        TNT1 A 0 A_FireBullets(7,5,14,5,"QuakeBulletPuffThruGhost",FBF_NORANDOM|FBF_NOFLASH)
        goto PickupFinish

      PickupAttackQuakeICoop:
        TNT1 A 0 A_FireBullets(7,5,14,5,"QuakeBulletPuffThruGhostCoop",FBF_NORANDOM|FBF_NOFLASH)
        goto PickupFinish

      // Deathmatch Classic sounds
      PickupAttackDMC:
        TNT1 A 0 A_PlaySound("quakeweps/dmc/ssgfire",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackDMCCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackDMCCoop")
      PickupAttackDMCDM:
        TNT1 A 0 A_FireBullets(7,5,14,5,"QuakeBulletPuffThruGhostSoundDMC",FBF_NORANDOM|FBF_NOFLASH)
        goto PickupFinish

      PickupAttackDMCCoop:
        TNT1 A 0 A_FireBullets(7,5,14,5,"QuakeBulletPuffThruGhostCoopSoundDMC",FBF_NORANDOM|FBF_NOFLASH)
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 ACS_NamedExecuteAlways("SamsaraRecoil", 0, 4, 3, 17)
        stop
    }
}
