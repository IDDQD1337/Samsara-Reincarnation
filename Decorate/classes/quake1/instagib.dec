actor "Rocket Powered Impaler" : SamsaraQuakeWeapon
{
    Inventory.PickupMessage "You got the Rocket Powered Impaler."
    Weapon.SlotNumber 0
    Weapon.SelectionOrder 100
    Weapon.Kickback 100
    Obituary "$SAMSARA_RANGER_OB_INSTAGIB_1"
    Tag "Rocket-Powered Impaler"
    +NOAUTOAIM
    States
    {
      Ready:
        QIMP A 1 A_WeaponReady
        loop

      Fire:
        QIMP B 0 A_GiveInventory("SamsaraQuakeWeaponAlert")
        QIMP B 0 A_PlaySound("quake/impalefire",CHAN_WEAPON)
        QIMP BCDEF 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        QIMP G 0 A_GunFlash
        QIMP G 0 A_GiveInventory("SamsaraQuakeRocketPoweredImpalerAttackHandler")
        QIMP G 10 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        QIMP H 0 A_PlaySound("quake/impaload")
        QIMP HI 4 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        QIMP JKLMA 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        QIMP A 0 A_ReFire
        goto Ready

      Flash:
        TNT1 A 5 Bright A_Light1
        TNT1 A 5 Bright A_Light2
        goto LightDone
    }
}

actor SamsaraQuakeRocketPoweredImpalerAttackHandler : Trigger
{
    States
    {
      // Attack stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("QuadDamagePower",1,"PickupQuad")
      // Normal
      PickupNormal:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupNormalCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupNormalCoop")
      PickupNormalDM:
        TNT1 A 0 A_FireBullets(0,0,1,10000,"QuakeImpalerPuff",FBF_NORANDOM|FBF_NOFLASH)
        goto PickupFinish

      PickupNormalCoop:
        TNT1 A 0 A_FireBullets(0,0,1,10000,"QuakeImpalerPuffCoop",FBF_NORANDOM|FBF_NOFLASH)
        goto PickupFinish

      // Quad
      PickupQuad:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupQuadCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupQuadCoop")
      PickupQuadDM:
        TNT1 A 0 A_FireBullets(0,0,1,10000,"QuakeImpalerPuffQuadded",FBF_NORANDOM|FBF_NOFLASH)
        goto PickupFinish

      PickupQuadCoop:
        TNT1 A 0 A_FireBullets(0,0,1,10000,"QuakeImpalerPuffQuaddedCoop",FBF_NORANDOM|FBF_NOFLASH)
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 A_Recoil(10)
        TNT1 A 0 ACS_NamedExecuteAlways("SamsaraRecoil", 0, 10, 8, 20)
        stop
    }
}

actor QuakeImpalerPuff : BulletPuff
{
    Scale 0.5
    VSpeed 0
    Decal "DoomImpScorch"
    -SOLID
    +PUFFONACTORS
    +ALWAYSPUFF
    +PUFFGETSOWNER
    States
    {
    Crash:
        TNT1 A 0
        TNT1 A 1 A_PlaySound("quake/impalwall")
        TNT1 AAAAA 0 A_SpawnItemEx("QuakeBulletPuff2",random(-7,7),random(-7,7),random(-3,1),0,0,random(-10,1)/10)
        TNT1 AAAAAAAAAA 0 A_SpawnItemEx("QuakeBulletPuff3",random(-7,7),random(-7,7),random(-3,1),0,0,random(-10,1)/10)
        TNT1 AAAAA 0 A_SpawnItemEx("QuakeBulletPuff4",random(-7,7),random(-7,7),random(-3,1),0,0,random(-10,1)/10)
        TNT1 A 0 A_SpawnItemEx("QuakeImpalerExplode",0,0,0,0,0,0,0,32)
        TNT1 A 0 A_SpawnItemEx("QuakeImpalerExplosion",0,0,0,0,0,0,0,32)
        TNT1 A 1
        Stop
    Spawn:
    Melee:
        TNT1 A 0
        TNT1 A 1 A_PlaySound("quake/impalehit")
        TNT1 A 0 A_SpawnItemEx("QuakeImpalerExplode",0,0,0,0,0,0,0,32)
        TNT1 A 0 A_SpawnItemEx("QuakeImpalerExplosion",0,0,0,0,0,0,0,32)
        TNT1 A 1
        Stop
   }
}

actor QuakeImpalerPuffCoop : QuakeImpalerPuff { +MTHRUSPECIES }

actor QuakeImpalerPuffQuadded : QuakeImpalerPuff
{
    States
    {
    Crash:
        TNT1 A 0
        TNT1 A 1 A_PlaySound("quake/impalwall")
        TNT1 AAAAA 0 A_SpawnItemEx("QuakeBulletPuff2",random(-7,7),random(-7,7),random(-3,1),0,0,random(-10,1)/10)
        TNT1 AAAAAAAAAA 0 A_SpawnItemEx("QuakeBulletPuff3",random(-7,7),random(-7,7),random(-3,1),0,0,random(-10,1)/10)
        TNT1 AAAAA 0 A_SpawnItemEx("QuakeBulletPuff4",random(-7,7),random(-7,7),random(-3,1),0,0,random(-10,1)/10)
        TNT1 A 0 A_SpawnItemEx("QuakeImpalerExplodeQuad",0,0,0,0,0,0,0,32)
        TNT1 A 0 A_SpawnItemEx("QuakeImpalerExplosion",0,0,0,0,0,0,0,32)
        TNT1 A 1
        Stop
    Spawn:
    Melee:
        TNT1 A 0
        TNT1 A 1 A_PlaySound("quake/impalehit")
        TNT1 A 0 A_SpawnItemEx("QuakeImpalerExplodeQuad",0,0,0,0,0,0,0,32)
        TNT1 A 0 A_SpawnItemEx("QuakeImpalerExplosion",0,0,0,0,0,0,0,32)
        TNT1 A 1
        Stop
   }
}

actor QuakeImpalerPuffQuaddedCoop : QuakeImpalerPuffQuadded { +MTHRUSPECIES }

actor QuakeImpalerExplode
{
    Radius 1
    Height 1
    Speed 0
    DamageType "EnemyInfightDamage"
    +NOCLIP
    -SOLID
    Obituary "%k shouldn't have killed %o with this."
    States
    {
      Spawn:
        TNT1 A 0
        TNT1 A 1 A_Explode(128,128)
        Stop
    }
}

actor QuakeImpalerExplodeQuad : QuakeImpalerExplode
{
    States
    {
      Spawn:
        TNT1 A 0
        TNT1 A 1 A_Explode(512,128)
        Stop
    }
}

actor QuakeImpalerExplosion : QuakeExplosion
{
    States
    {
      Spawn:
        QEXP A 0 bright
        QEXP A 0 ACS_NamedExecuteWithResult("SamsaraDecorate", 8)
        goto SpawnLoop

      SpawnLoop:
        QEXP A 0 A_JumpIfInventory("QuakeExplosionCounter", 1, 1)
        goto Death
        QEXP A 0 A_CustomMissile("QuakeExplosionParticle",  0, 0, random(-180, 180), 2, random(-180, 180))
        QEXP A 0 A_CustomMissile("QuakeExplosionParticle2", 0, 0, random(-180, 180), 2, random(-180, 180))
        QEXP A 0 A_CustomMissile("QuakeExplosionParticle3", 0, 0, random(-180, 180), 2, random(-180, 180))
        QEXP A 0 ACS_NamedExecuteWithResult("SamsaraDecorate", 10, 1)
        loop

      Death:
        QEXP A 4 bright A_PlaySound("quake/impalsplod")
        QEXP B 4 bright
        QEXP CD 3 bright
        QEXP EF 2 bright
        stop
    }
}
