actor "Gauntlet" : SamsaraQuakeWeapon
{
    Inventory.PickupMessage "You got the Gauntlet."
    Weapon.SelectionOrder 9
    Weapon.Kickback 100
    Weapon.YAdjust 16
    Obituary "$SAMSARA_RANGER_OB_PUNCHDRUNKC"
    Tag "Gauntlet"
    +MELEEWEAPON
    +CHEATNOTWEAPON
    States
    {
      Spawn:
        QGNT Z -1
        stop

      Ready:
        QGNT A 1 A_WeaponReady
        loop

      Deselect:
        TNT1 A 0 A_PlaySound("silence",CHAN_WEAPON)
        TNT1 A 0 A_StopSound(CHAN_WEAPON)
        goto Super::Deselect

      Select:
        TNT1 A 0 A_GiveInventory("Ranger_AxeSelected")
        TNT1 A 0 A_TakeInventory("Ranger_GunSelected")
        TNT1 A 0 A_TakeInventory("Ranger_ExplosiveSelected")
        TNT1 A 0 A_TakeInventory("Ranger_NailSelected")
        TNT1 A 0 A_TakeInventory("Ranger_PlasmaSelected")
        goto Super::Select

      Fire:
        QGNT B 0 A_GiveInventory("SamsaraQuakeWeaponAlert")
        QGNT B 0 A_GiveInventory("SamsaraQuakeGauntletAttackHandler")
        QGNT B 0 A_JumpIfInventory("SamsaraHasDoubleFiringSpeed",1,"FireRage")
        QGNT B 1 Bright Offset(0,33)
        QGNT C 1 Bright Offset(0,34)
        QGNT D 1 Bright Offset(0,33)
        QGNT A 1 Bright Offset(0,32)
        QGNT G 0 A_ReFire
        QGNT G 0 A_StopSound(CHAN_WEAPON)
        QGNT B 1 Bright A_ReFire
        QGNT CDEFGHIJK 1 A_ReFire
        goto Ready

      FireRage:
        QGNT B 0 A_Jump(256,"FireRage1","FireRage2")
      FireRage1:
        QGNT B 2 Bright Offset(0,34)
        QGNT C 2 Bright Offset(0,32)
        goto FireRageFinish

      FireRage2:
        QGNT D 2 Bright Offset(0,33)
        QGNT A 2 Bright Offset(0,35)
        goto FireRageFinish

      FireRageFinish:
        QGNT D 0 A_ReFire
        QGNT D 0 A_StopSound(CHAN_WEAPON)
        QGNT D 2 Bright A_ReFire
        QGNT CEFGHIJK 2 A_ReFire
        goto Ready
    }
}

actor SamsaraQuakeGauntletAttackHandler : Trigger
{
    States
    {
      // Attack stuff
      Pickup:
        TNT1 A 0 A_PlaySound("quakeweps/gauntlet",CHAN_WEAPON,1,true)
        TNT1 A 0 ACS_NamedExecuteAlways("SamsaraClientDecorate", 0, 9)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupCoop")
      PickupDM:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupDMSpread")
        TNT1 A 0 A_CustomPunch(30,true,0,"GauntletPuff")
        stop

      PickupDMSpread:
        TNT1 A 0 A_CustomPunch(30*3,true,0,"GauntletPuff")
        stop

      PickupCoop:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupCoopSpread")
        TNT1 A 0 A_CustomPunch(30,true,0,"GauntletPuffCoop")
        stop

      PickupCoopSpread:
        TNT1 A 0 A_CustomPunch(30*3,true,0,"GauntletPuffCoop")
        stop
    }
}

actor GauntletPuff: QuakeBulletPuff
{
    +PUFFONACTORS
    SeeSound "quakeweps/gauntlethit"
    States
    {
      Melee:
        TNT1 A 1
        stop

      Crash:
        goto Super::Spawn
    }
}

actor SpectralGauntletPuff : GauntletPuff { DamageType "Spectral" }

actor GauntletPuffCoop : GauntletPuff { +MTHRUSPECIES }
actor SpectralGauntletPuffCoop : SpectralGauntletPuff { +MTHRUSPECIES }

actor GauntletSparks
{
    +NOINTERACTION
    +CLIENTSIDEONLY
    Radius 2
    Height 2
    RenderStyle Add
    Alpha 0.75
    Scale 0.4
    States
    {
      Spawn:
        DPLE ABCDE 2 bright
        stop
    }
}
