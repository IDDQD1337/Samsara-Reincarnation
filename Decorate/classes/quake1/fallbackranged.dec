actor Ranger_GunSelected : Boolean { Inventory.InterHubAmount 0 }
actor QSG_FiredOnce: Boolean {}

actor "Single Shotgun" : SamsaraQuakeWeapon
{
    Inventory.PickupMessage "You got the Single-Barreled Shotgun."
    Weapon.SelectionOrder 7
    Weapon.Kickback 100
    Obituary "$SAMSARA_RANGER_OB_FALLBACKRANGED"
    Tag "Single Shotgun"
    +WIMPY_WEAPON
    States
    {
      Spawn:
        QSHT Z -1
        stop

	  Ready:
        QSGN A 1 A_WeaponReady
        loop

      Select:
        TNT1 A 0 A_TakeInventory("Ranger_AxeSelected")
        TNT1 A 0 A_GiveInventory("Ranger_GunSelected")
        TNT1 A 0 A_TakeInventory("Ranger_ExplosiveSelected")
        TNT1 A 0 A_TakeInventory("Ranger_NailSelected")
        TNT1 A 0 A_TakeInventory("Ranger_PlasmaSelected")
        goto Super::Select

      Fire:
        QSGN B 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto FireStart
        QSGN B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart")
        QSGN B 0 A_JumpIfInventory("Shell",1,"FireStart")
        goto NoAmmoSwitch

      // 18 tics
      FireStart:
        QSGN B 0 A_GiveInventory("SamsaraQuakeWeaponAlert")
        QSGN B 0 A_GiveInventory("SamsaraQuakeSingleShotgunAttackHandler")
        QSGN BC 2 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        QSGN DE 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        QSGN FG 4 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
      FireFinish:
        QSGN A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto FireFinishReFire
        QSGN A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireFinishReFire")
        QSGN A 0 A_JumpIfInventory("Shell",1,"FireFinishReFire")
        goto NoAmmoSwitch

      FireFinishReFire:
        QSGN A 0 A_ReFire
        goto Ready

      // "No ammo" switch
      NoAmmoSwitch:
        TNT1 A 0 A_ClearReFire
        TNT1 A 0 A_SelectWeapon("Axe")
        goto Deselect
    }
}

actor SamsaraQuakeSingleShotgunAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto PickupAttack
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Shell",1,1)
        stop
        TNT1 A 0 A_JumpIfInventory("QSG_FiredOnce",0,2)
        TNT1 A 0 A_GiveInventory("QSG_FiredOnce")
        goto PickupAttack
        TNT1 A 0 A_TakeInventory("Shell",1)
        TNT1 A 0 A_TakeInventory("QSG_FiredOnce")
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("quakeweps/shotgunfire",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireBullets(1.5,1.5,6,5,"QuakePistolPuff",FBF_NORANDOM|FBF_NOFLASH)
        goto PickupFinish

      PickupAttackCoop:
        TNT1 A 0 A_FireBullets(1.5,1.5,6,5,"QuakePistolPuffCoop",FBF_NORANDOM|FBF_NOFLASH)
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 ACS_NamedExecuteAlways("SamsaraRecoil", 0, 2, 3, 11)
        stop
    }
}
