Actor " RR Crowbar " : Weapon
{
    Weapon.Slotnumber 0
    Weapon.Slotpriority 0
    Weapon.BobRangeX 0.75
    Weapon.BobRangeY 0.95
    Weapon.BobSpeed 1.1    
    +Weapon.WIMPY_WEAPON
    +Weapon.MELEEWEAPON
    +Weapon.NOALERT
    +Inventory.UNDROPPABLE
    Weapon.SelectionOrder 7000
    Weapon.Kickback 100
    Obituary "%o was smacked by %k."
    Inventory.RestrictedTo "Leonard"
    Tag "Crowbar"    
    States
    {
        Spawn:
            TNT1 A -1
            Stop
        Select:
            TNT1 A 0 A_Raise
            RR_C A 1 A_Raise
            Loop
        Deselect:
            TNT1 A 0 A_Lower
            RR_C A 1 A_Lower
            Loop
        Ready:
            TNT1 A 0 A_JumpIfInventory("RRDrunkToken", 1, "SetDrunkView")
            Goto SetSoberView
            TNT1 A 0 A_JumpIfInventory("LeonardWaiting", 15, "WaitingTaunt")
            TNT1 A 0 A_JumpIfInventory("LeonardReady",1,"IntroTaunt")        
            RR_C A 1 A_WeaponReady
            Loop
        Fire:
            TNT1 A 0 A_JumpIfInventory("RRDrunkToken", 1, "SetDrunkViewFire")
            Goto SetSoberViewFire
            TNT1 A 0 A_PlaySound("RR/CrowbarS",CHAN_WEAPON)
            RR_C BCD 3 Bright
            RR_C EF 2 Bright
            RR_C G 3 Bright A_GiveInventory("SamsaraRedneckRampageCrowbarAttackHandler")
            RR_C H 2 Bright
            Goto Ready
        IntroTaunt:
            TNT1 A 0 A_JumpIfInventory("SamsaraBallgag",1,2)
            TNT1 A 0 A_PlaySoundEx("Leonard/Enter", "SoundSlot7", 0, 0)
            TNT1 A 0 A_TakeInventory("LeonardReady", 1)
            TNT1 A 0 A_GiveInventory("LeonardTauntCooldown",5)
            TNT1 A 0 ACS_NamedExecuteAlways("LeonardTauntCooldown", 0)
            Goto Ready
        WaitingTaunt:
            TNT1 A 0 A_JumpIfInventory("SamsaraIdleBallgag",1,"IdleBallgagStuff")
            TNT1 A 0 A_JumpIfInventory("SamsaraBallgag",1,2)
            TNT1 A 0 A_PlaySoundEx("Leonard/Bored", "SoundSlot7", 0, 0)
            TNT1 A 0 A_TakeInventory("LeonardWaiting", 15)
            TNT1 A 0 A_GiveInventory("LeonardTauntCooldown",5)
            TNT1 A 0 ACS_NamedExecuteAlways("LeonardTauntCooldown", 0)
            Goto Ready
        IdleBallgagStuff:
            TNT1 A 0 A_TakeInventory("LeonardWaiting", 15)
            goto Ready
        SetDrunkView:
            TNT1 A 0 A_ZoomFactor(0.55)
            Goto Ready+1
        SetDrunkViewFire:
            TNT1 A 0 A_ZoomFactor(0.55)
            Goto Fire+1
        SetSoberView:
            TNT1 A 0 A_ZoomFactor(1.0)
            Goto Ready+1
        SetSoberViewFire:
            TNT1 A 0 A_ZoomFactor(1.0)
            Goto Fire+1
    }
}

actor SamsaraRedneckRampageCrowbarAttackHandler : Trigger
{
    States
    {
      // Attack stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupCoop")
      PickupDM:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupDMSpread")
        TNT1 A 0 A_CustomPunch(random(15,20),true,0,"RRPuff3")
        stop

      PickupDMSpread:
        TNT1 A 0 A_CustomPunch(random(15,20)*3,true,0,"RRPuff3")
        stop

      PickupCoop:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupCoopSpread")
        TNT1 A 0 A_CustomPunch(random(15,20),true,0,"RRPuff3Coop")
        stop

      PickupCoopSpread:
        TNT1 A 0 A_CustomPunch(random(15,20)*3,true,0,"RRPuff3Coop")
        stop
    }
}
