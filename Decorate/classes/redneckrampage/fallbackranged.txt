Actor " .454 Casull Pistol " : Weapon
{
    Inventory.PickupSound "RR/Wpickup"
	+WEAPON.WIMPY_WEAPON
	+INVENTORY.UNDROPPABLE
    +NOALERT
    +AMMO_OPTIONAL
	Weapon.SelectionOrder 8000
    Weapon.BobRangeX 0.75
    Weapon.BobRangeY 0.95
    Weapon.BobSpeed 1.1	
	Weapon.SlotNumber 1
	Weapon.SlotPriority 1
	Weapon.AmmoType "LeonardPistolReload"
	Weapon.AmmoUse 1
	Inventory.PickupMessage "Casull Pistol"	
	Obituary "%k made %o leave his land."
	Weapon.UpSound "Leonard/PistolU"
	Inventory.RestrictedTo "Leonard"
    Tag ".454 Casull Pistol"
	States
	{
		Spawn:
			RRP_ A -1 Bright
			Stop

		Select:
			TNT1 A 0 A_Raise
			RRP_ B 1 A_Raise
			Loop

		Deselect:
			TNT1 A 0 A_Lower
			RRP_ B 1 A_Lower
			Loop

		Ready:
			TNT1 A 0 A_JumpIfInventory("RRDrunkToken", 1, "SetDrunkView")
			Goto SetSoberView
			TNT1 A 0 A_JumpIfInventory("LeonardWaiting", 15, "WaitingTaunt")
			TNT1 A 0 A_JumpIfInventory("LeonardReady",1,"IntroTaunt")
            TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",0,"ReadyModernReloadable")
        ReadyNonReloadable:
            RRP_ B 1 A_WeaponReady
            goto Ready

        ReadyModernReloadable:
            TNT1 A 0 A_JumpIfInventory("LeonardPistolReload",0,"ReadyNonReloadable")
            TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,"ReadyModernReloadableReserve")
        ReadyModernReloadableCanReload:
            RRP_ B 1 A_WeaponReady(WRF_ALLOWRELOAD)
            goto Ready

        ReadyModernReloadableReserve:
            TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
            TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReadyModernReloadableCanReload")
            TNT1 A 0 A_JumpIfInventory("Clip",1,"ReadyModernReloadableCanReload")
            goto ReadyNonReloadable

		Fire:
			TNT1 A 0 A_JumpIfInventory("RRDrunkToken", 1, "SetDrunkViewFire")
			Goto SetSoberViewFire
            TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"FireReloadable")
        FireNonReloadable:
            TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
            goto FireStart
            TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart")
            TNT1 A 0 A_JumpIfInventory("Clip",1,"FireStart")
            goto NoAmmoSwitch

        FireReloadable:
            TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireStart")
            TNT1 A 0 A_JumpIfInventory("LeonardPistolReload",1,"FireStart")
        FireReloadableDryCheck:
            TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
            goto Reload
            TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"Reload")
            TNT1 A 0 A_JumpIfInventory("Clip",1,"Reload")
            goto NoAmmoSwitch

        FireStart:
			RRP_ C 2 Bright A_AlertMonsters
			TNT1 A 0 A_GiveInventory("SamsaraLeonardPistolAttackHandler")
		A:	
			RRP_ DEFG 3 Bright
			RRP_ H 4 Bright
			RRP_ G 3 Bright
			RRP_ F 2 Bright
			RRP_ EI 1 Bright
            TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"AReloadable")
            goto AFinish

        AReloadable:
            TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"AFinish")
            TNT1 A 0 A_JumpIfInventory("LeonardPistolReload",1,"AFinish")
        AReloadableDryCheck:
            TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
            goto Reload
            TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"Reload")
            TNT1 A 0 A_JumpIfInventory("Clip",1,"Reload")
            goto AFinish

        AFinish:
			RRP_ B 2 Bright
			goto Ready

		Reload:
			RRP_ I 2 Bright A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
			TNT1 A 0 A_PlaySound("Leonard/PistolR",CHAN_WEAPON)
			RRP_ J 1 Bright A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
			RRP_ J 2 offset(-8,50) Bright A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
			RRP_ J 2 offset(-13,63) Bright A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
			RRP_ K 2 offset(-18,76) Bright A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
			RRP_ K 2 offset(-23,89) Bright A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
			RRP_ K 2 offset(-18,76) Bright A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)			
			RRP_ J 2 offset(-13,63) Bright A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
			RRP_ J 2 offset(-8,50) Bright A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)		
			RRP_ J 1 offset(0,37) Bright A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)			
			RRP_ I 2 Bright A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)			
            TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReloadAmmo")
        ReloadFinish:
            //RRP_ B 1 Bright A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
            TNT1 A 0
			goto Ready

      ReloadAmmo:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,"ReloadAmmoReserve")
        TNT1 A 0 A_JumpIfInventory("LeonardPistolReload",0,"ReloadFinish")
        TNT1 A 0 A_GiveInventory("LeonardPistolReload")
        loop

      ReloadAmmoReserve:
        TNT1 A 0 A_JumpIfInventory("LeonardPistolReload",0,"ReloadFinish")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        goto ReloadFinish
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_TakeInventory("Clip",1)
        TNT1 A 0 A_GiveInventory("LeonardPistolReload")
        goto ReloadAmmo

	  IntroTaunt:
			TNT1 A 0 A_JumpIfInventory("LeonardBallgag",1,2)
			TNT1 A 0 A_PlaySoundEx("Leonard/Enter", "SoundSlot7", 0, 0)
			TNT1 A 0 A_TakeInventory("LeonardReady", 1)
			TNT1 A 0 A_GiveInventory("LeonardTauntCooldown",5)
			TNT1 A 0 ACS_NamedExecuteAlways("LeonardTauntCooldown", 0)
			Goto Ready

	  WaitingTaunt:
			TNT1 A 0 A_JumpIfInventory("LeonardIdleBallgag",1,"IdleBallgagStuff")
			TNT1 A 0 A_JumpIfInventory("LeonardBallgag",1,2)
			TNT1 A 0 A_PlaySoundEx("Leonard/Bored", "SoundSlot7", 0, 0)
			TNT1 A 0 A_TakeInventory("LeonardWaiting", 15)
			TNT1 A 0 A_GiveInventory("LeonardTauntCooldown",5)
			TNT1 A 0 ACS_NamedExecuteAlways("LeonardTauntCooldown", 0)
			Goto Ready

	  IdleBallgagStuff:
			TNT1 A 0 A_TakeInventory("LeonardWaiting", 15)
			goto Ready

	  SetDrunkView:
			TNT1 A 0 A_ZoomFactor(0.55)
			Goto Ready+1

	  SetDrunkViewFire:
			TNT1 A 0 A_ZoomFactor(0.55)
			Goto Fire+1

	  SetSoberView:
			TNT1 A 0 A_ZoomFactor(1.0)
			Goto Ready+1

	  SetSoberViewFire:
			TNT1 A 0 A_ZoomFactor(1.0)
			Goto Fire+1

      // "No ammo" switch
      NoAmmoSwitch:
        TNT1 A 0 A_SelectWeapon(" RR Crowbar ")
        goto Deselect
	}
}

actor SamsaraLeonardPistolAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"PickupReloadable")
      PickupNonReloadable:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto PickupAttack
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",1)
        goto PickupAttack

      PickupReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("LeonardPistolReload",1,1)
        stop
        TNT1 A 0 A_TakeInventory("LeonardPistolReload",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("Leonard/PistolF",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireBullets(2,2,1,40,"RRPuff",FBF_NORANDOM|FBF_NOFLASH)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireBullets(2,2,1,40,"RRPuffCoop",FBF_NORANDOM|FBF_NOFLASH)
        stop
    }
}

Actor RRBullet1 : FastProjectile
{
  Speed 300
  Damage (40)
  Radius 1
  Height 1
  Species "Player"  
  Projectile
  +BLOODSPLATTER
  +NOTIMEFREEZE
  +NOEXTREMEDEATH
  +THRUSPECIES
  +DONTHARMCLASS
  DamageType "LeonardDamage0"  
  +DONTHARMSPECIES
  +MTHRUSPECIES
  Decal RedneckRampageBulletChip
  States
  {
  Spawn:
    TNT1 A 1
    Loop
  Death:
  Crash:  
   	TNT1 A 0 A_PlaySound("RR/RICOCHET")  
    TNT1 A 1 A_SpawnItemEx("RRPuff",0,0,0,0,0,1,0,0)        
    stop
  XDeath:
    TNT1 A 1
    stop
    }
}

actor LeonardPistolReload : Ammo
{
    Inventory.MaxAmount 6
    Tag ".454 Casull Pistol Cylinder"
    +IGNORESKILL
}
