// Tokens

actor DGStrHasStunnerRifle : Boolean {}
actor SamsaraDoomguyStrStunnerRifleReadySoundCheck : Boolean {}

// Weapon

actor "Stunner Rifle" : PlasmaRifle
{
	Inventory.PickupMessage "You got the stunner rifle!"
	Inventory.RestrictedTo "DoomguyPlayer"
	Weapon.SlotNumber 6
	Weapon.AmmoGive 0 //40
	Weapon.AmmoUse 5
	Tag "Stunner Rifle"
	+UNDROPPABLE
	States
	{
	Spawn:
		STPP A -1
		stop

	Ready:
		TNT1 A 0 A_PlaySound("doomguy/stronghold/stunnerriflehum",CHAN_6,1,true)
		TNT1 A 0 A_GiveInventory("SamsaraDoomguyStrStunnerRifleReadySoundCheck")
		STPO ABC 3 Bright A_WeaponReady
		loop

	// Unpowered
	Deselect:
		TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrStunnerRifleReadySoundCheck",1,"DeselectStopSound")
	DeselectStart:
		TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPower",1,"DeselectStartPowered")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"DeselectStartPowered")
	DeselectStartContinue:
		TNT1 A 0 A_Lower
		STPO E 1 A_Lower
		goto Deselect

	DeselectStopSound:
		TNT1 A 0 A_PlaySound("silence",CHAN_6)
		TNT1 A 0 A_StopSound(CHAN_6)
		TNT1 A 0 A_TakeInventory("SamsaraDoomguyStrStunnerRifleReadySoundCheck")
		goto DeselectStart

	Select:
		TNT1 A 0 A_TakeInventory("SamsaraDoomguyStrStunnerRifleReadySoundCheck")
		TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPower",1,"SelectPowered")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"SelectPowered")
	SelectContinue:
		TNT1 A 0 A_Raise
		STPO E 1 A_Raise
		goto Select

	Fire:
		TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrStunnerRifleReadySoundCheck",1,"FireStopSound")
	FireStart:
		TNT1 A 0 A_GunFlash("Flash",GFF_NOEXTCHANGE)
		TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPower",1,"FirePowered")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"FirePowered")
		TNT1 A 0 A_GiveInventory("SamsaraDoomguyStrStunnerRifleAttackHandler")
		STPO FGH 3 Bright
		STPO D 4 Bright
	FireFinish:
		STPO D 6 Bright A_ReFire
		goto Ready

	FireStopSound:
		TNT1 A 0 A_PlaySound("silence",CHAN_6)
		TNT1 A 0 A_StopSound(CHAN_6)
		TNT1 A 0 A_TakeInventory("SamsaraDoomguyStrStunnerRifleReadySoundCheck")
		goto FireStart

	Flash:
		TNT1 A 1 A_Light1
		TNT1 A 1 A_Light2
		TNT1 A 1 A_Light1
		goto LightDone

	// Powered
	DeselectStartPowered:
		TNT1 A 0 A_Lower
		goto DeselectStartContinue

	SelectPowered:
		TNT1 A 0 A_Raise
		goto SelectContinue

	FirePowered:
		TNT1 A 0 A_GiveInventory("SamsaraDoomguyStrStunnerRiflePoweredAttackHandler")
		STPO FGH 3 Bright
		STPO D 16 Bright
		goto FireFinish
	}
}

// Unpowered attack handler

actor SamsaraDoomguyStrStunnerRifleAttackHandler : Trigger
{
	States
	{
	// Ammo checking/taking stuff
	Pickup:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
		TNT1 A 0 A_JumpIfInventory("Cell",5,1)
		stop
		TNT1 A 0 A_TakeInventory("Cell",5)
		goto PickupAttack

	// Attack stuff
	PickupAttack:
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
	PickupAttackDM:
		TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrStunnerBall",0,false)
		stop

	PickupAttackCoop:
		TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrStunnerBallCoop",0,false)
		stop
	}
}

// Powered attack handler

actor SamsaraDoomguyStrStunnerRiflePoweredAttackHandler : Trigger
{
	States
	{
	// Ammo checking/taking stuff
	Pickup:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
		TNT1 A 0 A_JumpIfInventory("Cell",5,1)
		stop
		TNT1 A 0 A_TakeInventory("Cell",5)
		goto PickupAttack

	// Attack stuff
	PickupAttack:
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
	PickupAttackDM:
		TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrPowerStunnerBall",0,false)
		stop

	PickupAttackCoop:
		TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrPowerStunnerBallCoop",0,false)
		stop
	}
}

// Stunner ball

actor SamsaraDoomguyStrStunnerBall
{
	Projectile
	Radius 6
	Height 8
	Speed 20
	Damage (random(1,4))
	DamageType "DoomDamageTypeStunner"
	RenderStyle "Add"
	Scale 0.45
	SeeSound "doomguy/stronghold/stunnershot"
	DeathSound "doomguy/stronghold/stunnerhit"
	Decal "DoomStrStunnerScorch"
	Obituary "%k killed %o with the stunner rifle."
	-BLOODSPLATTER
	+RANDOMIZE
	+RIPPER
	States
	{
	Spawn:
		STPR A 1 Bright Light("StunnerBallLight") A_SpawnItemEx("SamsaraDoomguyStrStunnerBallFX")
		loop

	Death:
		STPR B 4 Bright Light("StunnerBallDeathA")
		STPR C 4 Bright Light("StunnerBallDeathB")
		STPR D 4 Bright Light("StunnerBallDeathC")
		STPR E 4 Bright Light("StunnerBallDeathD")
		STPR F 4 Bright Light("StunnerBallDeathE")
		stop
	}
}

actor SamsaraDoomguyStrStunnerBallCoop : SamsaraDoomguyStrStunnerBall
{
	Species "Player"
	+THRUSPECIES
}

// Powered stunner ball

actor SamsaraDoomguyStrPowerStunnerBall : SamsaraDoomguyStrStunnerBall
{
	+SCREENSEEKER
	+SEEKERMISSILE
	States
	{
	Spawn:
		STPR A 1 Bright Light("StunnerBallLight")
	SpawnLoop:
		TNT1 A 0 A_BishopMissileWeave
		TNT1 A 0 A_SeekerMissile(2,10,SMF_LOOK,50,10)
		STPR A 1 Bright Light("StunnerBallLight") A_SpawnItemEx("SamsaraDoomguyStrStunnerBallFX")
		loop
	}
}

actor SamsaraDoomguyStrPowerStunnerBallCoop : SamsaraDoomguyStrPowerStunnerBall
{
	Species "Player"
	+THRUSPECIES
}

// Stunner ball trail

actor SamsaraDoomguyStrStunnerBallFX
{
	Radius 1
	Height 2
	RenderStyle "Add"
	Scale 0.45
	+CLIENTSIDEONLY
	+NOBLOCKMAP
	+NOGRAVITY
	+NOTONAUTOMAP
	States
	{
	Spawn:
		STPR A 1 Bright A_FadeOut
		loop
	}
}

// Player speed debuff

actor SamsaraDoomguyStrStunnerSpeedDebuff : SamsaraPowerupGiver { Powerup.Type "SamsaraDoomguyStrStunnerSpeedDebuff" }

actor PowerSamsaraDoomguyStrStunnerSpeedDebuff : PowerSpeed // used for enemy attacks to slow down players
{
	Inventory.Icon ""
    Powerup.Duration 302
	Speed 0.1
	//-NOTELEPORTFREEZE // doesn't exist in zandronum yet
	+NOTRAIL
}
