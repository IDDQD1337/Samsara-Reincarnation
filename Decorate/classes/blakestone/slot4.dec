actor "Rapid Assault Weapon" : SamsaraBStoneWeapon
{
    Inventory.PickupMessage "You got a Rapid Assault Weapon!"
    Inventory.PickupSound "wolfen/knifeget"
    Weapon.SlotNumber 4
    Weapon.SelectionOrder 800
    Weapon.YAdjust 16
    Weapon.AmmoType "Clip"
    Weapon.AmmoGive 25
    Weapon.AmmoUse 1
    Obituary "%k punched holes in %o with the Rapid Assault Weapon."
    Tag "Rapid Assault Weapon"
    States
    {
      Spawn:
        BWP4 A -1
        stop

      // Planet Strike
      Ready:
        TNT1 A 0 A_JumpIfInventory("BlakeAOGToken",1,"ReadyAOG")
      ReadyPS:
        BRIF A 1 A_WeaponReady
        goto Ready

      Fire:
        TNT1 A 0 A_JumpIfInventory("BlakeAOGToken",1,"FireAOG")
      FirePS:
        BRIF B 2
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        goto HoldPS
        TNT1 A 0 A_GunFlash
        goto HoldPS

      Hold:
        TNT1 A 0 A_GunFlash
        TNT1 A 0 A_JumpIfInventory("BlakeAOGToken",1,"HoldAOG")
      HoldPS:
        BRIF C 3 Bright A_GiveInventory("SamsaraBStoneRapidAssaultWeaponAttackHandler")
        BRIF D 2
        BRIF A 3 A_ReFire
        goto Ready

      // Aliens Of Gold
      ReadyAOG:
        SRF5 F 1 A_WeaponReady(WRF_NOBOB)
        goto Ready

      FireAOG:
        SRF5 G 2
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        goto HoldAOG
        TNT1 A 0 A_GunFlash
        goto HoldAOG

      HoldAOG:
        SRF5 H 3 Bright A_GiveInventory("SamsaraBStoneRapidAssaultWeaponAttackHandler")
        SRF5 I 2
        SRF5 J 3 A_ReFire
        goto Ready
    }
}

actor SamsaraBStoneRapidAssaultWeaponAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("BRifle/Fire",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireBullets(0,0,-1,4,"BlankPuffBlakeThruGhost",FBF_NOFLASH)
        TNT1 A 0 A_FireBullets(0,0,-1,4,"BlankPuffBlakeBloodlessThruGhost",FBF_NOFLASH,120)
        TNT1 A 0 A_FireBullets(0,0,-1,8,"BlankPuffBlakeBloodlessThruGhost",FBF_NOFLASH,64)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireBullets(0,0,-1,4,"BlankPuffBlakeThruGhostCoop",FBF_NOFLASH)
        TNT1 A 0 A_FireBullets(0,0,-1,4,"BlankPuffBlakeBloodlessThruGhostCoop",FBF_NOFLASH,256)
        TNT1 A 0 A_FireBullets(0,0,-1,8,"BlankPuffBlakeBloodlessThruGhostCoop",FBF_NOFLASH,128)
        stop
    }
}
