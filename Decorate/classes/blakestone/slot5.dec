actor "Plasma Discharge Unit" : SamsaraBStoneWeapon
{
    Inventory.PickupMessage "You got a Plasma Discharge Unit!"
    Inventory.PickupSound "wolfen/knifeget"
    Weapon.SlotNumber 5
    Weapon.SelectionOrder 1200
    Weapon.YAdjust 16
    Weapon.AmmoType "RocketAmmo"
    Weapon.AmmoGive 8
    Weapon.AmmoUse 1
    Obituary "%k destroyed %o with the Plasma Discharge Unit."
    Tag "Plasma Discharge Unit"
    +EXPLOSIVE
    States
    {
      Spawn:
        BWP5 A -1
        stop

      // Planet Strike
      Ready:
        TNT1 A 0 A_JumpIfInventory("BlakeAOGToken",1,"ReadyAOG")
      ReadyPS:
        BPDU A 1 A_WeaponReady
        goto Ready

      Fire:
        TNT1 A 0 A_JumpIfInventory("BlakeAOGToken",1,"FireAOG")
      FirePS:
        BPDU B 3
        goto HoldPS

      Hold:
        TNT1 A 0 A_JumpIfInventory("BlakeAOGToken",1,"HoldAOG")
      HoldPS:
        BPDU C 4 Bright A_GiveInventory("SamsaraBStonePlasmaDischargeUnitAttackHandler")
        BPDU D 3
        BPDU E 3 A_ReFire
        goto Ready

      // Aliens Of Gold
      ReadyAOG:
        BPDU F 1 A_WeaponReady(WRF_NOBOB)
        goto Ready

      FireAOG:
        BPDU G 3
        goto HoldAOG

      HoldAOG:
        BPDU H 4 Bright A_GiveInventory("SamsaraBStonePlasmaDischargeUnitAttackHandler")
        BPDU I 3
        BPDU J 3 A_ReFire
        goto Ready
    }
}

actor SamsaraBStonePlasmaDischargeUnitAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,1)
        stop
        TNT1 A 0 A_TakeInventory("RocketAmmo",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("BCannon/Fire",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("BCannonProj",0,false,0,12,0,8)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("BCannonProjCoop",0,false,0,12,0,8)
        stop
    }
}

actor BCannonProj
{
    Projectile
    Radius 8
    Height 8
    Speed 16
    Damage 5
    DamageType "BCannonProj"
    Gravity 0.3
    Scale 1.25
    DeathSound "BCannon/Boom"
    Decal "Scorch"
    Obituary "%k destroyed %o with the Plasma Discharge Unit."
    +BLOODLESSIMPACT
    +FORCEXYBILLBOARD
    -NOGRAVITY
    +WINDTHRUST
    States
    {
      Spawn:
        BCAP A -1 Bright Light("PLASMADISCHARGE")
        stop

      Death:
        TNT1 A 0 A_ChangeFlag("NOGRAVITY",true)
        TNT1 A 0 A_Explode(100,64,0)
        TNT1 A 0 A_BFGSpray("BCannonPuff",6,1)
      DeathFinish:
        BCAP BCDEF 3 Bright Light("PLASMADISCHARGEBOOM")
        stop
    }
}

actor BCannonProjCoop : BCannonProj
{
    Damage 7
    Species "Player"
    +THRUSPECIES
    States
    {
      Death:
        TNT1 A 0 A_ChangeFlag("NOGRAVITY",true)
        TNT1 A 0 A_Explode(110,64,0)
        TNT1 A 0 A_BFGSpray("BCannonPuffCoop",15,3)
        goto DeathFinish
    }    
}

actor BCannonPuff 
{
    DamageType "BCannonProj"
    +NOGRAVITY
    +NOINTERACTION
    +PUFFGETSOWNER
    States
    {
      Spawn:
        BCAP BCDEF 3 Bright
        stop
    }
}

actor BCannonPuffCoop : BCannonPuff
{
    +MTHRUSPECIES
    +NODAMAGETHRUST
}
