actor "Slow Fire Protector" : SamsaraBStoneWeapon
{
    Inventory.PickupMessage "You got a Slow Fire Protector!"
    Inventory.PickupSound "wolfen/knifeget"
    Weapon.SlotNumber 2
    Weapon.SelectionOrder 1000
    Weapon.AmmoType "Shell"
    Weapon.AmmoGive 10
    Weapon.AmmoUse 1
    Obituary "%k ENERGIZED! %o with the Slow Fire Protector."
    Tag "Slow Fire Protector"
    States
    {
      Spawn:
        BWP2 A -1
        stop

      // Planet Strike
      Ready:
        TNT1 A 0 A_JumpIfInventory("BlakeAOGToken",1,"ReadyAOG")
      ReadyPS:
        BPI2 A 1 A_WeaponReady
        goto Ready

      Fire:
        TNT1 A 0 A_JumpIfInventory("BlakeAOGToken",1,"FireAOG")
      FirePS:
		TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"FirePSSmooth")
        BPI2 B 3
        BPI2 C 3 Bright A_GiveInventory("SamsaraBStoneSlowFireProtectorAttackHandler")
        BPI2 DEA 3
      FireFinish:
        TNT1 A 0 A_ReFire
        goto Ready
		
	  FirePSSmooth:
        BPI2 B 3
        BPI2 C 2 Bright A_GiveInventory("SamsaraBStoneSlowFireProtectorAttackHandler")
		9381 P 2
        BPI2 D 2
		9381 Q 2
		BPI2 E 3
		BPI2 A 3
		goto FireFinish

      // Aliens Of Gold
      ReadyAOG:
        BPI2 F 1 A_WeaponReady(WRF_NOBOB)
        goto Ready

      FireAOG:
		TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"FireAOGSmooth")
        BPI2 G 3
        BPI2 H 3 Bright A_GiveInventory("SamsaraBStoneSlowFireProtectorAttackHandler")
        BPI2 IJF 3
        goto FireFinish
		
      FireAOGSmooth:
        BPI2 G 3
        BPI2 H 2 Bright A_GiveInventory("SamsaraBStoneSlowFireProtectorAttackHandler")
		9381 C 2
        BPI2 I 2
		9381 D 2
		BPI2 J 3
		BPI2 F 3
        goto FireFinish
    }
}

actor SamsaraBStoneSlowFireProtectorAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Shell",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Shell",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("BPistol2/Fire",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireBullets(0,0,1,8,"BlankPuffBlake",FBF_NOFLASH)
        TNT1 A 0 A_FireBullets(0,0,1,8,"BlankPuffBlakeBloodless",FBF_NOFLASH,120)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireBullets(0,0,1,8,"BlankPuffBlakeCoop",FBF_NOFLASH)
        TNT1 A 0 A_FireBullets(0,0,1,16,"BlankPuffBlakeBloodlessCoop",FBF_NOFLASH,256)
        stop
    }
}
