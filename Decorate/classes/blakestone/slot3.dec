actor "Heavy Assault Weapon" : SamsaraBStoneWeapon
{
    Inventory.PickupMessage "You got a Heavy Assault Weapon!"
    Inventory.PickupSound "wolfen/knifeget"
    Weapon.SlotNumber 3
    Weapon.SelectionOrder 750
    Weapon.AmmoType "Shell"
    Weapon.AmmoGive 10
    Weapon.AmmoUse 1
    Obituary "%k throttled volatile energy through %o with the Heavy Assault Weapon."
    Tag "Heavy Assault Weapon"
    States
    {
      Spawn:
        BWP3 A -1
        stop

      // Planet Strike
      Ready:
        TNT1 A 0 A_JumpIfInventory("BlakeAOGToken",1,"ReadyAOG")
      ReadyPS:
        SRF5 A 1 A_WeaponReady
        goto Ready

      Fire:
        TNT1 A 0 A_JumpIfInventory("BlakeAOGToken",1,"FireAOG")
      FirePS:
        SRF5 B 3
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Shell",1,1)
        goto HoldPS
        TNT1 A 0 A_GunFlash
        goto HoldPS

      Hold:
        TNT1 A 0 A_GunFlash
        TNT1 A 0 A_JumpIfInventory("BlakeAOGToken",1,"HoldAOG")
      HoldPS:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"HoldPSCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"HoldPSCoop")
      HoldPSDM:
		TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"HoldPSDMSmooth")
        SRF5 C 3 Bright A_GiveInventory("SamsaraBStoneHeavyAssaultWeaponAttackHandler")
        goto HoldPSFinish

      HoldPSCoop:
		TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"HoldPSCoopSmooth")
        SRF5 C 2 Bright A_GiveInventory("SamsaraBStoneHeavyAssaultWeaponAttackHandler")
        goto HoldPSFinish
		
      HoldPSDMSmooth:
        SRF5 C 2 Bright A_GiveInventory("SamsaraBStoneHeavyAssaultWeaponAttackHandler")
		9381 T 1
        goto HoldPSFinishSmooth

      HoldPSCoopSmooth:
        SRF5 C 1 Bright A_GiveInventory("SamsaraBStoneHeavyAssaultWeaponAttackHandler")
		9381 T 1
        goto HoldPSFinishSmooth

      HoldPSFinish:
        SRF5 DE 2 
        SRF5 A 2 A_ReFire
        goto Ready
		
      HoldPSFinishSmooth:
        9381 U 2
		SRF5 E 2
        SRF5 A 2 A_ReFire
        goto Ready

      // Aliens Of Gold
      ReadyAOG:
        BRIF F 1 A_WeaponReady(WRF_NOBOB)
        goto Ready

      FireAOG:
        BRIF G 3
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Shell",1,1)
        goto HoldAOG
        TNT1 A 0 A_GunFlash
        goto HoldAOG

      HoldAOG:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"HoldAOGCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"HoldAOGCoop")
      HoldAOGDM:
		TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"HoldAOGDMSmooth")
        BRIF H 3 Bright A_GiveInventory("SamsaraBStoneHeavyAssaultWeaponAttackHandler")
        goto HoldAOGFinish

      HoldAOGCoop:
		TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"HoldAOGCoopSmooth")
        BRIF H 2 Bright A_GiveInventory("SamsaraBStoneHeavyAssaultWeaponAttackHandler")
        goto HoldAOGFinish
		
      HoldAOGDMSmooth:
        BRIF H 2 Bright A_GiveInventory("SamsaraBStoneHeavyAssaultWeaponAttackHandler")
		9381 G 1
        goto HoldAOGFinish

      HoldAOGCoopSmooth:
        BRIF H 1 Bright A_GiveInventory("SamsaraBStoneHeavyAssaultWeaponAttackHandler")
		9381 G 1
        goto HoldAOGFinish

      HoldAOGFinish:
        BRIF IJ 2 
        BRIF F 2 A_ReFire
        goto Ready
		
      HoldAOGFinishSmooth: //unused due to new HAW animation
        9381 H 2
		BRIF J 2
        BRIF F 2 A_ReFire
        goto Ready
    }
}

actor SamsaraBStoneHeavyAssaultWeaponAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Shell",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Shell",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("BPistol2/Fire",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireBullets(2,2,-1,8,"BlankPuffBlakeThruGhost",FBF_NOFLASH)
        TNT1 A 0 A_FireBullets(0,0,1,8,"BlankPuffBlakeBloodlessThruGhost",FBF_NOFLASH,120)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireBullets(0,0,1,8,"BlankPuffBlakeThruGhostCoop",FBF_NOFLASH)
        TNT1 A 0 A_FireBullets(0,0,1,16,"BlankPuffBlakeBloodlessThruGhostCoop",FBF_NOFLASH,256)
        stop
    }
}
