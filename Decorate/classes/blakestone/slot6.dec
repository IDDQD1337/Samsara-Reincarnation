actor "Dual Neutron Disruptor" : SamsaraBStoneWeapon
{
    Inventory.PickupMessage "You got a Dual Neutron Disruptor!"
    Inventory.PickupSound "wolfen/knifeget"
    Weapon.SlotNumber 6
    Weapon.SelectionOrder 500
    Weapon.YAdjust 16
    Weapon.AmmoType "Cell"
    Weapon.AmmoGive 20
    Weapon.AmmoUse 1
    Obituary "%k disintegrated %o with the Dual Neutron Disruptor."
    Tag "Dual Neutron Disruptor"
    States
    {
      Spawn:
        BWP6 A -1
        stop

      // Planet Strike
      Ready:
        TNT1 A 0 A_JumpIfInventory("BlakeAOGToken",1,"ReadyAOG")
      ReadyPS:
        DRIF A 1 A_WeaponReady
        goto Ready

      Fire:
        TNT1 A 0 A_JumpIfInventory("BlakeAOGToken",1,"FireAOG")
      FirePS:
        DRIF B 2
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Cell",1,1)
        goto HoldPS
        TNT1 A 0 A_GunFlash
        goto HoldPS

      Hold:
        TNT1 A 0 A_GunFlash
        TNT1 A 0 A_JumpIfInventory("BlakeAOGToken",1,"HoldAOG")
      HoldPS:
        DRIF C 4 Bright A_GiveInventory("SamsaraBStoneDualNeutronDisruptorAttackHandler")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Cell",1,1)
        goto Hold2PS
        TNT1 A 0 A_GunFlash
        goto Hold2PS

      Hold2PS:
        DRIF D 4 Bright A_GiveInventory("SamsaraBStoneDualNeutronDisruptorAttackHandler")
        DRIF E 3 A_ReFire
        DRIF B 3
        goto Ready

      // Aliens Of Gold
      ReadyAOG:
        DRIF F 1 A_WeaponReady(WRF_NOBOB)
        goto Ready

      FireAOG:
        DRIF G 2
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Cell",1,1)
        goto HoldAOG
        TNT1 A 0 A_GunFlash
        goto HoldAOG

      HoldAOG:
        DRIF H 4 Bright A_GiveInventory("SamsaraBStoneDualNeutronDisruptorAttackHandler")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Cell",1,1)
        goto Hold2AOG
        TNT1 A 0 A_GunFlash
        goto Hold2AOG

      Hold2AOG:
        DRIF I 4 Bright A_GiveInventory("SamsaraBStoneDualNeutronDisruptorAttackHandler")
        DRIF J 3 A_ReFire
        DRIF G 3
        goto Ready
    }
}

actor SamsaraBStoneDualNeutronDisruptorAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Cell",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Cell",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("DRifle/Fire",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireBullets(0,0,1,9,"BlankPuffBlake",FBF_NOFLASH)
        TNT1 A 0 A_FireBullets(0,0,1,9,"BlankPuffBlakeBloodless",FBF_NOFLASH,120)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireBullets(0,0,1,9,"BlankPuffBlakeCoop",FBF_NOFLASH)
        TNT1 A 0 A_FireBullets(0,0,1,18,"BlankPuffBlakeBloodlessCoop",FBF_NOFLASH,256)
        stop
    }
}
