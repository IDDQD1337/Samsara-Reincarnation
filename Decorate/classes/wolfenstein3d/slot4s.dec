actor Wolfen_IHaveDualChainguns : Boolean {}

actor "  Dual Chainguns  ": InstantSwitchWeapon
{
    Weapon.SelectionOrder 1700
    Weapon.SlotNumber 4
	Weapon.SlotPriority 1
    Inventory.PickUpSound "wolfen/weaponget"
    Weapon.AmmoUse 1
    //Weapon.AmmoGive 40
    Weapon.AmmoType "Clip"
    +DONTBOB
    +INVENTORY.UNDROPPABLE
    Inventory.PickupMessage "You got the Chaingun!"
    Obituary "$SAMSARA_WOLFEN_OB_SLOT4"
    Weapon.YAdjust 14
	Inventory.RestrictedTo "WolfenPlayer"
    Tag "Dual Chainguns"
	+WEAPON.NOALERT
    States
    {
      Spawn:
        WCHP A -1
        stop

      // Original
      Ready:
        TNT1 A 0 A_JumpIfInventory("WolfenClassMode",1,"ReadyLost")
      ReadyOriginal:
        TNT1 A 0 A_JumpIfInventory("  Chaingun  ",1,2)
        WCHN K 1 A_WeaponReady(WRF_NOSECONDARY)
        goto Ready
        WCHN K 1 A_WeaponReady
        goto Ready

      Fire:
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_JumpIfInventory("WolfenClassMode",1,"FireLost")
      FireOriginal:
        WCHN KL 2
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
		TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"HoldOriginalSmooth")
        goto HoldOriginal
        TNT1 A 0 A_GunFlash
        goto HoldOriginal

      Hold:
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_GunFlash
        TNT1 A 0 A_JumpIfInventory("WolfenClassMode",1,"HoldLost")
		//TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"HoldOriginalSmooth")
      HoldOriginal:
		TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"HoldOriginalSmooth")
        WCHN M 3 Bright A_GiveInventory("SamsaraWolfenDualChaingunsAttackHandler")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        goto Hold2Original
        TNT1 A 0 A_GunFlash
        goto Hold2Original

      Hold2Original:
		TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"Hold2OriginalSmooth")
        WCHN N 3 Bright A_GiveInventory("SamsaraWolfenDualChaingunsAttackHandler")
        WCHN O 2 A_ReFire
        goto Ready
		
      HoldOriginalSmooth:
        WCHN M 2 Bright A_GiveInventory("SamsaraWolfenDualChaingunsAttackHandler")
		9383 P 1 Bright
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        goto Hold2OriginalSmooth
        TNT1 A 0 A_GunFlash
        goto Hold2OriginalSmooth

      Hold2OriginalSmooth:
        WCHN N 2 Bright A_GiveInventory("SamsaraWolfenDualChaingunsAttackHandler")
		9383 Q 1 Bright
        WCHN O 2 A_ReFire
        goto Ready

      AltFire:
        WCHN K 8 A_JumpIfInventory("WolfenClassMode",1,"AltFireLost")
        TNT1 A 0 A_SelectWeapon("  Chaingun  ")
        goto Deselect

      // Lost Missions
      ReadyLost:
        TNT1 A 0 A_JumpIfInventory("  Chaingun  ",1,2)
        WCHN P 1 A_WeaponReady(WRF_NOSECONDARY)
        goto Ready
        WCHN P 1 A_WeaponReady
        goto Ready

      FireLost:
        WCHN PQ 2
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        goto HoldLost
        TNT1 A 0 A_GunFlash
        goto HoldLost

      HoldLost:
		TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"HoldLostSmooth")
        WCHN R 3 Bright A_GiveInventory("SamsaraWolfenLostDualChaingunsAttackHandler")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        goto Hold2Lost
        TNT1 A 0 A_GunFlash
        goto Hold2Lost

      Hold2Lost:
		TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"Hold2LostSmooth")
        WCHN S 3 Bright A_GiveInventory("SamsaraWolfenLostDualChaingunsAttackHandler")
        WCHN T 2 A_ReFire
        goto Ready
		
      HoldLostSmooth:
        WCHN R 2 Bright A_GiveInventory("SamsaraWolfenLostDualChaingunsAttackHandler")
		9383 R 1 Bright
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        goto Hold2Lost
        TNT1 A 0 A_GunFlash
        goto Hold2Lost

      Hold2LostSmooth:
        WCHN S 2 Bright A_GiveInventory("SamsaraWolfenLostDualChaingunsAttackHandler")
		9383 S 1 Bright
        WCHN T 2 A_ReFire
        goto Ready

      AltFireLost:
        WCHN P 8
        TNT1 A 0 A_SelectWeapon("  Chaingun  ")
        goto Deselect
    }
}

// Original

actor SamsaraWolfenDualChaingunsAttackHandler : Trigger
{
    States
    {
      Pickup:
      // Ammo checking/taking stuff (right)
      Pickup1:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"Pickup1Attack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        goto Pickup2
        TNT1 A 0 A_TakeInventory("Clip",1)
        goto Pickup1Attack

      // Attack stuff (right)
      Pickup1Attack:
        TNT1 A 0 A_PlaySound("wolfen/chaingun3",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"Pickup1AttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"Pickup1AttackCoop")
      Pickup1AttackDM:
        TNT1 A 0 A_FireBullets(2,2,-1,5,"BlankPuffThruGhost",FBF_NOFLASH)
        TNT1 A 0 A_FireBullets(0,0,1,1,"BlankPuffThruGhostBloodless",FBF_NOFLASH,120)
        TNT1 A 0 A_FireBullets(0,0,1,1,"BlankPuffThruGhostBloodless",FBF_NOFLASH,64)
        goto Pickup2

      Pickup1AttackCoop:
        TNT1 A 0 A_FireBullets(0,0,1,7,"BlankPuffThruGhostCoop",FBF_NOFLASH)
        TNT1 A 0 A_FireBullets(0,0,1,2,"BlankPuffThruGhostBloodlessCoop",FBF_NOFLASH,256)
        TNT1 A 0 A_FireBullets(0,0,1,1,"BlankPuffThruGhostBloodlessCoop",FBF_NOFLASH,128)
        goto Pickup2

      // Ammo checking/taking stuff (left)
      Pickup2:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"Pickup2Attack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",1)
        goto Pickup2Attack

      // Attack stuff (left)
      Pickup2Attack:
        TNT1 A 0 A_PlaySound("wolfen/chaingun3",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"Pickup2AttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"Pickup2AttackCoop")
      Pickup2AttackDM:
        TNT1 A 0 A_FireBullets(2,2,-1,5,"BlankPuffThruGhost",FBF_NOFLASH)
        TNT1 A 0 A_FireBullets(0,0,1,1,"BlankPuffThruGhostBloodless",FBF_NOFLASH,120)
        TNT1 A 0 A_FireBullets(0,0,1,1,"BlankPuffThruGhostBloodless",FBF_NOFLASH,64)
        stop

      Pickup2AttackCoop:
        TNT1 A 0 A_FireBullets(0,0,1,7,"BlankPuffThruGhostCoop",FBF_NOFLASH)
        TNT1 A 0 A_FireBullets(0,0,1,2,"BlankPuffThruGhostBloodlessCoop",FBF_NOFLASH,256)
        TNT1 A 0 A_FireBullets(0,0,1,1,"BlankPuffThruGhostBloodlessCoop",FBF_NOFLASH,128)
        stop
    }
}

// Lost Missions

actor SamsaraWolfenLostDualChaingunsAttackHandler : Trigger
{
    States
    {
      Pickup:
      // Ammo checking/taking stuff (right)
      Pickup1:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"Pickup1Attack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        goto Pickup2
        TNT1 A 0 A_TakeInventory("Clip",1)
        goto Pickup1Attack

      // Attack stuff (right)
      Pickup1Attack:
        TNT1 A 0 A_PlaySound("wolfen/chaingun4",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"Pickup1AttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"Pickup1AttackCoop")
      Pickup1AttackDM:
        TNT1 A 0 A_FireBullets(2,2,-1,5,"BlankPuffThruGhost",FBF_NOFLASH)
        TNT1 A 0 A_FireBullets(0,0,1,1,"BlankPuffThruGhostBloodless",FBF_NOFLASH,120)
        TNT1 A 0 A_FireBullets(0,0,1,1,"BlankPuffThruGhostBloodless",FBF_NOFLASH,64)
        goto Pickup2

      Pickup1AttackCoop:
        TNT1 A 0 A_FireBullets(0,0,1,7,"BlankPuffThruGhostCoop",FBF_NOFLASH)
        TNT1 A 0 A_FireBullets(0,0,1,2,"BlankPuffThruGhostBloodlessCoop",FBF_NOFLASH,256)
        TNT1 A 0 A_FireBullets(0,0,1,1,"BlankPuffThruGhostBloodlessCoop",FBF_NOFLASH,128)
        goto Pickup2

      // Ammo checking/taking stuff (left)
      Pickup2:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"Pickup2Attack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",1)
        goto Pickup2Attack

      // Attack stuff (left)
      Pickup2Attack:
        TNT1 A 0 A_PlaySound("wolfen/chaingun4",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"Pickup2AttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"Pickup2AttackCoop")
      Pickup2AttackDM:
        TNT1 A 0 A_FireBullets(2,2,-1,5,"BlankPuffThruGhost",FBF_NOFLASH)
        TNT1 A 0 A_FireBullets(0,0,1,1,"BlankPuffThruGhostBloodless",FBF_NOFLASH,120)
        TNT1 A 0 A_FireBullets(0,0,1,1,"BlankPuffThruGhostBloodless",FBF_NOFLASH,64)
        stop

      Pickup2AttackCoop:
        TNT1 A 0 A_FireBullets(0,0,1,7,"BlankPuffThruGhostCoop",FBF_NOFLASH)
        TNT1 A 0 A_FireBullets(0,0,1,2,"BlankPuffThruGhostBloodlessCoop",FBF_NOFLASH,256)
        TNT1 A 0 A_FireBullets(0,0,1,1,"BlankPuffThruGhostBloodlessCoop",FBF_NOFLASH,128)
        stop
    }
}
