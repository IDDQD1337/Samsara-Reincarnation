actor "  Dual Chainguns  ": InstantSwitchWeapon
{
    Weapon.SelectionOrder 1700
    Weapon.SlotNumber 4
	Weapon.SlotPriority 1
    Inventory.PickUpSound "wolfen/weaponget"
    Weapon.AmmoUse 1
    Weapon.AmmoGive 0
    Weapon.AmmoType "Clip"
    +DONTBOB
    +INVENTORY.UNDROPPABLE
    Inventory.PickupMessage "You got the Chaingun!"
    Obituary "$SAMSARA_WOLFEN_OB_SLOT4"
    Weapon.YAdjust 14
	Inventory.RestrictedTo "WolfenPlayer"
    Tag "Dual Chainguns"
	+WEAPON.NOALERT
    States
    {
      Spawn:
        WCHP A -1
        stop

      // Original
      Ready:
        TNT1 A 0 A_JumpIfInventory("WolfenClassMode",1,"ReadyLost")
      ReadyOriginal:
        WCHN A 1 A_WeaponReady(WRF_NOSECONDARY)
        goto Ready

      Fire:
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_JumpIfInventory("WolfenClassMode",1,"FireLost")
      FireOriginal:
        WCHN AB 2
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
		TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"HoldOriginalSmooth")
        goto HoldOriginal
        TNT1 A 0 A_GunFlash
        goto HoldOriginal

      Hold:
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_GunFlash
        TNT1 A 0 A_JumpIfInventory("WolfenClassMode",1,"HoldLost")
		//TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"HoldOriginalSmooth")
      HoldOriginal:
		TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"HoldOriginalSmooth")
        WCHN C 3 Bright A_GiveInventory("SamsaraWolfenDualChaingunsAttackHandler")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        goto Hold2Original
        TNT1 A 0 A_GunFlash
        goto Hold2Original

      Hold2Original:
		TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"Hold2OriginalSmooth")
        WCHN D 3 Bright A_GiveInventory("SamsaraWolfenDualChaingunsAttackHandler")
        WCHN E 2 A_ReFire
        goto Ready
		
      HoldOriginalSmooth:
        WCHN C 2 Bright A_GiveInventory("SamsaraWolfenDualChaingunsAttackHandler")
		9382 F 1 Bright
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        goto Hold2OriginalSmooth
        TNT1 A 0 A_GunFlash
        goto Hold2OriginalSmooth

      Hold2OriginalSmooth:
        WCHN D 2 Bright A_GiveInventory("SamsaraWolfenDualChaingunsAttackHandler")
		9382 G 1 Bright
        WCHN E 2 A_ReFire
        goto Ready


      // Lost Missions
      ReadyLost:
        WCHN F 1 A_WeaponReady(WRF_NOSECONDARY)
        goto Ready

      FireLost:
        WCHN FG 2
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        goto HoldLost
        TNT1 A 0 A_GunFlash
        goto HoldLost

      HoldLost:
		TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"HoldLostSmooth")
        WCHN H 3 Bright A_GiveInventory("SamsaraWolfenLostDualChaingunsAttackHandler")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        goto Hold2Lost
        TNT1 A 0 A_GunFlash
        goto Hold2Lost

      Hold2Lost:
		TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"Hold2LostSmooth")
        WCHN I 3 Bright A_GiveInventory("SamsaraWolfenLostDualChaingunsAttackHandler")
        WCHN J 2 A_ReFire
        goto Ready
		
      HoldLostSmooth:
        WCHN H 2 Bright A_GiveInventory("SamsaraWolfenLostDualChaingunsAttackHandler")
		9382 Z 1 Bright
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        goto Hold2Lost
        TNT1 A 0 A_GunFlash
        goto Hold2Lost

      Hold2LostSmooth:
        WCHN I 2 Bright A_GiveInventory("SamsaraWolfenLostDualChaingunsAttackHandler")
		9383 A 1 Bright
        WCHN J 2 A_ReFire
        goto Ready
    }
}

// Original

actor SamsaraWolfenDualChaingunsAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",2,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",2)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("wolfen/chaingun",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireBullets(2,2,2,5,"BlankPuffThruGhost",FBF_NOFLASH)
        TNT1 A 0 A_FireBullets(0,0,2,1,"BlankPuffThruGhostBloodless",FBF_NOFLASH,120)
        TNT1 A 0 A_FireBullets(0,0,2,1,"BlankPuffThruGhostBloodless",FBF_NOFLASH,64)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireBullets(0,0,2,7,"BlankPuffThruGhostCoop",FBF_NOFLASH)
        TNT1 A 0 A_FireBullets(0,0,2,2,"BlankPuffThruGhostBloodlessCoop",FBF_NOFLASH,256)
        TNT1 A 0 A_FireBullets(0,0,2,1,"BlankPuffThruGhostBloodlessCoop",FBF_NOFLASH,128)
        stop
    }
}

// Lost Missions

actor SamsaraWolfenLostDualChaingunsAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",2,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",2)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("wolfen/chaingun2",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireBullets(2,2,2,5,"BlankPuffThruGhost",FBF_NOFLASH)
        TNT1 A 0 A_FireBullets(0,0,2,1,"BlankPuffThruGhostBloodless",FBF_NOFLASH,120)
        TNT1 A 0 A_FireBullets(0,0,2,1,"BlankPuffThruGhostBloodless",FBF_NOFLASH,64)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireBullets(0,0,2,7,"BlankPuffThruGhostCoop",FBF_NOFLASH)
        TNT1 A 0 A_FireBullets(0,0,2,2,"BlankPuffThruGhostBloodlessCoop",FBF_NOFLASH,256)
        TNT1 A 0 A_FireBullets(0,0,2,1,"BlankPuffThruGhostBloodlessCoop",FBF_NOFLASH,128)
        stop
    }
}
