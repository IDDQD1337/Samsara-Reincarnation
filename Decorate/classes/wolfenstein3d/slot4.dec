actor "  Chaingun  ": InstantSwitchWeapon
{
    Weapon.SelectionOrder 1700
    Weapon.SlotNumber 4
	Weapon.SlotPriority 0
    Inventory.PickUpSound "wolfen/weaponget"
    Weapon.AmmoUse 1
    Weapon.AmmoGive 20
    Weapon.AmmoType "Clip"
    +DONTBOB
    +INVENTORY.UNDROPPABLE
    Inventory.PickupMessage "You got the Chaingun!"
    Obituary "$SAMSARA_WOLFEN_OB_SLOT4"
    Weapon.YAdjust 14
	Inventory.RestrictedTo "WolfenPlayer"
    Tag "Chaingun"
	+WEAPON.NOALERT
    States
    {
      Spawn:
        WCHP A -1
        stop

      // Original
      Ready:
        TNT1 A 0 A_JumpIfInventory("WolfenClassMode",2,"ReadyCTK")
        TNT1 A 0 A_JumpIfInventory("WolfenClassMode",1,"ReadyLost")
      ReadyOriginal:
        WCHN A 1 A_WeaponReady(WRF_NOSECONDARY)
        goto Ready

      Fire:
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_JumpIfInventory("WolfenClassMode",2,"FireCTK")
        TNT1 A 0 A_JumpIfInventory("WolfenClassMode",1,"FireLost")
      FireOriginal:
        WCHN AB 2
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
		TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"HoldOriginalSmooth")
        goto HoldOriginal
        TNT1 A 0 A_GunFlash
        goto HoldOriginal

      Hold:
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_JumpIfInventory("WolfenClassMode",2,"HoldCTK")
        TNT1 A 0 A_GunFlash
        TNT1 A 0 A_JumpIfInventory("WolfenClassMode",1,"HoldLost")
		//TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"HoldOriginalSmooth")
      HoldOriginal:
		TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"HoldOriginalSmooth")
        WCHN C 3 Bright A_GiveInventory("SamsaraWolfenChaingunAttackHandler")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        goto Hold2Original
        TNT1 A 0 A_GunFlash
        goto Hold2Original

      Hold2Original:
		TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"Hold2OriginalSmooth")
        WCHN D 3 Bright A_GiveInventory("SamsaraWolfenChaingunAttackHandler")
        WCHN E 2 A_ReFire
        goto Ready
		
      HoldOriginalSmooth:
        WCHN C 2 Bright A_GiveInventory("SamsaraWolfenChaingunAttackHandler")
		9382 F 1 Bright
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        goto Hold2OriginalSmooth
        TNT1 A 0 A_GunFlash
        goto Hold2OriginalSmooth

      Hold2OriginalSmooth:
        WCHN D 2 Bright A_GiveInventory("SamsaraWolfenChaingunAttackHandler")
		9382 G 1 Bright
        WCHN E 2 A_ReFire
        goto Ready


      // Lost Missions
      ReadyLost:
        WCHN F 1 A_WeaponReady(WRF_NOSECONDARY)
        goto Ready

      FireLost:
        WCHN FG 2
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        goto HoldLost
        TNT1 A 0 A_GunFlash
        goto HoldLost

      HoldLost:
		TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"HoldLostSmooth")
        WCHN H 3 Bright A_GiveInventory("SamsaraWolfenLostChaingunAttackHandler")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        goto Hold2Lost
        TNT1 A 0 A_GunFlash
        goto Hold2Lost

      Hold2Lost:
		TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"Hold2LostSmooth")
        WCHN I 3 Bright A_GiveInventory("SamsaraWolfenLostChaingunAttackHandler")
        WCHN J 2 A_ReFire
        goto Ready
		
      HoldLostSmooth:
        WCHN H 2 Bright A_GiveInventory("SamsaraWolfenLostChaingunAttackHandler")
		9382 Z 1 Bright
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        goto Hold2Lost
        TNT1 A 0 A_GunFlash
        goto Hold2Lost

      Hold2LostSmooth:
        WCHN I 2 Bright A_GiveInventory("SamsaraWolfenLostChaingunAttackHandler")
		9383 A 1 Bright
        WCHN J 2 A_ReFire
        goto Ready

      // Castle Totenkopf SDL (MP40 Submachine Gun (Single))
      ReadyCTK:
        TNT1 A 0 A_JumpIfInventory("Totenkopf_MP40Dual",1,2)
        TK04 A 1 A_WeaponReady(WRF_NOSECONDARY)
        goto Ready
        TK04 A 1 A_WeaponReady
        goto Ready

      FireCTK:
        TK04 B 3
      HoldCTK:
        TK04 C 2 Bright A_GiveInventory("SamsaraTotenkopfMP40SingleAttackHandler")
        TK04 D 3
        TK04 E 2 A_ReFire
        goto Ready

      AltFire:
        TK04 A 8
        TNT1 A 0 A_SelectWeapon("Totenkopf_MP40Dual")
        goto Deselect
    }
}

actor Totenkopf_MP40Dual : "  Chaingun  "
{
    Weapon.SelectionOrder 100
	Weapon.SlotPriority 1
	Weapon.AmmoGive 0
    Tag "MP40 Submachine Gun (Dual)"
    States
    {
      Spawn:
        TK04 K -1
        stop

      Ready:
        TNT1 A 0 A_JumpIfInventory("  Chaingun  ",1,2)
        TK04 F 1 A_WeaponReady(WRF_NOSECONDARY)
        loop
        TK04 F 1 A_WeaponReady
        loop

      Fire:
        TK04 G 3 A_AlertMonsters
        goto HoldStart

      Hold:
        TNT1 A 0 A_AlertMonsters
      HoldStart:
        TK04 H 2 Bright A_GiveInventory("SamsaraTotenkopfMP40DualAttackHandler")
        TNT1 A 0 A_CheckReload
        TK04 I 3 Bright A_GiveInventory("SamsaraTotenkopfMP40DualAttackHandler")
        TK04 J 2 A_ReFire
        goto Ready

      AltFire:
        TK04 F 8
        TNT1 A 0 A_SelectWeapon("  Chaingun  ")
        goto Deselect
    }
} 

// Original

actor SamsaraWolfenChaingunAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("wolfen/chaingun",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireBullets(2,2,-1,5,"BlankPuffThruGhost",FBF_NOFLASH)
        TNT1 A 0 A_FireBullets(0,0,1,1,"BlankPuffThruGhostBloodless",FBF_NOFLASH,120)
        TNT1 A 0 A_FireBullets(0,0,1,1,"BlankPuffThruGhostBloodless",FBF_NOFLASH,64)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireBullets(0,0,1,7,"BlankPuffThruGhostCoop",FBF_NOFLASH)
        TNT1 A 0 A_FireBullets(0,0,1,2,"BlankPuffThruGhostBloodlessCoop",FBF_NOFLASH,256)
        TNT1 A 0 A_FireBullets(0,0,1,1,"BlankPuffThruGhostBloodlessCoop",FBF_NOFLASH,128)
        stop
    }
}

// Lost Missions

actor SamsaraWolfenLostChaingunAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("wolfen/chaingun2",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireBullets(2,2,-1,5,"BlankPuffThruGhost",FBF_NOFLASH)
        TNT1 A 0 A_FireBullets(0,0,1,1,"BlankPuffThruGhostBloodless",FBF_NOFLASH,120)
        TNT1 A 0 A_FireBullets(0,0,1,1,"BlankPuffThruGhostBloodless",FBF_NOFLASH,64)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireBullets(0,0,1,7,"BlankPuffThruGhostCoop",FBF_NOFLASH)
        TNT1 A 0 A_FireBullets(0,0,1,2,"BlankPuffThruGhostBloodlessCoop",FBF_NOFLASH,256)
        TNT1 A 0 A_FireBullets(0,0,1,1,"BlankPuffThruGhostBloodlessCoop",FBF_NOFLASH,128)
        stop
    }
}

// Castle Totenkopf SDL

actor SamsaraTotenkopfMP40SingleAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("Totenkopf/atkmp40",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("Totenkopf_Tracer_MP40DM",random(-4,4),false)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("Totenkopf_Tracer_MP40Coop",random(-4,4),false)
        stop
    }
}

actor SamsaraTotenkopfMP40DualAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("Totenkopf/atkmp40",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("Totenkopf_Tracer_MP40DM",random(-8,8),false)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("Totenkopf_Tracer_MP40Coop",random(-8,8),false)
        stop
    }
}

actor Totenkopf_Tracer_MP40 : Totenkopf_Tracer
{
    Damage (8 + (random(0,256)%22))
    +THRUGHOST
}

actor Totenkopf_Tracer_MP40DM : Totenkopf_Tracer_MP40 { Damage (8 + (random(0,256)%5)) }
actor Totenkopf_Tracer_MP40Coop : Totenkopf_Tracer_MP40 { Species "Player" +THRUSPECIES }

Actor Totenkopf_MP40PickedUpX : MapCoordinate {}
Actor Totenkopf_MP40PickedUpY : MapCoordinate {}
Actor Totenkopf_MP40PickedUpZ : MapCoordinate {}
