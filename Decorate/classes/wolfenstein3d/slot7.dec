// Original/Lost Missions

actor "Spear of Destiny" : InstantSwitchWeapon
{
    Weapon.SelectionOrder 2900
    Inventory.PickUpSound "wolfen/spearget"
    Weapon.AmmoUse 40
    //Weapon.AmmoGive 120
    Weapon.AmmoType "Cell"
    +DONTBOB
    +INVENTORY.UNDROPPABLE
    +WEAPON.NOAUTOFIRE
    +WEAPON.BFG
	Inventory.RestrictedTo "WolfenPlayer"
    Obituary "$SAMSARA_WOLFEN_OB_SLOT7" 
    Inventory.PickupMessage "You got the Spear of Destiny!"
	Weapon.YAdjust 14
    Tag "Spear of Destiny"
    States
    {
      Spawn:
        WSPR E -1
        stop

      // Original
      Ready:
        TNT1 A 0 A_JumpIfInventory("WolfenClassMode",1,"ReadyLost")
      ReadyOriginal:
        WSPR A 1 A_WeaponReady
        goto Ready

      Fire:
        TNT1 A 0 A_PlaySound("wolfen/spearready",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("WolfenClassMode",1,"FireLost")
      FireOriginal:
	    TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"FireOriginalSmooth")
        WSPR B 20
        WSPR C 12 Bright
        WSPR D 12 Bright A_GiveInventory("SamsaraWolfenSpearOfDestinyAttackHandler")
        WSPR C 15 Bright
        WSPR B 15
        WSPR A 5 A_ReFire
        goto Ready

      FireOriginalSmooth:
        9382 L 5
		WSPR B 15

		9382 M 5 Bright
        WSPR C 4 Bright
		9382 N 3 Bright

        WSPR D 5 Bright A_GiveInventory("SamsaraWolfenSpearOfDestinyAttackHandler")
		9382 O 4 Bright
		9382 P 3 Bright

		9382 QR 5 Bright
		9382 S 5

		9382 TU 5
        WSPR B 5

		9382 L 5 A_ReFire
        goto Ready

      // Lost Missions
      ReadyLost:
        WSPR J 1 A_WeaponReady
        goto Ready

      FireLost:
		TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"FireLostSmooth")
        WSPR K 20
        WSPR L 12 Bright
        WSPR M 12 Bright A_GiveInventory("SamsaraWolfenSpearOfDestinyAttackHandler")
        WSPR L 15 Bright
        WSPR K 15
        WSPR J 5 A_ReFire
        goto Ready

      FireLostSmooth:
		9383 F 5
        WSPR K 15

		9383 G 5 Bright
        WSPR L 4 Bright
		9383 H 3 Bright

        WSPR M 5 Bright A_GiveInventory("SamsaraWolfenSpearOfDestinyAttackHandler")
		9383 I 4 Bright
		9383 J 3 Bright

        9383 KL 5 Bright
		9383 M 5

		9383 NO 5
        WSPR K 5

        9383 F 5 A_ReFire
        goto Ready
    }
}

actor SamsaraWolfenSpearOfDestinyAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Cell",40,1)
        stop
        TNT1 A 0 A_TakeInventory("Cell",40)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("wolfen/spearfire",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("SpearBoltDM",0,false)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("SpearBolt2",0,false)
        stop
    }
}

actor SpearBolt
{
    Speed 30 // Normal BFG Ball speed is 25
    Height 2
    Radius 4
    Mass 9
    Damage 55
    SeeSound ""
    DeathSound "wolfen/spearexplode1"
    Obituary "$SAMSARA_WOLFEN_OB_SLOT7" 
    PROJECTILE
    +DONTREFLECT
    DamageType "BJSpear"
    Scale 0.80
    RENDERSTYLE ADD
    States
    {
      Spawn:
        SPB1 ABABABAB 3 BRIGHT A_SpawnItemEx("SpearShotTrail", 0, 0, 0)
        goto Death

      Death:
        SPX1 A 0 A_Stop
        SPX1 AAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("SpearShotMini", 0, 0, random(-180, 180), 2, random(-25, 25))
        SPX1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("SpearShotMini2", 0, 0, random(-180, 180), 2, random(-25, 25))
        SPX1 A 0 A_Scream
        SPX1 A 4 BRIGHT A_Explode(120, 240, 0)
        SPX1 BCDEF 4 BRIGHT
        Stop
    }
}

actor SpearBoltDM: SpearBolt
{
    DamageType "BJSpear"
    Obituary "$SAMSARA_WOLFEN_OB_SLOT7" 
    States
    {
      Death:
        SPX1 A 0 A_Stop
        SPX1 AAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("SpearShotMiniDM", 0, 0, random(-180, 180), 2, random(-25, 25))
        SPX1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("SpearShotMini2DM", 0, 0, random(-180, 180), 2, random(-25, 25))
        SPX1 A 0 A_Scream
        SPX1 A 4 BRIGHT A_Explode(120, 198, 0)
        SPX1 BCDEF 4 BRIGHT
        Stop
    }
}

actor SpearBolt2: SpearBolt
{
    +THRUSPECIES
    Species "Player"
    States
    {
      Spawn:
        SPB1 ABABABAB 3 BRIGHT A_SpawnItemEx("SpearShotTrail", 0, 0, 0)
        goto Death

      Death:
        SPX1 A 0 A_Stop
        SPX1 AAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("SpearShotMiniCoop", 0, 0, random(-180, 180), 2, random(-25, 25))
        SPX1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("SpearShotMini2Coop", 0, 0, random(-180, 180), 2, random(-25, 25))
        SPX1 A 0 A_Scream
        SPX1 A 4 BRIGHT A_Explode(120, 240, 0)
        SPX1 BCDEF 4 BRIGHT
        Stop
    }
}

actor SpearShotMini
{
    Radius 10
    Height 16
    Speed 10
    Damage 4
    Scale 0.75
    Obituary "$SAMSARA_WOLFEN_OB_SLOT7" 
    PROJECTILE
    +RIPPER
    +NOBOSSRIP
    +DOOMBOUNCE
    +NODAMAGETHRUST
    +SKYEXPLODE
    +CANBOUNCEWATER
    +DONTREFLECT
    -ALLOWBOUNCEONACTORS
    -BOUNCEONACTORS
    DamageType "BJSpear"
    SeeSound "wolfen/spearbounce"
    DeathSound "wolfen/spearexplode2"
    States
    {
      Spawn:
        SPB3 ABABABABAB 10 BRIGHT A_SpawnItemEx("SpearShotTrail2", 0, 0, 1)
        TNT1 A 0 A_Jump(128, "Spawn")
        goto Death

      Death:
        TNT1 A 0 A_Scream
        SPX3 C 1 A_Explode(64, 64, 0)
        SPX3 C 1
        SPX3 C 4 BRIGHT
        SPX3 D 6 BRIGHT
        Stop
    }
}

actor SpearShotMiniDM: SpearShotMini
{
    DamageType "BJSpear"
    Damage 3
    +STRIFEDAMAGE
    States
    {
      Spawn:
        SPB3 ABABAB 10 BRIGHT A_SpawnItemEx("SpearShotTrail2", 0, 0, 1)
        TNT1 A 0 A_Jump(96, "Spawn")
        goto Death

      Death:
        TNT1 A 0 A_Scream
        SPX3 C 1 A_Explode(32, 64, 0)
        SPX3 C 1
        SPX3 C 4 BRIGHT
        SPX3 D 6 BRIGHT
        Stop
    }
}

actor SpearShotMiniCoop: SpearShotMini { Species "Player" +THRUSPECIES }

actor SpearShotMini2
{
    Radius 3
    Height 4
    Speed 10
    Damage 4
    Obituary "$SAMSARA_WOLFEN_OB_SLOT7" 
    PROJECTILE
    +RIPPER
    +NOBOSSRIP
    +STRIFEDAMAGE
    +DOOMBOUNCE
    +NODAMAGETHRUST
    +SKYEXPLODE
    +CANBOUNCEWATER
    +DONTREFLECT
    -ALLOWBOUNCEONACTORS
    -BOUNCEONACTORS
    DamageType "BJSpear"
    SeeSound "wolfen/spearbounce"
    DeathSound "wolfen/spearexplode2"
    States
    {
      Spawn:
        SPB2 ABABABABAB 10 BRIGHT A_SpawnItemEx("SpearShotTrail2", 0, 0, 1)
        TNT1 A 0 A_Jump(128, "Spawn")
        goto Death

      Death:
        TNT1 A 0 A_Scream
        SPX2 AA 2 A_Explode(64, 64, 0)
        SPX2 BCDE 4 BRIGHT
        Stop
    }
}

actor SpearShotMini2DM: SpearShotMini2
{
    DamageType "BJSpear"
    Damage 3
    +STRIFEDAMAGE
    States
    {
      Spawn:
        SPB2 ABABAB 10 BRIGHT A_SpawnItemEx("SpearShotTrail2", 0, 0, 1)
        TNT1 A 0 A_Jump(96, "Spawn")
        goto Death

      Death:
        TNT1 A 0 A_Scream
        SPX2 AA 2 A_Explode(32, 64, 0)
        SPX2 BCDE 4 BRIGHT
        Stop
    }
}

actor SpearShotMini2Coop: SpearShotMini2 { Species "Player" +THRUSPECIES }

actor SpearShotTrail 
{
    Speed 1
    Scale 0.8
    Damage 0
    PROJECTILE
    -SOLID
    +NOCLIP
    +NOINTERACTION
    +CLIENTSIDEONLY
    States
    {
      Spawn:
        TNT1 A 2
        SPTR ABCD 2 Bright
        Stop   
    }
}

actor SpearShotTrail2: SpearShotTrail
{
    Scale 0.3
    +CLIENTSIDEONLY
}

// Castle Totenkopf SDL

actor Totenkopf_Flakgun : InstantSwitchWeapon
{
    Weapon.SelectionOrder 2900
    Inventory.PickUpSound "Totenkopf/getweapon"
    Weapon.AmmoUse 3
    //Weapon.AmmoGive 120
    Weapon.AmmoType "Cell"
    +DONTBOB
    +INVENTORY.UNDROPPABLE
    +WEAPON.NOAUTOFIRE
	+EXPLOSIVE
    +WEAPON.BFG
	Inventory.RestrictedTo "WolfenPlayer"
    Obituary "$SAMSARA_WOLFEN_OB_SLOT7" 
    Inventory.PickupMessage "Flakgun"
	Weapon.YAdjust 14
    Tag "Flakgun"
    States
    {
      Spawn:
        TK09 F -1
        stop

      Ready:
        TK09 A 1 A_WeaponReady
        loop

      Fire:
        TK09 B 3
      Hold:
		TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"HoldSmooth")
        TK09 C 3 Bright A_GiveInventory("SamsaraTotenkopfFlakgunAttackHandler")
        TNT1 A 0 A_CheckReload
        TK09 D 4 Bright A_GiveInventory("SamsaraTotenkopfFlakgunAttackHandler")
        TK09 E 3 A_ReFire
        goto Ready
		
      HoldSmooth:
        TK09 C 2 Bright A_GiveInventory("SamsaraTotenkopfFlakgunAttackHandler")
		TK09 I 1 Bright
        TNT1 A 0 A_CheckReload
        TK09 D 2 Bright A_GiveInventory("SamsaraTotenkopfFlakgunAttackHandler")
		TK09 J 2 Bright
        TK09 E 3 A_ReFire
        goto Ready
    }
}

actor SamsaraTotenkopfFlakgunAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Cell",3,1)
        stop
        TNT1 A 0 A_TakeInventory("Cell",3)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("Totenkopf/atkflakgun",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("Totenkopf_Flakgun_ProjectileDM",random(-6,6),false)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("Totenkopf_Flakgun_ProjectileCoop",random(-6,6),false)
        stop
    }
}

Actor Totenkopf_Flakgun_Projectile : Totenkopf_Tracer
{
	Damage (0)
	Projectile
	DamageType "TotenkopfFlak"
	+BLOODLESSIMPACT
	+HITTRACER
	+NODAMAGETHRUST
	+FORCERADIUSDMG
	States
	{
		Spawn:
			TNT1 A 1
			Loop
		Crash:
		Death:
			TNT1 A 1
			TNT1 A 0 A_Jump(102,3)
			TNT1 A 0 A_ChangeFlag("NOEXTREMEDEATH",false)
			TNT1 A 0 A_ChangeFlag("EXTREMEDEATH",true)
			TNT1 A 0 A_Explode(100*CallACS("Totenkopf_ExplosiveFactor"),64,XF_HURTSOURCE,0,16)
			TNT1 A 0 A_Playsound("Totenkopf/explosion", CHAN_BODY)
			TK54 A 2 Bright A_ChangeFlag("NOTIMEFREEZE",false)
			TK54 BC 2 Bright
			Stop
		XDeath:
			TNT1 A 1
			TNT1 A 0 A_Jump(102,3)
			TNT1 A 0 A_ChangeFlag("NOEXTREMEDEATH",false)
			TNT1 A 0 A_ChangeFlag("EXTREMEDEATH",true)
			TNT1 A 0 A_Explode(100*CallACS("Totenkopf_ExplosiveFactor"),64,XF_HURTSOURCE,0,16)
			TNT1 A 0 A_JumpIf(CallACS("Totenkopf_BloodGenerator_Offsets")==1,1)
			TNT1 A 0 A_GiveInventory("Totenkopf_BloodGenerator",1,AAPTR_TRACER)
			TK54 A 2 Bright A_ChangeFlag("NOTIMEFREEZE",false)
			TK54 BC 2 Bright
			Stop
	}
}
Actor Totenkopf_Flakgun_ProjectileDM : Totenkopf_Flakgun_Projectile
{
	States
	{
		Crash:
		Death:
			TNT1 A 1
			TNT1 A 0 A_Jump(102,3)
			TNT1 A 0 A_ChangeFlag("NOEXTREMEDEATH",false)
			TNT1 A 0 A_ChangeFlag("EXTREMEDEATH",true)
			TNT1 A 0 A_Explode(50*CallACS("Totenkopf_ExplosiveFactor"),64,XF_HURTSOURCE,0,16)
			TNT1 A 0 A_Playsound("Totenkopf/explosion", CHAN_BODY)
			TK54 A 2 Bright A_ChangeFlag("NOTIMEFREEZE",false)
			TK54 BC 2 Bright
			Stop
		XDeath:
			TNT1 A 1
			TNT1 A 0 A_Jump(102,3)
			TNT1 A 0 A_ChangeFlag("NOEXTREMEDEATH",false)
			TNT1 A 0 A_ChangeFlag("EXTREMEDEATH",true)
			TNT1 A 0 A_Explode(50*CallACS("Totenkopf_ExplosiveFactor"),64,XF_HURTSOURCE,0,16)
			TNT1 A 0 A_JumpIf(CallACS("Totenkopf_BloodGenerator_Offsets")==1,1)
			TNT1 A 0 A_GiveInventory("Totenkopf_BloodGenerator",1,AAPTR_TRACER)
			TK54 A 2 Bright A_ChangeFlag("NOTIMEFREEZE",false)
			TK54 BC 2 Bright
			Stop
	}
}	
Actor Totenkopf_Flakgun_ProjectileCoop : Totenkopf_Flakgun_Projectile { Species "Player" +THRUSPECIES +DONTHARMCLASS +DONTHARMSPECIES +MTHRUSPECIES }
