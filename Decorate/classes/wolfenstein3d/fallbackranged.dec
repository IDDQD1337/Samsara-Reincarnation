// Original/Lost Missions

actor Luger : InstantSwitchWeapon
{
    Weapon.SelectionOrder 1900
    Weapon.SlotNumber 1
    Inventory.PickUpSound "wolfen/knifeget"
    Obituary "$SAMSARA_WOLFEN_OB_FALLBACKRANGED"
    Inventory.PickupMessage "You got the Luger Pistol!"
    DamageType "Pistol"
    Weapon.AmmoUse 0
    Weapon.AmmoGive 0
    +WEAPON.WIMPY_WEAPON
    +DONTBOB
    +INVENTORY.UNDROPPABLE
	+WEAPON.NOALERT
    Weapon.YAdjust 14
	Inventory.RestrictedTo "WolfenPlayer"
    Tag "Luger"
    States
    {
      Spawn:
        WPSP A -1
        stop

      // Original
      Ready:
        TNT1 A 0 A_JumpIfInventory("WolfenClassMode",1,"ReadyLost")
      ReadyOriginal:
        WPST A 1 A_WeaponReady
        goto Ready

      Fire:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto FireInit
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireInit")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"FireInit")
        goto NoAmmoSwitch

      FireInit:
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_JumpIfInventory("WolfenClassMode",1,"FireLost")
      FireOriginal:
        WPST B 3
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto FireAttackOriginal
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireAttackOriginal")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"FireAttackOriginal")
        goto FireContinueOriginal

      FireAttackOriginal:
        TNT1 A 0 A_GunFlash
      FireContinueOriginal:
		TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"FireContinueOriginalSmooth")
        WPST C 3 Bright A_GiveInventory("SamsaraWolfenLugerAttackHandler")
        WPST DE 4
      FireFinish:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto FireFinishReFire
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireFinishReFire")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"FireFinishReFire")
        goto NoAmmoSwitch

      FireContinueOriginalSmooth:
		9382 C 1 Bright A_GiveInventory("SamsaraWolfenLugerAttackHandler")
        WPST C 2 Bright
        WPST D 3
		9382 D 3
		WPST E 2
		goto FireFinish

      FireFinishReFire:
        TNT1 A 0 A_ReFire
        goto Ready

      // Lost Missions
      ReadyLost:
        WPST F 1 A_WeaponReady
        goto Ready

      FireLost:
        WPST G 3
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto FireAttackLost
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireAttackLost")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"FireAttackLost")
        goto FireContinueLost

      FireAttackLost:
        TNT1 A 0 A_GunFlash
      FireContinueLost:
		TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"FireContinueLostSmooth")
        WPST H 3 Bright A_GiveInventory("SamsaraWolfenLostLugerAttackHandler")
        WPST IJ 4
        goto FireFinish

      FireContinueLostSmooth:
		9382 W 1 Bright A_GiveInventory("SamsaraWolfenLostLugerAttackHandler")
        WPST H 2 Bright
        WPST I 3
		9382 X 3
		WPST J 2
        goto FireFinish

      // "No ammo" switch
      NoAmmoSwitch:
        TNT1 A 0 A_ClearReFire
        TNT1 A 0 A_SelectWeapon("Knife")
        goto Deselect
    }
}

// Original

actor SamsaraWolfenLugerAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto PickupAttack
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("wolfen/pistol",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireBullets(0,0,1,7,"BlankPuff2",FBF_NOFLASH)
        TNT1 A 0 A_FireBullets(0,0,1,2,"BlankPuff2Bloodless",FBF_NOFLASH,120)
        TNT1 A 0 A_FireBullets(0,0,1,1,"BlankPuff2Bloodless",FBF_NOFLASH,64)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireBullets(0,0,1,7,"BlankPuff2Coop",FBF_NOFLASH)
        TNT1 A 0 A_FireBullets(0,0,1,2,"BlankPuff2BloodlessCoop",FBF_NOFLASH,256)
        TNT1 A 0 A_FireBullets(0,0,1,1,"BlankPuff2BloodlessCoop",FBF_NOFLASH,128)
        stop
    }
}

actor BlankPuff2: BlankPuff { DamageType "Pistol" }
actor BlankPuff2Coop : BlankPuff2 { +MTHRUSPECIES }

actor BlankPuff2Bloodless : BlankPuff2 { +BLOODLESSIMPACT +DONTSPLASH +NOTRIGGER }
actor BlankPuff2BloodlessCoop : BlankPuff2Bloodless { +MTHRUSPECIES }

// Lost Missions

actor SamsaraWolfenLostLugerAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto PickupAttack
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("wolfen/pistol2",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireBullets(0,0,1,7,"BlankPuff2",FBF_NOFLASH)
        TNT1 A 0 A_FireBullets(0,0,1,2,"BlankPuff2Bloodless",FBF_NOFLASH,120)
        TNT1 A 0 A_FireBullets(0,0,1,1,"BlankPuff2Bloodless",FBF_NOFLASH,64)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireBullets(0,0,1,7,"BlankPuff2Coop",FBF_NOFLASH)
        TNT1 A 0 A_FireBullets(0,0,1,2,"BlankPuff2BloodlessCoop",FBF_NOFLASH,256)
        TNT1 A 0 A_FireBullets(0,0,1,1,"BlankPuff2BloodlessCoop",FBF_NOFLASH,128)
        stop
    }
}

// Castle Totenkopf SDL

actor Totenkopf_IHaveDualPistols : Boolean {}

actor Totenkopf_Pistol : InstantSwitchWeapon
{
    Weapon.SelectionOrder 1900
    Weapon.SlotNumber 1
    Weapon.SlotPriority 0
    Inventory.PickUpSound "Totenkopf/getweapon"
    Obituary "$SAMSARA_WOLFENTKF_OB_FALLBACKRANGED"
    Inventory.PickupMessage "Walther P38 Pistol"
    DamageType "Pistol"
    Weapon.AmmoUse 0
    Weapon.AmmoGive 0
    +WEAPON.WIMPY_WEAPON
    +DONTBOB
    +INVENTORY.UNDROPPABLE
	+WEAPON.NOALERT
    Weapon.YAdjust 14
	Inventory.RestrictedTo "WolfenPlayer"
    Tag "Walther P38 Pistol (Single)"
    States
    {
      Spawn:
        TK02 M -1
        stop

      Ready:
        TNT1 A 0 A_JumpIfInventory("Totenkopf_PistolDual",1,2)
        TK02 A 1 A_WeaponReady(WRF_NOSECONDARY)
        loop
        TK02 A 1 A_WeaponReady
        loop

      Fire:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto FireStart
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"FireStart")
        goto NoAmmoSwitch

      FireStart:
        TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"FireSmooth")
        TK02 B 3
        TK02 C 3 Bright A_GiveInventory("SamsaraTotenkopfPistolSingleAttackHandler")
        TK02 DE 3
        goto FireFinish
		
      FireSmooth:
        TK02 B 3
        TK02 C 2 Bright A_GiveInventory("SamsaraTotenkopfPistolSingleAttackHandler")
        TK02 N 1 Bright
		TK02 DE 3
        goto FireFinish

      FireFinish:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto FireFinishReFire
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireFinishReFire")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"FireFinishReFire")
        goto NoAmmoSwitch

      FireFinishReFire:
        TNT1 A 0 A_ReFire
        goto Ready

      AltFire:
        TK02 A 8
        TNT1 A 0 A_SelectWeapon("Totenkopf_PistolDual")
        goto Deselect

      // "No ammo" switch
      NoAmmoSwitch:
        TNT1 A 0 A_ClearReFire
        TNT1 A 0 A_SelectWeapon("Totenkopf_Knife")
        goto Deselect
    }
}

actor Totenkopf_PistolDual : Totenkopf_Pistol
{
	Tag "Walther P38 Pistol (Dual)"
    Weapon.SelectionOrder 950
	Weapon.SlotPriority 1
    States
    {
      Ready:
        TNT1 A 0 A_JumpIfInventory("Totenkopf_Pistol",1,2)
        TK02 F 1 A_WeaponReady(WRF_NOSECONDARY)
        loop
        TK02 F 1 A_WeaponReady
        loop

      Fire:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto FireStart
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"FireStart")
        goto NoAmmoSwitch

      FireStart:
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"FireStartSmooth")
        TK02 G 3
        TK02 H 3 Bright A_GiveInventory("SamsaraTotenkopfPistolDualAttackHandler")
        TK02 I 5
      Fire2:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto Fire2Start
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"Fire2Start")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"Fire2Start")
        goto NoAmmoSwitch
		
      Fire2Start:
		TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"Fire2StartSmooth")
        TK02 J 3 Bright A_GiveInventory("SamsaraTotenkopfPistolDualAttackHandler")
        TK02 K 3
        goto FireFinish

      FireStartSmooth:
        TK02 G 3
        TK02 H 2 Bright A_GiveInventory("SamsaraTotenkopfPistolDualAttackHandler")
		TK02 O 1 Bright
        TK02 I 5
		goto Fire2

      Fire2StartSmooth:
        TK02 J 2 Bright A_GiveInventory("SamsaraTotenkopfPistolDualAttackHandler")
		TK02 P 1 Bright
        TK02 K 3
		goto FireFinish

      FireFinish:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto FireFinishReFire
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireFinishReFire")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"FireFinishReFire")
        goto NoAmmoSwitch

      FireFinishReFire:
        TK02 L 3 A_ReFire
        goto Ready

      AltFire:
        TK02 F 8
        TNT1 A 0 A_SelectWeapon("Totenkopf_Pistol")
        goto Deselect
    }
}

actor SamsaraTotenkopfPistolSingleAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto PickupAttack
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("Totenkopf/atkpistol",CHAN_WEAPON)
        TNT1 A 0 A_FireBullets(0,0,1,0,"SamsaraBlankPuff",FBF_NORANDOM|FBF_NOFLASH)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("Totenkopf_Tracer_PistolDM",random(-4,4),false)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("Totenkopf_Tracer_PistolCoop",random(-4,4),false)
        stop
    }
}

actor SamsaraTotenkopfPistolDualAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto PickupAttack
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("Totenkopf/atkpistol",CHAN_WEAPON)
        TNT1 A 0 A_FireBullets(0,0,1,0,"SamsaraBlankPuff",FBF_NORANDOM|FBF_NOFLASH)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("Totenkopf_Tracer_PistolDM",random(-6,6),false)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("Totenkopf_Tracer_PistolCoop",random(-6,6),false)
        stop
    }
}

actor Totenkopf_Tracer_Pistol : Totenkopf_Tracer { Damage(10 + (random(0,256)%25)) }
actor Totenkopf_Tracer_PistolDM : Totenkopf_Tracer { Damage(15 + (random(0,256)%10)) }
actor Totenkopf_Tracer_PistolCoop : Totenkopf_Tracer_Pistol { Species "Player" +THRUSPECIES }
