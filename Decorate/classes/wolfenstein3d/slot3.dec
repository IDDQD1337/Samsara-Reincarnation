actor Wolfen_IHaveSlot3 : Boolean {}

// Original/Lost Missions

actor "Mauser Rifle": InstantSwitchWeapon
{
    Weapon.SelectionOrder 1200
    Weapon.SlotNumber 3
    Inventory.PickUpSound "wolfen/rifleget"
    Weapon.AmmoUse 5
    Weapon.AmmoGive 0
    Weapon.AmmoType "Clip"
    +DONTBOB
    +INVENTORY.UNDROPPABLE
    Inventory.PickupMessage "You got the Mauser Rifle!"
    Obituary "$SAMSARA_WOLFEN_OB_SLOT3"
    Weapon.YAdjust 14
	Inventory.RestrictedTo "WolfenPlayer"
    Tag "Mauser Rifle"
    States
    {
      Spawn:
        WRFP I -1
        stop

      // Original
      Ready:
        TNT1 A 0 A_JumpIfInventory("WolfenClassMode",1,"ReadyLost")
      ReadyOriginal:
        WRFL A 1 A_WeaponReady
        goto Ready

      Fire:
        TNT1 A 0 A_JumpIfInventory("WolfenClassMode",1,"FireLost")
      FireOriginal:
        WRFL AB 1
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Clip",5,1)
        goto FireContinueOriginal
        TNT1 A 0 A_GunFlash
        goto FireContinueOriginal

      FireContinueOriginal:
        WRFL C 3 Bright A_GiveInventory("SamsaraWolfenRifleAttackHandler")
        WRFL D 3 Bright
        WRFL B 3
        WRFL EFGHA 6
        TNT1 A 0 A_ReFire
        goto Ready

      // Lost Missions
      ReadyLost:
        WRFL I 1 A_WeaponReady
        goto Ready

      FireLost:
        WRFL IJ 1
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Clip",5,1)
        goto FireContinueLost
        TNT1 A 0 A_GunFlash
        goto FireContinueLost

      FireContinueLost:
        WRFL K 3 Bright A_GiveInventory("SamsaraWolfenLostRifleAttackHandler")
        WRFL L 3 Bright
        WRFL J 3
        WRFL MNOPI 6
        TNT1 A 0 A_ReFire
        goto Ready
    }
}

// Original

actor SamsaraWolfenRifleAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",5,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",5)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("wolfen/rifle",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireBullets(0,0,1,75,"BlankPuffThruGhost",FBF_NORANDOM|FBF_NOFLASH)
        TNT1 A 0 A_FireBullets(0,0,1,75,"BlankPuffThruGhostBloodless",FBF_NORANDOM|FBF_NOFLASH,196)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireBullets(0,0,1,60,"BlankPuffThruGhostCoop",FBF_NORANDOM|FBF_NOFLASH)
        TNT1 A 0 A_FireBullets(0,0,1,60,"BlankPuffThruGhostBloodlessCoop",FBF_NORANDOM|FBF_NOFLASH)
        stop
    }
}

// Lost Missions

actor SamsaraWolfenLostRifleAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",5,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",5)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("wolfen/rifle2",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireBullets(0,0,1,75,"BlankPuffThruGhost",FBF_NORANDOM|FBF_NOFLASH)
        TNT1 A 0 A_FireBullets(0,0,1,75,"BlankPuffThruGhostBloodless",FBF_NORANDOM|FBF_NOFLASH,196)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireBullets(0,0,1,60,"BlankPuffThruGhostCoop",FBF_NORANDOM|FBF_NOFLASH)
        TNT1 A 0 A_FireBullets(0,0,1,60,"BlankPuffThruGhostBloodlessCoop",FBF_NORANDOM|FBF_NOFLASH)
        stop
    }
}

// Castle Totenkopf SDL

actor Totenkopf_STGFiredOnce : Boolean {}

actor Totenkopf_STG44 : InstantSwitchWeapon
{
	Tag "STG44 Assault Rifle"
    Weapon.SelectionOrder 450
    Weapon.SlotNumber 3
    Inventory.PickUpSound "Totenkopf/getweapon"
    Obituary "$SAMSARA_WOLFENTKF_OB_SLOT3"
    Weapon.AmmoUse 1
    Weapon.AmmoGive 0
    Weapon.AmmoType "Shell"
    +DONTBOB
    +INVENTORY.UNDROPPABLE
    Inventory.PickupMessage "STG44 Assault Rifle"
	Inventory.RestrictedTo "WolfenPlayer"
    Weapon.YAdjust 16
    States
    {
      Spawn:
        TK05 F -1
        stop

      Ready:
        TK05 A 1 A_WeaponReady
        loop

      Fire:
        TK05 B 3
      Hold:
		TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"HoldSmooth")
        TK05 C 2 Bright A_GiveInventory("SamsaraTotenkopfSTG44AttackHandler")
        TK05 D 3
        TK05 E 2 A_ReFire
        goto Ready

      HoldSmooth:
        TK05 C 1 Bright A_GiveInventory("SamsaraTotenkopfSTG44AttackHandler")
		TK05 I 1 Bright
        TK05 D 1
		TK05 J 2
        TK05 E 2 A_ReFire
        goto Ready
    }
} 

actor SamsaraTotenkopfSTG44AttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Shell",1,1)
        stop
        TNT1 A 0 A_JumpIfInventory("Totenkopf_STGFiredOnce",0,2)
        TNT1 A 0 A_GiveInventory("Totenkopf_STGFiredOnce")
        goto PickupAttack
        TNT1 A 0 A_TakeInventory("Shell",1)
        TNT1 A 0 A_TakeInventory("Totenkopf_STGFiredOnce")
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("Totenkopf/atkstg44",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("Totenkopf_Tracer_STG44DM",0,false)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("Totenkopf_Tracer_STG44Coop",0,false)
        stop
    }
}

actor Totenkopf_Tracer_STG44 : Totenkopf_Tracer
{
    Damage (25 + (random(0,20)))
    +THRUGHOST
}

actor Totenkopf_Tracer_STG44DM : Totenkopf_Tracer_STG44 { Damage (25 + (random(0,10))) }
actor Totenkopf_Tracer_STG44Coop : Totenkopf_Tracer_STG44 { Species "Player" +THRUSPECIES }
