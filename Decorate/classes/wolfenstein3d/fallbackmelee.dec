// Fallback Melee

actor Knife: InstantSwitchWeapon
{
    Weapon.Kickback 0
    Weapon.SelectionOrder 2200
    Weapon.SlotNumber 0
    Inventory.PickUpSound "wolfen/knifeget"
    Inventory.PickupMessage "You got a Knife! Wait, shouldn't you start with this?"
    Obituary "$SAMSARA_WOLFEN_OB_FIST_1"
    +WEAPON.MELEEWEAPON
    +DONTBOB
    +NOALERT
    +INVENTORY.UNDROPPABLE
    +WIMPY_WEAPON
    Weapon.YAdjust 14
	Inventory.RestrictedTo "WolfenPlayer"
    Tag "Knife"
    States
    {
      Spawn:
        WKNP A -1
        stop

      // Original
      Ready:
        TNT1 A 0 A_JumpIfInventory("WolfenClassMode",2,"ReadyCTK")
        TNT1 A 0 A_JumpIfInventory("WolfenClassMode",1,"ReadyLost")
      ReadyOriginal:
        //TNT1 A 0 A_JumpIfInventory("BJUberKnife",1,"ReadyUberOriginal")
        TNT1 A 0 A_JumpIfInventory("BJSuperKnife",1,"ReadySuperOriginal")
      ReadyNormalOriginal:
        WKNF F 1 A_WeaponReady
        goto Ready

      ReadySuperOriginal:
      ReadyUberOriginal:
        WKNF A 1 A_WeaponReady
        goto Ready

      Fire:
        TNT1 A 0 A_JumpIfInventory("WolfenClassMode",2,"FireCTK")
        TNT1 A 0 A_JumpIfInventory("WolfenClassMode",1,"FireLost")
      FireOriginal:
        //TNT1 A 0 A_JumpIfInventory("BJUberKnife",1,"FireUberOriginal")
        TNT1 A 0 A_JumpIfInventory("BJSuperKnife",1,"FireSuperOriginal")
      FireNormalOriginal:
		TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"FireNormalOriginalSmooth")
        WKNF GH 3
        WKNF I 3 A_GiveInventory("SamsaraWolfenKnifeAttackHandler")
        WKNF H 3
        goto Ready

      FireSuperOriginal:
		TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"FireSuperOriginalSmooth")
        WKNF BC 3 A_GiveInventory("SamsaraWolfenKnifeSuperAttackHandler")
        WKNF D 3 A_GiveInventory("SamsaraWolfenKnifeSuperPopcornAttackHandler")
        WKNF E 3
        goto Ready

      FireUberOriginal:
        WKNF BC 2 A_GiveInventory("SamsaraWolfenKnifeUberAttackHandler")
        WKNF D 2 A_GiveInventory("SamsaraWolfenKnifeUberPopcornAttackHandler")
        WKNF E 2
        goto Ready
		
      FireNormalOriginalSmooth:
        WKNF GH 3
        9382 K 3 A_GiveInventory("SamsaraWolfenKnifeAttackHandler")
        WKNF I 2
		WKNF H 1
        goto Ready

      FireSuperOriginalSmooth:
        WKNF BC 3 A_GiveInventory("SamsaraWolfenKnifeSuperAttackHandler")
        9382 B 3 A_GiveInventory("SamsaraWolfenKnifeSuperPopcornAttackHandler")
        WKNF D 2
		WKNF E 1
        goto Ready

      // Lost Missions
      ReadyLost:
        //TNT1 A 0 A_JumpIfInventory("BJUberKnife",1,"ReadyUberLost")
        TNT1 A 0 A_JumpIfInventory("BJSuperKnife",1,"ReadySuperLost")
      ReadyNormalLost:
        WKNF O 1 A_WeaponReady
        goto Ready

      ReadySuperLost:
      ReadyUberLost:
        WKNF J 1 A_WeaponReady
        goto Ready

      FireLost:
        //TNT1 A 0 A_JumpIfInventory("BJUberKnife",1,"FireUberLost")
        TNT1 A 0 A_JumpIfInventory("BJSuperKnife",1,"FireSuperLost")
      FireNormalLost:
		TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"FireNormalLostSmooth")
        WKNF PQ 3
        WKNF R 3 A_GiveInventory("SamsaraWolfenKnifeAttackHandler")
        WKNF Q 3
        goto Ready

      FireSuperLost:
		TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"FireSuperLostSmooth")
        WKNF KL 3 A_GiveInventory("SamsaraWolfenKnifeSuperAttackHandler")
        WKNF M 3 A_GiveInventory("SamsaraWolfenKnifeSuperPopcornAttackHandler")
        WKNF N 3
        goto Ready

      FireUberLost:
        WKNF KL 2 A_GiveInventory("SamsaraWolfenKnifeUberAttackHandler")
        WKNF M 2 A_GiveInventory("SamsaraWolfenKnifeUberPopcornAttackHandler")
        WKNF N 2
        goto Ready
		
      FireNormalLostSmooth:
        WKNF PQ 3
        9383 E 3 A_GiveInventory("SamsaraWolfenKnifeAttackHandler")
        WKNF R 2
		WKNF Q 1
        goto Ready

      FireSuperLostSmooth:
        WKNF KL 3 A_GiveInventory("SamsaraWolfenKnifeSuperAttackHandler")
        9382 V 3 A_GiveInventory("SamsaraWolfenKnifeSuperPopcornAttackHandler")
        WKNF M 2
		WKNF N 1
        goto Ready

      // Castle Totenkopf SDL (its own version)
      ReadyCTK:
        TK01 A 1 A_WeaponReady
        goto Ready

      FireCTK:
        TK01 BC 3
        TK01 D 3 A_GiveInventory("SamsaraTotenkopfKnifeAttackHandler")
        TK01 E 3
        goto Ready
    }
}

// Original/Lost Missions

actor SamsaraWolfenKnifeAttackHandler : Trigger
{
    States
    {
      // Attack stuff
      Pickup:
        TNT1 A 0 A_PlaySound("wolfen/knife2",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupCoop")
      PickupDM:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupDMSpread")
        TNT1 A 0 A_CustomPunch(7,true,0,"BlankPuff2")
        stop

      PickupDMSpread:
        TNT1 A 0 A_CustomPunch(7*3,true,0,"BlankPuff2")
        stop

      PickupCoop:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupCoopSpread")
        TNT1 A 0 A_CustomPunch(7,true,0,"BlankPuff2Coop")
        stop

      PickupCoopSpread:
        TNT1 A 0 A_CustomPunch(7*3,true,0,"BlankPuff2Coop")
        stop
    }
}

actor SamsaraWolfenKnifeSuperAttackHandler : Trigger
{
    States
    {
      // Attack stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupCoop")
      PickupDM:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupDMSpread")
        TNT1 A 0 A_CustomPunch(6,true,0,"BlankPuff2")
        stop

      PickupDMSpread:
        TNT1 A 0 A_CustomPunch(6*3,true,0,"BlankPuff2")
        stop

      PickupCoop:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupCoopSpread")
        TNT1 A 0 A_CustomPunch(6,true,0,"BlankPuff2Coop")
        stop

      PickupCoopSpread:
        TNT1 A 0 A_CustomPunch(6*3,true,0,"BlankPuff2Coop")
        stop
    }
}

actor SamsaraWolfenKnifeSuperPopcornAttackHandler : Trigger
{
    States
    {
      Pickup:
        TNT1 A 0 A_PlaySound("wolfen/knife",CHAN_WEAPON)
      // Attack stuff (main)
      Pickup1:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"Pickup1Coop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"Pickup1Coop")
      Pickup1DM:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"Pickup1DMSpread")
        TNT1 A 0 A_CustomPunch(6,true,0,"BlankPuff2")
        goto Pickup2

      Pickup1DMSpread:
        TNT1 A 0 A_CustomPunch(6*3,true,0,"BlankPuff2")
        goto Pickup2

      Pickup1Coop:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"Pickup1CoopSpread")
        TNT1 A 0 A_CustomPunch(6,true,0,"BlankPuff2Coop")
        goto Pickup2

      Pickup1CoopSpread:
        TNT1 A 0 A_CustomPunch(6*3,true,0,"BlankPuff2Coop")
        goto Pickup2

      // Attack stuff (popcorn)
      Pickup2:
        TNT1 A 0 //A_JumpIfInventory("PopcornStab",1,1)
        stop
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"Pickup2Coop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"Pickup2Coop")
      Pickup2DM:
        TNT1 A 0 A_FireBullets(0,0,1,0,"KnifePopcorn",FBF_NOFLASH,64)
        stop

      Pickup2Coop:
        TNT1 A 0 A_FireBullets(0,0,1,0,"KnifePopcornCoop",FBF_NOFLASH,64)
        stop
    }
}

actor SamsaraWolfenKnifeUberAttackHandler : Trigger
{
    States
    {
      // Attack stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupCoop")
      PickupDM:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupDMSpread")
        TNT1 A 0 A_CustomPunch(7,true,0,"BlankPuff2")
        stop

      PickupDMSpread:
        TNT1 A 0 A_CustomPunch(7*3,true,0,"BlankPuff2")
        stop

      PickupCoop:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupCoopSpread")
        TNT1 A 0 A_CustomPunch(7,true,0,"BlankPuff2Coop")
        stop

      PickupCoopSpread:
        TNT1 A 0 A_CustomPunch(7*3,true,0,"BlankPuff2Coop")
        stop
    }
}

actor SamsaraWolfenKnifeUberPopcornAttackHandler : Trigger
{
    States
    {
      Pickup:
        TNT1 A 0 A_PlaySound("wolfen/knife",CHAN_WEAPON)
      // Attack stuff (main)
      Pickup1:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"Pickup1Coop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"Pickup1Coop")
      Pickup1DM:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"Pickup1DMSpread")
        TNT1 A 0 A_CustomPunch(7,true,0,"BlankPuff2")
        goto Pickup2

      Pickup1DMSpread:
        TNT1 A 0 A_CustomPunch(7*3,true,0,"BlankPuff2")
        goto Pickup2

      Pickup1Coop:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"Pickup1CoopSpread")
        TNT1 A 0 A_CustomPunch(7,true,0,"BlankPuff2Coop")
        goto Pickup2

      Pickup1CoopSpread:
        TNT1 A 0 A_CustomPunch(7*3,true,0,"BlankPuff2Coop")
        goto Pickup2

      // Attack stuff (popcorn)
      Pickup2:
        TNT1 A 0 //A_JumpIfInventory("PopcornStab",1,1)
        stop
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"Pickup2Coop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"Pickup2Coop")
      Pickup2DM:
        TNT1 A 0 A_FireBullets(0,0,1,0,"KnifePopcorn",FBF_NOFLASH,64)
        stop

      Pickup2Coop:
        TNT1 A 0 A_FireBullets(0,0,1,0,"KnifePopcornCoop",FBF_NOFLASH,64)
        stop
    }
}

actor KnifePopcorn: BulletPuff
{
    Radius 4
    Height 4
    VSpeed 0

    Obituary "$SAMSARA_WOLFEN_OB_FIST_2" 

    DamageType "Pistol"
    DeathSound "wolfen/spearexplode2"

    Scale 0.35
    Alpha 0.75
    RenderStyle Add

    +PUFFGETSOWNER
    +ALWAYSPUFF
    +PUFFONACTORS

    States
    {
      Spawn:
        TNT1 A 0
        TNT1 A 0 A_Scream
        SPB2 AB 0 BRIGHT A_SpawnItemEx("SpearShotTrail2", 0,0,0, random(-3,3),0,random(-3,3), random(0,360))
        SPX2 AA 2 A_Explode(12, 80, 0)
        SPX2 BCDE 4 BRIGHT
        Stop
    }
}

actor KnifePopcornCoop : KnifePopcorn { +MTHRUSPECIES }

// Castle Totenkopf SDL

actor SamsaraTotenkopfKnifeAttackHandler : Trigger
{
    States
    {
      // Attack stuff
      Pickup:
        TNT1 A 0 A_PlaySound("Totenkopf/atkknife",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupCoop")
      PickupDM:
        TNT1 A 0 A_FireCustomMissile("Totenkopf_KnifeProjectile",0,false)
        stop

      PickupCoop:
        TNT1 A 0 A_FireCustomMissile("Totenkopf_KnifeProjectileCoop",0,false)
        stop
    }
}

Actor Totenkopf_KnifeProjectile : FastProjectile
{
	Projectile
	Speed 32
	Damage (random(0,256)>>3)
	Height 4
	Radius 2
	+NOEXTREMEDEATH
	+HITTRACER
	+FORCERADIUSDMG
	+NODAMAGETHRUST
	-BLOODSPLATTER
	+BLOODLESSIMPACT
    +DONTBLAST
    +NOTIMEFREEZE
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 1 
			Stop
		Crash:
		Death:
			TNT1 A 1
			Stop
		XDeath:
			TNT1 A 0 A_JumpIf(CallACS("Totenkopf_BloodGenerator_Offsets")==1,1)
			TNT1 A 0 A_Playsound("Totenkopf/hitenemy", CHAN_WEAPON)
			TNT1 A 1
			TNT1 A 0 A_GiveInventory("Totenkopf_BloodGenerator",1,AAPTR_TRACER)
			TNT1 A 1
			Stop
	}
}

Actor Totenkopf_KnifeProjectileCoop : Totenkopf_KnifeProjectile { +THRUSPECIES Species "Player" }

// Punchdrunk Unique

actor "Spear of Longinus" : InstantSwitchWeapon
{
    Weapon.Kickback 0
    Weapon.SelectionOrder 2200
    Inventory.PickUpSound "wolfen/knifeget"
    Inventory.PickupMessage "You got the Spear of...Destiny?"
    Obituary "$SAMSARA_WOLFEN_OB_PUNCHDRUNKB"
    +WEAPON.MELEEWEAPON
    +DONTBOB
    +NOALERT
    +INVENTORY.UNDROPPABLE
    +CHEATNOTWEAPON
    Weapon.YAdjust 16
    Tag "Spear of Longinus"
    States
    {
      Spawn:
        UNIQ R -1
        stop

      Ready:
        WSPR F 1 A_WeaponReady
        loop

      Fire:
        TNT1 A 0 A_PlayWeaponSound("wolfen/spearstab")
        WSPR G 3 A_CustomPunch(20,1,0,"SpearPopcornPuff",98)
        WSPR HI 3 A_CustomPunch(20,1,0,"BlankPuff2",98)
        WSPR I 5
        WSPR I 0
        WSPR H 1
        goto Ready
    }
}

actor SpearPopcornPuff: BulletPuff
{
    Radius 4
    Height 4
    VSpeed 0

    Scale 0.35
    Alpha 0.75
    RenderStyle Add

    DeathSound "wolfen/spearexplode2"
    +PUFFONACTORS
    +CLIENTSIDEONLY

    States
    {
      Spawn:
      Melee:
      XDeath:
        TNT1 A 0
        TNT1 A 0 A_Scream
        SPB2 AB 0 BRIGHT A_SpawnItemEx("SpearShotTrail2", 0,0,0, random(-3,3),0,random(-3,3), random(0,360))
        SPX2 ABCDE 4 BRIGHT
        Stop
    }
}
