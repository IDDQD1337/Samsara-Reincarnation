actor Wolfen_IHaveSlot2 : Boolean {}

// Original/Lost Missions

actor "Machine Gun" : InstantSwitchWeapon
{
    Weapon.SelectionOrder 1300
    Weapon.SlotNumber 2
    Inventory.PickUpSound "wolfen/mgunget"
    Weapon.AmmoUse 1
    //Weapon.AmmoGive 20
    Weapon.AmmoType "Clip"
    +DONTBOB
    +INVENTORY.UNDROPPABLE
    Inventory.PickupMessage "You got the Machine Gun!"
    Obituary "$SAMSARA_WOLFEN_OB_SLOT2"
    Weapon.YAdjust 14
	Inventory.RestrictedTo "WolfenPlayer"
    Tag "Machine Gun"
    States
    {
      Spawn:
        WMGP A -1
        stop

      // Original
      Ready:
        TNT1 A 0 A_JumpIfInventory("WolfenClassMode",1,"ReadyLost")
      ReadyOriginal:
        WMGN A 1 A_WeaponReady
        goto Ready

      Fire:
        TNT1 A 0 A_JumpIfInventory("WolfenClassMode",1,"FireLost")
      FireOriginal:
        WMGN AB 2
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        goto HoldOriginal
        TNT1 A 0 A_GunFlash
        goto HoldOriginal

      Hold:
        TNT1 A 0 A_GunFlash
        TNT1 A 0 A_JumpIfInventory("WolfenClassMode",1,"HoldLost")
      HoldOriginal:
		TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"HoldOriginalSmooth")
        WMGN C 3 Bright A_GiveInventory("SamsaraWolfenMachineGunAttackHandler")
        WMGN D 3
        WMGN E 3 A_ReFire
        goto Ready
		
      HoldOriginalSmooth:
		9382 E 1 Bright A_GiveInventory("SamsaraWolfenMachineGunAttackHandler")
        WMGN C 2 Bright
        WMGN D 3
        WMGN E 3 A_ReFire
        goto Ready

      // Lost Missions
      ReadyLost:
        WMGN F 1 A_WeaponReady
        goto Ready

      FireLost:
        WMGN FG 2
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        goto HoldLost
        TNT1 A 0 A_GunFlash
        goto HoldLost

      HoldLost:
		TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"HoldLostSmooth")
        WMGN H 3 Bright A_GiveInventory("SamsaraWolfenLostMachineGunAttackHandler")
        WMGN I 3
        WMGN J 3 A_ReFire
        goto Ready

      HoldLostSmooth:
		9382 Y 1 Bright A_GiveInventory("SamsaraWolfenLostMachineGunAttackHandler")
        WMGN H 2 Bright
        WMGN I 3
        WMGN J 3 A_ReFire
        goto Ready
    }
}

// Original

actor SamsaraWolfenMachineGunAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("wolfen/machinegun",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireBullets(0,0,1,6,"BlankPuffThruGhost",FBF_NOFLASH)
        TNT1 A 0 A_FireBullets(0,0,1,2,"BlankPuffThruGhostBloodless",FBF_NOFLASH,120)
        TNT1 A 0 A_FireBullets(0,0,1,1,"BlankPuffThruGhostBloodless",FBF_NOFLASH,64)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireBullets(0,0,1,7,"BlankPuffThruGhostCoop",FBF_NOFLASH)
        TNT1 A 0 A_FireBullets(0,0,1,2,"BlankPuffThruGhostBloodlessCoop",FBF_NOFLASH,256)
        TNT1 A 0 A_FireBullets(0,0,1,1,"BlankPuffThruGhostBloodlessCoop",FBF_NOFLASH,128)
        stop
    }
}

// Lost Missions

actor SamsaraWolfenLostMachineGunAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("wolfen/machinegun2",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireBullets(0,0,1,6,"BlankPuffThruGhost",FBF_NOFLASH)
        TNT1 A 0 A_FireBullets(0,0,1,2,"BlankPuffThruGhostBloodless",FBF_NOFLASH,120)
        TNT1 A 0 A_FireBullets(0,0,1,1,"BlankPuffThruGhostBloodless",FBF_NOFLASH,64)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireBullets(0,0,1,7,"BlankPuffThruGhostCoop",FBF_NOFLASH)
        TNT1 A 0 A_FireBullets(0,0,1,2,"BlankPuffThruGhostBloodlessCoop",FBF_NOFLASH,256)
        TNT1 A 0 A_FireBullets(0,0,1,1,"BlankPuffThruGhostBloodlessCoop",FBF_NOFLASH,128)
        stop
    }
}

// Castle Totenkopf SDL

actor Totenkopf_Kar98k : InstantSwitchWeapon
{
    Weapon.SelectionOrder 1300
    Weapon.SlotNumber 2
    Inventory.PickUpSound "Totenkopf/getweapon"
    Weapon.AmmoUse 1
    //Weapon.AmmoGive 5
    Weapon.AmmoType "Shell"
    +DONTBOB
    +INVENTORY.UNDROPPABLE
	+WEAPON.NOALERT
    Inventory.PickupMessage "Kar98k Rifle"
    Obituary "$SAMSARA_WOLFEN_OB_SLOT2"
    Weapon.YAdjust 14
	Inventory.RestrictedTo "WolfenPlayer"
    Tag "Kar98k Rifle (Unscoped)"
    States
    {
      Spawn:
        TK03 M -1
        stop

      Ready:
        TNT1 A 0 A_JumpIfInventory("Totenkopf_Sniper",1,2)
        TK03 A 1 A_WeaponReady(WRF_NOSECONDARY)
        goto Ready
        TK12 A 1 A_WeaponReady(WRF_ALLOWZOOM)
        goto Ready

      Fire:
        TNT1 A 0 A_AlertMonsters
        TK03 A 0 A_JumpIfInventory("Totenkopf_Sniper",1,2)
		TK03 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"FireStartSmooth")
        goto FireStart
        TK12 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"FireStartSmooth")
        goto FireStart

      FireStart:
        "####" B 3
        "####" C 3 Bright A_GiveInventory("SamsaraTotenkopfKar98kAttackHandler")
        "####" D 5
        "####" A 0 A_CheckReload
        "####" E 5 A_PlaySound("Totenkopf/kar98bolt")
        "####" F 5
        "####" GHI 2
        "####" JK 5
        "####" L 3
        TNT1 A 0 A_ReFire
        goto Ready

      FireStartSmooth:
        "####" B 3
        "####" C 2 Bright A_GiveInventory("SamsaraTotenkopfKar98kAttackHandler")
		"####" P 1 Bright
        "####" D 5
        "####" A 0 A_CheckReload
        "####" E 5 A_PlaySound("Totenkopf/kar98bolt")
        "####" F 5
        "####" GQRS 1
		"####" I 2
        "####" J 3
		"####" Q 2
		"####" K 5
        "####" L 3
        TNT1 A 0 A_ReFire
        goto Ready

	  AltFire:
      Zoom:
        TK12 A 8
        TNT1 A 0 A_SelectWeapon("Totenkopf_Sniper")
        goto Deselect
    }
}

actor SamsaraTotenkopfKar98kAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Shell",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Shell",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("Totenkopf/atkkar98",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("Totenkopf_Tracer_Kar98k",0,false)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("Totenkopf_Tracer_Kar98kCoop",0,false)
        stop
    }
}

actor Totenkopf_Tracer_Kar98k : Totenkopf_Tracer
{
    Damage (75)
    +THRUGHOST
}

actor Totenkopf_Tracer_Kar98kCoop : Totenkopf_Tracer_Kar98k { Species "Player" +THRUSPECIES }
