actor " Rocket Launcher ": InstantSwitchWeapon
{
    Weapon.SelectionOrder 2500
    Weapon.SlotNumber 5
    Inventory.PickUpSound "wolfen/rocketget"
    Weapon.AmmoUse 1
    Weapon.AmmoGive 2
    Weapon.AmmoType "RocketAmmo"
    +DONTBOB
    +EXPLOSIVE
    +NOAUTOFIRE
    +UNDROPPABLE
    Inventory.PickupMessage "You got the Rocket Launcher!"
    Weapon.YAdjust 16
	Inventory.RestrictedTo "WolfenPlayer"
    Tag "Rocket Launcher"
    States
    {
      Spawn:
        WRLP A -1
        stop

      // Original
      Ready:
        TNT1 A 0 A_JumpIfInventory("WolfenClassMode",2,"ReadyCTK")
        TNT1 A 0 A_JumpIfInventory("WolfenClassMode",1,"ReadyLost")
      ReadyOriginal:
        WRCK A 1 A_WeaponReady
        goto Ready

      Fire:
        TNT1 A 0 A_JumpIfInventory("WolfenClassMode",2,"FireCTK")
        TNT1 A 0 A_JumpIfInventory("WolfenClassMode",1,"FireLost")
      FireOriginal:
        WRCK B 4
        WRCK C 4 Bright A_GiveInventory("SamsaraWolfenRocketLauncherAttackHandler")
        WRCK DB 4
      FireFinish:
        TNT1 A 0 A_ReFire
        goto Ready

      // Lost Missions
      ReadyLost:
        WRCK E 1 A_WeaponReady
        goto Ready

      FireLost:
        WRCK F 4
        WRCK G 4 Bright A_GiveInventory("SamsaraWolfenLostRocketLauncherAttackHandler")
        WRCK HF 4
        goto FireFinish

      // Castle Totenkopf SDL (Panzerschreck)
      ReadyCTK:
        TK07 A 1 A_WeaponReady
        goto Ready

      FireCTK:
        TK07 B 3
        TK07 C 3 Bright A_GiveInventory("SamsaraTotenkopfPanzerschreckAttackHandler")
        TK07 DE 3
        TK07 F 32
        TK07 A 3
        goto FireFinish
    }
}

// Original

actor SamsaraWolfenRocketLauncherAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,1)
        stop
        TNT1 A 0 A_TakeInventory("RocketAmmo",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_JumpIfInventory("RJingModeOn",1,"PickupAttackDMRJ")
        TNT1 A 0 A_FireCustomMissile("WolfRocket",0,false)
        stop

      PickupAttackDMRJ:
        TNT1 A 0 A_FireCustomMissile("WolfRocketRJ",0,false)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_JumpIfInventory("RJingModeOn",1,"PickupAttackCoopRJ")
        TNT1 A 0 A_FireCustomMissile("WolfRocket2",0,false)
        stop

      PickupAttackCoopRJ:
        TNT1 A 0 A_FireCustomMissile("WolfRocket2RJ",0,false)
        stop
    }
}

actor WolfRocket
{
    Scale 0.75
    Radius 8
    Height 6
    Speed 30
    Damage 20
    Projectile
    +RIPPER
    +FORCERADIUSDMG
    +NOEXTREMEDEATH
    +NOBOSSRIP
    SeeSound "wolfen/missilefire"
    DeathSound "wolfen/missilexplod"
    Obituary "$SAMSARA_WOLFEN_OB_SLOT5"
    States
    {
      Spawn:
        WMSL A 3 A_SpawnItem("WolfRocketSmoke")
        Loop

      Death:
        WFLB C 8 Bright A_Explode(128,32,0)
        WFLB D 6 Bright
        WFLB E 4 Bright
        Stop
    }
}

actor WolfRocketRJ : WolfRocket
{
    States
    {
      Death:
        WFLB C 8 Bright A_Explode(128,64)
        WFLB D 6 Bright
        WFLB E 4 Bright
        Stop
    }
}

actor WolfRocket2: WolfRocket { +THRUSPECIES Species "Player" }
actor WolfRocket2RJ: WolfRocketRJ { +THRUSPECIES Species "Player" }

actor WolfRocketSmoke
{
    Radius 1
    Height 1
    Speed 0
    Renderstyle Translucent
    Alpha 0.5
    +NOGRAVITY
    -SOLID
    +NOCLIP
    +CLIENTSIDEONLY
    States
    {
      Spawn:
        TNT1 A 2
        RTRL ABCD 4
        stop
    }
}

// Lost Missions

actor SamsaraWolfenLostRocketLauncherAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,1)
        stop
        TNT1 A 0 A_TakeInventory("RocketAmmo",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_JumpIfInventory("RJingModeOn",1,"PickupAttackDMRJ")
        TNT1 A 0 A_FireCustomMissile("LostRocket",0,false)
        stop

      PickupAttackDMRJ:
        TNT1 A 0 A_FireCustomMissile("LostRocketRJ",0,false)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_JumpIfInventory("RJingModeOn",1,"PickupAttackCoopRJ")
        TNT1 A 0 A_FireCustomMissile("LostRocket2",0,false)
        stop

      PickupAttackCoopRJ:
        TNT1 A 0 A_FireCustomMissile("LostRocket2RJ",0,false)
        stop
    }
}

actor LostRocket : WolfRocket
{
	SeeSound "wolfen/missilefire2"
	States
	{
	  Spawn:
        WMSL B 3 A_SpawnItem("SODRocketSmoke")
        Loop

      Death:
        WEX2 A 8 Bright A_Explode(128,32,0)
        WEX2 B 6 Bright
        WEX2 C 4 Bright
        Stop
	}
}

actor LostRocketRJ : LostRocket
{
    States
    {
      Death:
        WEX2 A 8 Bright A_Explode(128,64)
        WEX2 B 6 Bright
        WEX2 C 4 Bright
        Stop
    }
}

actor LostRocket2: LostRocket { +THRUSPECIES Species "Player" }
actor LostRocket2RJ: LostRocketRJ { +THRUSPECIES Species "Player" }

ACTOR SODRocketSmoke : WolfRocketSmoke
{
	States
	{
		Spawn:
			TNT1 A 2
			WSM2 ABCD 4
			Stop
	}
}

// Castle Totenkopf SDL

actor SamsaraTotenkopfPanzerschreckAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,1)
        stop
        TNT1 A 0 A_TakeInventory("RocketAmmo",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("Totenkopf/missilefire",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("Totenkopf_Panzerschreck_ProjectileDM",0,false,0,8)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("Totenkopf_Panzerschreck_ProjectileCoop",0,false,0,8)
        stop
    }
}

Actor Totenkopf_Panzerschreck_Projectile
{
	Speed 24
	Damage (0)
	Height 16
	Radius 8
	Projectile
	DamageType "TotenkopfRocket"
	+BLOODLESSIMPACT
	+NODAMAGETHRUST
	//+EXTREMEDEATH
	+FORCERADIUSDMG
	States
	{
		Spawn:
			TK53 A 1 Bright
			TK53 A 0 A_SpawnItemEx("Totenkopf_Panzerschreck_Smoke",-16,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
			Loop
		Death:
			TNT1 A 0 A_Explode(400,128,XF_HURTSOURCE,1,16)
			TNT1 A 0 A_Playsound("Totenkopf/missilehit", CHAN_6)
			TK53 FGH 6 Bright
			Stop
	}
} 

Actor Totenkopf_Panzerschreck_ProjectileDM : Totenkopf_Panzerschreck_Projectile 
{ 
	Speed 24 
	States
	{	
		Death:
			TNT1 A 0 A_Explode(200,128,XF_HURTSOURCE,1,16)
			TNT1 A 0 A_Playsound("Totenkopf/missilehit", CHAN_6)
			TK53 FGH 6 Bright
			Stop
	}	
}

Actor Totenkopf_Panzerschreck_ProjectileCoop : Totenkopf_Panzerschreck_Projectile { Species "Player" +THRUSPECIES +DONTHARMCLASS +DONTHARMSPECIES +MTHRUSPECIES }

Actor Totenkopf_Panzerschreck_Smoke
{
	+NOINTERACTION
	+CLIENTSIDEONLY
	States
	{
		Spawn:
			TK53 A 0
			TK53 B 6
			TK53 CDE 3
			Stop
	}
}
