actor DiscOfRepulsionCooldown : Counter { Inventory.MaxAmount 3 }
actor DarkServantCooldown : Counter { Inventory.MaxAmount 60 }
actor SamsaraHexenDarkServantSpawnCount : Counter {}

actor PortMysticAmbit : CustomInventory
{
    Inventory.PickupMessage "Mystic Ambit Incant"
    Inventory.PickupSound "hexen/artiget"
    Inventory.PickupFlash "PickupFlash"
    Inventory.UseSound ""
    Inventory.Icon "ARTICOMB"
    Inventory.MaxAmount 16
    Inventory.InterHubAmount 16
    Inventory.RestrictedTo "HexenPlayer"
    Tag "Mystic Ambit Incant"
    +FLOATBOB
    +INVBAR
    +UNDROPPABLE
    States
    {
      Spawn:
        HRAD ABCDEFGHIJKLMNOP 4 Bright
        loop

      // Parias
      Use:
        TNT1 A 0 A_JumpIfInventory("HexenClassMode",2,"UseFighter")
        TNT1 A 0 A_JumpIfInventory("HexenClassMode",1,"UseMage")
      UseCleric:
        TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("SamsaraDecorate", 22), "UseSuccessCleric")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasProsperity",1,"UseClericProsperity")
      UseClericNormal:
        TNT1 A 0 A_JumpIfHealthLower(100,"UseSuccessCleric")
        goto UseFailCleric

      UseClericProsperity:
        TNT1 A 0 A_JumpIfHealthLower(250,"UseSuccessCleric")
        goto UseFailCleric

      UseSuccessCleric:
        TNT1 A 0 A_PlaySound("hexen/artiuse",CHAN_ITEM)
        TNT1 A 0 A_GiveInventory("SamsaraHexenMysticAmbitIncantHealingModeCheck")
        stop

      UseFailCleric:
        TNT1 A 0 ACS_NamedExecuteAlways("SamsaraClientDecorate", 0, 1) //A_Print("You cannot use this unless injured.")
        fail

      // Daedolon (Disc of Repulsion)
      UseMage:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasDisabledInventoryCooldown",1,2)
        TNT1 A 0 A_JumpIfInventory("DiscOfRepulsionCooldown",1,"UseFailMage")
        TNT1 A 0 A_PlaySound("hexen/artiuse",CHAN_ITEM)
        TNT1 A 0 A_PlaySound("BlastRadius",CHAN_AUTO)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasDisabledInventoryCooldown",1,3)
        TNT1 A 0 A_GiveInventory("DiscOfRepulsionCooldown",3)
        TNT1 A 0 ACS_NamedExecuteAlways("DiscOfRepulsionCooldown")
        TNT1 A 0 A_Blast(0,255,255,20,"SamsaraHexenBlastEffect","")
        fail

      UseFailMage:
        TNT1 A 0 A_Print("$DORCCOOLDOWN")
        fail

      // Baratus (Dark Servant)
      UseFighter:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasDisabledInventoryCooldown",1,2)
        TNT1 A 0 A_JumpIfInventory("DarkServantCooldown",1,"UseFailFighter")
        TNT1 A 0 A_PlaySound("hexen/artiuse",CHAN_ITEM)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasDisabledInventoryCooldown",1,"UseFighterContinue")
        TNT1 A 0 A_GiveInventory("DarkServantCooldown",60)
        TNT1 A 0 ACS_NamedExecuteAlways("DarkServantCooldown")
        TNT1 A 0 A_TakeInventory("SamsaraHexenDarkServantSpawnCount") // reset
        TNT1 A 0 A_GiveInventory("SamsaraHexenDarkServantSpawnCount")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,1)
        goto UseFighterContinue
        TNT1 A 0 A_GiveInventory("SamsaraHexenDarkServantSpawnCount",2)
        goto UseFighterContinue

      UseFighterContinue:
        TNT1 A 0 A_GiveInventory("SamsaraHexenDarkServantSpawner")
        fail
    
      UseFailFighter:
        TNT1 A 0 A_Print("$DSCCOOLDOWN")
        fail

      UseFail:
        TNT1 A 0
        fail
    }
}

// Parias

/*actor MysticAmbitArmor : BasicArmorBonus
{
    Armor.SavePercent 33.335
    Armor.SaveAmount 50
    Armor.MaxSaveAmount 300
}*/

actor SamsaraHexenMysticAmbitIncantHealingModeCheck : Trigger
{
    States
    {
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupCoop")
        TNT1 A 0 A_JumpIfInventory("SamsaraCurrentTeam",0,"PickupDMTeam8")
        TNT1 A 0 A_JumpIfInventory("SamsaraCurrentTeam",7,"PickupDMTeam7")
        TNT1 A 0 A_JumpIfInventory("SamsaraCurrentTeam",6,"PickupDMTeam6")
        TNT1 A 0 A_JumpIfInventory("SamsaraCurrentTeam",5,"PickupDMTeam5")
        TNT1 A 0 A_JumpIfInventory("SamsaraCurrentTeam",4,"PickupDMTeam4")
        TNT1 A 0 A_JumpIfInventory("SamsaraCurrentTeam",3,"PickupDMTeam3")
        TNT1 A 0 A_JumpIfInventory("SamsaraCurrentTeam",2,"PickupDMTeam2")
        TNT1 A 0 A_JumpIfInventory("SamsaraCurrentTeam",1,"PickupDMTeam1")
      PickupDM:
        TNT1 A 0 A_GiveInventory("SamsaraHexenMysticAmbitIncantHealing")
        stop

      PickupDMTeam1:
        TNT1 A 0 A_RadiusGive("SamsaraHexenMysticAmbitIncantHealingTeam1",255,RGF_GIVESELF|RGF_PLAYERS)
        stop

      PickupDMTeam2:
        TNT1 A 0 A_RadiusGive("SamsaraHexenMysticAmbitIncantHealingTeam2",255,RGF_GIVESELF|RGF_PLAYERS)
        stop

      PickupDMTeam3:
        TNT1 A 0 A_RadiusGive("SamsaraHexenMysticAmbitIncantHealingTeam3",255,RGF_GIVESELF|RGF_PLAYERS)
        stop

      PickupDMTeam4:
        TNT1 A 0 A_RadiusGive("SamsaraHexenMysticAmbitIncantHealingTeam4",255,RGF_GIVESELF|RGF_PLAYERS)
        stop

      PickupDMTeam5:
        TNT1 A 0 A_RadiusGive("SamsaraHexenMysticAmbitIncantHealingTeam5",255,RGF_GIVESELF|RGF_PLAYERS)
        stop

      PickupDMTeam6:
        TNT1 A 0 A_RadiusGive("SamsaraHexenMysticAmbitIncantHealingTeam6",255,RGF_GIVESELF|RGF_PLAYERS)
        stop

      PickupDMTeam7:
        TNT1 A 0 A_RadiusGive("SamsaraHexenMysticAmbitIncantHealingTeam7",255,RGF_GIVESELF|RGF_PLAYERS)
        stop

      PickupDMTeam8:
        TNT1 A 0 A_RadiusGive("SamsaraHexenMysticAmbitIncantHealingTeam8",255,RGF_GIVESELF|RGF_PLAYERS)
        stop

      PickupCoop:
        TNT1 A 0 A_RadiusGive("SamsaraHexenMysticAmbitIncantHealing",255,RGF_GIVESELF|RGF_PLAYERS)
        stop
    }
}

actor SamsaraHexenMysticAmbitIncantHealing : Trigger
{
    States
    {
      Pickup:
      // This is where teams are checked for in the actors after this one.
      PickupCheck:
      // Health check
      PickupCheckHealth:
        TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("SamsaraDecorate", 22), "PickupSuccessSound")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasProsperity",1,"PickupCheckHealthProsperity")
      PickupCheckHealthNormal:
        TNT1 A 0 A_JumpIfHealthLower(100,"PickupSuccessSound")
        goto PickupCheckArmor

      PickupCheckHealthProsperity:
        TNT1 A 0 A_JumpIfHealthLower(250,"PickupSuccessSound")
        goto PickupCheckArmor

      // Armor check
      PickupCheckArmor:
        TNT1 A 0 A_JumpIf(CallACS("SamsaraArmorCapCheck", 3, 0, 1), "PickupStop") // fail if we're full on health and armor
        goto PickupSuccessSound

      // Sound
      PickupSuccessSound:
        TNT1 A 0 A_PlaySound("hexen/ambituse",CHAN_AUTO)
      // Health giving
      PickupSuccessHealth:
        TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("SamsaraDecorate", 22), "PickupSuccessHealthInfinite")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasProsperity",1,"PickupSuccessHealthProsperity")
      PickupSuccessHealthNormal:
        TNT1 A 0 A_GiveInventory("FiniteHealth",random(50,99))
        goto PickupSuccessArmor

      PickupSuccessHealthProsperity:
        TNT1 A 0 A_GiveInventory("FiniteHealth250",random(50,99))
        goto PickupSuccessArmor

      PickupSuccessHealthInfinite:
        TNT1 A 0 A_GiveInventory("InfiniteHealth",random(50,99))
        goto PickupSuccessArmor

      // Armor giving
      PickupSuccessArmor:
        TNT1 A 0 ACS_NamedExecuteWithResult("SamsaraArmorAdjust", 0, -random(1,15), 0, 1)
        TNT1 A 0 ACS_NamedExecuteWithResult("SamsaraArmorAdjust", 3, 0, 0, 1)
        stop
    }
}

actor SamsaraHexenMysticAmbitIncantHealingTeam1 : SamsaraHexenMysticAmbitIncantHealing
{
    States
    {
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraCurrentTeam",2,"PickupStop")
        TNT1 A 0 A_JumpIfInventory("SamsaraCurrentTeam",1,"PickupCheck")
        stop
    }
}

actor SamsaraHexenMysticAmbitIncantHealingTeam2 : SamsaraHexenMysticAmbitIncantHealing
{
    States
    {
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraCurrentTeam",3,"PickupStop")
        TNT1 A 0 A_JumpIfInventory("SamsaraCurrentTeam",2,"PickupCheck")
        stop
    }
}

actor SamsaraHexenMysticAmbitIncantHealingTeam3 : SamsaraHexenMysticAmbitIncantHealing
{
    States
    {
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraCurrentTeam",4,"PickupStop")
        TNT1 A 0 A_JumpIfInventory("SamsaraCurrentTeam",3,"PickupCheck")
        stop
    }
}

actor SamsaraHexenMysticAmbitIncantHealingTeam4 : SamsaraHexenMysticAmbitIncantHealing
{
    States
    {
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraCurrentTeam",5,"PickupStop")
        TNT1 A 0 A_JumpIfInventory("SamsaraCurrentTeam",4,"PickupCheck")
        stop
    }
}

actor SamsaraHexenMysticAmbitIncantHealingTeam5 : SamsaraHexenMysticAmbitIncantHealing
{
    States
    {
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraCurrentTeam",6,"PickupStop")
        TNT1 A 0 A_JumpIfInventory("SamsaraCurrentTeam",5,"PickupCheck")
        stop
    }
}

actor SamsaraHexenMysticAmbitIncantHealingTeam6 : SamsaraHexenMysticAmbitIncantHealing
{
    States
    {
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraCurrentTeam",7,"PickupStop")
        TNT1 A 0 A_JumpIfInventory("SamsaraCurrentTeam",6,"PickupCheck")
        stop
    }
}

actor SamsaraHexenMysticAmbitIncantHealingTeam7 : SamsaraHexenMysticAmbitIncantHealing
{
    States
    {
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraCurrentTeam",0,"PickupStop")
        TNT1 A 0 A_JumpIfInventory("SamsaraCurrentTeam",7,"PickupCheck")
        stop
    }
}

actor SamsaraHexenMysticAmbitIncantHealingTeam8 : SamsaraHexenMysticAmbitIncantHealing
{
    States
    {
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraCurrentTeam",0,"PickupCheck")
        stop
    }
}

// Daedolon

actor SamsaraHexenBlastEffect : BlastEffect
{
    States
    {
      Spawn:
        H125 ABCDEFGHI 4
        stop
    }
}

// Baratus

actor SamsaraHexenDarkServantSpawner : Trigger
{
    States
    {
      // Spawner stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasDisabledInventoryCooldown",1,"PickupInfinite")
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupCoop")
      PickupDM:
        TNT1 A 0 A_FireCustomMissile("SamsaraHexenSummoningDoll",0,false)
        stop

      PickupCoop:
        TNT1 A 0 A_FireCustomMissile("SamsaraHexenSummoningDollCoop",0,false)
        stop

      PickupInfinite:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupInfiniteCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupInfiniteCoop")
      PickupInfiniteDM:
        TNT1 A 0 A_FireCustomMissile("SamsaraHexenSummoningDollInfinite",0,false)
        stop

      PickupInfiniteCoop:
        TNT1 A 0 A_FireCustomMissile("SamsaraHexenSummoningDollInfiniteCoop",0,false)
        stop
    }
}

actor SamsaraHexenSummoningDoll : SummoningDoll
{
    // these flags are to ensure inventory stuff works right at (possibly nearly) all times
    +DONTREFLECT
    +SKYEXPLODE
    States
    {
      Spawn:
        H125 I -1
        stop

      Death:
        H125 I 8
        TNT1 A 0 A_RearrangePointers(AAPTR_TARGET,AAPTR_NULL,AAPTR_TARGET)
        TNT1 A 0 A_JumpIf(!CallACS("SamsaraHexenDarkServantAutoRemoval"), "Remove")
        H125 I 4 A_Summon
        stop

      Remove:
        TNT1 A 0 A_TakeInventory("SamsaraHexenDarkServantSpawnCount",1,0,AAPTR_TARGET)
        TNT1 A 0 A_JumpIfInventory("SamsaraHexenDarkServantSpawnCount",1,"RemoveFinish",AAPTR_TARGET)
        TNT1 A 0 A_TakeInventory("DarkServantCooldown",0x7FFFFFFF,0,AAPTR_TARGET)
      RemoveFinish:
        TNT1 A 1 A_SpawnItemEx("SamsaraHexenMinotaurSmoke",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
        stop
    }
}

actor SamsaraHexenSummoningDollCoop : SamsaraHexenSummoningDoll
{
    Species "Player"
    +THRUSPECIES
}

actor SamsaraHexenSummoningDollInfinite : SamsaraHexenSummoningDoll
{
    States
    {
      Remove:
        goto RemoveFinish
    }
}

actor SamsaraHexenSummoningDollInfiniteCoop : SamsaraHexenSummoningDollInfinite
{
    Species "Player"
    +THRUSPECIES
}

actor SamsaraHexenMinotaurFriendThruCheck : Boolean {}

actor SamsaraHexenMinotaurFriend : MinotaurFriend replaces MinotaurFriend
{
    Obituary "$OB_MINOTAUR"
    HitObituary "$OB_MINOTAURHIT"
    Species "Player"
    +THRUSPECIES
    States
    {
      Spawn: // hack
        H126 A 0 NoDelay A_JumpIfInventory("SPModeOn",1,"SpawnThru",AAPTR_TRACER)
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"SpawnThru",AAPTR_TRACER)
        TNT1 A 0 A_ChangeFlag("THRUSPECIES",false)
        goto SpawnStart

      SpawnThru:
        TNT1 A 0 A_GiveInventory("SamsaraHexenMinotaurFriendThruCheck")
        goto SpawnStart

      SpawnStart:
        H126 A 15
        H126 A 15 A_SetTranslucent(0.66,0)
        H126 A 3 A_SetTranslucent(1,0)
      Idle:
        H126 AB 10 A_MinotaurLook
        loop

      Roam:
        H126 ABCD 5 A_MinotaurRoam
        loop

      See:
        H126 ABCD 5 A_MinotaurChase
        loop

      Melee:
        H126 V 10 A_FaceTarget
        H126 W 7 A_FaceTarget
        H126 X 12 A_MinotaurAtk1
        goto See

      Missile:
        H126 V 10 A_MinotaurDecide
        H126 Y 4 A_FaceTarget
        H126 Z 9 A_MinotaurAtk2
        goto See

      Hammer:
        H126 V 10 A_FaceTarget
        H126 W 7 A_FaceTarget
        H126 X 12 A_MinotaurAtk3
        goto See

      HammerLoop:
        H126 X 12
        goto Hammer

      Charge:
        H126 U 2 A_MinotaurCharge
        loop

      Pain:
        H126 E 3
        H126 E 6 A_Pain
        goto See

      //I could go to FadeOut, but I honestly kinda found that dumb
      Death:
        TNT1 A 0 A_JumpIfInventory("PowerMinotaur",1,1,AAPTR_TRACER)
        goto FadeOut
        H126 F 6 //A_MinotaurDeath
        H126 G 5
        H126 H 6 A_Scream
        H126 I 5
        H126 J 6
        H126 K 5
        H126 L 6
        H126 M 5 A_NoBlocking
        H126 N 6
        H126 O 5
        H126 P 6
        H126 Q 5
        H126 R 6
        H126 S 5
        H126 T -1 A_BossDeath
        stop

      FadeOut:
        H126 E 6
        H126 E 2 A_Scream
        H126 E 5 A_SpawnItemEx("SamsaraHexenMinotaurSmoke")
        H126 E 5
        H126 E 5 A_NoBlocking
        H126 E 5
        H126 E 5 A_SetTranslucent(0.66,0)
        H126 E 5 A_SetTranslucent(0.33,0)
        H126 E 10 A_BossDeath
        stop
    }
}

actor SamsaraHexenMinotaurFriendFX1 : MinotaurFX1 replaces MinotaurFX1
{
    Decal "MinotaurScorch"
    Species "Player"
    +BLOODSPLATTER
    +THRUSPECIES
    States
    {
      Spawn: // hack
        FX12 A 0 Bright NoDelay A_JumpIfInventory("SamsaraHexenMinotaurFriendThruCheck",1,"SpawnStart",AAPTR_TARGET)
        TNT1 A 0 A_ChangeFlag("THRUSPECIES",false)
      SpawnStart:
        goto Super::Spawn
    }
}

actor SamsaraHexenMinotaurFriendFX2 : MinotaurFX2 replaces MinotaurFX2
{
    Species "Player"
    +BLOODSPLATTER
    +THRUSPECIES
    States
    {
      Spawn: // hack
        FX13 A 0 Bright NoDelay A_JumpIfInventory("SamsaraHexenMinotaurFriendThruCheck",1,"SpawnStart",AAPTR_TARGET)
        TNT1 A 0 A_ChangeFlag("THRUSPECIES",false)
      SpawnStart:
        goto Super::Spawn
    }
}

actor SamsaraHexenMinotaurFriendFX3 : MinotaurFX3 replaces MinotaurFX3
{
    Species "Player"
    +BLOODSPLATTER
    +THRUSPECIES
    States
    {
      Spawn: // hack
        FX13 D 0 Bright NoDelay A_JumpIfInventory("SamsaraHexenMinotaurFriendThruCheck",1,"SpawnStart",AAPTR_TARGET)
        TNT1 A 0 A_ChangeFlag("THRUSPECIES",false)
      SpawnStart:
        goto Super::Spawn
    }
}

actor SamsaraHexenMinotaurSmoke : MinotaurSmoke replaces MinotaurSmoke
{
    States
    {
      Spawn:
        H127 ABCDEFGHIJKLMNOPQ 3
        stop
    }
}
