actor SamsaraHexenSpikedGauntletsHitCounter : Counter { Inventory.MaxAmount 2 }

actor "Mace of Contrition" : SamsaraHexenWeapon
{
    Inventory.PickupMessage "Mace of Contrition"
    Weapon.SlotNumber 0
    Weapon.SelectionOrder 3500 // CWeapMace's value; FWeapFist used 3400
    Weapon.YAdjust -8 // CWeapMace's value; FWeapFist didn't use this property
    Obituary "$SAMSARA_HEXEN_OB_FIST"
    Tag "Mace of Contrition"
    +BLOODSPLATTER
    +MELEEWEAPON
    +NOALERT
    +WIMPY_WEAPON
    States
    {
      // Parias
      Ready:
        TNT1 A 0 A_JumpIfInventory("HexenClassMode",3,"ReadyFighter")
        TNT1 A 0 A_JumpIfInventory("HexenClassMode",2,"ForceSwitchMage")
      ReadyCleric:
        CMCE A 1 A_WeaponReady
        goto Ready

      Deselect:
        TNT1 A 0 A_JumpIfInventory("HexenClassMode",3,"DeselectFighter")
        TNT1 A 0 A_JumpIfInventory("HexenClassMode",2,"ForceSwitchMage")
      DeselectCleric:
        CMCE A 1 A_Lower
        goto Deselect

      Select:
        TNT1 A 0 A_TakeInventory("SamsaraHexenSpikedGauntletsHitCounter") // map change ignores this
      SelectLoop:
        TNT1 A 0 A_JumpIfInventory("HexenClassMode",3,"SelectLoopFighter")
        TNT1 A 0 A_JumpIfInventory("HexenClassMode",2,"ForceSwitchMage")
      SelectLoopCleric:
        CMCE A 1 A_Raise
        goto SelectLoop

      Fire:
        TNT1 A 0 A_JumpIfInventory("HexenClassMode",3,"FireFighter")
        TNT1 A 0 A_JumpIfInventory("HexenClassMode",2,"ForceSwitchMage")
      FireCleric:
        CMCE B 2 Offset(60,20)
        CMCE B 1 Offset(30,33)
        CMCE B 2 Offset(8,45)
        CMCE CDE 1 Offset(8,45)
        CMCE E 1 Offset(-11,58) A_GiveInventory("SamsaraHexenMaceOfContritionAttackHandler")
        CMCE F 1 Offset(8,45)
        CMCE F 2 Offset(-8,74)
        CMCE F 1 Offset(-20,96)
        CMCE F 8 Offset(-33,160)
        CMCE A 2 Offset(8,75) A_ReFire
        CMCE A 1 Offset(8,65)
        CMCE A 2 Offset(8,60)
        CMCE A 1 Offset(8,55)
        CMCE A 2 Offset(8,50)
        CMCE A 1 Offset(8,45)
        goto Ready

      // Daedolon (If he gets ahold of and somehow uses this, then there are problems.)
      ForceSwitchMage:
        TNT1 A 0 A_ClearReFire
        TNT1 A 0 A_SelectWeapon("Sapphire Wand")
        TNT1 A 1 A_TakeInventory("Mace of Contrition")
        loop

      // Baratus (Spiked Gauntlets)
      ReadyFighter:
        FPCH A 1 A_WeaponReady
        goto Ready

      DeselectFighter:
        FPCH A 1 A_Lower
        goto Deselect

      SelectLoopFighter:
        FPCH A 1 A_Raise
        goto SelectLoop

      FireFighter:
        FPCH B 5 Offset(5,40)
        FPCH C 4 Offset(5,40)
        TNT1 A 0 A_JumpIfInventory("SamsaraHexenSpikedGauntletsHitCounter",0,"FireFighterStrong")
      FireFighterNormal:
        TNT1 A 0 A_GiveInventory("SamsaraHexenSpikedGauntletsAttackHandler")
      FireFighterNormalFinish:
        FPCH DC 4 Offset(5,40)
        FPCH B 5 Offset(5,40) A_ReFire
        goto Ready

      FireFighterStrong:
        TNT1 A 0 A_JumpIfCloser(129,1)
        goto FireFighterNormal
        TNT1 A 0 A_GiveInventory("SamsaraHexenSpikedGauntletsAttackHandler")
      FireFighterStrongFinish:
        FPCH DE 4 Offset(5,40)
        FPCH E 1 Offset(15,50)
        FPCH E 1 Offset(25,60)
        FPCH E 1 Offset(35,70)
        FPCH E 1 Offset(45,80)
        FPCH E 1 Offset(55,90)
        FPCH E 1 Offset(65,100)
        FPCH E 10 Offset(0,150)
        goto Ready
    }
}

// Parias

actor SamsaraHexenMaceOfContritionAttackHandler : Trigger
{
    States
    {
      // Attack stuff
      Pickup:
        TNT1 A 0 A_JumpIfCloser(129,"PickupHitActor")
      // Hit map geometry
      PickupHitOther:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupHitOtherCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupHitOtherCoop")
      PickupHitOtherDM:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupHitOtherDMSpread")
        TNT1 A 0 A_CustomPunch(random(25,40),true,0,"HammerPuff")
        stop

      PickupHitOtherDMSpread:
        TNT1 A 0 A_CustomPunch(random(25,40)*3,true,0,"HammerPuff")
        stop

      PickupHitOtherCoop:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupHitOtherCoopSpread")
        TNT1 A 0 A_CustomPunch(random(25,40),true,0,"HammerPuffCoop")
        stop

      PickupHitOtherCoopSpread:
        TNT1 A 0 A_CustomPunch(random(25,40)*3,true,0,"HammerPuffCoop")
        stop

      // Hit an actor
      PickupHitActor:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupHitActorCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupHitActorCoop")
      PickupHitActorDM:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupHitActorDMSpread")
        TNT1 A 0 A_CustomPunch(random(25,40),true,0,"HammerPuff",128)
        stop

      PickupHitActorDMSpread:
        TNT1 A 0 A_CustomPunch(random(25,40)*3,true,0,"HammerPuff",128)
        stop

      PickupHitActorCoop:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupHitActorCoopSpread")
        TNT1 A 0 A_CustomPunch(random(25,40),true,0,"HammerPuffCoop",128)
        stop

      PickupHitActorCoopSpread:
        TNT1 A 0 A_CustomPunch(random(25,40)*3,true,0,"HammerPuffCoop",128)
        stop
    }
}

actor HammerPuffCoop : HammerPuff { +MTHRUSPECIES }

// Baratus

actor SamsaraHexenSpikedGauntletsAttackHandler : Trigger
{
    States
    {
      // Attack stuff
      Pickup:
        TNT1 A 0 A_JumpIfCloser(129,"PickupHitActor")
      // Hit map geometry
      PickupHitOther:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupHitOtherCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupHitOtherCoop")
      PickupHitOtherDM:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupHitOtherDMSpread")
        TNT1 A 0 A_CustomPunch(random(40,55),true,0,"PunchPuff")
        goto PickupFinishHitOther

      PickupHitOtherDMSpread:
        TNT1 A 0 A_CustomPunch(random(40,55)*3,true,0,"PunchPuff")
        goto PickupFinishHitOther

      PickupHitOtherCoop:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupHitOtherCoopSpread")
        TNT1 A 0 A_CustomPunch(random(40,55),true,0,"PunchPuffCoop")
        goto PickupFinishHitOther

      PickupHitOtherCoopSpread:
        TNT1 A 0 A_CustomPunch(random(40,55)*3,true,0,"PunchPuffCoop")
        goto PickupFinishHitOther

      // Hit an actor
      PickupHitActor:
        TNT1 A 0 A_JumpIfInventory("SamsaraHexenSpikedGauntletsHitCounter",0,"PickupHitActorStrong")
      // Normal
      PickupHitActorNormal:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupHitActorNormalCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupHitActorNormalCoop")
      PickupHitActorNormalDM:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupHitActorNormalDMSpread")
        TNT1 A 0 A_CustomPunch(random(40,55),true,0,"PunchPuff",128)
        goto PickupFinishHitActor

      PickupHitActorNormalDMSpread:
        TNT1 A 0 A_CustomPunch(random(40,55)*3,true,0,"PunchPuff",128)
        goto PickupFinishHitActor

      PickupHitActorNormalCoop:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupHitActorNormalCoopSpread")
        TNT1 A 0 A_CustomPunch(random(40,55),true,0,"PunchPuffCoop",128)
        goto PickupFinishHitActor

      PickupHitActorNormalCoopSpread:
        TNT1 A 0 A_CustomPunch(random(40,55)*3,true,0,"PunchPuffCoop",128)
        goto PickupFinishHitActor

      // Strong
      PickupHitActorStrong:
        TNT1 A 0 A_PlaySound("*fistgrunt",CHAN_VOICE)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupHitActorStrongCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupHitActorStrongCoop")
      PickupHitActorStrongDM:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupHitActorStrongDMSpread")
        TNT1 A 0 A_CustomPunch(random(40,55)*2,true,0,"HammerPuff",128)
        goto PickupFinishHitActor

      PickupHitActorStrongDMSpread:
        TNT1 A 0 A_CustomPunch((random(40,55)*2)*3,true,0,"HammerPuff",128)
        goto PickupFinishHitActor

      PickupHitActorStrongCoop:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupHitActorStrongCoopSpread")
        TNT1 A 0 A_CustomPunch(random(40,55)*2,true,0,"HammerPuffCoop",128)
        goto PickupFinishHitActor

      PickupHitActorStrongCoopSpread:
        TNT1 A 0 A_CustomPunch((random(40,55)*2)*3,true,0,"HammerPuffCoop",128)
        goto PickupFinishHitActor

      // Finishing stuff
      // Hit map geometry
      PickupFinishHitOther:
        TNT1 A 0 A_TakeInventory("SamsaraHexenSpikedGauntletsHitCounter")
        stop

      // Hit an actor
      PickupFinishHitActor:
        TNT1 A 0 A_JumpIfInventory("SamsaraHexenSpikedGauntletsHitCounter",0,"PickupFinishHitActorStrong")
      // Normal
      PickupFinishHitActorNormal:
        TNT1 A 0 A_GiveInventory("SamsaraHexenSpikedGauntletsHitCounter")
        stop

      // Strong
      PickupFinishHitActorStrong:
        TNT1 A 0 A_TakeInventory("SamsaraHexenSpikedGauntletsHitCounter")
        stop
    }
}

actor PunchPuffCoop : PunchPuff { +MTHRUSPECIES }

// Unused punchdrunk stuff

actor "Spiked Gauntlet" : FWeapFist
{
    Weapon.SelectionOrder 3400
    Weapon.KickBack 150
    +WIMPY_WEAPON
    +MELEEWEAPON
    +NOALERT
    +INVENTORY.UNDROPPABLE
    +CHEATNOTWEAPON
    Inventory.PickupMessage "You got the Spiked Gauntlet of Baratus!"
    Inventory.PickupSound "hexen/weaponget"
    Obituary "$SAMSARA_HEXEN_OB_PUNCHDRUNKB"
    Tag "Spiked Gauntlet"
    States
    {
      Select:
        FPCH A 1 A_Raise
        Loop

      Deselect:
        FPCH A 1 A_Lower
        Loop

      Ready:
        FPCH A 1 A_WeaponReady
        Loop

      Fire:
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopFire")
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"CoopFire")
        FPCH B 5 Offset(5, 40)
        FPCH C 4 Offset(5, 40)
        TNT1 A 0 A_FPunchAttack
        FPCH D 4 Offset(5, 40) A_CustomPunch(random(10,15),1,0,"BlankPuff",96)
        FPCH C 4 Offset(5, 40)
        FPCH B 5 Offset(5, 40) A_ReFire
        Goto Ready

      CoopFire:
        FPCH B 5 Offset(5, 40)
        FPCH C 4 Offset(5, 40)
        TNT1 A 0 A_FPunchAttack
        FPCH D 4 Offset(5, 40) A_CustomPunch(random(40,55),1,0,"BlankPuff",96)
        FPCH C 4 Offset(5, 40)
        FPCH B 5 Offset(5, 40) A_ReFire
        Goto Ready

      Fire2:
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopFire2")
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"CoopFire2")
        TNT1 A 0 A_CustomPunch(random(40,50),1,0,"BlankPuff",96)
        FPCH DE 4 Offset(5, 40)
        FPCH E 1 Offset(15, 50)
        FPCH E 1 Offset(25, 60)
        FPCH E 1 Offset(35, 70)
        FPCH E 1 Offset(45, 80)
        FPCH E 1 Offset(55, 90)
        FPCH E 1 Offset(65, 100)
        FPCH E 10 Offset(0, 150)
        Goto Ready

      CoopFire2:
        TNT1 A 0 A_CustomPunch(random(80,110),1,0,"BlankPuff",96)
        FPCH DE 4 Offset(5, 40)
        FPCH E 1 Offset(15, 50)
        FPCH E 1 Offset(25, 60)
        FPCH E 1 Offset(35, 70)
        FPCH E 1 Offset(45, 80)
        FPCH E 1 Offset(55, 90)
        FPCH E 1 Offset(65, 100)
        FPCH E 10 Offset(0, 150)
        Goto Ready
    }
}
