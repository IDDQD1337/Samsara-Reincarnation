// Parias (Reincarnation/Classic)

actor FlechetteCooldown : Counter {}

actor PortFlechette : CustomInventory
{
    Inventory.UseSound ""
    Inventory.Icon "ARTIPSBG"
    Inventory.MaxAmount 1
    Inventory.InterHubAmount 1
    Inventory.RestrictedTo "HexenPlayer"
    Tag "Poison Cloud Flechette"
    +INVBAR
    +UNDROPPABLE
    States
    {
      Use:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasDisabledInventoryCooldown",1,2)
        TNT1 A 0 A_JumpIfInventory("FlechetteCooldown",1,"UseFailCooldown")
        TNT1 A 0 A_PlaySound("hexen/artiuse",CHAN_ITEM)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasDisabledInventoryCooldown",1,3)
        TNT1 A 0 A_GiveInventory("FlechetteCooldown",15)
        TNT1 A 0 ACS_NamedExecuteAlways("FlechetteCooldown", 0, 0)
        TNT1 A 0 A_SpawnItemEx("PoisonBagCoop",0,0,0,0,0,0,0,SXF_ABSOLUTEANGLE|SXF_ABSOLUTEVELOCITY|SXF_SETMASTER)
        fail

      UseFailCooldown:
        TNT1 A 0 ACS_NamedExecuteAlways("SamsaraClientDecorate", 0, 17, 1)
        fail
    }
}

actor PoisonBagCoop
{
    Height 30
    DamageType "PoisonCloud"
    ReactionTime 7
    Mass 0x7FFFFFFF
    Obituary "$SAMSARA_HEXEN_OB_SLOT1"
    +BLOODLESSIMPACT
    +DONTSPLASH
    +DROPOFF
    +FOILINVUL
    +FORCERADIUSDMG
    +NOBLOCKMAP
    +NODAMAGETHRUST
    +NOGRAVITY
    +PIERCEARMOR
    States
    {
      Spawn:
        PSBG A 20 Bright // Previously 18
        PSBG BC 4 Bright
        TNT1 A 0 A_SetTranslucent(0.7,1)
        PSBG D 1 Bright A_PlaySound("PoisonShroomDeath",CHAN_VOICE)
        PSBG D 1 Bright A_Explode(10,128,0)
      SpawnLoop:
        PSBG EEEFFFGGGHHHIII 2 Bright A_Explode(6,128,0)
        TNT1 A 0 A_Countdown
        loop

      Death:
        PSBG HG 7 Bright
        PSBG FD 6 Bright
        stop
    }
}

// Daedolon

actor Flechette2Cooldown : Counter {}

actor PortFlechette2 : PortFlechette
{
    Tag "Time Bomb Flechette"
    States
    {
      Use:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasDisabledInventoryCooldown",1,2)
        TNT1 A 0 A_JumpIfInventory("Flechette2Cooldown",1,"UseFailCooldown")
        TNT1 A 0 A_PlaySound("hexen/artiuse",CHAN_ITEM)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasDisabledInventoryCooldown",1,3)
        TNT1 A 0 A_GiveInventory("Flechette2Cooldown",12)
        TNT1 A 0 ACS_NamedExecuteAlways("FlechetteCooldown", 0, 1)
        TNT1 A 0 A_SpawnItemEx("FireBombCoop",0,0,0,0,0,0,0,SXF_ABSOLUTEANGLE|SXF_ABSOLUTEVELOCITY|SXF_SETMASTER)
        fail

      UseFailCooldown:
        TNT1 A 0 ACS_NamedExecuteAlways("SamsaraClientDecorate", 0, 17, 2)
        fail
    }
}

actor FireBombCoop : PoisonBagCoop
{
    DamageType "FireExplosive"
    -BLOODLESSIMPACT
    -NODAMAGETHRUST
    -PIERCEARMOR
    States
    {
      Spawn:
        PSBG A 20 // Previously 40
        PSBG BC 4
        TNT1 A 0 A_PlaySound("ClericFlameCircle",CHAN_VOICE)
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_Explode(128,128)
        TNT1 A 1 A_SpawnItemEx("HexenFireBombExplosion",0,0,24,0,0,0,0,SXF_NOCHECKPOSITION)
        stop
    }
}

actor HexenFireBombExplosion 
{
    RenderStyle "Add"
    Alpha 0.9
    +CLIENTSIDEONLY
    +NOINTERACTION
    States
    {
      Spawn:
        XPL1 ABCDEF 4 Bright
        stop
    }
}

// Baratus

actor Flechette3Cooldown : Counter {}

actor PortFlechette3 : PortFlechette
{
    Tag "Throwing Flechette"
    States
    {
      Use:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasDisabledInventoryCooldown",1,2)
        TNT1 A 0 A_JumpIfInventory("Flechette3Cooldown",1,"UseFailCooldown")
        TNT1 A 0 A_PlaySound("hexen/artiuse",CHAN_ITEM)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasDisabledInventoryCooldown",1,3)
        TNT1 A 0 A_GiveInventory("Flechette3Cooldown",9)
        TNT1 A 0 ACS_NamedExecuteAlways("FlechetteCooldown", 0, 2)
        TNT1 A 0 A_GiveInventory("SamsaraHexenThrowingFlechetteAttackHandler")
        fail

      UseFailCooldown:
        TNT1 A 0 ACS_NamedExecuteAlways("SamsaraClientDecorate", 0, 17, 3)
        fail
    }
}

actor SamsaraHexenThrowingFlechetteAttackHandler : Trigger
{
    States
    {
      // Attack stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupCoop")
      PickupDM:
        TNT1 A 0 A_FireCustomMissile("ThrowingBomb2",0,false)
        stop

      PickupCoop:
        TNT1 A 0 A_FireCustomMissile("ThrowingBomb2Coop",0,false)
        stop
    }
}

actor ThrowingBomb2 : ThrowingBomb
{
    DamageType "FireExplosive"
    BounceType "Hexen"
    SeeSound ""
    BounceSound "FlechetteBounce"
    DeathSound "ClericFlameCircle"
    +FORCERADIUSDMG
    States
    {
      Spawn:
        H106 A 4 A_CheckThrowBomb
        H106 BCDE 3 A_CheckThrowBomb
        H106 F 3 A_CheckThrowBomb2
        loop

      Tail:
        H106 G 6 A_CheckThrowBomb
        H106 F 4 A_CheckThrowBomb
        H106 H 6 A_CheckThrowBomb
        H106 F 4 A_CheckThrowBomb
        H106 G 6 A_CheckThrowBomb
        H106 F 3 A_CheckThrowBomb
        wait

      Death:
        CFCF Q 4 Bright A_NoGravity
        CFCF R 3 Bright A_Scream
        TNT1 A 0 A_AlertMonsters
        CFCF S 4 Bright A_Explode
        CFCF T 3 Bright
        CFCF U 4 Bright
        CFCF W 3 Bright
        CFCF X 4 Bright
        CFCF Z 3 Bright
        stop
    }
}

actor ThrowingBomb2Coop : ThrowingBomb2
{
    Species "Player"
    +THRUSPECIES
}
