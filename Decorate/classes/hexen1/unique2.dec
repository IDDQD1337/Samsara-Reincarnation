actor SamsaraDiscOfRepulsionCooldown : Counter { Inventory.MaxAmount 3 }

actor SamsaraHexenDiscOfRepulsion : CustomInventory
{
    Inventory.PickupMessage "Disc of Repulsion"
    Inventory.PickupSound "hexen/artiget"
    Inventory.PickupFlash "PickupFlash"
    Inventory.UseSound ""
    Inventory.Icon "ARTIBLST"
    Inventory.MaxAmount 1
    Inventory.InterHubAmount 1
    Inventory.RestrictedTo "HexenPlayer"
    Tag "Disc of Repulsion"
    +FLOATBOB
    +INVBAR
    +UNDROPPABLE
    States
    {
      Spawn:
        BLST ABCDEFGH 4 Bright
        loop

      Use:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasDisabledInventoryCooldown",1,2)
        TNT1 A 0 A_JumpIfInventory("SamsaraDiscOfRepulsionCooldown",1,"UseFailCooldown")
        TNT1 A 0 A_PlaySound("hexen/artiuse",CHAN_ITEM)
        TNT1 A 0 A_PlaySound("BlastRadius",CHAN_AUTO)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasDisabledInventoryCooldown",1,3)
        TNT1 A 0 A_GiveInventory("SamsaraDiscOfRepulsionCooldown",3)
        TNT1 A 0 ACS_NamedExecuteAlways("DiscOfRepulsionCooldown")
        TNT1 A 0 A_Blast(0,255,255,20,"SamsaraHexenBlastEffect","")
        fail

      UseFailCooldown:
        TNT1 A 0 A_Print("$DORCOOLDOWN")
        fail

      UseFail:
        TNT1 A 0
        fail
    }
}

actor SamsaraHexenBlastEffect : BlastEffect
{
    States
    {
      Spawn:
        H125 ABCDEFGHI 4
        stop
    }
}
