actor "Sapphire Wand" : SamsaraHexenWeapon
{
    Inventory.PickupMessage "Jade Wand"
    Weapon.SlotNumber 1
    Weapon.SelectionOrder 3600
    Weapon.KickBack 0
    Weapon.YAdjust 9
    Tag "Jade Wand"
    +NOALERT
    +WIMPY_WEAPON
    States
    {
      // Parias
      Ready:
        TNT1 A 0 A_JumpIfInventory("HexenClassMode",3,"ReadyFighter")
      ReadyLoop:
        TNT1 A 0 A_JumpIfInventory("HexenClassMode",3,"ReadyLoopFighter")
        TNT1 A 0 A_JumpIfInventory("HexenClassMode",2,"ReadyLoopMage")
        TNT1 A 0 A_JumpIfInventory("HexenClassMode",1,"ReadyLoopClassic")
      ReadyLoopCleric:
        H116 A 1 A_WeaponReady
        goto ReadyLoop

      Deselect:
        TNT1 A 0 A_JumpIfInventory("HexenClassMode",3,"DeselectFighter")
        TNT1 A 0 A_JumpIfInventory("HexenClassMode",2,"DeselectMage")
        TNT1 A 0 A_JumpIfInventory("HexenClassMode",1,"DeselectClassic")
      DeselectCleric:
        H116 A 1 A_Lower
        goto Deselect

      Select:
        TNT1 A 0 A_JumpIfInventory("HexenClassMode",3,"SelectFighter")
        TNT1 A 0 A_JumpIfInventory("HexenClassMode",2,"SelectMage")
        TNT1 A 0 A_JumpIfInventory("HexenClassMode",1,"SelectClassic")
      SelectCleric:
        H116 A 1 A_Raise
        goto Select

      Fire:
        TNT1 A 0 A_JumpIfInventory("HexenClassMode",3,"FireFighter")
        TNT1 A 0 A_JumpIfInventory("HexenClassMode",2,"FireMage")
        TNT1 A 0 A_JumpIfInventory("HexenClassMode",1,"FireClassic")
      FireCleric:
        H116 A 2 A_AlertMonsters
        H116 B 6 Bright Offset(0,48) A_GiveInventory("SamsaraHexenJadeWandAttackHandler")
        H116 A 5 Offset(0,40)
        TNT1 A 0 Offset(0,36) A_ReFire
        goto ReadyLoop
		
      // Classic Parias (Sapphire Wand)
      ReadyLoopClassic:
        MWND A 1 A_WeaponReady
        goto ReadyLoop

      DeselectClassic:
        MWND A 1 A_Lower
        goto Deselect

      SelectClassic:
        MWND A 1 A_Raise
        goto Select

      FireClassic:
        TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"FireMageSmooth")
      FireClassicVanilla:
        MWND A 6
        MWND A 0 A_AlertMonsters
        MWND B 6 Bright Offset(0,48) A_GiveInventory("SamsaraHexenSapphireWandAttackHandler")
        MWND A 3 Offset(0,40)
        MWND A 3 Offset(0,36) A_ReFire
        goto ReadyLoop

      FireClassicSmooth:
        MWND AJ 2
        MWND K 2 Bright
        MWND K 0 A_AlertMonsters
        MWND B 4 Bright Offset(0,48) A_GiveInventory("SamsaraHexenSapphireWandAttackHandler")
        MWND J 3 Offset(0,40)
        MWND A 2 Offset(0,36)
        MWND A 3 Offset(0,32) A_ReFire
        goto ReadyLoop

      // Daedolon (Sapphire Wand)
      ReadyLoopMage:
        MWND A 1 A_WeaponReady
        goto ReadyLoop

      DeselectMage:
        MWND A 1 A_Lower
        goto Deselect

      SelectMage:
        MWND A 1 A_Raise
        goto Select

      FireMage:
        TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"FireMageSmooth")
      FireMageVanilla:
        MWND A 6
        MWND B 6 Bright Offset(0,48) A_GiveInventory("SamsaraHexenSapphireWandAttackHandler")
        MWND A 3 Offset(0,40)
        MWND A 3 Offset(0,36) A_ReFire
        goto ReadyLoop

      FireMageSmooth:
        MWND AJ 2
        MWND K 2 Bright
        MWND B 4 Bright Offset(0,48) A_GiveInventory("SamsaraHexenSapphireWandAttackHandler")
        MWND J 3 Offset(0,40)
        MWND A 2 Offset(0,36)
        MWND A 3 Offset(0,32) A_ReFire
        goto ReadyLoop

      // Baratus (Impaler Crossbow)
      ReadyFighter:
        H100 EF 5
        goto ReadyLoop

      ReadyLoopFighter:
        H100 A 1 A_WeaponReady
        goto ReadyLoop

      DeselectFighter:
        H100 E 1 A_Lower
        goto Deselect

      SelectFighter:
        H100 E 1 A_Raise
        goto Select

      FireFighter:
        TNT1 A 0 A_JumpIfCloser(64,"FireFighterMelee")
        goto FireFighterRanged

      FireFighterMelee:
        H100 A 2 Offset(0,24)
        H100 A 2 Offset(0,14)
        H100 A 2 Offset(0,12) A_GiveInventory("SamsaraHexenImpalerCrossbowMeleeAttackHandler")
        H100 A 2 Offset(0,14)
        H100 A 2 Offset(0,24)
        H100 A 10 Offset(0,32)
        goto ReadyLoop

      FireFighterRanged:
        TNT1 A 0 A_AlertMonsters
        H100 B 6 A_GiveInventory("SamsaraHexenImpalerCrossbowRangedAttackHandler")
        H100 C 8 A_PlaySound("Hexen/Weapon/XBowLoad",CHAN_WEAPON)
        H100 DEF 5
        H100 A 12
        goto ReadyLoop
    }
}

// Parias

actor SamsaraHexenJadeWandAttackHandler : Trigger
{
    States
    {
      // Attack stuff
      Pickup:
        TNT1 A 0 A_PlaySound("hexen/jadefire",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupCoop")
      PickupDM:
        TNT1 A 0 A_FireBullets(4,4,4,3,"JadePuff",FBF_NOFLASH)
        stop

      PickupCoop:
        TNT1 A 0 A_FireBullets(4,4,4,3,"JadePuffCoop",FBF_NOFLASH)
        stop
    }
}

actor JadePuff
{
    DamageType "Electric"
    RenderStyle "Add"
    Alpha 0.5
    Scale 0.5
    +NOBLOCKMAP
    +NOGRAVITY
    +PAINLESS
    +PUFFONACTORS
    States
    {
      Spawn:
        H116 IJKLMNO 2 Bright
        stop
    }
}

actor JadePuffCoop : JadePuff { +MTHRUSPECIES }

// Daedolon

actor SamsaraHexenSapphireWandAttackHandler : Trigger
{
    States
    {
      // Attack stuff
      Pickup:
        TNT1 A 0 A_PlaySound("MageWandFire",CHAN_WEAPON)
        TNT1 A 0 A_FireBullets(0,0,1,0,"SamsaraBlankPuff",FBF_NORANDOM|FBF_NOFLASH)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupCoop")
      PickupDM:
        TNT1 A 0 A_FireCustomMissile("MageWandMissile2",0,false)
        stop

      PickupCoop:
        TNT1 A 0 A_FireCustomMissile("MageWandMissile3",0,false)
        stop
    }
}

actor MageWandMissile2 : MageWandMissile
{
    Radius 3
    Height 2
    Damage (random(1,4)*2)
    DamageType "Pistol"
    SeeSound "" // currently handled in attack handler due to +SPAWNSOUNDSOURCE not working properly in netplay
    Decal "RailScorchLower"
    Obituary "$SAMSARA_HEXEN_OB_PISTOL"
    +BLOODSPLATTER
    +NOBOSSRIP
    -SPAWNSOUNDSOURCE
}

actor MageWandMissile3 : MageWandMissile2
{
    Species "Player"
    +THRUSPECIES
}

// Baratus

// Melee

actor SamsaraHexenImpalerCrossbowMeleeAttackHandler : Trigger
{
    States
    {
      // Attack stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupCoop")
      PickupDM:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupDMSpread")
        TNT1 A 0 A_CustomPunch(random(10,30),true,0,"HexenImpalerPuff")
        stop

      PickupDMSpread:
        TNT1 A 0 A_CustomPunch(random(10,30)*3,true,0,"HexenImpalerPuff")
        stop

      PickupCoop:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupCoopSpread")
        TNT1 A 0 A_CustomPunch(random(10,30),true,0,"HexenImpalerPuffCoop")
        stop

      PickupCoopSpread:
        TNT1 A 0 A_CustomPunch(random(10,30)*3,true,0,"HexenImpalerPuffCoop")
        stop
    }
}

actor HexenImpalerPuff
{
    Radius 4
    Height 8
    SeeSound "dsempty"
    ActiveSound "FighterPunchMiss"
    +NOINTERACTION
    +PUFFONACTORS
    States
    {
      Spawn:
        H100 IJKLMN 3
        stop

      Melee:
      Crash:
        TNT1 A 0 A_PlaySound("Hexen/Weapon/XBowHit")
        goto Spawn

      XDeath:
        TNT1 A 1 A_PlaySound("Hexen/Weapon/XBowSplatter")
        stop
    }
}

actor HexenImpalerPuffCoop : HexenImpalerPuff { +MTHRUSPECIES }

// Ranged

actor SamsaraHexenImpalerCrossbowRangedAttackHandler : Trigger
{
    States
    {
      // Attack stuff
      Pickup:
        TNT1 A 0 A_FireBullets(0,0,1,0,"SamsaraBlankPuff",FBF_NORANDOM|FBF_NOFLASH)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupCoop")
      PickupDM:
        TNT1 A 0 A_FireCustomMissile("HexenImpalerMissile",0,false)
        goto PickupFinish

      PickupCoop:
        TNT1 A 0 A_FireCustomMissile("HexenImpalerMissileCoop",0,false)
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 A_Recoil(1)
        stop
    }
}

actor HexenImpalerMissile : FastProjectile
{
    Radius 4
    Height 8
    Speed 80
    Damage (random(1,4)*10)
    SeeSound "Hexen/Weapon/XBowFire"
    +BLOODSPLATTER
    States
    {
      Spawn:
        H100 H -1
        stop

      Death:
      Crash:
        H100 I 3 A_PlaySound("Hexen/Weapon/XBowHit",CHAN_VOICE)
        H100 JKLMN 3
        stop

      XDeath:
        TNT1 A 1 A_PlaySound("Hexen/Weapon/XBowSplatter",CHAN_VOICE)
        stop
    }
}

actor HexenImpalerMissileCoop : HexenImpalerMissile
{
    Species "Player"
    +THRUSPECIES
}
