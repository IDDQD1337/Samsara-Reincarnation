actor SamsaraClassicPariasHasBloodscourge : Boolean {}

actor Bloodscourge : SamsaraHexenWeapon
{
    Inventory.PickupMessage "Bloodscourge"
    Weapon.AmmoType1 "Clip"
    Weapon.AmmoGive1 0
    Weapon.AmmoUse1 15
    Weapon.AmmoType2 "Cell"
    Weapon.AmmoGive2 0
    Weapon.AmmoUse2 15
    Obituary "$SAMSARA_HEXEN_OB_SLOT7S"
    Tag "Bloodscourge"
    +ALT_AMMO_OPTIONAL
    +AMMO_OPTIONAL
    +BFG
    +NOALERT
    States
    {
      Spawn:
        H105 A -1
        stop

      Ready:
        TNT1 A 0 A_TakeInventory("SamsaraHexenWeaponPaletteBlendCounter")
        MSTF AAAAAABBBBBBCCCCCCDDDDDDEEEEEEFFFFF 1 A_WeaponReady
        loop

      Deselect:
        TNT1 A 0 A_TakeInventory("SamsaraHexenWeaponPaletteBlendCounter")
        MSTF A 1 A_Lower
        loop

      Select:
        TNT1 A 0 A_TakeInventory("SamsaraHexenWeaponPaletteBlendCounter")
        MSTF A 1 A_Raise
        loop

      Fire:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,3)
        TNT1 A 0 A_JumpIfInventory("Clip",15,1)
        goto NoAmmoSwitch
        TNT1 A 0 A_JumpIfInventory("Cell",15,1)
        goto NoAmmoSwitch
        MSTF G 4 Offset(0,40) A_AlertMonsters
        TNT1 A 0 A_Light1
        MSTF H 4 Bright Offset(0,48) A_GiveInventory("SamsaraHexenClassicBloodscourgeAttackHandler")
        MSTF H 2 Bright Offset(0,48) A_TakeInventory("SamsaraHexenWeaponPaletteBlendCounter",1)
        TNT1 A 0 A_Light0
        MSTF II 2 Offset(0,48) A_TakeInventory("SamsaraHexenWeaponPaletteBlendCounter",1)
        MSTF I 3 Offset(0,40)
        MSTF J 5 Offset(0,36)
        MSTF A 15
        goto Ready

      // "No ammo" switch
      NoAmmoSwitch:
        TNT1 A 0 A_ClearReFire
        TNT1 A 0 ACS_NamedExecuteWithResult("Hexen_SwitchPriority")
        goto Deselect
    }
}

actor SamsaraHexenClassicBloodscourgeAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",15,1)
        stop
        TNT1 A 0 A_JumpIfInventory("Cell",15,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",15)
        TNT1 A 0 A_TakeInventory("Cell",15)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("MageStaffFire",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("MageStaffFX3Classic",0,false,0,8)
        TNT1 A 0 A_FireCustomMissile("MageStaffFX3Classic",-3,false,0,8)
        TNT1 A 0 A_FireCustomMissile("MageStaffFX3Classic",3,false,0,8)
        goto PickupFinish

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("MageStaffFX3ClassicCoop",0,false,0,8)
        TNT1 A 0 A_FireCustomMissile("MageStaffFX3ClassicCoop",-3,false,0,8)
        TNT1 A 0 A_FireCustomMissile("MageStaffFX3ClassicCoop",3,false,0,8)
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 A_JumpIfInventory("WeaponFlashesAreDisabled",1,"PickupStop")
        TNT1 A 0 A_GiveInventory("SamsaraHexenWeaponPaletteBlendCounter",3)
        TNT1 A 0 ACS_NamedExecuteWithResult("SamsaraHexenWeaponPaletteBlend", 1)
        stop
    }
}

actor MageStaffFX3Classic : MageStaffFX3
{
    Speed 48 // was 60, slowed down a bit
    Damage 14
    -SCREENSEEKER
    -SEEKERMISSILE
    +FORCERADIUSDMG
    States
    {
      Spawn:
        MSP2 ABCD 2 Bright
        loop

      Death:
        MSP2 E 4 Bright A_SetTranslucent(1,1)
        MSP2 F 5 Bright A_Explode(140,192,0) // was 80 damage
        MSP2 GH 5 Bright
        MSP2 I 4 Bright
        stop
    }
}

actor MageStaffFX3ClassicCoop : MageStaffFX3Classic
{
    Species "Player"
    +THRUSPECIES
}
