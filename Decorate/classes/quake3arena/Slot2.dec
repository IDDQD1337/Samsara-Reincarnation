Actor Q3Shotgun : Q3Weapon
{
	Weapon.SlotNumber 2
	Weapon.AmmoUse 1
	Weapon.AmmoGive 10
	Weapon.AmmoType "Shell"
	Inventory.PickupMessage "Shotgun"
    Inventory.Icon "Q3CMPW04"
	Obituary "%o was gunned down by %k."
	Tag "Shotgun"
	+FLOATBOB
	States
	{
		Spawn:
			Q399 A -1
			stop

		ReadyActual:
			Q300 A 1 A_WeaponReady
			loop

		Deselect:
            Q300 A 0 A_JumpIfHealthLower(1,"DeselectInstant")
			Q300 A 1 A_PlaySound("Quake3/Weapons/Change",CHAN_5)
			Q301 ABCD 1
            Q301 E 0
			goto DeselectInstant

		Ready: // our "Select" state
			Q302 ABCD 1
            goto ReadyActual

		Fire:
            Q303 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart")
            Q303 A 0 A_JumpIfInventory("Shell",1,"FireStart")
            goto FireDry

        FireDry:
            Q300 A 0 A_ClearReFire
			Q300 A 0 A_PlaySound("Quake3/Weapons/NoAmmo",CHAN_5)
            Q300 A 17 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			Q300 A 0 A_SelectWeapon("Q3Machinegun")
			goto Deselect

        FireStart:
            Q303 A 0 A_AlertMonsters
			Q303 A 0 A_GunFlash
			Q303 A 0 A_GiveInventory("SamsaraQuake3ShotgunAttackHandler")
			Q303 ABCDEF 6 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto ReadyActual

		Flash:
			Q304 A 3 Bright
			stop
	}
}

actor SamsaraQuake3ShotgunAttackHandler : Trigger
{
    States
    {
	// Ammo checking/taking stuff
	Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupSound")
        TNT1 A 0 A_JumpIfInventory("Shell",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Shell",1)
        goto PickupSound

	// Sound stuff
	PickupSound:
		TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",2,"PickupSoundRevolution")
		TNT1 A 0 A_PlaySound("Quake3/ShotGun/Fire",CHAN_WEAPON)
		goto PickupAttack
	PickupSoundRevolution:
		TNT1 A 0 A_PlaySound("Quake3Revolution/ShotGun/Fire",CHAN_WEAPON)
		goto PickupAttack

	// Attack stuff
	PickupAttack:
        TNT1 A 0 A_GiveInventory("Q3QuadDamageSound")
        TNT1 A 0 A_SpawnItemEx("Q3ShotgunShell",8,4,49,0,1.5+frandom(0.0,1.2),1.0+frandom(0.0,0.8),frandom(-2.0,2.0))
        TNT1 A 0 A_SpawnItemEx("Q3ShotgunShell",8,2,49,0,-(1.5+frandom(0.0,1.2)),1.0+frandom(0.0,0.8),frandom(-2.0,2.0))
        TNT1 A 0 A_SpawnItemEx("Q3ShotgunSmoke",40,-16,20,0,0,frandom(0.125,0.25))
        TNT1 A 0 A_SpawnItemEx("Q3ShotgunSmoke",40,16,20,0,0,frandom(0.125,0.25))
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
	PickupAttackDM:
        TNT1 A 0 A_FireBullets(8,8,11,10,"Q3ShotgunPuff",FBF_NORANDOM|FBF_NOFLASH|FBF_NORANDOMPUFFZ)
        stop
	PickupAttackCoop:
        TNT1 A 0 A_FireBullets(8,8,11,10,"Q3ShotgunPuffCoop",FBF_NORANDOM|FBF_NOFLASH|FBF_NORANDOMPUFFZ)
        stop
    }
}

Actor Q3ShotgunPuff : Q3BulletPuff
{
	States
	{
		Spawn:
			Q300 A 0
			Q300 A 0 A_JumpIf(abs(z - floorz) <= 4,"HitFloor")
			Q300 A 0 A_CheckCeiling("HitCeiling")
        SpawnStart:
			Q300 A 0 A_JumpIf(waterlevel == 0, 2)
			Q300 A 0 ACS_NamedExecuteWithResult("Q3DrawLaser", 2)
			Q300 A 0 A_FaceTarget
			Q300 A 1 A_ChangeFlag("NOTIMEFREEZE",false)
			Q300 BCDEF 1
			stop

		HitFloor:
			TNT1 A 0 ACS_NamedExecuteWithResult("Q3SpawnDecal")
			TNT1 A 0 A_SetPitch(90)
			goto SpawnStart

		HitCeiling:
			TNT1 A 0 ACS_NamedExecuteWithResult("Q3SpawnDecal", 0, 1)
			TNT1 A 0 A_SetPitch(-90)
			goto SpawnStart

	}
}

Actor Q3ShotgunPuffCoop : Q3ShotgunPuff { +MTHRUSPECIES }

Actor Q3ShotgunSmoke
{
	+NOINTERACTION
	+CLIENTSIDEONLY
	Alpha 0.5
	Height 4
	Radius 2
	States
	{
		Spawn:
			Q302 A 0
			Q302 A 4 A_Fadeout(0.05)
			wait
	}
}

Actor Q3ShotgunShell : Q3MachineGunBrass {}
