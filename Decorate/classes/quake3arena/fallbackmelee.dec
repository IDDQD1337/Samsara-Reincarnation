actor SamsaraQuake3GauntletFiringSoundCheck : Boolean {}

Actor Q3Gauntlet : Q3Weapon
{
	Weapon.SlotNumber 0
	Inventory.PickupMessage "Gauntlet"
	Obituary "%o was pummeled by %k."
	Tag "Gauntlet"
	+FLOATBOB
	+MELEEWEAPON
	+WIMPY_WEAPON
	States
	{
		Spawn:
			Q399 A -1
			stop

		ReadyActual:
			Q300 A 0 A_TakeInventory("SamsaraQuake3GauntletFiringSoundCheck")
			Q300 A 1 A_WeaponReady
			loop

		Deselect:
			Q301 A 0 A_GiveInventory("SamsaraQuake3GauntletFiringSoundStopper")
			Q301 A 0 A_JumpIfHealthLower(1,"DeselectInstant")
			Q301 A 0 A_PlaySound("Quake3/Weapons/Change",CHAN_5)
			Q301 ABCDE 1
			goto DeselectInstant

		Ready: // our "Select" state
			Q302 AB 1 A_TakeInventory("SamsaraQuake3GauntletFiringSoundCheck") // map change hack
			Q302 CD 1
			goto ReadyActual

		Fire:
			Q303 A 0 A_GunFlash
			Q303 A 0 A_GiveInventory("SamsaraQuake3GauntletAttackHandler")
			Q303 ACEG 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			Q303 J 0 A_ReFire
			Q303 A 0 A_GiveInventory("SamsaraQuake3GauntletFiringSoundStopper")
			Q303 "ADGJMPSVY\" 1 A_WeaponReady
			Q304 "BEH" 2 A_WeaponReady
			Q303 "DGJMPSVY\" 2 A_WeaponReady
			Q304 "BEHK" 3 A_WeaponReady
			goto ReadyActual

		Flash:
			Q305 AB 2 Bright
			stop
	}
}

actor SamsaraQuake3GauntletAttackHandler : Trigger
{
	States
	{
	// Sound stuff
	Pickup:
		TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",2,"PickupSoundRevolution")
		TNT1 A 0 A_PlaySound("Quake3/Gauntlet/Loop",CHAN_6,1.0,true)
		goto PickupContinue
	PickupSoundRevolution:
		TNT1 A 0 A_PlaySound("Quake3Revolution/Gauntlet/Loop",CHAN_6,1.0,true)
		goto PickupContinue

	// Attack stuff
	PickupContinue:
		TNT1 A 0 A_GiveInventory("SamsaraQuake3GauntletFiringSoundCheck")
		TNT1 A 0 A_GiveInventory("Q3QuadDamageSound")
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupCoop")
	PickupDM:
		TNT1 A 0 A_Saw("","Quake3/Gauntlet/Hit",20,"Q3GauntletPuff",SF_NORANDOM|SF_NOUSEAMMO,48)
		stop
	PickupCoop:
		TNT1 A 0 A_Saw("","Quake3/Gauntlet/Hit",20,"Q3GauntletPuffCoop",SF_NORANDOM|SF_NOUSEAMMO,48)
		stop
	}
}

actor SamsaraQuake3GauntletFiringSoundStopper : Trigger
{
	States
	{
	// Sound stopping stuff
	Pickup:
		TNT1 A 0 A_JumpIfInventory("SamsaraQuake3GauntletFiringSoundCheck",1,1)
		stop
		TNT1 A 0 A_PlaySound("silence",CHAN_6)
		TNT1 A 0 A_StopSound(CHAN_6)
		TNT1 A 0 A_TakeInventory("SamsaraQuake3GauntletFiringSoundCheck")
		stop
	}
}

Actor Q3GauntletPuff
{
	+NOINTERACTION
	+NOGRAVITY
	+PUFFONACTORS
	+BLOODLESSIMPACT
	+NOTIMEFREEZE
	DamageType "Q3Bullet"
	Height 4
	Radius 2
	States
	{
		Spawn:
			TNT1 A 1
			stop

		XDeath:
			TNT1 A 0 ACS_NamedExecuteWithResult("Q3_BloodGenerator_Offsets")
			TNT1 A 0 A_GiveInventory("Q3_BloodGenerator",1,AAPTR_TRACER)
			TNT1 A 1
			stop
	}
}

Actor Q3GauntletPuffCoop : Q3GauntletPuff { +MTHRUSPECIES }
