Actor Q3Nailgun : Q3Weapon
{
	Weapon.SlotNumber 3
	Weapon.AmmoUse 5
	Weapon.AmmoGive 50
	Weapon.AmmoType "Clip"
	Inventory.PickupMessage "Nailgun"
	Obituary "%o was nailed by %k."
	Tag "Nailgun"
	+FLOATBOB
	States
	{
		Spawn:
			Q399 A -1
			stop

		ReadyActual:
			Q300 A 1 A_WeaponReady
			loop

		Deselect:
			Q301 A 0 A_JumpIfHealthLower(1,"DeselectInstant")
			Q301 A 1 A_PlaySound("Quake3/Weapons/Change",CHAN_5)
			Q301 BCDE 1
			goto DeselectInstant

		Ready: // our "Select" state
			Q302 ABCD 1
			goto ReadyActual

		Fire:
			Q303 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart")
			Q303 A 0 A_JumpIfInventory("Clip",5,"FireStart")
			goto FireDry

		FireDry:
			Q300 A 0 A_ClearReFire
			Q300 A 0 A_PlaySound("Quake3/Weapons/NoAmmo",CHAN_5)
			Q300 A 17 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			Q300 A 0 A_SelectWeapon("Q3Machinegun")
			goto Deselect

		FireStart:
			Q303 A 0 A_AlertMonsters
			Q303 A 0 A_GunFlash
			Q303 A 0 A_GiveInventory("SamsaraQuake3NailgunAttackHandler")
			Q303 ABCDEF 6 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto ReadyActual

		Flash:
			Q304 A 3 Bright
			stop
	}
}

actor SamsaraQuake3NailgunAttackHandler : Trigger
{
	States
	{
	  // Ammo checking/taking stuff
	  Pickup:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
		TNT1 A 0 A_JumpIfInventory("Clip",5,1)
		stop
		TNT1 A 0 A_TakeInventory("Clip",5)
		goto PickupAttack

	  // Attack stuff
	  PickupAttack:
		TNT1 A 0 A_PlaySound("Quake3/Nailgun/Fire",CHAN_WEAPON)
		TNT1 A 0 A_GiveInventory("Q3QuadDamageSound")
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
	  PickupAttackDM:
		TNT1 AAAAAAAAAA 0 A_FireCustomMissile("Q3NailProjectile",frandom(-5.0,5.0),false,6,0,0,frandom(-5.0,5.0))
		stop
	  PickupAttackCoop:
		TNT1 AAAAAAAAAA 0 A_FireCustomMissile("Q3NailProjectileCoop",frandom(-5.0,5.0),false,6,0,0,frandom(-5.0,5.0))
		stop
	}
}

Actor Q3NailProjectile
{
	Height 4
	Radius 2
	Speed 24
	Damage (20)
	Projectile
	Decal "Q3BulletImpact"
	+BLOODLESSIMPACT
	+HITTRACER
	States
	{
		Spawn:
			Q300 A 0 NoDelay A_PlaySound("Quake3/Nailgun/Fly",CHAN_VOICE,1.0,true)
			Q300 A 1 A_SpawnItemEx("Q3NailSmoke")
			loop

		Death:
			TNT1 A 0 A_PlaySound("Quake3/Nailgun/HitWall",CHAN_VOICE)
			TNT1 A 0 A_JumpIf(abs(z - floorz) <= 4, "HitFloor")
			TNT1 A 0 A_CheckCeiling("HitCeiling")
			goto Finish

		Crash:
			TNT1 A 0 A_PlaySound("Quake3/Nailgun/HitMetal",CHAN_VOICE)
			TNT1 A 0 A_JumpIf(abs(z - floorz) <= 4, "HitFloor")
			TNT1 A 0 A_CheckCeiling("HitCeiling")
			goto Finish

		HitFloor:
			TNT1 A 0 ACS_NamedExecuteWithResult("Q3SpawnDecal")
			TNT1 A 0 A_SetPitch(90)
			goto Finish

		HitCeiling:
			TNT1 A 0 ACS_NamedExecuteWithResult("Q3SpawnDecal", 0, 1)
			TNT1 A 0 A_SetPitch(-90)
			goto Finish

		XDeath:
			TNT1 A 0 A_PlaySound("Quake3/Nailgun/HitFlesh",CHAN_VOICE)
			TNT1 A 0 ACS_NamedExecuteWithResult("Q3_BloodGenerator_Offsets")
			TNT1 A 0 A_GiveInventory("Q3_BloodGenerator",1,AAPTR_TRACER)
			goto Finish

		Finish:
			TNT1 A 1
			stop
	}
}

Actor Q3NailProjectileCoop : Q3NailProjectile { +THRUSPECIES Species "Player" }

Actor Q3NailSmoke
{
	+NOINTERACTION
	+FORCEXYBILLBOARD
	+CLIENTSIDEONLY
	RenderStyle Add
	Alpha 1.0
	Scale 0.1
	Height 4
	Radius 2
	States
	{
		Spawn:
			TNT1 A 0 NoDelay A_JumpIf(waterlevel > 0, "Bubble")
			Q308 A 0 ACS_NamedExecuteWithResult("Q3EffectLifeCycle", 0, 50, -32, 17)
			Q308 A 100
			stop

		Bubble:
			TNT1 A 1 A_SpawnItemEx("Q3Bubble")
			stop
	}
}
