actor SamsaraQuake3HeavyMachinegunSkipFlash : Boolean {}
actor SamsaraQuake3HeavyMachinegunFiredOnce : Boolean {}

actor Q3HeavyMachineGun : Q3Weapon
{
	Weapon.SlotNumber 4
	Weapon.SlotPriority 1
	Weapon.AmmoUse 1
	Weapon.AmmoGive 20
	Weapon.AmmoType "Clip"
	Inventory.PickupMessage "Heavy Machinegun"
	Obituary "%o was ripped by %k's HMG."
	Tag "Heavy Machine Gun"
	+FLOATBOB
	States
	{
		Spawn:
			Q399 A -1
			stop

		ReadyActual:
			Q300 A 1 A_WeaponReady
			loop

		Deselect:
			Q301 A 0 A_JumpIfHealthLower(1,"DeselectInstant")
			Q301 A 1 A_PlaySound("Quake3/Weapons/Change",CHAN_5)
			Q301 BCDE 1
			goto DeselectInstant

		Ready: // our "Select" state
			Q302 ABCD 1
			goto ReadyActual

		Fire:
			Q303 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart")
			Q303 A 0 A_JumpIfInventory("Clip",1,"FireStart")
			goto FireDry

		FireDry:
			Q300 A 0 A_ClearReFire
			Q300 A 0 A_PlaySound("Quake3/Weapons/NoAmmo",CHAN_5)
			Q300 A 17 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			Q300 A 0 A_SelectWeapon("Q3Machinegun")
			goto Deselect

		FireStart:
			Q303 A 0 A_AlertMonsters
			Q303 A 0 A_GunFlash
			Q303 A 0 A_GiveInventory("SamsaraQuake3HeavyMachinegunAttackHandler")
			Q303 A 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			Q303 B 0 A_ReFire("Fire2")
			goto ReadyActual

		Fire2:
			Q303 B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"Fire2Start")
			Q303 B 0 A_JumpIfInventory("Clip",1,"Fire2Start")
			goto FireDry

		Fire2Start:
			Q303 B 0 A_AlertMonsters
			Q303 B 0 A_GunFlash("Flash2")
			Q303 B 0 A_GiveInventory("SamsaraQuake3HeavyMachinegunAttackHandler")
			Q303 B 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			Q303 C 0 A_ReFire("Fire3")
			goto ReadyActual

		Fire3:
			Q303 C 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"Fire3Start")
			Q303 C 0 A_JumpIfInventory("Clip",1,"Fire3Start")
			goto FireDry

		Fire3Start:
			Q303 C 0 A_AlertMonsters
			Q303 C 0 A_GunFlash("Flash3")
			Q303 C 0 A_GiveInventory("SamsaraQuake3HeavyMachinegunAttackHandler")
			Q303 C 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			Q303 D 0 A_ReFire("Fire4")
			goto ReadyActual

		Fire4:
			Q303 D 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"Fire4Start")
			Q303 D 0 A_JumpIfInventory("Clip",1,"Fire4Start")
			goto FireDry

		Fire4Start:
			Q303 D 0 A_AlertMonsters
			Q303 D 0 A_GunFlash("Flash4")
			Q303 D 0 A_GiveInventory("SamsaraQuake3HeavyMachinegunAttackHandler")
			Q303 D 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			Q303 E 0 A_ReFire("Fire5")
			goto ReadyActual

		Fire5:
			Q303 E 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"Fire5Start")
			Q303 E 0 A_JumpIfInventory("Clip",1,"Fire5Start")
			goto FireDry

		Fire5Start:
			Q303 E 0 A_AlertMonsters
			Q303 E 0 A_GunFlash("Flash5")
			Q303 E 0 A_GiveInventory("SamsaraQuake3HeavyMachinegunAttackHandler")
			Q303 E 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			Q303 F 0 A_ReFire("Fire6")
			goto ReadyActual

		Fire6:
			Q303 F 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"Fire6Start")
			Q303 F 0 A_JumpIfInventory("Clip",1,"Fire6Start")
			goto FireDry

		Fire6Start:
			Q303 F 0 A_AlertMonsters
			Q303 F 0 A_GunFlash("Flash6")
			Q303 F 0 A_GiveInventory("SamsaraQuake3HeavyMachinegunAttackHandler")
			Q303 F 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			Q303 A 0 A_ReFire
			goto ReadyActual

		Flash:
			TNT1 A 0 A_JumpIfInventory("SamsaraQuake3HeavyMachinegunSkipFlash",1,2)
			Q304 A 2 Bright
			Q304 B 0 Bright
			stop

		Flash2:
			TNT1 A 0 A_JumpIfInventory("SamsaraQuake3HeavyMachinegunSkipFlash",1,2)
			Q304 B 2 Bright
			Q304 C 0 Bright
			stop

		Flash3:
			TNT1 A 0 A_JumpIfInventory("SamsaraQuake3HeavyMachinegunSkipFlash",1,2)
			Q304 C 2 Bright
			Q304 D 0 Bright
			stop

		Flash4:
			TNT1 A 0 A_JumpIfInventory("SamsaraQuake3HeavyMachinegunSkipFlash",1,2)
			Q304 D 2 Bright
			Q304 E 0 Bright
			stop

		Flash5:
			TNT1 A 0 A_JumpIfInventory("SamsaraQuake3HeavyMachinegunSkipFlash",1,2)
			Q304 E 2 Bright
			Q304 F 0 Bright
			stop

		Flash6:
			TNT1 A 0 A_JumpIfInventory("SamsaraQuake3HeavyMachinegunSkipFlash",1,2)
			Q304 F 2 Bright
			Q304 A 0 Bright
			stop
	}
}

actor SamsaraQuake3HeavyMachinegunAttackHandler : Trigger
{
	States
	{
	// Ammo checking/taking stuff
	Pickup:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
		TNT1 A 0 A_JumpIfInventory("Clip",1,1)
		stop
		TNT1 A 0 A_JumpIfInventory("SamsaraQuake3HeavyMachinegunFiredOnce",0,2)
		TNT1 A 0 A_GiveInventory("SamsaraQuake3HeavyMachinegunFiredOnce")
		goto PickupAttack
		TNT1 A 0 A_TakeInventory("Clip",1)
		TNT1 A 0 A_TakeInventory("SamsaraQuake3HeavyMachinegunFiredOnce")
		goto PickupAttack

	// Attack stuff
	PickupAttack:
		TNT1 A 0 A_PlaySound("Quake3/HMG/Fire",CHAN_WEAPON)
		TNT1 A 0 A_GiveInventory("Q3QuadDamageSound")
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
	PickupAttackDM:
		TNT1 A 0 A_FireBullets(2,2,-1,16,"Q3BulletPuffThruGhost",FBF_NORANDOM|FBF_NOFLASH|FBF_NORANDOMPUFFZ)
		goto PickupFinish
	PickupAttackCoop:
		TNT1 A 0 A_FireBullets(2,2,-1,16,"Q3BulletPuffThruGhostCoop",FBF_NORANDOM|FBF_NOFLASH|FBF_NORANDOMPUFFZ)
		goto PickupFinish

	// Finishing stuff
	PickupFinish:
		TNT1 A 0 A_JumpIfInventory("SamsaraQuake3HeavyMachinegunSkipFlash",1,2)
		TNT1 A 0 A_GiveInventory("SamsaraQuake3HeavyMachinegunSkipFlash")
		stop
		TNT1 A 0 A_TakeInventory("SamsaraQuake3HeavyMachinegunSkipFlash")
		stop
	}
}
