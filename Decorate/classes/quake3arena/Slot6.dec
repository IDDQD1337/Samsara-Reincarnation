actor SamsaraQuake3PlasmaGunFireAnim : Boolean {}

Actor Q3Plasmagun : Q3Weapon
{
	Weapon.SlotNumber 6
	Weapon.AmmoUse 1
	Weapon.AmmoGive 50
	Weapon.AmmoType "Cell"
	Inventory.PickupMessage "Plasma Gun"
	Obituary "%o was melted by %k."
	Tag "Plasma Gun"
	+FLOATBOB
	States
	{
		Spawn:
			Q399 ABCDEFGH 2
			loop

		ReadyActual:
			Q300 A 0 A_GunFlash("ReadyActualGlow",GFF_NOEXTCHANGE)
			Q300 AAAAAAAA 3 A_WeaponReady
			loop

		ReadyActualGlow:
			Q305 ABCDEFGH 3 Bright
			stop

		Deselect:
			Q301 A 0 A_JumpIfHealthLower(1,"DeselectInstant")
			Q301 A 0 A_GunFlash("DeselectGlow",GFF_NOEXTCHANGE)
			Q301 A 1 A_PlaySound("Quake3/Weapons/Change",CHAN_5)
			Q301 BCDE 1
			goto Super::DeselectInstant

		DeselectGlow:
			Q306 ABCDE 1 Bright
			stop

		DeselectInstant:
			TNT1 A 0 A_GunFlash("DeselectInstantGlow",GFF_NOEXTCHANGE)
			goto Super::DeselectInstant

		DeselectInstantGlow:
			TNT1 A 0
			stop

		Ready: // our "Select" state
			Q302 A 1 A_GunFlash("ReadyGlow",GFF_NOEXTCHANGE)
			Q302 BCD 1
			goto ReadyActual

		ReadyGlow:
			Q307 ABCD 1 Bright
			stop

		Fire:
			Q303 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireInit")
			Q303 A 0 A_JumpIfInventory("Cell",1,"FireInit")
			goto FireDry

		FireDry:
			Q300 A 0 A_ClearReFire
			Q300 A 0 A_GunFlash("FireDryGlow",GFF_NOEXTCHANGE)
			Q300 A 0 A_PlaySound("Quake3/Weapons/NoAmmo",CHAN_5)
			Q300 A 17 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			Q300 A 0 A_SelectWeapon("Q3Machinegun")
			goto Deselect

		FireDryGlow:
			Q305 ABCDEFGH 3 Bright
			stop

		FireInit:
			Q303 A 0 A_JumpIfInventory("SamsaraQuake3PlasmaGunFireAnim",1,2)
			Q303 A 0 A_GiveInventory("SamsaraQuake3PlasmaGunFireAnim")
			goto FireStart
			Q303 A 0 A_TakeInventory("SamsaraQuake3PlasmaGunFireAnim")
			goto FireStart

		FireStart:
			Q303 A 0 A_AlertMonsters
			Q303 A 0 A_GunFlash
			Q303 A 0 A_GiveInventory("SamsaraQuake3PlasmaGunAttackHandler")
			Q303 A 1 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			Q303 B 0 A_JumpIfInventory("SamsaraQuake3PlasmaGunFireAnim",1,1)
			goto FireContinue
			Q303 B 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
		FireContinue:
			Q303 CD 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto ReadyActual

		Flash:
			Q304 A 1 Bright
			Q308 B 0 Bright A_JumpIfInventory("SamsaraQuake3PlasmaGunFireAnim",1,1)
			goto FlashContinue
			Q308 B 1 Bright
		FlashContinue:
			Q308 CD 1 Bright
			stop
	}
}

actor SamsaraQuake3PlasmaGunAttackHandler : Trigger
{
	States
	{
	// Ammo checking/taking stuff
	Pickup:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
		TNT1 A 0 A_JumpIfInventory("Cell",1,1)
		stop
		TNT1 A 0 A_TakeInventory("Cell",1)
		goto PickupAttack

	// Attack stuff
	PickupAttack:
		TNT1 A 0 A_PlaySound("Quake3/Plasma/Fire",CHAN_WEAPON)
		TNT1 A 0 A_GiveInventory("Q3QuadDamageSound")
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
	PickupAttackDM:
		TNT1 A 0 A_FireCustomMissile("Q3PlasmaProjectile",0,false,6)
		stop
	PickupAttackCoop:
		TNT1 A 0 A_FireCustomMissile("Q3PlasmaProjectileCoop",0,false,6)
		stop
	}
}

Actor Q3PlasmaProjectile
{
	Height 12
	Radius 6
	Damage (20)
	Speed 32
	Scale 0.33
	Renderstyle Add
	DamageType "Q3Energy"
	Decal "Q3PlasmaImpact"
	Projectile
	+FORCEXYBILLBOARD
	+HITTRACER
	+BRIGHT
	States
	{
		Spawn:
			Q306 A 0
			Q306 A 0 A_PlaySound("Quake3/Plasma/Loop",CHAN_VOICE,1.0,true)
			Q306 A 1
			wait

		XDeath:
			TNT1 A 0 ACS_NamedExecuteWithResult("Q3_BloodGenerator_Offsets")
			TNT1 A 0 A_GiveInventory("Q3_BloodGenerator",1,AAPTR_TRACER)
		Death:
			TNT1 A 0 A_PlaySound("Quake3/Plasma/Explode",CHAN_VOICE)
			TNT1 A 0 A_Explode(14,14)
			TNT1 A 0 A_JumpIf(abs(z - floorz) <= 4, "HitFloor")
			TNT1 A 0 A_CheckCeiling("HitCeiling")
			TNT1 A 0 A_JumpIfCloser(64,1)
			goto Finish
			TNT1 A 0 A_JumpIf(CallACS("Q3_Decorate", 2) == 1, 1)
			goto Finish
			TNT1 A 0 A_GiveToTarget("Q3PlasmaClimber")
			goto Finish

		HitFloor:
			TNT1 A 0 ACS_NamedExecuteWithResult("Q3SpawnDecal", 3)
			TNT1 A 0 A_SetPitch(90)
			goto Finish

		HitCeiling:
			TNT1 A 0 ACS_NamedExecuteWithResult("Q3SpawnDecal", 3, 1)
			TNT1 A 0 A_SetPitch(-90)
			goto Finish

		Finish:
			Q306 A 0 ACS_NamedExecuteWithResult("Q3EffectLifeCycle", 0, 25, -50, 16)
			Q306 A 100
			stop
	}
}

Actor Q3PlasmaProjectileCoop : Q3PlasmaProjectile { +THRUSPECIES Species "Player" }

Actor Q3PlasmaProjectileSoundRevolution : Q3PlasmaProjectile
{
	States
	{
		XDeath:
			TNT1 A 0 ACS_NamedExecuteWithResult("Q3_BloodGenerator_Offsets")
			TNT1 A 0 A_GiveInventory("Q3_BloodGenerator",1,AAPTR_TRACER)
		Death:
			TNT1 A 0 A_PlaySound("Quake3Revolution/Plasma/Explode",CHAN_VOICE)
			TNT1 A 0 A_Explode(14,14)
			TNT1 A 0 A_JumpIf(abs(z - floorz) <= 4, "HitFloor")
			TNT1 A 0 A_CheckCeiling("HitCeiling")
			TNT1 A 0 A_JumpIfCloser(64,1)
			goto Finish
			TNT1 A 0 A_JumpIf(CallACS("Q3_Decorate", 2) == 1, 1)
			goto Finish
			TNT1 A 0 A_GiveToTarget("Q3PlasmaClimber")
			goto Finish
	}
}

Actor Q3PlasmaProjectileCoopSoundRevolution : Q3PlasmaProjectileCoop
{
	States
	{
		XDeath:
			TNT1 A 0 ACS_NamedExecuteWithResult("Q3_BloodGenerator_Offsets")
			TNT1 A 0 A_GiveInventory("Q3_BloodGenerator",1,AAPTR_TRACER)
		Death:
			TNT1 A 0 A_PlaySound("Quake3Revolution/Plasma/Explode",CHAN_VOICE)
			TNT1 A 0 A_Explode(14,14)
			TNT1 A 0 A_JumpIf(abs(z - floorz) <= 4, "HitFloor")
			TNT1 A 0 A_CheckCeiling("HitCeiling")
			TNT1 A 0 A_JumpIfCloser(64,1)
			goto Finish
			TNT1 A 0 A_JumpIf(CallACS("Q3_Decorate", 2) == 1, 1)
			goto Finish
			TNT1 A 0 A_GiveToTarget("Q3PlasmaClimber")
			goto Finish
	}
}

actor Q3PlasmaClimber : Trigger
{
	States
	{
	Pickup:
		TNT1 A 0 A_JumpIf(CallACS("Q3_Decorate", 1) == 1, 1)
		stop
		TNT1 A 0 ThrustThingZ(0,4,0,1)
		stop
	}
}
