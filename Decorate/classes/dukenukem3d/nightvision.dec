actor DukeVision: Boolean {}
actor DukeVisionFuel: Counter { Inventory.MaxAmount 100 }

actor DukePortNiVi : CustomInventory
{
    Inventory.UseSound ""
    Inventory.Icon "ARTINIVI"
    Inventory.MaxAmount 1
    Inventory.InterHubAmount 1
    Inventory.RestrictedTo "DukePlayer"
    Tag "Night Vision Goggles"
    +INVBAR
    +UNDROPPABLE
    States
    {
      Use:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteInventory",1,"UseActivateCheck")
        TNT1 A 0 A_JumpIfInventory("DukeVisionFuel",1,"UseActivateCheck")
        fail

      UseActivateCheck:
        TNT1 A 0 A_JumpIfInventory("DukeVision",1,"UseDeactivate")
      UseActivate:
        TNT1 A 0 A_PlaySound("duke/nightvision",CHAN_ITEM)
        TNT1 A 0 A_GiveInventory("DukeVision")
        TNT1 A 0 A_GiveInventory("DukeNightVision")
        TNT1 A 0 ACS_NamedExecuteAlways("DukeResourceDrain", 0, 2)
        fail

      UseDeactivate:
        TNT1 A 0 A_PlaySound("duke/nightvision",CHAN_ITEM)
        TNT1 A 0 A_TakeInventory("DukeVision")
        TNT1 A 0 A_TakeInventory("PowerLightAmp")
        fail
    }
}

actor DukePortNiViLAB : DukePortNiVi
{
    Inventory.Icon "ARTINIV2"
    Tag "Sunglasses"
}

actor DukePortNiVi64 : DukePortNiVi { Inventory.Icon "ARTINIV3" }

actor DukeNightVision : Infrared { Powerup.Duration 0x7FFFFFFF }
