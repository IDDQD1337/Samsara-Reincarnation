actor DukeSteroidsActive : Boolean {}
actor DukeSteroidsSpawnRemove : Boolean {}
actor DukeSteroidsFuel : Counter { Inventory.MaxAmount 100 }

actor DukePortSteroids : CustomInventory
{
    tag "Steroids"
    +INVBAR
    +INVENTORY.UNDROPPABLE
    Inventory.Icon ARTISTER
    +inventory.hubpower
    Inventory.PickupSound "duke/itemget"
    Inventory.PickupMessage "You got the Steroids!"
    Inventory.UseSound ""
    Inventory.RestrictedTo "DukePlayer"
    Inventory.MaxAmount 1
    States
    {
    Spawn:
        PSTB C -1
        Stop

    Use:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteInventory",1,"CanStartFlying")
        TNT1 A 0 A_JumpIfInventory("DukeSteroidsActive",1,"UseFail")
        TNT1 A 0 A_JumpIfInventory("DukeSteroidsFuel",1,"CanStartFlying")
        fail

    CanStartFlying:
		TNT1 A 0 A_JumpIfInventory("DukeSteroidsActive",1,"IsFlying")

	NotFlying:
        TNT1 A 0 A_JumpIfInventory("DukeBallgag",1,2)
        TNT1 A 0 A_PlaySoundEx("duke/steroidsuse","item",0,0)
        TNT1 A 0 A_GiveInventory("DukeSteroidsActive",1)
        TNT1 A 0 A_GiveInventory("DukeSteroidsSpawnRemove",1)
        TNT1 A 0 A_GiveInventory("DukeSteroidsSpeed",1)
        TNT1 A 0 A_GiveInventory("DukeSteroidsDamage",1)
        TNT1 A 0 ACS_NamedExecuteWithResult("DukeSteroidsSoundLoop")
        TNT1 A 1 ACS_NamedExecuteAlways("DukeResourceDrain",0,3)
        fail

	IsFlying:
        TNT1 A 0 A_TakeInventory("DukeSteroidsActive")
        TNT1 A 0 A_TakeInventory("DukeSteroidsSpawnRemove")
        TNT1 A 0 A_TakeInventory("DukeSteroidsSpeed")
        TNT1 A 0 A_TakeInventory("DukeSteroidsDamage")
        fail

	UseFail:
		TNT1 A 0
		fail
    }
}

actor DukePortSteroidsLAB : DukePortSteroids
{
    Inventory.Icon "ARTISTE2"
    Tag "Hot Sauce"
}

actor DukePortSteroids64 : DukePortSteroids
{
    //Inventory.Icon "ARTISTE3"
    Tag "Vitamin X"
}

actor DukeSteroidsSpeed : PowerSpeed
{
    Inventory.Icon ""
    Powerup.Duration 0x7FFFFFFF
}

actor DukeSteroidsDamage : PowerDamage
{
    Powerup.Duration 0x7FFFFFFF
    DamageFactor "MightyBoot", 4
}
