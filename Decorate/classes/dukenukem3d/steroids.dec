actor DukeSteroidsActive : Boolean {}
actor DukeSteroidsSpawnRemove : Boolean {}
actor DukeSteroidsFuel : Counter { Inventory.MaxAmount 100 }

actor DukePortSteroids : CustomInventory
{
    Inventory.UseSound ""
    Inventory.Icon "ARTISTER"
    Inventory.MaxAmount 1
    Inventory.InterHubAmount 1
    Inventory.RestrictedTo "DukePlayer"
    Tag "Steroids"
    +INVBAR
    +UNDROPPABLE
    States
    {
      Use:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteInventory",1,"UseActivateCheck")
        TNT1 A 0 A_JumpIfInventory("DukeSteroidsActive",1,"UseFail")
        TNT1 A 0 A_JumpIfInventory("DukeSteroidsFuel",1,"UseActivateCheck")
        fail

      UseActivateCheck:
        TNT1 A 0 A_JumpIfInventory("DukeSteroidsActive",1,"UseDeactivate")
      UseActivate:
        TNT1 A 0 A_JumpIfInventory("DukeBallgag",1,2)
        TNT1 A 0 A_PlaySound("duke/steroidsuse",CHAN_ITEM)
        TNT1 A 0 A_GiveInventory("DukeSteroidsActive")
        TNT1 A 0 A_GiveInventory("DukeSteroidsSpawnRemove")
        TNT1 A 0 A_GiveInventory("DukeSteroidsSpeed")
        TNT1 A 0 A_GiveInventory("DukeSteroidsDamage")
        TNT1 A 0 ACS_NamedExecuteWithResult("DukeSteroidsSoundLoop")
        TNT1 A 0 ACS_NamedExecuteAlways("DukeResourceDrain", 0, 3)
        fail

      UseDeactivate:
        TNT1 A 0 A_TakeInventory("DukeSteroidsActive")
        TNT1 A 0 A_TakeInventory("DukeSteroidsSpawnRemove")
        TNT1 A 0 A_TakeInventory("DukeSteroidsSpeed")
        TNT1 A 0 A_TakeInventory("DukeSteroidsDamage")
        fail

      UseFail:
        TNT1 A 0
        fail
    }
}

actor DukePortSteroidsLAB : DukePortSteroids
{
    Inventory.Icon "ARTISTE2"
    Tag "Hot Sauce"
}

actor DukePortSteroids64 : DukePortSteroids
{
    Inventory.Icon "ARTISTE3"
    Tag "Vitamin X"
}

actor DukeSteroidsSpeed : PowerSpeed
{
    Inventory.Icon ""
    Powerup.Duration 0x7FFFFFFF
}

actor DukeSteroidsDamage : PowerDamage
{
    Powerup.Duration 0x7FFFFFFF
    DamageFactor "MightyBoot", 4
}
