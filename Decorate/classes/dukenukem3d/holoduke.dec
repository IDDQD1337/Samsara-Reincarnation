// The Holoduke!

actor HolodukeCooldown : Counter {}

actor ItemHoloduke : CustomInventory
{
    Inventory.PickupMessage "HoloDuke"
    Inventory.PickupSound "duke/itemget"
    Inventory.UseSound ""
    Inventory.Icon "HOLOICON"
    Inventory.MaxAmount 3
    Inventory.InterHubAmount 3
    Inventory.RestrictedTo "DukePlayer"
    Tag "HoloDuke"
    +INVBAR
    +UNDROPPABLE
    States
    {
      Use:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasDisabledInventoryCooldown",1,2)
        TNT1 A 0 A_JumpIfInventory("HolodukeCooldown",1,"UseFailCooldown")
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"UseCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"UseCoop")
      UseDM:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasDisabledInventoryCooldown",1,3)
        TNT1 A 0 A_GiveInventory("HolodukeCooldown",5)
        TNT1 A 0 ACS_NamedExecuteAlways("HolodukeCooldown")
        TNT1 A 0 A_GiveInventory("SamsaraDukeHoloDukeSpawner")
        stop

      UseCoop:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasDisabledInventoryCooldown",1,3)
        TNT1 A 0 A_GiveInventory("HolodukeCooldown",10)
        TNT1 A 0 ACS_NamedExecuteAlways("HolodukeCooldown")
        TNT1 A 0 A_GiveInventory("SamsaraDukeHoloDukeSpawner")
        stop

      UseFailCooldown:
        TNT1 A 0 ACS_NamedExecuteAlways("SamsaraClientDecorate", 0, 17, 6)
        fail
    }
}

actor SamsaraDukeHoloDukeSpawner : Trigger
{
    States
    {
      // Spawner stuff
      // Original/Duke Nukem 64
      Pickup:
        TNT1 A 0 A_JumpIfInventory("DukeClassMode",2,"PickupOriginal")
        TNT1 A 0 A_JumpIfInventory("DukeClassMode",1,"PickupLAB")
      PickupOriginal:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupOriginalCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupOriginalCoop")
      PickupOriginalDM:
        TNT1 A 0 A_SpawnItemEx("HoloDukeDM",32,0,8,0,0,0,0,SXF_TRANSFERTRANSLATION)
        stop

      PickupOriginalCoop:
        TNT1 A 0 A_SpawnItemEx("HoloDuke",32,0,8,0,0,0,0,SXF_TRANSFERTRANSLATION)
        stop

      // Life's A Beach
      PickupLAB:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupLABCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupLABCoop")
      PickupLABDM:
        TNT1 A 0 A_SpawnItemEx("HoloDukeLABDM",32,0,8,0,0,0,0,SXF_TRANSFERTRANSLATION)
        stop

      PickupLABCoop:
        TNT1 A 0 A_SpawnItemEx("HoloDukeLAB",32,0,8,0,0,0,0,SXF_TRANSFERTRANSLATION)
        stop
    }
}

// Original/Duke Nukem 64

ACTOR Holoduke
{
    Radius 20
    Height 56
    Speed 8
    Scale 0.725
    Tag "HoloDuke"
    +FLOORCLIP
    +SHOOTABLE
    +FRIENDLY
    +INVULNERABLE
    +NODAMAGE
    +NOBLOCKMAP
    -SOLID
    +NONSHOOTABLE
    var int user_seconds;
    States
    {
    Spawn:
        DUKE E 0
        DUKE E 0 A_PlaySound("duke/holoduke1")
        DUKE E 0 Thing_ChangeTID (0, 3333)
        DUKE E 0 A_RadiusGive ("ForgetAboutDre", 2048, RGF_MONSTERS, 1)
        DUKE E 2 A_SetScale (0.1)
        DUKE E 2 A_SetScale (0.3,0.2)
        DUKE E 2 A_SetScale (0.4,0.5)
        DUKE E 2 A_SetScale (0.6,0.8)
        DUKE E 2 A_SetScale (0.8,0.7)
        DUKE E 2 A_SetScale (0.725,0.725)
        goto Active
    Active:
        DUKE E 0 A_AlertMonsters(0, AMF_TARGETEMITTER)
        DUKE EEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEE 1 A_SetTranslucent(frandom(0.5,1.0),0)
        DUKE E 0 A_SetUserVar(user_seconds, (user_seconds + 1))
        DUKE E 0 A_JumpIf(user_seconds >= 10, "End")
        Loop
    End:
        DUKE E 0 A_PlaySound("duke/holoduke2")
        DUKE E 2 A_SetScale (0.6,0.725)
        DUKE E 2 A_SetScale (0.5,0.725)
        DUKE E 2 A_SetScale (0.3,0.725)
        DUKE E 2 A_SetScale (0.1,0.725)
        Stop
    }
}

ACTOR HolodukeDM
{
    Radius 20
    Height 20
    Speed 0
    Scale 0.725
    Tag "HoloDuke"
    +FLOORCLIP
    -SOLID
    PROJECTILE
    -NOGRAVITY
    +NOEXPLODEFLOOR
    +STEPMISSILE
    States
    {
    Spawn:
        DUKE E 0
        DUKE E 0 A_PlaySound("duke/holoduke1")
        DUKE E 2 A_SetScale (0.1)
        DUKE E 2 A_SetScale (0.3,0.2)
        DUKE E 2 A_SetScale (0.4,0.5)
        DUKE E 2 A_SetScale (0.6,0.8)
        DUKE E 2 A_SetScale (0.8,0.7)
        DUKE E 2 A_SetScale (0.725,0.725)
        goto Active
    Active:
        DUKE A 0 ThrustThing(angle*256/360, 4, 0, 0)
        DUKE AAAA 1 A_SetTranslucent(frandom(0.5,1.0),0)
        DUKE B 0 ThrustThing(angle*256/360, 4, 0, 0)
        DUKE BBBB 1 A_SetTranslucent(frandom(0.5,1.0),0)
        DUKE C 0 ThrustThing(angle*256/360, 4, 0, 0)
        DUKE CCCC 1 A_SetTranslucent(frandom(0.5,1.0),0)
        DUKE D 0 ThrustThing(angle*256/360, 4, 0, 0)
        DUKE DDDD 1 A_SetTranslucent(frandom(0.5,1.0),0)
        Loop
    Death:
        DUKE E 0 A_PlaySound("duke/holoduke2")
        DUKE E 2 A_SetScale (0.6,0.725)
        DUKE E 2 A_SetScale (0.5,0.725)
        DUKE E 2 A_SetScale (0.3,0.725)
        DUKE E 2 A_SetScale (0.1,0.725)
        Stop
    }
}

actor ForgetAboutDre : Trigger
{
    States
    {
      Pickup:
        //TNT1 A 0 A_ClearTarget
        TNT1 A 0 Thing_Hate(0,3333,0)
        stop
    }
}

// Life's A Beach

ACTOR HolodukeLAB : Holoduke
{
    States
    {
    Spawn:
        DUKE M 0
        DUKE M 0 A_PlaySound("duke/holoduke1")
        DUKE M 0 Thing_ChangeTID (0, 3333)
        DUKE M 0 A_RadiusGive ("ForgetAboutDre", 2048, RGF_MONSTERS, 1)
        DUKE M 2 A_SetScale (0.1)
        DUKE M 2 A_SetScale (0.3,0.2)
        DUKE M 2 A_SetScale (0.4,0.5)
        DUKE M 2 A_SetScale (0.6,0.8)
        DUKE M 2 A_SetScale (0.8,0.7)
        DUKE M 2 A_SetScale (0.725,0.725)
        goto Active
    Active:
        DUKE M 0 A_AlertMonsters(0, AMF_TARGETEMITTER)
        DUKE MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM 1 A_SetTranslucent(frandom(0.5,1.0),0)
        DUKE M 0 A_SetUserVar(user_seconds, (user_seconds + 1))
        DUKE M 0 A_JumpIf(user_seconds >= 10, "End")
        Loop
    End:
        DUKE M 0 A_PlaySound("duke/holoduke2")
        DUKE M 2 A_SetScale (0.6,0.725)
        DUKE M 2 A_SetScale (0.5,0.725)
        DUKE M 2 A_SetScale (0.3,0.725)
        DUKE M 2 A_SetScale (0.1,0.725)
        Stop
    }
}

ACTOR HolodukeLABDM : HolodukeDM
{
    Radius 20
    Height 20
    Speed 0
    Scale 0.725
    +FLOORCLIP
    -SOLID
    PROJECTILE
    -NOGRAVITY
    +NOEXPLODEFLOOR
    +STEPMISSILE
    States
    {
    Spawn:
        DUKE M 0
        DUKE M 0 A_PlaySound("duke/holoduke1")
        DUKE M 2 A_SetScale (0.1)
        DUKE M 2 A_SetScale (0.3,0.2)
        DUKE M 2 A_SetScale (0.4,0.5)
        DUKE M 2 A_SetScale (0.6,0.8)
        DUKE M 2 A_SetScale (0.8,0.7)
        DUKE M 2 A_SetScale (0.725,0.725)
        goto Active
    Active:
        DUKE I 0 ThrustThing(angle*256/360, 4, 0, 0)
        DUKE IIII 1 A_SetTranslucent(frandom(0.5,1.0),0)
        DUKE J 0 ThrustThing(angle*256/360, 4, 0, 0)
        DUKE JJJJ 1 A_SetTranslucent(frandom(0.5,1.0),0)
        DUKE K 0 ThrustThing(angle*256/360, 4, 0, 0)
        DUKE KKKK 1 A_SetTranslucent(frandom(0.5,1.0),0)
        DUKE L 0 ThrustThing(angle*256/360, 4, 0, 0)
        DUKE LLLL 1 A_SetTranslucent(frandom(0.5,1.0),0)
        Loop
    Death:
        DUKE M 0 A_PlaySound("duke/holoduke2")
        DUKE M 2 A_SetScale (0.6,0.725)
        DUKE M 2 A_SetScale (0.5,0.725)
        DUKE M 2 A_SetScale (0.3,0.725)
        DUKE M 2 A_SetScale (0.1,0.725)
        Stop
    }
}
