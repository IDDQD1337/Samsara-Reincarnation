actor DukeFlying: Boolean {}
actor DukeJPMotor: Boolean {}
actor DukeJetpackFuel: Counter { Inventory.MaxAmount 100 }

actor DukePortJetpack : CustomInventory
{
	tag "Jetpack"
	+INVBAR
	+INVENTORY.UNDROPPABLE
	Inventory.Icon ARTIJETP
	+inventory.hubpower
	Inventory.PickupSound "duke/itemget"
	Inventory.PickupMessage "You got the Jetpack!"
	Inventory.UseSound ""
	Inventory.MaxAmount 1
	Inventory.RestrictedTo "DukePlayer"
	States
	{
	Spawn:
		DKJT A -1
		Stop

	Use:
		TNT1 A 0 A_JumpIfInventory("PowerFlight",1,"UseFail")
		TNT1 A 0 A_JumpIfInventory("PowerFlight2",1,"UseFail")
        TNT1 A 0 A_JumpIfInventory("SamsaraPowerFly",1,"UseFail")
        TNT1 A 0 A_JumpIfInventory("SamsaraPowerFlyForHexen",1,"UseFail")
		TNT1 A 0 A_JumpIfInventory("DukeJetpackFuel",1,"CanStartFlying")
		TNT1 A 1 ACS_NamedExecuteAlways("SamsaraClientDecorate",0,5)
		fail

	CanStartFlying:
		TNT1 A 0 A_JumpIfInventory("DukeFlying",1,"IsFlying")

	NotFlying:
		TNT1 A 0 A_PlaySoundEx("duke/jetpack","item",0,0)
		TNT1 A 0 A_GiveInventory("DukeFlying", 1)
		TNT1 A 0 ThrustThingZ(0,32,0,0)
		TNT1 A 0 A_GiveInventory("DukePetjack", 1)
		TNT1 A 1 ACS_NamedExecuteAlways("DukeResourceDrain",0,1)
		fail

	IsFlying:
		TNT1 A 0 A_PlaySoundEx("duke/jetpackoff","item",0,0)
		TNT1 A 0 A_GiveInventory("DukeJetPackIdleRemover", 1)
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteInventory", 1, "IsFlyingInfiniteInventory")
		TNT1 A 1 A_TakeInventory("DukeFlying", 1)
		TNT1 A 1 A_TakeInventory("DukeJetpackFuel", 1)
		TNT1 A 1 A_TakeInventory("DukePetjack", 1)
		TNT1 A 15
		fail
	
	IsFlyingInfiniteInventory:
		TNT1 A 1 A_TakeInventory("DukeFlying", 1)
		TNT1 A 1 A_TakeInventory("DukePetjack", 1)
		TNT1 A 15
		fail

	UseFail:
		TNT1 A 0 ACS_NamedExecuteAlways("SamsaraClientDecorate",0,7)
		fail
	}
}

actor DukeJetPackIdleRemover : CustomInventory
{
	+INVENTORY.AUTOACTIVATE
	States
	{
		Pickup:
			TNT1 A 0
			TNT1 A 0 A_StopSound(CHAN_BODY)
			stop
	}
}

actor DukePetjack : PowerFlight
{
    Inventory.Icon "TNT1A0"
	Powerup.Duration 0x7FFFFFFD
	Powerup.Color "Yellow", 0.05
}
