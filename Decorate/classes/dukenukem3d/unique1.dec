actor DukePortJetpackGiven : Boolean {}
actor DukeFlying: Boolean {}
actor DukeJPMotor: Boolean {}
actor DukeJetpackFuel: Counter { Inventory.MaxAmount 100 }

actor DukePortJetpack : CustomInventory
{
	tag "Jetpack"
	+INVBAR
	+INVENTORY.UNDROPPABLE
	Inventory.Icon ARTIJETP
	+inventory.hubpower
	Inventory.PickupSound "duke/itemget"
	Inventory.PickupMessage "You got the Jetpack!"
	Inventory.UseSound ""
	Inventory.MaxAmount 1
	Inventory.RestrictedTo "DukePlayer"
	States
	{
	Spawn:
		DKJT A -1
		Stop

	Use:
		TNT1 A 0 A_JumpIfInventory("PowerFlight",1,"UseFail")
        TNT1 A 0 A_JumpIfInventory("PowerSamsaraFlight",1,"UseFail")
		TNT1 A 0 A_JumpIfInventory("DukeJetpackFuel",1,"CanStartFlying")
		TNT1 A 1 ACS_NamedExecuteAlways("SamsaraClientDecorate",0,5)
		fail

	CanStartFlying:
		TNT1 A 0 A_JumpIfInventory("DukeFlying",1,"IsFlying")

	NotFlying:
		TNT1 A 0 A_PlaySound("duke/jetpack",CHAN_ITEM)
		TNT1 A 0 A_GiveInventory("DukeFlying")
		TNT1 A 0 ThrustThingZ(0,32,0,0)
		TNT1 A 0 A_GiveInventory("DukePetjack")
		TNT1 A 0 ACS_NamedExecuteAlways("DukeResourceDrain",0,1)
		fail

	IsFlying:
		TNT1 A 0 A_PlaySound("duke/jetpackoff",CHAN_ITEM)
		TNT1 A 0 A_GiveInventory("DukeJetPackIdleRemover")
		TNT1 A 0 A_TakeInventory("DukeFlying")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteInventory",1,2)
		TNT1 A 0 A_TakeInventory("DukeJetpackFuel",1)
		TNT1 A 0 A_TakeInventory("DukePetjack")
		fail

	UseFail:
		TNT1 A 0 ACS_NamedExecuteAlways("SamsaraClientDecorate",0,7)
		fail
	}
}

actor DukePortJetpack64 : DukePortJetpack { Inventory.Icon "ARTIJET2" }

actor DukeJetPackIdleRemover : CustomInventory
{
	+INVENTORY.AUTOACTIVATE
	States
	{
		Pickup:
			TNT1 A 0
			TNT1 A 0 A_StopSound(CHAN_BODY)
			stop
	}
}

actor DukePetjack : PowerFlight
{
    Inventory.Icon "TNT1A0" // can't use ""
	Powerup.Duration 0x7FFFFFFD
	Powerup.Color "Yellow", 0.05
}
