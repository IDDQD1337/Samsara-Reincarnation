// Token

actor SamsaraChexGigazorcherFiringLastShot : Boolean {}

// Weapon

actor "Gigazorcher 2100" : Weapon
{
    Weapon.SelectionOrder 100
    Weapon.SlotNumber 6
    Inventory.PickUpSound "chex/weaponget"
    Weapon.AmmoType1 "ChexGigazorcherClip"
    Weapon.AmmoUse1 1
    Weapon.AmmoType2 "Cell"
    Weapon.AmmoGive2 40
    +AMMO_OPTIONAL
    +BLOODLESSIMPACT
    +NOALERT
    +PUFFONACTORS
    +UNDROPPABLE
    Inventory.PickupMessage "You got the Phasing Zorcher!"
	Obituary "$SAMSARA_CHEX_OB_SLOT6S"
	Inventory.RestrictedTo "ChexWarrior"
    Tag "Gigazorcher 2100"
    States
    {
      Spawn:
        PLAC B -1
        stop

      Ready:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"ReadyReloadable")
      ReadyNonReloadable:
        CHIZ A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReadyStart")
        CHIZ A 0 A_JumpIfInventory("Cell",40,"ReadyStart")
        CHIZ D 0 A_JumpIfInventory("Cell",30,"ReadyStart")
        CHIZ G 0 A_JumpIfInventory("Cell",20,"ReadyStart")
        CHIZ J 0 A_JumpIfInventory("Cell",10,"ReadyStart")
        CHIZ M 0
        goto ReadyStart

      ReadyReloadable:
        CHIZ A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"ReadyStart")
        CHIZ A 0 A_JumpIfInventory("ChexGigazorcherClip",4,"ReadyStart")
        CHIZ D 0 A_JumpIfInventory("ChexGigazorcherClip",3,"ReadyStart")
        CHIZ G 0 A_JumpIfInventory("ChexGigazorcherClip",2,"ReadyStart")
        CHIZ J 0 A_JumpIfInventory("ChexGigazorcherClip",1,"ReadyStart")
        CHIZ M 0
        goto ReadyStart

      ReadyStart:
        "####" "#" 0 A_JumpIfInventory("SamsaraReloadMode",0,"ReadyAllowReload")
      ReadyDisallowReload:
        "####" "#" 1 A_WeaponReady
        goto Ready

      ReadyAllowReload:
        "####" "#" 0 A_JumpIfInventory("ChexGigazorcherClip",0,"ReadyDisallowReload")
        "####" "#" 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        "####" "#" 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        "####" "#" 0 A_JumpIfInventory("Cell",10,1)
        goto ReadyDisallowReload
        "####" "#" 1 A_WeaponReady(WRF_ALLOWRELOAD)
        goto Ready

      Deselect:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"DeselectReloadable")
      DeselectNonReloadable:
        CHIZ A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"DeselectStart")
        CHIZ A 0 A_JumpIfInventory("Cell",40,"DeselectStart")
        CHIZ D 0 A_JumpIfInventory("Cell",30,"DeselectStart")
        CHIZ G 0 A_JumpIfInventory("Cell",20,"DeselectStart")
        CHIZ J 0 A_JumpIfInventory("Cell",10,"DeselectStart")
        CHIZ M 0
        goto DeselectStart

      DeselectReloadable:
        CHIZ A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"DeselectStart")
        CHIZ A 0 A_JumpIfInventory("ChexGigazorcherClip",4,"DeselectStart")
        CHIZ D 0 A_JumpIfInventory("ChexGigazorcherClip",3,"DeselectStart")
        CHIZ G 0 A_JumpIfInventory("ChexGigazorcherClip",2,"DeselectStart")
        CHIZ J 0 A_JumpIfInventory("ChexGigazorcherClip",1,"DeselectStart")
        CHIZ M 0
        goto DeselectStart

      DeselectStart:
        "####" "#" 1 A_Lower
        goto Deselect

      Select:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"SelectReloadable")
      SelectNonReloadable:
        CHIZ A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"SelectStart")
        CHIZ A 0 A_JumpIfInventory("Cell",40,"SelectStart")
        CHIZ D 0 A_JumpIfInventory("Cell",30,"SelectStart")
        CHIZ G 0 A_JumpIfInventory("Cell",20,"SelectStart")
        CHIZ J 0 A_JumpIfInventory("Cell",10,"SelectStart")
        CHIZ M 0
        goto SelectStart

      SelectReloadable:
        CHIZ A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"SelectStart")
        CHIZ A 0 A_JumpIfInventory("ChexGigazorcherClip",4,"SelectStart")
        CHIZ D 0 A_JumpIfInventory("ChexGigazorcherClip",3,"SelectStart")
        CHIZ G 0 A_JumpIfInventory("ChexGigazorcherClip",2,"SelectStart")
        CHIZ J 0 A_JumpIfInventory("ChexGigazorcherClip",1,"SelectStart")
        CHIZ M 0
        goto SelectStart

      SelectStart:
        "####" "#" 1 A_Raise
        goto Select

      Fire:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"FireReloadable")
      FireNonReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart1")
        TNT1 A 0 A_JumpIfInventory("Cell",40,"FireStart1")
        TNT1 A 0 A_JumpIfInventory("Cell",30,"FireStart2")
        TNT1 A 0 A_JumpIfInventory("Cell",20,"FireStart3")
        TNT1 A 0 A_JumpIfInventory("Cell",10,"FireStart4")
        goto NoAmmoSwitch

      FireReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireStart1")
        TNT1 A 0 A_JumpIfInventory("ChexGigazorcherClip",4,"FireStart1")
        TNT1 A 0 A_JumpIfInventory("ChexGigazorcherClip",3,"FireStart2")
        TNT1 A 0 A_JumpIfInventory("ChexGigazorcherClip",2,"FireStart3")
        TNT1 A 0 A_JumpIfInventory("ChexGigazorcherClip",1,"FireStart4")
      FireReloadableDryCheck:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"Reload")
        TNT1 A 0 A_JumpIfInventory("Cell",10,"Reload")
        goto NoAmmoSwitch

      FireStart1:
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_GunFlash("Flash",GFF_NOEXTCHANGE)
        TNT1 A 0 A_TakeInventory("SamsaraChexGigazorcherFiringLastShot")
        CHIZ B 4 Bright A_GiveInventory("SamsaraChexGigazorcherAttackHandler")
        CHIZ C 4 Bright
        CHIZ T 4
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",2,1)
        goto FireFinish1
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireFinish1")
        TNT1 A 0 A_JumpIfInventory("ChexGigazorcherClip",1,"FireFinish1")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReloadAfterFire")
        TNT1 A 0 A_JumpIfInventory("Cell",10,"ReloadAfterFire")
        goto FireFinish1

      FireFinish1:
        CHIZ D 6
        goto FireFinishReFireAmmoCheck

      FireStart2:
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_GunFlash("Flash",GFF_NOEXTCHANGE)
        TNT1 A 0 A_TakeInventory("SamsaraChexGigazorcherFiringLastShot")
        CHIZ E 4 Bright A_GiveInventory("SamsaraChexGigazorcherAttackHandler")
        CHIZ F 4 Bright
        CHIZ U 4
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",2,1)
        goto FireFinish2
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireFinish2")
        TNT1 A 0 A_JumpIfInventory("ChexGigazorcherClip",1,"FireFinish2")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReloadAfterFire")
        TNT1 A 0 A_JumpIfInventory("Cell",10,"ReloadAfterFire")
        goto FireFinish2

      FireFinish2:
        CHIZ G 6
        goto FireFinishReFireAmmoCheck

      FireStart3:
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_GunFlash("Flash",GFF_NOEXTCHANGE)
        TNT1 A 0 A_TakeInventory("SamsaraChexGigazorcherFiringLastShot")
        CHIZ H 4 Bright A_GiveInventory("SamsaraChexGigazorcherAttackHandler")
        CHIZ I 4 Bright
        CHIZ V 4
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",2,1)
        goto FireFinish3
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireFinish3")
        TNT1 A 0 A_JumpIfInventory("ChexGigazorcherClip",1,"FireFinish3")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReloadAfterFire")
        TNT1 A 0 A_JumpIfInventory("Cell",10,"ReloadAfterFire")
        goto FireFinish3

      FireFinish3:
        CHIZ J 6
        goto FireFinishReFireAmmoCheck

      FireStart4:
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_GunFlash("Flash",GFF_NOEXTCHANGE)
        TNT1 A 0 A_GiveInventory("SamsaraChexGigazorcherFiringLastShot")
        CHIZ K 4 Bright A_GiveInventory("SamsaraChexGigazorcherAttackHandler")
        CHIZ L 4 Bright
        CHIZ M 4
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",2,1)
        goto FireFinish4
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireFinish4")
        TNT1 A 0 A_JumpIfInventory("ChexGigazorcherClip",1,"FireFinish4")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReloadAfterFire")
        TNT1 A 0 A_JumpIfInventory("Cell",10,"ReloadAfterFire")
        goto FireFinish4

      FireFinish4:
        CHIZ M 6
        goto FireFinishReFireAmmoCheck

      FireFinishReFireAmmoCheck:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"FireFinishReFireAmmoCheckMagazine")
        goto FireFinishReFireAmmoCheckReserve

      FireFinishReFireAmmoCheckMagazine:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireFinishReFire")
        TNT1 A 0 A_JumpIfInventory("ChexGigazorcherClip",1,"FireFinishReFire")
      FireFinishReFireAmmoCheckReserve:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireFinishReFire")
        TNT1 A 0 A_JumpIfInventory("Cell",10,"FireFinishReFire")
        goto NoAmmoSwitch

      FireFinishReFire:
        TNT1 A 0 A_ReFire
        goto Ready

      Flash:
        TNT1 A 5 Bright A_Light1
        TNT1 A 5 Bright A_Light2
        goto LightDone

      Reload:
        CHIZ M 6
      ReloadAfterFire:
        CHIZ M 10
        CHIZ M 3 A_PlaySound("chex/instazorchload")
        CHIZ NOPQR 3
        CHIZ S 2
        // doom railgun = 6 + 6 + FireFinish's 6
        CHIZ S 3 A_GiveInventory("SamsaraChexGigazorcherReloader")
        CHIZ A 15
        goto FireFinishReFireAmmoCheck

      // "No ammo" switch
      NoAmmoSwitch:
        TNT1 A 0 A_ClearReFire
        TNT1 A 0 A_SelectWeapon("Mini-Zorcher")
        goto Deselect
    }
}

// Attack handler

actor SamsaraChexGigazorcherAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"PickupReloadable")
      PickupNonReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Cell",10,1)
        stop
        TNT1 A 0 A_TakeInventory("Cell",10)
        goto PickupAttack

      PickupReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("ChexGigazorcherClip",1,1)
        stop
        TNT1 A 0 A_TakeInventory("ChexGigazorcherClip",1)
        goto PickupAttack

      // Effect initializing stuff
      /*PickupEffectInit:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"PickupEffectInitReloadable")
      PickupEffectInitNonReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupEffectInitNormal")
        TNT1 A 0 A_JumpIfInventory("Cell",10,"PickupEffectInitNormal")
        goto PickupEffectInitLast

      PickupEffectInitReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"PickupEffectInitNormal")
        TNT1 A 0 A_JumpIfInventory("ChexGigazorcherClip",1,"PickupEffectInitNormal")
        goto PickupEffectInitLast

      PickupEffectInitNormal:
        TNT1 A 0 A_TakeInventory("SamsaraChexGigazorcherFiringLastShot")
        goto PickupAttack

      PickupEffectInitLast:
        TNT1 A 0 A_GiveInventory("SamsaraChexGigazorcherFiringLastShot")
        goto PickupAttack*/

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("chex/instazorchfire",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_JumpIfInventory("SamsaraChexGigazorcherFiringLastShot",1,"PickupAttackDMLastShot")
        TNT1 A 0 A_RailAttack(0,3,false,None,Pink,RGF_SILENT|RGF_FULLBRIGHT,0,"GigazorcherPuffEffect")
        TNT1 A 0 A_RailAttack(0,3,false,None,White,RGF_SILENT|RGF_FULLBRIGHT,0,"GigazorcherPuffEffect")
        TNT1 A 0 A_RailAttack(0,3,false,None,Red,RGF_SILENT|RGF_FULLBRIGHT,0,"GigazorcherPuffEffect")
        goto PickupAttackDMActual

      PickupAttackDMLastShot:
        TNT1 A 0 A_RailAttack(0,3,false,None,Pink,RGF_SILENT|RGF_FULLBRIGHT,5,"GigazorcherPuffEffect")
        TNT1 A 0 A_RailAttack(0,3,false,None,White,RGF_SILENT|RGF_FULLBRIGHT,5,"GigazorcherPuffEffect")
        TNT1 A 0 A_RailAttack(0,3,false,None,Red,RGF_SILENT|RGF_FULLBRIGHT,5,"GigazorcherPuffEffect")
        goto PickupAttackDMActual

      PickupAttackDMActual:
        TNT1 A 0 A_RailAttack(75,0,false,None,None,RGF_SILENT|RGF_FULLBRIGHT,0,"GigazorcherPuff")
        stop

      PickupAttackCoop:
        TNT1 A 0 A_JumpIfInventory("SamsaraChexGigazorcherFiringLastShot",1,"PickupAttackCoopLastShot")
        TNT1 A 0 A_RailAttack(0,3,false,None,Pink,RGF_SILENT|RGF_FULLBRIGHT,0,"GigazorcherPuff2Effect")
        TNT1 A 0 A_RailAttack(0,3,false,None,White,RGF_SILENT|RGF_FULLBRIGHT,0,"GigazorcherPuff2Effect")
        TNT1 A 0 A_RailAttack(0,3,false,None,Red,RGF_SILENT|RGF_FULLBRIGHT,0,"GigazorcherPuff2Effect")
        goto PickupAttackCoopActual

      PickupAttackCoopLastShot:
        TNT1 A 0 A_RailAttack(0,3,false,None,Pink,RGF_SILENT|RGF_FULLBRIGHT,5,"GigazorcherPuff2Effect")
        TNT1 A 0 A_RailAttack(0,3,false,None,White,RGF_SILENT|RGF_FULLBRIGHT,5,"GigazorcherPuff2Effect")
        TNT1 A 0 A_RailAttack(0,3,false,None,Red,RGF_SILENT|RGF_FULLBRIGHT,5,"GigazorcherPuff2Effect")
        goto PickupAttackCoopActual

      PickupAttackCoopActual:
        TNT1 A 0 A_RailAttack(200,0,false,None,None,RGF_SILENT|RGF_FULLBRIGHT,0,"GigazorcherPuff2")
        stop
    }
}

actor ChexGigazorcherClip : Ammo
{
    Inventory.MaxAmount 4
    Tag "Gigazorcher 2100 Magazine"
    +IGNORESKILL
    +UNTOSSABLE
}

actor SamsaraChexGigazorcherSpawnReloader : Trigger // used for (re)spawn
{
    States
    {
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",2,1)
        stop
        TNT1 A 0 A_JumpIfInventory("ChexGigazorcherClip",0,"PickupStop")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Cell",10,1)
        stop
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_TakeInventory("Cell",10)
        TNT1 A 0 A_GiveInventory("ChexGigazorcherClip")
        loop
    }
}

actor SamsaraChexGigazorcherReloader : SamsaraChexGigazorcherSpawnReloader {} // used for reload call in weapon

actor GigazorcherPuff
{
    Radius 1
    Height 1
    Speed 0
    Damage 0
    DamageType "Zorch"
    +ALWAYSPUFF
    +SKYEXPLODE
    +PIERCEARMOR
    +PUFFONACTORS
    +PUFFGETSOWNER
    +BLOODLESSIMPACT
    +NOEXTREMEDEATH
    States
    {
      Spawn:
        TNT1 A 0
        TNT1 A 8
        stop
    }
}

actor GigazorcherPuffEffect : GigazorcherPuff
{
    +CLIENTSIDEONLY
    +DONTSPLASH
    +NOTRIGGER
}

actor GigazorcherPuff2 : GigazorcherPuff 
{
    //+ALLOWTHRUFLAGS // doesn't exist in zandronum yet
    +MTHRUSPECIES
}

actor GigazorcherPuff2Effect : GigazorcherPuff2
{
    +CLIENTSIDEONLY
    +DONTSPLASH
    +NOTRIGGER
}
