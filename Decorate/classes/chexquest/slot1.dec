actor "Super Bootspork" : Weapon
{
    Weapon.Kickback 0
    Weapon.SelectionOrder 2200
    Weapon.SlotNumber 1
    Weapon.SlotPriority 0
    Inventory.PickUpSound "chex/weaponget"
    Weapon.UpSound "chex/sawup"
    Weapon.ReadySound "chex/sawidle"
    Inventory.PickupMessage "You got a Super Bootspork! Find some milk!"
    Obituary "$SAMSARA_CHEX_OB_SLOT1"
    +WEAPON.MELEEWEAPON
    +BLOODLESSIMPACT
    +INVENTORY.UNDROPPABLE
    DamageType "Zorch"
	Inventory.RestrictedTo "ChexWarrior"
    Tag "Super Bootspork"
    States
    {
      Spawn:
        CSPR A -1
        stop

      Ready:
        SAWC CD 4 A_WeaponReady
        loop

      Deselect:
        SAWC C 1 A_Lower
        loop

      Select:
        SAWC C 1 A_Raise
        loop

      Fire:
        TNT1 A 0 A_JumpIfInventory("PowerStrength",1,"FireBerserk")
        SAWC AB 4 A_GiveInventory("SamsaraChexSuperBootsporkAttackHandler")
        goto FireFinish

      FireBerserk:
        SAWC AB 3 A_GiveInventory("SamsaraChexSuperBootsporkAttackHandler")
        goto FireFinish

      FireFinish:
        TNT1 A 0 A_ReFire
        goto Ready
    }
}

actor SamsaraChexSuperBootsporkAttackHandler : Trigger
{
    States
    {
      Pickup:
        TNT1 A 0 A_JumpIfInventory("PowerStrength",1,"PickupBerserk")
      // Normal
      PickupNormal:
        TNT1 A 0 A_JumpIfInventory("SamsaraImprovedDoomguyBalance",1,"PickupNormalImproved")
      // Normal (vanilla)
      PickupNormalVanilla:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupNormalVanillaCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupNormalVanillaCoop")
      PickupNormalVanillaDM:
        TNT1 A 0 A_Saw("chex/sawfull","chex/sawhit",2,"ChexPuff",SF_NOUSEAMMO)
        stop

      PickupNormalVanillaCoop:
        TNT1 A 0 A_Saw("chex/sawfull","chex/sawhit",2,"ChexPuffCoop",SF_NOUSEAMMO)
        stop

      // Normal (improved)
      PickupNormalImproved:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupNormalImprovedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupNormalImprovedCoop")
      PickupNormalImprovedDM:
        TNT1 A 0 A_Saw("chex/sawfull","chex/sawhit",random(1,2)*10,"ChexPuff",SF_NORANDOM|SF_NOUSEAMMO)
        stop

      PickupNormalImprovedCoop:
        TNT1 A 0 A_Saw("chex/sawfull","chex/sawhit",random(1,2)*10,"ChexPuffCoop",SF_NORANDOM|SF_NOUSEAMMO)
        stop

      // Berserk
      PickupBerserk:
        TNT1 A 0 A_JumpIfInventory("SamsaraImprovedDoomguyBalance",1,"PickupBerserkImproved")
        TNT1 A 0 A_JumpIf(CallACS("SamsaraDecorate", 28), "PickupBerserkImproved")
      // Berserk (vanilla)
      PickupBerserkVanilla:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupBerserkVanillaCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupBerserkVanillaCoop")
      PickupBerserkVanillaDM:
        TNT1 A 0 A_Saw("chex/sawfull","chex/sawhit",7,"ChexPuff",SF_NOUSEAMMO)
        stop

      PickupBerserkVanillaCoop:
        TNT1 A 0 A_Saw("chex/sawfull","chex/sawhit",7,"ChexPuffCoop",SF_NOUSEAMMO)
        stop

      // Berserk (improved)
      PickupBerserkImproved:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupBerserkImprovedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupBerserkImprovedCoop")
      PickupBerserkImprovedDM:
        TNT1 A 0 A_Saw("chex/sawfull","chex/sawhit",random(4,7)*10,"ChexPuff",SF_NORANDOM|SF_NOUSEAMMO)
        stop

      PickupBerserkImprovedCoop:
        TNT1 A 0 A_Saw("chex/sawfull","chex/sawhit",random(4,7)*10,"ChexPuffCoop",SF_NORANDOM|SF_NOUSEAMMO)
        stop
    }
}
