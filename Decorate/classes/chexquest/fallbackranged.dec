actor SamsaraChexLoadoutFallbackRanged : Boolean {}

actor "Mini-Zorcher" : Weapon
{
    Weapon.SelectionOrder 1900
    Weapon.SlotNumber 1
    Weapon.SlotPriority 1
    Inventory.PickUpSound "chex/weaponget"
    Obituary "$SAMSARA_CHEX_OB_FALLBACKRANGED"
    Inventory.PickupMessage "You got the Mini-Zorcher!"
    Weapon.AmmoUse 0
    Weapon.AmmoGive 0
    +WEAPON.WIMPY_WEAPON
    +BLOODLESSIMPACT
    +PUFFONACTORS
    +INVENTORY.UNDROPPABLE
    +NOALERT
    DamageType "ZorchPistol"
    Inventory.RestrictedTo "ChexWarrior"
    Tag "Mini-Zorcher"
    States
    {
      Spawn:
        CIST A -1
        stop

      // Mini-Zorcher
      Ready:
        TNT1 A 0 A_JumpIfInventory("SamsaraChexLoadoutFallbackRanged",1,"ReadyMicroZorcher")
        TNT1 A 0 A_JumpIfInventory("Dual Mini-Zorchers",1,2)
        PISC A 1 A_WeaponReady(WRF_NOSECONDARY)
        loop
        PISC A 1 A_WeaponReady
        loop

      Deselect:
        TNT1 A 0 A_JumpIfInventory("SamsaraChexLoadoutFallbackRanged",1,"DeselectMicroZorcher")
        PISC A 1 A_Lower
        loop

      Select:
        TNT1 A 0 A_JumpIfInventory("SamsaraChexLoadoutFallbackRanged",1,"SelectMicroZorcher")
        PISC A 1 A_Raise
        loop

      Fire:
        TNT1 A 0 A_JumpIfInventory("SamsaraChexLoadoutFallbackRanged",1,"FireMicroZorcher")
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto FireStart
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"FireStart")
        goto NoAmmoSwitch

      FireStart:
        PISC A 4 A_AlertMonsters
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto FireAttack
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"FireAttack")
        goto FireContinue

      FireAttack:
        TNT1 A 0 A_GunFlash
      FireContinue:
        PISC B 6 A_GiveInventory("SamsaraChexMiniZorcherAttackHandler")
        PISC C 4
      FireFinish:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto FireFinishReFire
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireFinishReFire")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"FireFinishReFire")
        goto NoAmmoSwitch

      FireFinishReFire:
        PISC D 5 A_ReFire
        goto Ready

      Flash:
        CPIS A 7 Bright A_Light1
        goto LightDone

      // Micro-Zorcher
      ReadyMicroZorcher:
        TNT1 A 0 A_JumpIfInventory("Dual Mini-Zorchers",1,2)
        PISC J 1 A_WeaponReady(WRF_NOSECONDARY)
        goto Ready
        PISC J 1 A_WeaponReady
        goto Ready

      DeselectMicroZorcher:
        PISC J 1 A_Lower
        goto Deselect

      SelectMicroZorcher:
        PISC J 1 A_Raise
        goto Select

      FireMicroZorcher:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto FireStartMicroZorcher
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStartMicroZorcher")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"FireStartMicroZorcher")
        goto NoAmmoSwitch

      FireStartMicroZorcher:
        PISC J 4 A_AlertMonsters
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto FireAttackMicroZorcher
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireAttackMicroZorcher")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"FireAttackMicroZorcher")
        goto FireContinueMicroZorcher

      FireAttackMicroZorcher:
        TNT1 A 0 A_GunFlash("FlashMicroZorcher")
      FireContinueMicroZorcher:
        PISC K 5 A_GiveInventory("SamsaraChexMicroZorcherAttackHandler")
        PISC L 3
      FireFinishMicroZorcher:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto FireFinishReFireMicroZorcher
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireFinishReFireMicroZorcher")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"FireFinishReFireMicroZorcher")
        goto NoAmmoSwitch

      FireFinishReFireMicroZorcher:
        PISC M 4 A_ReFire
        goto Ready

      FlashMicroZorcher:
        CPIS D 6 Bright A_Light1
        goto LightDone

      AltFire:
        TNT1 A 0 A_SelectWeapon("Dual Mini-Zorchers")
        goto Deselect

      // "No ammo" switch
      NoAmmoSwitch:
        TNT1 A 0 A_ClearReFire
        TNT1 A 0 A_SelectWeapon(" Bootspoon ")
        goto Deselect
    }
}

actor "Dual Mini-Zorchers" : "Mini-Zorcher"
{
    Weapon.SelectionOrder 1700
    Weapon.SlotPriority 2
    Inventory.PickupMessage "You got the Dual Mini-Zorchers!"
    Tag "Dual Mini-Zorchers"
    States
    {
      // Dual Mini-Zorchers
      Ready:
        TNT1 A 0 A_JumpIfInventory("SamsaraChexLoadoutFallbackRanged",1,"ReadyMicroZorcher")
        TNT1 A 0 A_JumpIfInventory("Mini-Zorcher",1,2)
        PISC E 1 A_WeaponReady(WRF_NOSECONDARY)
        loop
        PISC E 1 A_WeaponReady
        loop

      Deselect:
        TNT1 A 0 A_JumpIfInventory("SamsaraChexLoadoutFallbackRanged",1,"DeselectMicroZorcher")
        PISC E 1 A_Lower
        loop

      Select:
        TNT1 A 0 A_JumpIfInventory("SamsaraChexLoadoutFallbackRanged",1,"SelectMicroZorcher")
        PISC E 1 A_Raise
        loop

      Fire:
        TNT1 A 0 A_JumpIfInventory("SamsaraChexLoadoutFallbackRanged",1,"FireMicroZorcher")
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto FireStart
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"FireStart")
        goto NoAmmoSwitch

      FireStart:
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_GunFlash
        PISC F 5 A_GiveInventory("SamsaraChexMiniZorcherAttackHandler")
        PISC G 4
      Fire2:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto Fire2Attack
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"Fire2Attack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"Fire2Attack")
        goto Fire2Start

      Fire2Attack:
        TNT1 A 0 A_GunFlash("Flash2")
      Fire2Start:
        PISC H 5 A_GiveInventory("SamsaraChexMiniZorcherAttackHandler")
        PISC I 4
      FireFinish:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto FireFinishReFire
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireFinishReFire")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"FireFinishReFire")
        goto NoAmmoSwitch

      FireFinishReFire:
        PISC E 5 A_ReFire
        goto Ready

      Flash:
        CPIS B 6 Bright A_Light1
        goto LightDone

      Flash2:
        CPIS C 6 Bright A_Light1
        goto LightDone

      // Dual Micro-Zorchers
      ReadyMicroZorcher:
        TNT1 A 0 A_JumpIfInventory("Mini-Zorcher",1,2)
        PISC N 1 A_WeaponReady(WRF_NOSECONDARY)
        goto Ready
        PISC N 1 A_WeaponReady
        goto Ready

      DeselectMicroZorcher:
        PISC N 1 A_Lower
        goto Deselect

      SelectMicroZorcher:
        PISC N 1 A_Raise
        goto Select

      FireMicroZorcher:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto FireStartMicroZorcher
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStartMicroZorcher")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"FireStartMicroZorcher")
        goto NoAmmoSwitch

      FireStartMicroZorcher:
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_GunFlash("FlashMicroZorcher")
        PISC O 4 A_GiveInventory("SamsaraChexMicroZorcherAttackHandler")
        PISC P 3
      Fire2MicroZorcher:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto Fire2AttackMicroZorcher
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"Fire2AttackMicroZorcher")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"Fire2AttackMicroZorcher")
        goto Fire2StartMicroZorcher

      Fire2AttackMicroZorcher:
        TNT1 A 0 A_GunFlash("Flash2MicroZorcher")
      Fire2StartMicroZorcher:
        PISC Q 4 A_GiveInventory("SamsaraChexMicroZorcherAttackHandler")
        PISC R 3
      FireFinishMicroZorcher:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto FireFinishReFireMicroZorcher
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireFinishReFireMicroZorcher")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"FireFinishReFireMicroZorcher")
        goto NoAmmoSwitch

      FireFinishReFireMicroZorcher:
        PISC N 4 A_ReFire
        goto Ready

      FlashMicroZorcher:
        CPIS D 5 Bright A_Light1
        goto LightDone

      Flash2MicroZorcher:
        CPIS E 5 Bright A_Light1
        goto LightDone

      AltFire:
        TNT1 A 0 A_SelectWeapon("Mini-Zorcher")
        goto Deselect

      // "No ammo" switch
      NoAmmoSwitch:
        TNT1 A 0 A_ClearReFire
        TNT1 A 0 A_SelectWeapon(" Bootspoon ")
        goto Deselect
    }
}

actor SamsaraChexMiniZorcherAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto PickupAttack
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("chex/pistol",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SamsaraImprovedDoomguyBalance",1,"PickupAttackImproved")
      // Vanilla
      PickupAttackVanilla:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackVanillaCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackVanillaCoop")
      PickupAttackVanillaDM:
        TNT1 A 0 A_FireBullets(5,0,1,5,"ChexPuff2",FBF_NOFLASH)
        stop

      PickupAttackVanillaCoop:
        TNT1 A 0 A_FireBullets(5,0,1,5,"ChexPuff2Coop",FBF_NOFLASH)
        stop

      // Improved
      PickupAttackImproved:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackImprovedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackImprovedCoop")
      PickupAttackImprovedDM:
        TNT1 A 0 A_FireBullets(5,0,1,random(6,8)*2,"ChexPuff2",FBF_NORANDOM|FBF_NOFLASH)
        stop

      PickupAttackImprovedCoop:
        TNT1 A 0 A_FireBullets(5,0,1,random(6,8)*2,"ChexPuff2Coop",FBF_NORANDOM|FBF_NOFLASH)
        stop
    }
}

actor SamsaraChexMicroZorcherAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto PickupAttack
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("chex/wimppistol",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SamsaraImprovedDoomguyBalance",1,"PickupAttackImproved")
      // Vanilla
      PickupAttackVanilla:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackVanillaCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackVanillaCoop")
      PickupAttackVanillaDM:
        TNT1 A 0 A_FireBullets(6.2,0,1,5,"ChexPuff2",FBF_NOFLASH)
        stop

      PickupAttackVanillaCoop:
        TNT1 A 0 A_FireBullets(6.2,0,1,5,"ChexPuff2Coop",FBF_NOFLASH)
        stop

      // Improved
      PickupAttackImproved:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackImprovedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackImprovedCoop")
      PickupAttackImprovedDM:
        TNT1 A 0 A_FireBullets(6.2,0,1,random(6,8)*2,"ChexPuff2",FBF_NORANDOM|FBF_NOFLASH)
        stop

      PickupAttackImprovedCoop:
        TNT1 A 0 A_FireBullets(6.2,0,1,random(6,8)*2,"ChexPuff2Coop",FBF_NORANDOM|FBF_NOFLASH)
        stop
    }
}

actor ChexPuff2 : ChexPuff { DamageType "ZorchPistol" }
actor ChexPuff2Coop : ChexPuff2 { +MTHRUSPECIES }
