actor SamsaraChexLoadoutFallbackRanged : Boolean {}
actor SamsaraChexWarriorHasTwoMicroZorchers : Boolean {}

actor "Mini-Zorcher" : Weapon
{
    Weapon.SelectionOrder 1900
    Weapon.SlotNumber 1
    Weapon.SlotPriority 1
    Inventory.PickUpSound "chex/weaponget"
    Obituary "$SAMSARA_CHEX_OB_FALLBACKRANGED"
    Inventory.PickupMessage "You got the Mini-Zorcher!"
    Weapon.AmmoUse 0
    Weapon.AmmoGive 0
    +WEAPON.WIMPY_WEAPON
    +BLOODLESSIMPACT
    +PUFFONACTORS
    +INVENTORY.UNDROPPABLE
    +NOALERT
    DamageType "ZorchPistol"
	Inventory.RestrictedTo "ChexWarrior"
    Tag "Mini-Zorcher"
    States
    {
      Spawn:
        CIST A -1
        stop

      // Mini-Zorcher
	  Ready:
        TNT1 A 0 A_JumpIfInventory("SamsaraChexLoadoutFallbackRanged",1,"ReadyMicroZorcher")
	  ReadyContinue:
		PISC A 1 A_WeaponReady
        goto Ready

      Deselect:
		TNT1 A 0 A_JumpIfInventory("SamsaraChexLoadoutFallbackRanged",1,"DeselectMicroZorcher")
		PISC A 1 A_Lower
        loop

      Select:
		TNT1 A 0 A_JumpIfInventory("SamsaraChexLoadoutFallbackRanged",1,"SelectMicroZorcher")
        PISC A 1 A_Raise
        loop

      Fire:
		TNT1 A 0 A_JumpIfInventory("SamsaraChexLoadoutFallbackRanged",1,"FireMicroZorcher")
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto FireStart
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"FireStart")
        goto NoAmmoSwitch

      FireStart:
        PISC A 4 A_AlertMonsters
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto FireAttack
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"FireAttack")
        goto FireContinue

      FireAttack:
        TNT1 A 0 A_GunFlash
      FireContinue:
        PISC B 6 A_GiveInventory("SamsaraChexMiniZorcherAttackHandler")
        PISC C 4
      FireFinish:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto FireFinishReFire
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireFinishReFire")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"FireFinishReFire")
        goto NoAmmoSwitch

      FireFinishReFire:
        PISC D 5 A_ReFire
        goto Ready

      Flash:
        CPIS A 7 Bright A_Light1
        goto LightDone

      // Micro-Zorcher
	  ReadyMicroZorcher:
		TNT1 A 0 A_JumpIfInventory("SamsaraChexWarriorHasTwoMicroZorchers",1,"ReadyMicroZorcherDual")
	  ReadyContinueMicroZorcher:
		PISC E 1 A_WeaponReady
        goto Ready

	  ReadyMicroZorcherDual:
		PISC I 1 A_WeaponReady
        loop

      DeselectMicroZorcher:
		TNT1 A 0 A_JumpIfInventory("SamsaraChexWarriorHasTwoMicroZorchers",1,"DeselectMicroZorcherDual")
		PISC E 1 A_Lower
        loop

      DeselectMicroZorcherDual:
		PISC I 1 A_Lower
        loop

      SelectMicroZorcher:
		TNT1 A 0 A_JumpIfInventory("SamsaraChexWarriorHasTwoMicroZorchers",1,"SelectMicroZorcherDual")
        PISC E 1 A_Raise
        loop

      SelectMicroZorcherDual:
        PISC I 1 A_Raise
        loop

      FireMicroZorcher:
		TNT1 A 0 A_JumpIfInventory("SamsaraChexWarriorHasTwoMicroZorchers",1,"FireMicroZorcherDual")
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto FireStartMicroZorcher
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStartMicroZorcher")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"FireStartMicroZorcher")
        goto NoAmmoSwitch

      FireMicroZorcherDual:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto FireStartMicroZorcher
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStartMicroZorcherDual")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"FireStartMicroZorcherDual")
        goto NoAmmoSwitch

      FireStartMicroZorcher:
		TNT1 A 0 A_JumpIfInventory("SamsaraChexWarriorHasTwoMicroZorchers",1,"FireStartMicroZorcherDual")
        PISC E 4 A_AlertMonsters
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto FireAttackMicroZorcher
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireAttackMicroZorcher")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"FireAttackMicroZorcher")
        goto FireContinueMicroZorcher

      FireStartMicroZorcherDual:
        PISC I 2 A_AlertMonsters
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto FireAttackMicroZorcher
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireAttackMicroZorcher")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"FireAttackMicroZorcher")
        goto FireContinueMicroZorcherDual

      FireAttackMicroZorcher:
		TNT1 A 0 A_JumpIfInventory("SamsaraChexWarriorHasTwoMicroZorchers",1,"FireAttackMicroZorcherDual")
        TNT1 A 0 A_GunFlash("FlashMicroZorcher")
      FireContinueMicroZorcher:
        PISC F 5 A_GiveInventory("SamsaraChexMicroZorcherAttackHandler")
        PISC G 3
      FireFinishMicroZorcher:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto FireFinishReFireMicroZorcher
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireFinishReFireMicroZorcher")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"FireFinishReFireMicroZorcher")
        goto NoAmmoSwitch

      FireAttackMicroZorcherDual:
        TNT1 A 0 A_GunFlash("FlashMicroZorcherDual")
      FireContinueMicroZorcherDual:
        PISC J 3 A_GiveInventory("SamsaraChexMicroZorcherAttackHandler")
        PISC K 2
        PISC L 2
      FireAgainCheckMicroZorcher:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto FireAgainMicroZorcher
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireAgainMicroZorcher")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"FireAgainMicroZorcher")
        goto NoAmmoSwitch
      FireAgainMicroZorcher:
		TNT1 A 0 A_GunFlash("FlashMicroZorcherDual2")
      FireAgainContinueMicroZorcher:
		PISC M 3 A_GiveInventory("SamsaraChexMicroZorcherAttackHandler")
        PISC N 2
      FireFinishMicroZorcherDual:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto FireFinishReFireMicroZorcherDual
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireFinishReFireMicroZorcherDual")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"FireFinishReFireMicroZorcherDual")
        goto NoAmmoSwitch

      FireFinishReFireMicroZorcher:
        PISC H 4 A_ReFire
        goto Ready

      FireFinishReFireMicroZorcherDual:
        PISC O 2 A_ReFire
        goto Ready

      FlashMicroZorcher:
        CPIS B 6 Bright A_Light1
        goto LightDone

      FlashMicroZorcherDual:
        CPIS B 5 Bright A_Light1
        goto LightDone

      FlashMicroZorcherDual2:
        CPIS C 5 Bright A_Light1
        goto LightDone

      // "No ammo" switch
      NoAmmoSwitch:
        TNT1 A 0 A_ClearReFire
        TNT1 A 0 A_SelectWeapon(" Bootspoon ")
        goto Deselect
    }
}

actor SamsaraChexMiniZorcherAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto PickupAttack
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("chex/pistol",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SamsaraImprovedDoomguyBalance",1,"PickupAttackImproved")
      // Vanilla
      PickupAttackVanilla:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackVanillaCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackVanillaCoop")
      PickupAttackVanillaDM:
        TNT1 A 0 A_FireBullets(5,0,1,5,"ChexPuff2",FBF_NOFLASH)
        stop

      PickupAttackVanillaCoop:
        TNT1 A 0 A_FireBullets(5,0,1,5,"ChexPuff2Coop",FBF_NOFLASH)
        stop

      // Improved
      PickupAttackImproved:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackImprovedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackImprovedCoop")
      PickupAttackImprovedDM:
        TNT1 A 0 A_FireBullets(5,0,1,random(6,8)*2,"ChexPuff2",FBF_NORANDOM|FBF_NOFLASH)
        stop

      PickupAttackImprovedCoop:
        TNT1 A 0 A_FireBullets(5,0,1,random(6,8)*2,"ChexPuff2Coop",FBF_NORANDOM|FBF_NOFLASH)
        stop
    }
}

actor SamsaraChexMicroZorcherAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto PickupAttack
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("chex/wimppistol",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SamsaraImprovedDoomguyBalance",1,"PickupAttackImproved")
      // Vanilla
      PickupAttackVanilla:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackVanillaCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackVanillaCoop")
      PickupAttackVanillaDM:
        TNT1 A 0 A_FireBullets(6.2,0,1,5,"ChexPuff2",FBF_NOFLASH)
        stop

      PickupAttackVanillaCoop:
        TNT1 A 0 A_FireBullets(6.2,0,1,5,"ChexPuff2Coop",FBF_NOFLASH)
        stop

      // Improved
      PickupAttackImproved:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackImprovedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackImprovedCoop")
      PickupAttackImprovedDM:
        TNT1 A 0 A_FireBullets(6.2,0,1,random(6,8)*2,"ChexPuff2",FBF_NORANDOM|FBF_NOFLASH)
        stop

      PickupAttackImprovedCoop:
        TNT1 A 0 A_FireBullets(6.2,0,1,random(6,8)*2,"ChexPuff2Coop",FBF_NORANDOM|FBF_NOFLASH)
        stop
    }
}

actor ChexPuff2 : ChexPuff { DamageType "ZorchPistol" }
actor ChexPuff2Coop : ChexPuff2 { +MTHRUSPECIES }
