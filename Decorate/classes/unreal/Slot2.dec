actor IsUShockBall : Boolean {}
Actor Unreal_ShockCoreTidToken : UnrealInt {}
actor SamsaraUnrealShockComboAmmoTaken : Boolean {} // for spread rune

Actor Unreal_ASMDShockRifle : UnrealWeapon
{
	Weapon.SelectionOrder 1700
	Weapon.SlotPriority 1
	Weapon.SlotNumber 2
	Weapon.AmmoType "Shell"
	Weapon.AmmoGive 20
	Weapon.AmmoUse 1
	Obituary "%k inflicted mortal damage upon %o with the ASMD."
	Inventory.PickupMessage "You got the ASMD."
	Tag "ASMD"
	+ALT_USES_BOTH // only one ammo type with same ammo amount use, so this makes alt fire behave as intended
	States
	{
		Spawn:
			UN00 A 10 Bright
			UN00 A 10
			loop

		ReadyActual:
			UN01 I 4 A_WeaponReady
			goto ReadyActualJump

		ReadyActualJump:
			UN01 I 0 A_Jump(12,"ReadyActualIdle")
			goto ReadyActual

		ReadyActualIdle:
			UN01 I 4 A_WeaponReady
			UN02 OP 4 A_WeaponReady
			UN02 Q 8 A_WeaponReady
			UN02 P 4 A_WeaponReady
			UN02 O 8 A_WeaponReady
			UN02 PQ 4 A_WeaponReady
			UN01 I 0 A_Jump(128,"ReadyActualSteam")
			goto ReadyActual

		ReadyActualSteam:
			UN01 I 0 A_GiveInventory("SamsaraUnrealASMDSteamSoundPlayer")
			UN01 I 12 A_WeaponReady
			UN02 ABCDE 12 A_WeaponReady
			UN01 I 0 A_Jump(128,"ReadyActualSteam")
			goto ReadyActual

		Deselect:
			UN01 I 0 A_TakeInventory("Unreal_ASMDSelected")
			UN01 I 2 A_JumpIfHealthLower(1,"DeselectInstant")
			UN01 KLM 2
            UN01 N 0
			goto DeselectInstant

		Ready: // our "Select" state
			UN01 A 0 A_GiveInventory("Unreal_ASMDSelected")
			UN01 A 0 A_GiveInventory("SamsaraUnrealASMDSelectSoundPlayer")
			UN01 ABCDEFGH 2 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
			UN01 I 0 A_Jump(128,"ReadyActualSteam")
			goto ReadyActual

		Fire:
			UN01 P 0 A_GiveInventory("SamsaraUnrealASMDPrimaryAttackHandler")
			UN01 P 5 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			UN01 Q 6 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			UN01 RS 5 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			UN01 T 4 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			UN01 I 0 A_CheckReload
			UN01 I 0 A_Jump(51,"ReadyActualSteam")
			goto ReadyActual

		AltFire:
			UN01 U 0 A_GiveInventory("SamsaraUnrealASMDSecondaryAttackHandler")
			UN01 UV 4 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			UN01 WXY 4 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			UN01 I 0 A_CheckReload
			UN01 I 0 A_Jump(51,"ReadyActualSteam")
			goto ReadyActual
	}
}

actor SamsaraUnrealASMDPrimaryAttackHandler : Trigger
{
	States
	{
	// Initializing stuff
	Pickup:
		TNT1 A 0 A_TakeInventory("SamsaraUnrealShockComboAmmoTaken")
		goto PickupAmmoCheck

	// Ammo checking/taking stuff
	PickupAmmoCheck:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupPlaySound")
		TNT1 A 0 A_JumpIfInventory("Shell",1,1)
		stop
		TNT1 A 0 A_TakeInventory("Shell",1)
		goto PickupPlaySound

	// Sound stuff
	PickupPlaySound:
		TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupPlaySoundUnreal")
	// Unreal Gold sounds
	PickupPlaySoundUnrealGold:
		TNT1 A 0 A_PlaySound("Unreal/ASMD/Fire1",CHAN_WEAPON)
		goto PickupAttack

	// Unreal sounds
	PickupPlaySoundUnreal:
		TNT1 A 0 A_PlaySound("UnrealOld/ASMD/Fire1",CHAN_WEAPON)
		goto PickupAttack

	// Attack stuff
	PickupAttack:
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
		TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupAttackDMSoundUnreal")
	PickupAttackDM:
		TNT1 A 0 A_FireBullets(0.5,0.5,-1,35,"U1ASMDTracerPuff",FBF_NORANDOM|FBF_NOFLASH|FBF_NORANDOMPUFFZ)
		goto PickupFinish
	PickupAttackDMSoundUnreal:
		TNT1 A 0 A_FireBullets(0.5,0.5,-1,35,"U1ASMDTracerPuffSoundUnreal",FBF_NORANDOM|FBF_NOFLASH|FBF_NORANDOMPUFFZ)
		goto PickupFinish
	PickupAttackCoop:
		TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupAttackCoopSoundUnreal")
		TNT1 A 0 A_FireBullets(0.5,0.5,-1,35,"U1ASMDTracerPuffCoop",FBF_NORANDOM|FBF_NOFLASH|FBF_NORANDOMPUFFZ)
		goto PickupFinish
	PickupAttackCoopSoundUnreal:
		TNT1 A 0 A_FireBullets(0.5,0.5,-1,35,"U1ASMDTracerPuffCoopSoundUnreal",FBF_NORANDOM|FBF_NOFLASH|FBF_NORANDOMPUFFZ)
		goto PickupFinish

	// Finishing stuff
	PickupFinish:
		TNT1 A 0 ACS_NamedExecuteWithResult("Unreal_Recoil")
		stop
	}
}

actor SamsaraUnrealASMDSecondaryAttackHandler : Trigger
{
	States
	{
	// Ammo checking/taking stuff
	Pickup:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupPlaySound")
		TNT1 A 0 A_JumpIfInventory("Shell",1,1)
		stop
		TNT1 A 0 A_TakeInventory("Shell",1)
		goto PickupPlaySound

	// Sound stuff
	PickupPlaySound:
		TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupPlaySoundUnreal")
	// Unreal Gold sounds
	PickupPlaySoundUnrealGold:
		TNT1 A 0 A_PlaySound("Unreal/ASMD/Fire2",CHAN_WEAPON)
		goto PickupAttack

	// Unreal sounds
	PickupPlaySoundUnreal:
		TNT1 A 0 A_PlaySound("UnrealOld/ASMD/Fire2",CHAN_WEAPON)
		goto PickupAttack

	// Attack stuff
	PickupAttack:
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
		TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupAttackDMSoundUnreal")
	PickupAttackDM:
		TNT1 A 0 A_FireCustomMissile("U1ASMDBall",0,false,13,5)
		goto PickupFinish
	PickupAttackDMSoundUnreal:
		TNT1 A 0 A_FireCustomMissile("U1ASMDBallSoundUnreal",0,false,13,5)
		goto PickupFinish
	PickupAttackCoop:
		TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupAttackCoopSoundUnreal")
		TNT1 A 0 A_FireCustomMissile("U1ASMDBallCoop",0,false,13,5)
		goto PickupFinish
	PickupAttackCoopSoundUnreal:
		TNT1 A 0 A_FireCustomMissile("U1ASMDBallCoopSoundUnreal",0,false,13,5)
		goto PickupFinish

	// Finishing stuff
	PickupFinish:
		TNT1 A 0 ACS_NamedExecuteWithResult("Unreal_Recoil")
		TNT1 A 0 A_JumpIfInventory("WeaponFlashesAreDisabled",1,"PickupStop")
		TNT1 A 0 A_SetBlend("298FE8",0.1,10)
		stop
	}
}

actor SamsaraUnrealASMDShockComboAmmoTaker : Trigger
{
	States
	{
	// Ammo taking stuff
	Pickup:
		TNT1 A 0 A_JumpIfInventory("SamsaraUnrealShockComboAmmoTaken",1,"PickupStop")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupStop")
		TNT1 A 0 A_TakeInventory("Shell",2)
		TNT1 A 0 A_GiveInventory("SamsaraUnrealShockComboAmmoTaken")
		stop
	}
}

actor SamsaraUnrealASMDSelectSoundPlayer : Trigger
{
    States
    {
      // Sound playing stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupUnreal")
      // Unreal Gold sounds
      PickupUnrealGold:
        TNT1 A 0 A_PlaySound("Unreal/ASMD/Select",CHAN_7)
        stop

      // Unreal sounds
      PickupUnreal:
        TNT1 A 0 A_PlaySound("UnrealOld/ASMD/Select",CHAN_7)
        stop
    }
}

actor SamsaraUnrealASMDSteamSoundPlayer : Trigger
{
    States
    {
      // Sound playing stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupUnreal")
      // Unreal Gold sounds
      PickupUnrealGold:
        TNT1 A 0 A_PlaySound("Unreal/ASMD/Steam",CHAN_7)
        stop

      // Unreal sounds
      PickupUnreal:
        TNT1 A 0 A_PlaySound("UnrealOld/ASMD/Steam",CHAN_7)
        stop
    }
}

Actor U1ASMDTracerPuff
{
	DamageType "ASMDShockTrace"
	Height 16
	Radius 8
	+HITTRACER
	+PUFFGETSOWNER
	+PUFFONACTORS
	+ALWAYSPUFF
	+NOINTERACTION
	+CLIENTSIDEONLY
	+NOGRAVITY
	+SKYEXPLODE
	+NOTIMEFREEZE
	+THRUGHOST
	States
	{
		Spawn:
		Melee:
		Crash:
			TNT1 A 0
			TNT1 A 0 ACS_NamedExecuteWithResult("Unreal_ASMDTrace")
			TNT1 A 0 A_RadiusThrust(700,20, !RTF_AFFECTSOURCE, 20)
			TNT1 A 0 A_GiveInventory("Unreal_ASMDZThrust", 1, AAPTR_TRACER)
			TNT1 A 0 A_JumpIf(z - floorz < 6,"HitPlane")
			TNT1 A 0 A_CheckCeiling("HitPlane")
			TNT1 A 0 A_JumpIfInventory("IsUShockBall",1,"RemoveClean",AAPTR_Tracer)
			TNT1 A 0 A_PlaySound("Unreal/ASMD/Explode",CHAN_BODY)
			TNT1 A 1 A_SpawnItemEx("U1ShockRippleB_Large")
			Stop
		RemoveClean:
			TNT1 A 1
			Stop
		XDeath:
			TNT1 A 0 ACS_NamedExecuteWithResult("Unreal_ASMDTrace")
			TNT1 A 0 ACS_NamedExecuteWithResult("Unreal_BloodGenerator_Offsets")
			TNT1 A 0 A_RadiusThrust(700,20, !RTF_AFFECTSOURCE, 20)
			TNT1 A 0 A_GiveInventory("Unreal_ASMDZThrust", 1, AAPTR_TRACER)
			TNT1 A 0 A_JumpIf(z - floorz < 6,"HitPlane")
			TNT1 A 0 A_CheckCeiling("HitPlane")
			TNT1 A 0 A_JumpIfInventory("IsUShockBall",1,"RemoveBloody",AAPTR_Tracer)
			TNT1 A 0 A_PlaySound("Unreal/ASMD/Explode",CHAN_BODY)
			TNT1 A 1 A_SpawnItemEx("U1ShockRippleB_Large")
			Goto SpawnBlood
		RemoveBloody:
			TNT1 A 1
			Goto SpawnBlood
		SpawnBlood:
			TNT1 A 0 A_GiveInventory("UnrealBloodyPuffGenerator",1,AAPTR_TRACER)
			Stop
		HitPlane:
			TNT1 A 0 A_PlaySound("Unreal/ASMD/Explode",CHAN_BODY)
			TNT1 A 1 A_SpawnItemEx("U1ShockRippleB_Plane")
			Stop
	}
}

Actor U1ASMDTracerPuffCoop : U1ASMDTracerPuff { +MTHRUSPECIES }

Actor U1ASMDTracerPuffSoundUnreal : U1ASMDTracerPuff
{
	States
	{
		Spawn:
		Melee:
		Crash:
			TNT1 A 0
			TNT1 A 0 ACS_NamedExecuteWithResult("Unreal_ASMDTrace")
			TNT1 A 0 A_RadiusThrust(700,20, !RTF_AFFECTSOURCE, 20)
			TNT1 A 0 A_GiveInventory("Unreal_ASMDZThrust", 1, AAPTR_TRACER)
			TNT1 A 0 A_JumpIf(z - floorz < 6,"HitPlane")
			TNT1 A 0 A_CheckCeiling("HitPlane")
			TNT1 A 0 A_JumpIfInventory("IsUShockBall",1,"RemoveClean",AAPTR_Tracer)
			TNT1 A 0 A_PlaySound("UnrealOld/ASMD/Explode",CHAN_BODY)
			TNT1 A 1 A_SpawnItemEx("U1ShockRippleB_Large")
			Stop
		XDeath:
			TNT1 A 0 ACS_NamedExecuteWithResult("Unreal_ASMDTrace")
			TNT1 A 0 ACS_NamedExecuteWithResult("Unreal_BloodGenerator_Offsets")
			TNT1 A 0 A_RadiusThrust(700,20, !RTF_AFFECTSOURCE, 20)
			TNT1 A 0 A_GiveInventory("Unreal_ASMDZThrust", 1, AAPTR_TRACER)
			TNT1 A 0 A_JumpIf(z - floorz < 6,"HitPlane")
			TNT1 A 0 A_CheckCeiling("HitPlane")
			TNT1 A 0 A_JumpIfInventory("IsUShockBall",1,"RemoveBloody",AAPTR_Tracer)
			TNT1 A 0 A_PlaySound("UnrealOld/ASMD/Explode",CHAN_BODY)
			TNT1 A 1 A_SpawnItemEx("U1ShockRippleB_Large")
			Goto SpawnBlood
		HitPlane:
			TNT1 A 0 A_PlaySound("UnrealOld/ASMD/Explode",CHAN_BODY)
			TNT1 A 1 A_SpawnItemEx("U1ShockRippleB_Plane")
			Stop
	}
}

Actor U1ASMDTracerPuffCoopSoundUnreal : U1ASMDTracerPuffSoundUnreal { +MTHRUSPECIES }

Actor Unreal_ASMDTracerIntermediate
{
	+ISMONSTER
	-COUNTKILL
	-SOLID
	+THRUACTORS
	-SHOOTABLE
	Speed 0
	Height 0
	Radius 0
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 3 A_Stop
			Stop
	}
}

Actor U1ASMDBall
{
	Speed 20
	Damage (0)
	Projectile
	+HITTRACER
	+FORCERADIUSDMG
	+THRUSPECIES
	+BRIGHT
	+THRUGHOST
	Radius 12
	Height 24
	Health 1
	Scale 0.5
	RenderStyle Add
	Alpha 0.9
	DamageType "UnrealPlasma"
	Species "UnrealASMD"
	DamageFactor "ASMDShockTrace", 1
	DamageFactor "Normal", 0
	States
	{
		Spawn:
			UN00 A 0
			UN00 A 1 ACS_NamedExecuteWithResult("Unreal_SpawnShockRifleHitbox", 3)
		SpawnLoop:
			UN00 A 2
			Loop
		Explode:
			TNT1 A 0 A_Explode(215,240,1,0,20)
			TNT1 A 0 A_PlaySound("Unreal/ASMD/SuperExplosion", CHAN_VOICE, 1.0, false, ATTN_NONE)
			TNT1 A 0 A_SpawnItemEx("U1ShockRippleP_Large")
			TNT1 A 1 A_GiveToTarget("SamsaraUnrealASMDShockComboAmmoTaker")
			Stop
		XDeath:
			TNT1 A 1 ACS_NamedExecuteWithResult("Unreal_BloodGenerator_Offsets")
			TNT1 A 0 A_GiveInventory("UnrealBloodyPuffGenerator",1,AAPTR_TRACER)
		Crash:
		Death:
			TNT1 A 0 A_RadiusThrust(7000,20, !RTF_AFFECTSOURCE| RTF_NOIMPACTDAMAGE, 20)
			TNT1 A 0 A_Explode(55,70,0,0,15)
			TNT1 A 0 A_KillChildren
			TNT1 A 0 A_GiveInventory("Unreal_ASMDZThrust", 1, AAPTR_TRACER)
			TNT1 A 0 A_PlaySound("Unreal/ASMD/Explode",CHAN_VOICE)
			TNT1 A 1 A_SpawnItemEx("U1ShockRippleB_Large")
			Stop
	}
}

Actor U1ASMDBallCoop : U1ASMDBall { +MTHRUSPECIES }

Actor U1ASMDBallSoundUnreal : U1ASMDBall
{
	States
	{
		Explode:
			TNT1 A 0 A_Explode(215,240,1,0,20)
			TNT1 A 0 A_PlaySound("UnrealOld/ASMD/SuperExplosion", CHAN_VOICE, 1.0, false, ATTN_NONE)
			TNT1 A 0 A_SpawnItemEx("U1ShockRippleP_Large")
			TNT1 A 1 A_GiveToTarget("SamsaraUnrealASMDShockComboAmmoTaker")
			Stop
		XDeath:
			TNT1 A 1 ACS_NamedExecuteWithResult("Unreal_BloodGenerator_Offsets")
			TNT1 A 0 A_GiveInventory("UnrealBloodyPuffGenerator",1,AAPTR_TRACER)
		Crash:
		Death:
			TNT1 A 0 A_RadiusThrust(7000,20, !RTF_AFFECTSOURCE| RTF_NOIMPACTDAMAGE, 20)
			TNT1 A 0 A_Explode(55,70,0,0,15)
			TNT1 A 0 A_KillChildren
			TNT1 A 0 A_GiveInventory("Unreal_ASMDZThrust", 1, AAPTR_TRACER)
			TNT1 A 0 A_PlaySound("UnrealOld/ASMD/Explode",CHAN_VOICE)
			TNT1 A 1 A_SpawnItemEx("U1ShockRippleB_Large")
			Stop
	}
}

Actor U1ASMDBallCoopSoundUnreal : U1ASMDBallSoundUnreal { +MTHRUSPECIES }

Actor UASMDBallHitbox
{
	Height 64
	Radius 32
	Health 1
	Mass 50000
    Tag "ASMD Energy Core"
	-COUNTKILL
	-SOLID
	+SHOOTABLE
	+NOBLOOD
	+NOGRAVITY
	+FORCERADIUSDMG
	DamageFactor "ASMDShockTrace", 1
	DamageFactor "Normal", 0
	DamageType "UnrealPlasma"
	Species "UnrealASMD"
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_GiveInventory("IsUShockBall",1)
		SpawnLoop:
			TNT1 A 0 ACS_NamedExecuteWithResult("Painkiller_HitBoxRemover") // I don't feel like making an Unreal version of this
			TNT1 A 2 A_Warp(AAPTR_TRACER,0,0,0,0,WARPF_NOCHECKPOSITION|WARPF_COPYINTERPOLATION)
			Loop
		Death:
			TNT1 A 2
			Stop
		Death.ASMDShockTrace:
			TNT1 A 5 ACS_NamedExecuteWithResult("Unreal_ShockRifleCombo")
			Stop
	}
}

Actor U1ShockRippleB_Large
{
	+CLIENTSIDEONLY
	+NOINTERACTION
	+NOGRAVITY
	Radius 4
	Height 4
	Scale 1.5
	RenderStyle Add
	Alpha 1.0
	States
	{
		Spawn:
			UN00 A 0
			UN00 AAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("Unreal_ASMDParticle",0,0,0,random(-6,6),random(-6,6),random(-6,6))
			UN00 ABCD 5 A_FadeOut(0.2)
			Stop
	}
}

Actor U1ShockRippleB_Plane : U1ShockRippleB_Large
{
	-NOINTERACTION
	+FLOORHUGGER
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_SetPitch(90)
			Goto Super::Spawn
	}
}

Actor U1ShockRippleB_Small : U1ShockRippleB_Large
{
	Scale 0.25
	States
	{
		Spawn:
			UN00 A 0
			UN00 ABCD 5 A_FadeOut(0.2)
			Stop
	}
}

Actor U1ShockRippleP_Large : U1ShockRippleB_Large
{
	States
	{
		Spawn:
			UN00 A 0
			UN00 AAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("Unreal_ASMDParticleSuper",0,0,0,random(-6,6),random(-6,6),random(-6,6))
			UN00 A 0 A_SpawnItemEx("Unreal_ASMDSuperExplosion")
			UN00 ABCD 5 A_FadeOut(0.2)
			Stop
	}
}

ACTOR UNreal_ASMDZThrust : CustomInventory
{
	-COUNTITEM
	+INVENTORY.ALWAYSPICKUP
	Inventory.PickupMessage ""
	Inventory.PickupSound ""
	States
	{
		Pickup:
			TNT1 A 0 A_CheckFlag(NOICEDEATH, "PickupContinue")
			Stop
		PickupContinue:
			TNT1 A 0 ThrustThingZ(0,3500/mass,0,1)
			Stop
	}
}

Actor Unreal_ShockBallDissipate : Boolean {}

Actor Unreal_ASMDParticle
{
	Height 2
	Radius 1
	Scale 0.35
	RenderStyle Add
	+NOINTERACTION
	+CLIENTSIDEONLY
	States
	{
		Spawn:
			UN25 BBBBB 2 A_Fadeout(0.2)
			Stop
	}
}

Actor Unreal_ASMDParticleSuper : Unreal_ASMDParticle
{
	Scale 0.7
	States
	{
		Spawn:
			UN25 GGGGG 2 A_Fadeout(0.2)
			Stop
	}
}

Actor Unreal_ASMDSuperExplosion : Unreal_ASMDParticle
{
	Scale 2.8
	States
	{
		Spawn:
			UN25 ABCDEF 3
			Stop
	}
}
