Actor Unreal_AutoMag : UnrealWeapon
{
	Weapon.SelectionOrder 2100
	Weapon.SlotNumber 1
	Weapon.SlotPriority 1
	Weapon.AmmoType "UnrealAutomagClip"
	Weapon.AmmoUse 1
	Tag "AutoMag"
	Inventory.PickupMessage "You got the AutoMag."
	Obituary "%o got gatted by %k's Automag."
	+AMMO_OPTIONAL
	+NOALERT
	+WIMPY_WEAPON
	States
	{
	Spawn:
		UN07 A -1
		stop

	ReadyActual:
		UN00 A 0 A_JumpIfInventory("SamsaraReloadMode",0,"ReadyActualAllowReload")
	ReadyActualDisallowReload:
		UN00 A 10 A_WeaponReady
		goto ReadyActualJump

	ReadyActualAllowReload:
		UN00 A 0 A_JumpIfInventory("UnrealAutomagClip",0,"ReadyActualDisallowReload")
		UN00 A 0 A_JumpIfInventory("PistolModeOn",1,1)
		goto ReadyActualAllowReloadSuccess
		UN00 A 0 A_JumpIfInventory("SamsaraBottomlessMagazines",1,2)
		UN00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReadyActualAllowReloadSuccess")
		UN00 A 0 A_JumpIfInventory("Clip",1,"ReadyActualAllowReloadSuccess")
		goto ReadyActualDisallowReload

	ReadyActualAllowReloadSuccess:
		UN00 A 10 A_WeaponReady(WRF_ALLOWRELOAD)
		goto ReadyActualJump

	ReadyActualJump:
		UN00 A 0 A_Jump(20,"ReadyActualIdle")
		goto ReadyActualAmmoCheck

	ReadyActualIdle:
		UN00 A 0 A_JumpIfInventory("SamsaraReloadMode",0,"ReadyActualIdleAllowReload")
	ReadyActualIdleDisallowReload:
		UN00 A 4 A_WeaponReady
		UN02 DEFGHIJKLMNO 4 A_WeaponReady
		goto ReadyActual

	ReadyActualIdleAllowReload:
		UN00 A 0 A_JumpIfInventory("UnrealAutomagClip",0,"ReadyActualIdleDisallowReload")
		UN00 A 0 A_JumpIfInventory("PistolModeOn",1,1)
		goto ReadyActualIdleAllowReloadSuccess
		UN00 A 0 A_JumpIfInventory("SamsaraBottomlessMagazines",1,2)
		UN00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReadyActualIdleAllowReloadSuccess")
		UN00 A 0 A_JumpIfInventory("Clip",1,"ReadyActualIdleAllowReloadSuccess")
		goto ReadyActualIdleDisallowReload

	ReadyActualIdleAllowReloadSuccess:
		UN00 A 4 A_WeaponReady(WRF_ALLOWRELOAD)
		UN02 DEFGHIJKLMNO 4 A_WeaponReady(WRF_ALLOWRELOAD)
		goto ReadyActualJump

	ReadyActualAmmoCheck:
		UN00 A 0 A_JumpIfInventory("SamsaraReloadMode",1,1)
		goto ReadyActual
		UN00 A 0 A_JumpIfInventory("SamsaraBottomlessMagazines",1,"ReadyActual")
		UN00 A 0 A_JumpIfInventory("UnrealAutomagClip",1,"ReadyActual")
		UN00 A 0 A_JumpIfInventory("PistolModeOn",1,1)
		goto Reload
		UN00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"Reload")
		UN00 A 0 A_JumpIfInventory("Clip",1,"Reload")
		goto ReadyActual

	Deselect:
		UN05 A 2 A_JumpIfHealthLower(1,"DeselectInstant")
		UN05 BCD 2
		UN05 EF 1
		goto DeselectInstant

	Ready: // our "Select" state
		UN04 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReadyReloadAmmo")
	ReadyStart:
		UN04 A 0 A_PlaySound("Unreal/Automag/Hammer")
		UN04 ABCDEFGHIJ 2 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
		//UN04 KL 2 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
		goto ReadyActual

	ReadyReloadAmmo:
		UN04 A 0 A_JumpIfInventory("PistolModeOn",1,"ReadyReloadAmmoReserve")
		UN04 A 0 A_JumpIfInventory("UnrealAutomagClip",0,"ReadyStart")
		UN04 A 0 A_GiveInventory("UnrealAutomagClip")
		loop

	ReadyReloadAmmoReserve:
		UN04 A 0 A_JumpIfInventory("UnrealAutomagClip",0,"ReadyStart")
		UN04 A 0 A_JumpIfInventory("SamsaraBottomlessMagazines",1,2)
		UN04 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
		UN04 A 0 A_JumpIfInventory("Clip",1,1)
		goto ReadyStart
		UN04 A 0 A_JumpIfInventory("SamsaraBottomlessMagazines",1,2)
		UN04 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
		UN04 A 0 A_TakeInventory("Clip",1)
		UN04 A 0 A_GiveInventory("UnrealAutomagClip")
		goto ReadyReloadAmmo

	Fire:
		UN00 B 0 A_JumpIfInventory("SamsaraReloadMode",1,"FireReloadable")
	FireNonReloadable:
		UN00 B 0 A_JumpIfInventory("PistolModeOn",1,1)
		goto FireStart
		UN00 B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart")
		UN00 B 0 A_JumpIfInventory("Clip",1,"FireStart")
		goto NoAmmoSwitch

	FireReloadable:
		UN00 B 0 A_JumpIfInventory("SamsaraBottomlessMagazines",1,"FireStart")
		UN00 B 0 A_JumpIfInventory("UnrealAutomagClip",1,"FireStart")
	FireReloadableDryCheck:
		UN00 B 0 A_JumpIfInventory("PistolModeOn",1,1)
		goto Reload
		UN00 B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"Reload")
		UN00 B 0 A_JumpIfInventory("Clip",1,"Reload")
		goto NoAmmoSwitch

	FireStart:
		UN00 B 0 A_AlertMonsters
		UN00 B 0 A_SpawnItemEx("UnrealBrassShell",8,-16,39,0,0,3,-90,SXF_NOCHECKPOSITION|SXF_MULTIPLYSPEED|SXF_TRANSFERPITCH)
		UN00 B 0 A_GiveInventory("SamsaraUnrealAutomagPrimaryAttackHandler")
		UN00 BC 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
		goto FireContinue

	Hold:
		UN00 D 0 A_JumpIfInventory("SamsaraReloadMode",1,"HoldReloadable")
	HoldNonReloadable:
		UN00 D 0 A_JumpIfInventory("PistolModeOn",1,1)
		goto HoldStart
		UN00 D 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"HoldStart")
		UN00 D 0 A_JumpIfInventory("Clip",1,"HoldStart")
		goto NoAmmoSwitch

	HoldReloadable:
		UN00 D 0 A_JumpIfInventory("SamsaraBottomlessMagazines",1,"HoldStart")
		UN00 D 0 A_JumpIfInventory("UnrealAutomagClip",1,"HoldStart")
		goto FireReloadableDryCheck

	HoldStart:
		UN00 D 0 A_AlertMonsters
		UN00 D 0 A_SpawnItemEx("UnrealBrassShell",8,-16,39,0,0,3,-90,SXF_NOCHECKPOSITION|SXF_MULTIPLYSPEED|SXF_TRANSFERPITCH)
		UN00 D 0 A_GiveInventory("SamsaraUnrealAutomagPrimaryAttackHandler")
	FireContinue:
		UN00 D 0 A_Jump(256,"FireContinue1","FireContinue2","FireContinue3")
	FireContinue1:
		UN00 D 2 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
		UN00 EF 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
		goto FireClick

	FireContinue2:
		UN00 G 2 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
		UN00 HI 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
		goto FireClick

	FireContinue3:
		UN00 J 2 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
		UN00 KL 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
		goto FireClick

	FireClick:
		UN00 M 0 A_GiveInventory("SamsaraUnrealAutomagClickSoundHandler")
	FireClickCheckAmmo:
		UN00 M 0 A_JumpIfInventory("SamsaraReloadMode",1,1)
		goto FireClickContinue
		UN00 M 0 A_JumpIfInventory("SamsaraBottomlessMagazines",1,"FireClickContinue")
		UN00 M 0 A_JumpIfInventory("UnrealAutomagClip",1,"FireClickContinue")
		UN00 M 0 A_JumpIfInventory("PistolModeOn",1,1)
		goto Reload
		UN00 M 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"Reload")
		UN00 M 0 A_JumpIfInventory("Clip",1,"Reload")
		goto FireClickContinue

	FireClickContinue:
		UN00 M 4 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
	// kinda messes with interpolation, but eh
	FireReFireCheck:
		UN00 M 0 A_JumpIfInventory("SamsaraReloadMode",1,"FireReFireCheckReloadable")
	FireReFireCheckNonReloadable:
		UN00 M 0 A_JumpIfInventory("PistolModeOn",1,1)
		goto FireReFire
		UN00 M 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireReFire")
		UN00 M 0 A_JumpIfInventory("Clip",1,"FireReFire")
		goto FireFinish

	FireReFireCheckReloadable:
		UN00 M 0 A_JumpIfInventory("SamsaraBottomlessMagazines",1,"FireReFire")
		UN00 M 0 A_JumpIfInventory("UnrealAutomagClip",1,"FireReFire")
		goto FireFinish

	FireReFire:
		UN00 M 0 A_ReFire
		UN00 M 0 A_JumpIfInventory("SynthFireRight",1,"AltFire")
	FireFinish:
		UN00 M 0 A_ClearReFire
		UN00 N 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
		UN00 O 5 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
	FireCheckReload:
		UN00 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"FireCheckReloadReloadable")
	FireCheckReloadNonReloadable:
		UN00 A 0 A_JumpIfInventory("PistolModeOn",1,1)
		goto ReadyActual
		UN00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReadyActual")
		UN00 A 0 A_JumpIfInventory("Clip",1,"ReadyActual")
		goto NoAmmoSwitch

	FireCheckReloadReloadable:
		UN00 A 0 A_JumpIfInventory("SamsaraBottomlessMagazines",1,"ReadyActual")
		UN00 A 0 A_JumpIfInventory("UnrealAutomagClip",1,"ReadyActual")
		UN00 A 0 A_JumpIfInventory("PistolModeOn",1,1)
		goto Reload
		UN00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"Reload")
		UN00 A 0 A_JumpIfInventory("Clip",1,"Reload")
		goto NoAmmoSwitch

	AltFire:
		UN06 F 0 A_JumpIfInventory("SamsaraReloadMode",1,"AltFireReloadable")
	AltFireNonReloadable:
		UN06 F 0 A_JumpIfInventory("PistolModeOn",1,1)
		goto AltFireStart
		UN06 F 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"AltFireStart")
		UN06 F 0 A_JumpIfInventory("Clip",1,"AltFireStart")
		goto NoAmmoSwitch

	AltFireReloadable:
		UN06 F 0 A_JumpIfInventory("SamsaraBottomlessMagazines",1,"AltFireStart")
		UN06 F 0 A_JumpIfInventory("UnrealAutomagClip",1,"AltFireStart")
		goto FireReloadableDryCheck

	AltFireStart:
		UN06 F 0 A_AlertMonsters
		UN06 FE 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
	AltFireCasingCheck:
		UN01 F 0 A_JumpIfInventory("SamsaraReloadMode",1,"AltFireCasingCheckReloadable")
	AltFireCasingCheckNonReloadable:
		UN01 F 0 A_JumpIfInventory("PistolModeOn",1,1)
		goto AltFireAttack
		UN01 F 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"AltFireAttack")
		UN01 F 0 A_JumpIfInventory("Clip",1,"AltFireAttack")
		goto AltFireContinue

	AltFireCasingCheckReloadable:
		UN01 F 0 A_JumpIfInventory("SamsaraBottomlessMagazines",1,"AltFireAttack")
		UN01 F 0 A_JumpIfInventory("UnrealAutomagClip",1,"AltFireAttack")
		goto AltFireContinue

	AltHold:
		UN01 F 0 A_JumpIfInventory("SamsaraReloadMode",1,"AltHoldReloadable")
	AltHoldNonReloadable:
		UN01 F 0 A_JumpIfInventory("PistolModeOn",1,1)
		goto AltHoldStart
		UN01 F 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"AltHoldStart")
		UN01 F 0 A_JumpIfInventory("Clip",1,"AltHoldStart")
		goto NoAmmoSwitch

	AltHoldReloadable:
		UN01 F 0 A_JumpIfInventory("SamsaraBottomlessMagazines",1,"AltHoldStart")
		UN01 F 0 A_JumpIfInventory("UnrealAutomagClip",1,"AltHoldStart")
		goto FireReloadableDryCheck

	AltHoldStart:
		UN01 F 0 A_AlertMonsters
	AltFireAttack:
		UN01 F 0 A_SpawnItemEx("UnrealBrassShell",11,-16,42,0,0,3,-90,SXF_NOCHECKPOSITION|SXF_MULTIPLYSPEED|SXF_TRANSFERPITCH)
	AltFireContinue:
		UN01 F 0 A_GiveInventory("SamsaraUnrealAutomagSecondaryAttackHandler")
		UN01 F 0 A_Jump(256,"AltFireContinue1","AltFireContinue2","AltFireContinue3","AltFireContinue4","AltFireContinue5")
	AltFireContinue1:
		UN01 F 3 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
		UN01 GH 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
		goto AltFireClick

	AltFireContinue2:
		UN01 I 3 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
		UN01 JK 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
		goto AltFireClick

	AltFireContinue3:
		UN01 L 3 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
		UN01 MN 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
		goto AltFireClick

	AltFireContinue4:
		UN01 O 3 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
		UN01 PQ 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
		goto AltFireClick

	AltFireContinue5:
		UN01 R 3 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
		UN01 ST 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
		goto AltFireClick

	AltFireClick:
		UN02 A 0 A_GiveInventory("SamsaraUnrealAutomagClickSoundHandler")
		UN02 A 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
	// kinda messes with interpolation, but eh
	AltFireReFireCheck:
		UN02 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"AltFireReFireCheckReloadable")
	AltFireReFireCheckNonReloadable:
		UN02 A 0 A_JumpIfInventory("PistolModeOn",1,1)
		goto AltFireReFire
		UN02 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"AltFireReFire")
		UN02 A 0 A_JumpIfInventory("Clip",1,"AltFireReFire")
		goto AltFireFinish

	AltFireReFireCheckReloadable:
		UN02 A 0 A_JumpIfInventory("SamsaraBottomlessMagazines",1,"AltFireReFire")
		UN02 A 0 A_JumpIfInventory("UnrealAutomagClip",1,"AltFireReFire")
		goto AltFireFinish

	AltFireReFire:
		UN02 A 0 A_JumpIfInventory("SynthFireRight",1,"AltHold")
	AltFireFinish:
		UN02 A 0 A_ClearReFire
		UN02 B 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
		//UN01 Z 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
		UN06 EF 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
	AltFireCheckReload:
		UN00 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"AltFireCheckReloadReloadable")
	AltFireCheckReloadNonReloadable:
		UN00 A 0 A_JumpIfInventory("PistolModeOn",1,1)
		goto ReadyActual
		UN00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReadyActual")
		UN00 A 0 A_JumpIfInventory("Clip",1,"ReadyActual")
		goto NoAmmoSwitch

	AltFireCheckReloadReloadable:
		UN00 A 0 A_JumpIfInventory("SamsaraBottomlessMagazines",1,"ReadyActual")
		UN00 A 0 A_JumpIfInventory("UnrealAutomagClip",1,"ReadyActual")
		UN00 A 0 A_JumpIfInventory("PistolModeOn",1,1)
		goto Reload
		UN00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"Reload")
		UN00 A 0 A_JumpIfInventory("Clip",1,"Reload")
		goto NoAmmoSwitch

	Reload:
		UN03 A 0 A_ClearReFire
		UN03 A 0 A_PlaySound("Unreal/Automag/Reload1")
		UN03 ABCDEF 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
		UN03 GHIJKLMNOPQRST 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
		UN04 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReloadAmmo")
	ReloadFinish:
		UN04 A 0 A_PlaySound("Unreal/Automag/Reload2")
		UN04 ABCDEFGHIJ 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
		goto ReadyActual

	ReloadAmmo:
		UN04 A 0 A_JumpIfInventory("PistolModeOn",1,"ReloadAmmoReserve")
		UN04 A 0 A_JumpIfInventory("UnrealAutomagClip",0,"ReloadFinish")
		UN04 A 0 A_GiveInventory("UnrealAutomagClip")
		loop

	ReloadAmmoReserve:
		UN04 A 0 A_JumpIfInventory("UnrealAutomagClip",0,"ReloadFinish")
		UN04 A 0 A_JumpIfInventory("SamsaraBottomlessMagazines",1,2)
		UN04 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
		UN04 A 0 A_JumpIfInventory("Clip",1,1)
		goto ReloadFinish
		UN04 A 0 A_JumpIfInventory("SamsaraBottomlessMagazines",1,2)
		UN04 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
		UN04 A 0 A_TakeInventory("Clip",1)
		UN04 A 0 A_GiveInventory("UnrealAutomagClip")
		goto ReloadAmmo

	// "No ammo" switch
	NoAmmoSwitch:
		TNT1 A 0 A_ClearReFire
		TNT1 A 0 A_SelectWeapon("Unreal_DispersionPistol")
		goto Deselect
	}
}

actor SamsaraUnrealAutomagPrimaryAttackHandler : Trigger
{
	States
	{
	// Ammo checking/taking stuff
	Pickup:
		TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"PickupReloadable")
	PickupNonReloadable:
		TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
		goto PickupAttack
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
		TNT1 A 0 A_JumpIfInventory("Clip",1,1)
		stop
		TNT1 A 0 A_TakeInventory("Clip",1)
		goto PickupAttack
	PickupReloadable:
		TNT1 A 0 A_JumpIfInventory("SamsaraBottomlessMagazines",1,"PickupAttack")
		TNT1 A 0 A_JumpIfInventory("UnrealAutomagClip",1,1)
		stop
		TNT1 A 0 A_TakeInventory("UnrealAutomagClip",1)
		goto PickupAttack

	// Attack stuff
	PickupAttack:
		TNT1 A 0 A_PlaySound("Unreal/Automag/Fire",CHAN_WEAPON)
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
	PickupAttackDM:
		TNT1 A 0 A_FireBullets(0,0,1,17,"UnrealPuff",FBF_NORANDOM|FBF_NOFLASH)
		goto PickupFinish
	PickupAttackCoop:
		TNT1 A 0 A_FireBullets(0,0,1,17,"UnrealPuffCoop",FBF_NORANDOM|FBF_NOFLASH)
		goto PickupFinish

	// Finishing stuff
	PickupFinish:
		TNT1 A 0 ACS_NamedExecuteWithResult("Unreal_Recoil")
		stop
	}
}

actor SamsaraUnrealAutomagSecondaryAttackHandler : Trigger
{
	States
	{
	// Ammo checking/taking stuff
	Pickup:
		TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"PickupReloadable")
	PickupNonReloadable:
		TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
		goto PickupAttack
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
		TNT1 A 0 A_JumpIfInventory("Clip",1,1)
		stop
		TNT1 A 0 A_TakeInventory("Clip",1)
		goto PickupAttack
	PickupReloadable:
		TNT1 A 0 A_JumpIfInventory("SamsaraBottomlessMagazines",1,"PickupAttack")
		TNT1 A 0 A_JumpIfInventory("UnrealAutomagClip",1,1)
		stop
		TNT1 A 0 A_TakeInventory("UnrealAutomagClip",1)
		goto PickupAttack

	// Attack stuff
	PickupAttack:
		TNT1 A 0 A_PlaySound("Unreal/Automag/Fire",CHAN_WEAPON)
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
	PickupAttackDM:
		TNT1 A 0 A_FireBullets(6,6,-1,17,"UnrealPuff",FBF_NORANDOM|FBF_NOFLASH)
		goto PickupFinish
	PickupAttackCoop:
		TNT1 A 0 A_FireBullets(6,6,-1,17,"UnrealPuffCoop",FBF_NORANDOM|FBF_NOFLASH)
		goto PickupFinish

	// Finishing stuff
	PickupFinish:
		TNT1 A 0 ACS_NamedExecuteWithResult("Unreal_Recoil")
		stop
	}
}

actor SamsaraUnrealAutomagClickSoundHandler : Trigger
{
	States
	{
	// Ammo checking stuff
	Pickup:
		TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"PickupReloadable")
	PickupNonReloadable:
		TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
		stop
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupStop")
		TNT1 A 0 A_JumpIfInventory("Clip",5,"PickupStop")
		goto PickupPlaySound
	PickupReloadable:
		TNT1 A 0 A_JumpIfInventory("SamsaraBottomlessMagazines",1,"PickupStop")
		TNT1 A 0 A_JumpIfInventory("UnrealAutomagClip",5,"PickupStop")
		goto PickupPlaySound

	// Sound stuff
	PickupPlaySound:
		TNT1 A 0 A_PlaySound("Unreal/Automag/Click",CHAN_6)
		stop
	}
}

actor UnrealAutomagClip : Ammo
{
	Inventory.MaxAmount 20
	Tag "AutoMag Clip"
	+IGNORESKILL
}
