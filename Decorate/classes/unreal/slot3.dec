Actor Unreal_FlakCannon : UnrealWeapon
{
	Weapon.SelectionOrder 1600
	Weapon.SlotNumber 3
	Weapon.AmmoType "Shell"
	Weapon.AmmoGive 20
	Weapon.AmmoUse 2
	Obituary "%o was ripped to shreds by %k's Flak Cannon."
	Inventory.PickupMessage "You got the Flak Cannon."
	Tag "Flak Cannon"
    +ALT_USES_BOTH // only one ammo type with same ammo amount use, so this makes alt fire behave as intended
	States
	{
		Spawn:
			UN00 A 10 Bright
			UN00 A 10
			loop

		ReadyActual:
			UN03 A 2 A_WeaponReady
			goto ReadyActualJump

        ReadyActualJump:
            UN03 A 0 A_Jump(24,"ReadyActualIdle")
            goto ReadyActual

		ReadyActualIdle:
			UN03 A 12 A_WeaponReady // interpolation hack
			UN07 ABABA 12 A_WeaponReady
			goto ReadyActual

		Deselect:
			UN06 A 0 A_TakeInventory("Unreal_FlakSelected")
			UN06 A 2 A_JumpIfHealthLower(1,"DeselectInstant")
			UN06 BCDEFG 2
			goto DeselectInstant

		Ready: // our "Select" state
			UN01 A 0 A_GiveInventory("Unreal_FlakSelected")
			UN01 A 0 A_PlaySound("Unreal/Flak/Down")
			UN01 ABCDEF 1 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
			UN01 GH 2 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
			UN01 I 0 A_PlaySound("Unreal/Flak/Select")
			UN01 IJKLMNO 2 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
			UN02 ABCDEFGH 2 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
			goto ReadyActual

		Fire:
			UN03 B 0 A_GiveInventory("SamsaraUnrealFlakCannonPrimaryAttackHandler")
			UN03 B 3 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			UN03 CDE 1 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			UN03 FGHIJK 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			UN05 A 0 A_PlaySound("Unreal/Flak/Barrel")
			UN05 ABCDEFGHIJ 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			UN02 A 0 A_PlaySound("Unreal/Flak/Load")
			UN02 ABCDEFGH 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
            UN03 A 0 A_CheckReload
			goto ReadyActual

		AltFire:
			UN04 A 0 A_GiveInventory("SamsaraUnrealFlakCannonSecondaryAttackHandler")
			UN04 A 3 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			UN04 CDEFGHIJK 2 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			UN02 A 0 A_PlaySound("Unreal/Flak/Load")
			UN02 ABCDEFGH 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
            UN03 A 0 A_CheckReload
			goto ReadyActual
	}
}

actor SamsaraUnrealFlakCannonPrimaryAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Shell",2,1)
        stop
        TNT1 A 0 A_TakeInventory("Shell",2)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("Unreal/Flak/Fire1",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("Unreal_FlakChunk",0,false,20)
        TNT1 A 0 A_FireCustomMissile("Unreal_FlakChunk2",random(-10,10),false,16,4,0,random(-6,6))
        TNT1 A 0 A_FireCustomMissile("Unreal_FlakChunk3",random(-10,10),false,16,2,0,random(-10,10))
        TNT1 A 0 A_FireCustomMissile("Unreal_FlakChunk4",random(-10,10),false,16,4,0,random(-6,6))
        TNT1 A 0 A_FireCustomMissile("Unreal_FlakChunk",0,false,14)
        TNT1 A 0 A_FireCustomMissile("Unreal_FlakChunk2",random(-10,10),false,16,-4,0,random(-6,6))
        TNT1 A 0 A_FireCustomMissile("Unreal_FlakChunk3",random(-10,10),false,16,-2,0,random(-10,10))
        TNT1 A 0 A_FireCustomMissile("Unreal_FlakChunk4",random(-10,10),false,16,-4,0,random(-6,6))
        goto PickupFinish

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("Unreal_FlakChunkCoop",0,false,20)
        TNT1 A 0 A_FireCustomMissile("Unreal_FlakChunk2Coop",random(-10,10),false,16,4,0,random(-6,6))
        TNT1 A 0 A_FireCustomMissile("Unreal_FlakChunk3Coop",random(-10,10),false,16,2,0,random(-10,10))
        TNT1 A 0 A_FireCustomMissile("Unreal_FlakChunk4Coop",random(-10,10),false,16,4,0,random(-6,6))
        TNT1 A 0 A_FireCustomMissile("Unreal_FlakChunkCoop",0,false,14)
        TNT1 A 0 A_FireCustomMissile("Unreal_FlakChunk2Coop",random(-10,10),false,16,-4,0,random(-6,6))
        TNT1 A 0 A_FireCustomMissile("Unreal_FlakChunk3Coop",random(-10,10),false,16,-2,0,random(-10,10))
        TNT1 A 0 A_FireCustomMissile("Unreal_FlakChunk4Coop",random(-10,10),false,16,-4,0,random(-6,6))
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 ACS_NamedExecuteWithResult("Unreal_Recoil")
        TNT1 A 0 A_JumpIfInventory("WeaponFlashesAreDisabled",1,"PickupStop")
        TNT1 A 0 A_SetBlend("E3BE2A",0.1,10)
        stop
    }
}

actor SamsaraUnrealFlakCannonSecondaryAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Shell",2,1)
        stop
        TNT1 A 0 A_TakeInventory("Shell",2)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("Unreal/Flak/Explode1",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("Unreal_FlakShell",0,false,13)
        goto PickupFinish

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("Unreal_FlakShellCoop",0,false,13)
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 ACS_NamedExecuteWithResult("Unreal_Recoil")
        TNT1 A 0 A_JumpIfInventory("WeaponFlashesAreDisabled",1,"PickupStop")
        TNT1 A 0 A_SetBlend("E3BE2A",0.1,10)
        stop
    }
}

Actor Unreal_FlakChunk
{
	Projectile
	Gravity 0.70
	+ALLOWBOUNCEONACTORS
	+BOUNCEONFLOORS
	+BOUNCEONCEILINGS
	+BOUNCEONWALLS
	+USEBOUNCESTATE
	+CANBOUNCEWATER
	+NOGRAVITY
	+HITTRACER
	BounceCount 5
	BounceSound "Unreal/FlakChunk/Bounce"
	BounceType Grenade
	Speed 50
	Damage (20)
	Radius 3
	Height 5
	BounceFactor 0.8
	DamageType "UnrealRazor"
	Var Int user_pitch;
	States
	{
		Spawn:
			UN00 A 0 A_SetUserVar(user_pitch, random(0,360))
			UN00 A 0 A_SetAngle(random(0,360))
			UN00 A 0 A_Jump(96, 1)
			Goto SpawnLoop
			UN00 A 0
			UN00 A 0 A_SetDamageType("UnrealShredded")
		SpawnLoop:
			UN00 A 0 A_SetAngle(angle-5)
			UN00 A 0 A_SetPitch(user_pitch+5)
			UN00 A 2 A_JumpIf(velx <= 0 && vely <= 0 && velz <= 0, "Remove")
			Loop
		Remove:
			UN00 AAAAAAAAAAA 2
			UN00 A 2 A_FadeOut(0.5)
			Wait
		Bounce:
			UN00 A 0 A_Gravity
			Goto SpawnLoop
		Death:
		Crash:
			TNT1 A 1
			Stop
		XDeath:
			TNT1 A 0
			TNT1 A 1 ACS_NamedExecuteWithResult("Unreal_BloodGenerator_Offsets")
			TNT1 A 0 A_GiveInventory("UnrealBloodyPuffGenerator",1,AAPTR_TRACER)	
			TNT1 A 1 A_PlaySound("Unreal/FlakChunk/HitMeat",CHAN_VOICE)
			Stop
	}
}

Actor Unreal_FlakChunkCoop : Unreal_FlakChunk { +THRUSPECIES Species "Player" }

Actor Unreal_FlakChunk2 : Unreal_FlakChunk {}
Actor Unreal_FlakChunk2Coop : Unreal_FlakChunk2 { +THRUSPECIES Species "Player" }
Actor Unreal_FlakChunk3 : Unreal_FlakChunk {}
Actor Unreal_FlakChunk3Coop : Unreal_FlakChunk3 { +THRUSPECIES Species "Player" }
Actor Unreal_FlakChunk4 : Unreal_FlakChunk {}
Actor Unreal_FlakChunk4Coop : Unreal_FlakChunk4 { +THRUSPECIES Species "Player" }

Actor Unreal_FlakShell
{
	PROJECTILE
	-NOGRAVITY
	Speed 24
	Damage (70)
	DeathSound "Unreal/Flak/Explode2"
	Radius 12
	Height 12
	Gravity 0.70
	DamageType "UnrealExplosive"
	States
	{
		Spawn:
			UN00 A 0
			UN00 A 0 ThrustThingZ(0,20,0,1)
		SpawnLoop:
			UN00 A 2 A_SpawnItemEx("UnrealSmoke", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION)
			Loop
		Death:
			TNT1 A 0 A_Explode(70, 150)
			TNT1 A 0 A_SpawnItemEX("Unreal_FlareExplosiveEffect", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION)
			TNT1 AA 0 A_CustomMissile("Unreal_FlakChunk", 0,0,Random(0,360), CMF_TRACKOWNER)
			TNT1 AA 0 A_CustomMissile("Unreal_FlakChunk2", 0,0,Random(0,360), CMF_TRACKOWNER)
			TNT1 A 0 A_CustomMissile("Unreal_FlakChunk3", 0,0,Random(0,360), CMF_TRACKOWNER)
			TNT1 A 5 A_CustomMissile("Unreal_FlakChunk4", 0,0,Random(0,360), CMF_TRACKOWNER)
			Stop
	}
}

Actor Unreal_FlakShellCoop : Unreal_FlakShell
{
	+THRUSPECIES
	Species "Player"
	States
	{
		Death:
			TNT1 A 0 A_Explode(70, 150)
			TNT1 A 0 A_SpawnItemEX("Unreal_FlareExplosiveEffect", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION)
			TNT1 AA 0 A_CustomMissile("Unreal_FlakChunkCoop", 0,0,Random(0,360), CMF_TRACKOWNER)
			TNT1 AA 0 A_CustomMissile("Unreal_FlakChunk2Coop", 0,0,Random(0,360), CMF_TRACKOWNER)
			TNT1 A 0 A_CustomMissile("Unreal_FlakChunk3Coop", 0,0,Random(0,360), CMF_TRACKOWNER)
			TNT1 A 5 A_CustomMissile("Unreal_FlakChunk4Coop", 0,0,Random(0,360), CMF_TRACKOWNER)
			Stop
	}
}
