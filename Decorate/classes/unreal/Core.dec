Actor UnrealWeapon : Weapon
{
	Weapon.BobStyle Smooth
	Weapon.BobSpeed 1.6
	Weapon.BobRangeX 0.3
	Weapon.BobRangeY 0.8
	+INVENTORY.UNDROPPABLE
	Inventory.PickupSound "Unreal/WeaponPickup"
	Inventory.RestrictedTo "UnrealPlayer"
	Tag "Unreal Weapon"
	States
	{
	Ready:
		TNT1 A 0 A_TakeInventory("UnrealWeapon")
		TNT1 A 1 A_WeaponReady
		loop

	Deselect:
	DeselectInstant:
		TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_Lower
		TNT1 A 1 A_Lower
		loop

	Select:
		TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_Raise
		TNT1 A 1 A_Raise
		loop

	Fire:
		TNT1 A 1
		goto Ready
	}
}

Actor Unreal_Combo : Boolean {}
Actor Unreal_Secondary : Boolean {}

Actor UnrealInt : TemporaryCounter { Inventory.MaxAmount 65536 }

Actor UnrealBrassShell
{
	Speed 2
	Mass 8
	Radius 2
	Height 2
	+MISSILE
	+CLIENTSIDEONLY
	+THRUACTORS
	+DROPOFF
	+NOTELEPORT
	+FLOORCLIP
	-SOLID
	States
	{
	Spawn:
		UN00 A 0
		UN00 A 0 A_SetPitch(pitch-15)
		UN00 A 1 A_SetAngle(angle-15)
		loop
	Death:
		UN00 A 0 A_JumpIf(waterlevel != 0,"Done")
		UN00 A 0 A_PlaySound("Unreal/Automag/Casing",CHAN_BODY)
		UN00 A 0 ThrustThingZ(0,4,0,0)
		goto Done
	Done:
		UN00 A 500
		UN00 A 1 A_FadeOut
		Wait
	}
}

Actor Unreal_EightballTargeter : FastProjectile
{
	Speed 2000
	Damage (0)
	Radius 2
	Height 2
	Species "Player"
	+THRUSPECIES
	+PAINLESS
	+HITTRACER
	States
	{
		Spawn:
			TNT1 A 3
			Stop
		Death:
			TNT1 A 3
			Stop
	}
}

Actor UnrealPuff
{
	+NOBLOCKMAP
	+NOGRAVITY
	+ALLOWPARTICLES
	+RANDOMIZE
	+NOEXTREMEDEATH
	+CANNOTPUSH
	+HITTRACER
	+PUFFONACTORS
	+BLOODLESSIMPACT
	RenderStyle Translucent
	Alpha 0.5
	VSpeed 1
	Mass 5
	DamageType "UnrealBullet"
	States
	{
	Spawn:
	Melee:
		TNT1 A 0
		TNT1 A 1 ACS_NamedExecuteWithResult("Unreal_BloodGenerator_Offsets")
		TNT1 A 1 A_GiveInventory("UnrealBloodyPuffGenerator",1,AAPTR_TRACER)
		Stop
	Crash:
		TNT1 A 0
		TNT1 A 0 A_SpawnItemEx("UnrealSmoke",0,0,0,0,0,2,0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
		TNT1 A 0 A_SpawnItemEx("UnrealBulletSpark2",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
		TNT1 A 1 A_PlaySound("Unreal/Ricochet")
		Stop
	}
}

Actor UnrealPuffCoop : UnrealPuff { +MTHRUSPECIES }

Actor UnrealSmoke
{
	+NOGRAVITY
	+NOEXTREMEDEATH
	RenderStyle Add
	Alpha 0.4
	Scale 2
	VSpeed 1
	Mass 5
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_Jump(256, "Type1", "Type2", "Type3", "Type4", "Type5", "Type6", "Type7", "Type8", "Type9", "Type10")
		Goto Type1
	Type1:
		UN04 ABCDEFGH 2
		Stop
	Type2:
		UN06 ABCDEFGHI 2
		Stop
	Type3:
		UN07 ABCDEFGHIJ 2
		Stop
	Type4:
		UN08 ABCDEFG 2
		Stop
	Type5:
		UN09 ABCDEFGHIJKL 2
		Stop
	Type6:
		UN10 ABCDE 2
		Stop
	Type7:
		UN11 ABCDEFGHI 2
		Stop
	Type8:
		UN12 ABCDEFGHIJK 2
		Stop
	Type9:
		UN13 ABCDEFGHIJKL 2
		Stop
	Type10:
		UN14 ABCDEFGHI 2
		Stop
	}
}

Actor UnrealMiniPuff : UnrealPuff
{
	DamageType "UnrealDamage"
	+THRUGHOST
	States
	{
		Crash:
			TNT1 A 0 A_SpawnItemEx("UnrealSmoke",0,0,0,0,0,2,0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
			TNT1 A 0 A_Jump(204,2)
			TNT1 A 0 A_SpawnItemEx("UnrealBulletSpark",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
			TNT1 A 1 A_PlaySound("Unreal/Ricochet")
			stop
	}
}

Actor UnrealMiniPuffCoop : UnrealMiniPuff { +MTHRUSPECIES }

Actor UnrealExtremePuff : UnrealPuff { DamageType "UnrealExtreme" }

Actor UnrealExtremePuffCoop : UnrealExtremePuff { +MTHRUSPECIES }

Actor UnrealBulletSpark
{
	+CLIENTSIDEONLY
	+NOINTERACTION
	+BRIGHT
	Scale 0.5
	States
	{
		Spawn:
			UN00 A 0
			UN00 A 3 A_SetAngle(random(0,360))
			UN00 B 3
			Stop
	}
}

Actor UnrealBulletSpark2 : UnrealBulletSpark {}

Actor UnrealBloodyPuff
{
	+NOGRAVITY
	RenderStyle Add
	VSpeed 1
	Mass 5
	States
	{
		Spawn:
			TNT1 AAA 0
			TNT1 A 0 A_Jump(256, "TypeO", "TypeANegative", "TypeB")
			Goto TypeO
		TypeO:
			UN15 ABCDEFGHIJK 2
			Stop
		TypeANegative:
			UN16 ABCDEFGH 2
			Stop
		TypeB:
			UN17 ABCDEFGHIJ 2
			Stop
	}
}

Actor UnrealToxicGasPuff
{
	+CLIENTSIDEONLY
	+NOGRAVITY
	RenderStyle Translucent
	Alpha 0.4
	VSpeed 5
	Mass 5
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_Jump(256, "TypeNerve", "TypeArsenic", "TypeChlorine", "TypeMustard")
		TypeNerve:
			UN19 ABCDEF 2
			Stop
		TypeArsenic:
			UN20 ABCDE 2
			Stop
		TypeChlorine:
			UN21 ABCDEFGH 2
			Stop
		TypeMustard:
			UN22 ABCDEFG 2
			Stop
	}
}

Actor UnrealBlackSmoke
{
	+CLIENTSIDEONLY
	+NOGRAVITY
	+NOEXTREMEDEATH
	+BRIGHT
	RenderStyle Translucent
	Alpha 0.4
	VSpeed 1
	Mass 5
	States
	{
		Spawn:
			UN23 ABCDEFG 2
			Stop
	}
}

Actor Unreal_FlareExplosiveEffect
{
	+CLIENTSIDEONLY
	+NOINTERACTION
	+BRIGHT
	RenderStyle Add
	Alpha 0.7
	Scale 2.0
	States
	{
		Spawn:
			UN05 ABCDEFGHIJ 2
			Stop
		}
}

Actor Unreal_EightballExplosiveEffect
{
	+CLIENTSIDEONLY
	+NOINTERACTION
	+BRIGHT
	RenderStyle Add
	Scale 1.5
	Alpha 0.6
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_Jump(256, "Explosion1", "Explosion2", "Explosion3", "Explosion4", "Explosion5")
			Goto Explosion1
		Explosion1:
			UN18 ABCDEFG 3
			Stop
		Explosion2:
			UN28 ABCDEFG 3
			Stop
		Explosion3:
			UN29 ABCDEFG 3
			Stop
		Explosion4:
			UN30 ABCDEFG 3
			Stop
		Explosion5:
			UN31 ABCDEFG 3
			Stop
	}
}

Actor Unreal_SmallExplosion : Unreal_EightballExplosiveEffect { Scale 0.5 }

Actor UnrealBloodyPuffGenerator : Trigger
{
	States
	{
		Pickup:
			TNT1 A 0 A_CheckFlag("NOBLOOD", "PickupFail")
			TNT1 A 0 A_SpawnItemEx("Server_FlyingBloodSpawner",radius*cos(CallACS("Unreal_BloodGenerator_Return",1)),radius*sin(CallACS("Unreal_BloodGenerator_Return",1)),CallACS("Unreal_BloodGenerator_Return",2),0,0,0,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR|SXF_ABSOLUTEPOSITION)
			TNT1 A 0 A_SpawnItemEx("UnrealBloodSprayGenerator",radius*cos(CallACS("Unreal_BloodGenerator_Return",1)),radius*sin(CallACS("Unreal_BloodGenerator_Return",1)),CallACS("Unreal_BloodGenerator_Return",2),0,0,-0.33,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR|SXF_ABSOLUTEPOSITION)
			TNT1 A 0 A_SpawnItemEx("UnrealBloodyPuff",radius*cos(CallACS("Unreal_BloodGenerator_Return",1)),radius*sin(CallACS("Unreal_BloodGenerator_Return",1)),CallACS("Unreal_BloodGenerator_Return",2)-16,0,0,0,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR|SXF_ABSOLUTEPOSITION)
			Stop
		PickupFail:
			TNT1 A 1 A_SpawnItemEx("UnrealSmoke",radius*cos(CallACS("Unreal_BloodGenerator_Return",1)),radius*sin(CallACS("Unreal_BloodGenerator_Return",1)),CallACS("Unreal_BloodGenerator_Return",2),0,0,0,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEPOSITION)
			Stop
	}
}

Actor Unreal_BloodGeneratorAngle : Inventory { Inventory.MaxAmount 131072 }
Actor Unreal_BloodGeneratorZ : Inventory { Inventory.MaxAmount 131072 }

Actor Unreal_UsingDPistol : Boolean {}
Actor Unreal_UsingAutomag : Boolean {}
Actor Unreal_UsingRazor : Boolean {}
Actor Unreal_UsingShock : Boolean {}
Actor Unreal_UsingStinger : Boolean {}
Actor Unreal_UsingFlak : Boolean {}
Actor Unreal_Using8Ball : Boolean {}
Actor Unreal_UsingMinigun : Boolean {}
Actor Unreal_UsingBio : Boolean {}
Actor Unreal_UsingSniper : Boolean {}
Actor Unreal_UsingCAR : Boolean {}
Actor Unreal_UsingRedeemer : Boolean {}

/*
Actor Unreal_BackPackReplacer : BackPack replaces BackPack
{
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_SpawnItemEx("UDPistolUpgrade")
			BPAK A -1
			Stop
	}
}
*/

Actor Unreal_Step
{
	+NOBLOCKMAP
	+CLIENTSIDEONLY
	+NOINTERACTION
	+NOTIMEFREEZE
	Height 4
	Radius 2
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 1 A_CheckFloor("Step")
			stop
		Step:
			TNT1 A 1 A_PlaySound("Unreal/Player/Step")
			stop
	}
}
