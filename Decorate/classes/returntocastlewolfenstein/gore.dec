Actor RTCW_BloodGenerator : Trigger
{
	States
	{
		Pickup:
			TNT1 A 0 A_CheckFlag("NOBLOOD","PickupStop")
			TNT1 A 0 A_SpawnItemEx("Server_FlyingBloodSpawner",(radius-16)*cos(CallACS("RTCW_BloodGenerator_Return",1)),(radius-16)*sin(CallACS("RTCW_BloodGenerator_Return",1)),CallACS("RTCW_BloodGenerator_Return",2),0,0,0,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR|SXF_ABSOLUTEPOSITION)
			TNT1 AAAAA 0 A_SpawnItemEx("RTCW_BloodTrail",(radius-16)*cos(CallACS("RTCW_BloodGenerator_Return",1)),(radius-16)*sin(CallACS("RTCW_BloodGenerator_Return",1)),CallACS("RTCW_BloodGenerator_Return",2),frandom(0.25,0.5)*sin(CallACS("RTCW_BloodGenerator_Return",3)),frandom(-0.5,0.5)*cos(CallACS("RTCW_BloodGenerator_Return",3)),frandom(0.05,0.25),0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR|SXF_ABSOLUTEPOSITION|SXF_ABSOLUTEVELOCITY)
			stop
	}
}

Actor RTCW_BloodGeneratorBleedAngle : Inventory { Inventory.MaxAmount 131072 }
Actor RTCW_BloodGeneratorAngle : Inventory { Inventory.MaxAmount 131072 }
Actor RTCW_BloodGeneratorZ : Inventory { Inventory.MaxAmount 131072 }

Actor RTCW_BloodTrail
{
	Height 4
	Radius 2
	Renderstyle Translucent
	+NOINTERACTION
	States
	{
		Spawn:
			RW00 ABCDEFGHIJ 1
			Stop
	}
}

Actor RTCW_Gib
{
	Height 2
	Radius 2
	Speed 2
	Gravity 0.5
	Damage (0)
	Projectile
	BounceType "Doom"
	Bouncecount 2
	Bouncesound ""
	-NOGRAVITY
	+BLOODLESSIMPACT
	+THRUACTORS
	+MOVEWITHSECTOR
	+USEBOUNCESTATE
	+CLIENTSIDEONLY
	States
	{
		Spawn:
			RW00 A 0 NoDelay A_SpawnItemEx("RTCW_BloodCloud",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
			RW00 A 0 A_SpawnItemEx("RTCW_BloodTrail",0,0,frandom(-0.5,0.5),frandom(-0.5,0.5),frandom(0.05,0.25),0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR|SXF_ABSOLUTEVELOCITY)
		SpawnLoop:
			RW00 A 2 A_SetPitch(pitch+2.5)
			Loop
		Death:
			RW00 A 0 A_CheckFloor("SplatSurface")
		Death2:
			RW00 A 100
			TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("SamsaraDecorate",31) == 1, "Death2")
			RW00 AAAAAAAA 1 A_FadeOut(0.1)
			Stop
		Bounce.Floor:
			RW00 A 0 A_SpawnItemEx("RTCW_BloodSpot",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION,128)
			Goto SpawnLoop
		SplatSurface:
			RW00 A 0 A_SpawnItemEx("RTCW_BloodSpot",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION,128)
			Goto Death+1
	}
}

Actor RTCW_BloodCloud : RTCW_SmokePuff
{
	Alpha 1.0
	Scale 0.75
	States
	{
		Spawn:
			TNT1 A 0
			RW09 AAAAAAAAAA 2 A_FadeOut(0.1)
			Stop
	}
}

Actor RTCW_Gib_Skull : RTCW_Gib {}
Actor RTCW_Gib_Abdomen : RTCW_Gib {}
Actor RTCW_Gib_Intestine : RTCW_Gib {}
Actor RTCW_Gib_Forearm : RTCW_Gib {}
Actor RTCW_Gib_Leg : RTCW_Gib {}
Actor RTCW_Gib_Foot : RTCW_Gib {}

Actor RTCW_BloodSpot
{
	+NOINTERACTION
	+CLIENTSIDEONLY
	Renderstyle Translucent
	Radius 2
	Height 4
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_SetAngle(random(0,360))
			TNT1 A 0 A_Jump(256,"Spawn1","Spawn2","Spawn3")
		Spawn1:
			RW00 A 300
			TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("SamsaraDecorate",31) == 1, "Spawn1")
			RW00 A 2 A_FadeOut(0.05)
			Wait
		Spawn2:
			RW00 B 300
			TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("SamsaraDecorate",31) == 1, "Spawn2")
			RW00 B 2 A_FadeOut(0.05)
			Wait
		Spawn3:
			RW00 C 300
			TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("SamsaraDecorate",31) == 1, "Spawn3")
			RW00 C 2 A_FadeOut(0.05)
			Wait
	}
}

Actor RTCW_DeathGibberKeep : TemporaryBoolean {}
Actor RTCW_DeathGibber
{
	Health 75
	Height 56
	Radius 20
	Mass 5000000
	Painchance "RTCWVenomBullet", 256
	DamageFactor "Normal", 0.0
	DamageFactor "RTCWVenomBullet", 1.0
	+NOPAIN
	+NOBLOOD
	+ISMONSTER
	-COUNTKILL
	+SHOOTABLE
	-SOLID
	States
	{
		Spawn:
			TNT1 A 0 ACS_NamedExecuteWithResult("RTCW_Decorate",6)
			TNT1 AAAAA 2 A_Warp(AAPTR_Master,0,0,0,0,WARPF_NOCHECKPOSITION)
			Stop
		Pain:
			TNT1 A 0 A_GiveInventory("RTCW_DeathGibberKeep",1,AAPTR_Master)
			Goto Spawn
		Death:
			TNT1 A 0 ACS_NamedExecuteWithResult("RTCW_Decorate",5)
			TNT1 A 1
			Stop
	}
}
Actor RTCW_GibSpawner
{
	+CLIENTSIDEONLY
	+NOINTERACTION
	+NOCLIP
	-TELESTOMP
	-SOLID
	+NOGRAVITY
	States
	{
		Spawn:
			TNT1 A 0 A_NoBlocking
			TNT1 A 0 A_PlaySound("RTCW/Gib",CHAN_BODY)
			TNT1 A 0 A_SpawnItemEx("UniversalBloodyHellGoreXDeathEffect",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION|SXF_TRANSFERPOINTERS)
			TNT1 A 0 A_SpawnItemEx("RTCW_Gib_Skull",0,0,48,frandom(-1.0,1.0),frandom(-1.0,1.0),4,angle,SXF_TRANSFERTRANSLATION)
			TNT1 A 0 A_SpawnItemEx("RTCW_Gib_Abdomen",0,0,44,frandom(-1.0,1.0),frandom(-1.0,1.0),4,angle,SXF_TRANSFERTRANSLATION)
			TNT1 A 0 A_SpawnItemEx("RTCW_Gib_Intestine",0,0,40,frandom(-1.0,1.0),frandom(-1.0,1.0),4,angle,SXF_TRANSFERTRANSLATION)
			TNT1 A 0 A_SpawnItemEx("RTCW_Gib_Forearm",0,12,36,frandom(-1.0,1.0),frandom(-1.0,1.0),4,angle,SXF_TRANSFERTRANSLATION)
			TNT1 A 0 A_SpawnItemEx("RTCW_Gib_Forearm",0,-12,36,frandom(-1.0,1.0),frandom(-1.0,1.0),4,angle,SXF_TRANSFERTRANSLATION)
			TNT1 A 0 A_SpawnItemEx("RTCW_Gib_Leg",0,12,20,frandom(-1.0,1.0),frandom(-1.0,1.0),4,angle,SXF_TRANSFERTRANSLATION)
			TNT1 A 0 A_SpawnItemEx("RTCW_Gib_Leg",0,-12,20,frandom(-1.0,1.0),frandom(-1.0,1.0),4,angle,SXF_TRANSFERTRANSLATION)
			TNT1 A 0 A_SpawnItemEx("RTCW_Gib_Foot",0,12,4,frandom(-1.0,1.0),frandom(-1.0,1.0),4,angle,SXF_TRANSFERTRANSLATION)
			TNT1 A 0 A_SpawnItemEx("RTCW_Gib_Foot",0,-12,4,frandom(-1.0,1.0),frandom(-1.0,1.0),4,angle,SXF_TRANSFERTRANSLATION)
			TNT1 A 1
			Stop
	}
}

Actor RTCW_RemoveCorpseChildren : Trigger
{
	States
	{
		Pickup:
			TNT1 A 0 A_GiveToChildren("RTCW_GibChildRemovalToken",1)
			Stop
	}
}

Actor RTCW_GibChildRemovalToken : Trigger
{
	States
	{
		Pickup:
			TNT1 A 0 A_CheckFlag("SEESDAGGERS","Remove")
			Stop
		Remove:
			TNT1 A 0 Thing_Remove(0)
			Stop
	}
}