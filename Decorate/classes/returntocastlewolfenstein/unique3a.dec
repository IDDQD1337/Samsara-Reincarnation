actor RTCW_ColtDualWield : Boolean {}
actor RTCW_ColtFiredLeft : Boolean {}
actor SamsaraRTCWColtDualFireState : Boolean {}

Actor RTCW_Colt : RTCW_Weapon
{
	Weapon.SlotNumber 1
	Weapon.SlotPriority 3
	Weapon.SelectionOrder 1600
	Weapon.AmmoUse 1
	Weapon.AmmoType "RTCW_ColtMagazine"
	Obituary "%o was killed (colt) by %k."
	Inventory.PickupMessage "Colt"
    Inventory.Icon "CWCMPW16"
	Tag "Colt"
	+WIMPY_WEAPON
	States
	{
		Spawn:
			RW98 A -1
			stop

		// Single
		ReadyActual:
			RW00 A 0 A_JumpIfInventory("RTCW_Kicking",1,"Kicking")
			RW00 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"ReadyActualReloadable")
		ReadyActualNonReloadable:
			RW00 A 0 A_JumpIfInventory("RTCW_ColtDualWield",1,"ReadyActualDualNonReloadable")
			RW00 A 1 A_WeaponReady
			goto ReadyActualAmmoCheck

		ReadyActualReloadable:
			RW00 A 0 A_JumpIfInventory("RTCW_ColtDualWield",1,"ReadyActualDualReloadable")
			RW00 A 1 A_WeaponReady(WRF_ALLOWRELOAD)
			goto ReadyActualAmmoCheck

		ReadyActualAmmoCheck:
			RW00 A 0 A_JumpIfInventory("SamsaraReloadMode",2,1)
			goto ReadyActual
			RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"ReadyActual")
			RW00 A 0 A_JumpIfInventory("RTCW_ColtMagazine",1,"ReadyActual")
			RW00 A 0 A_JumpIfInventory("RTCW_ColtDualWield",1,"ReadyActualAmmoCheckDual") // if we're dual wielding, check if the other magazine has ammo as well
			goto ReloadCheck

		Deselect:
			RW00 H 0 A_JumpIfHealthLower(1,"DeselectInstant")
			RW00 H 0 A_GiveInventory("SamsaraRTCWWeaponSwitchSoundPlayer")
			RW00 H 2 A_JumpIfInventory("RTCW_ColtDualWield",1,"DeselectDual")
			RW00 IJK 2
			goto DeselectInstant

		Ready: // our "Select" state
			RW00 A 0 A_TakeInventory("RTCW_WeaponToken",256)
			RW00 A 0 A_JumpIfInventory("RTCW_ColtDualWield",1,"ReadyDual")
			RW00 L 2 A_GiveInventory("RTCW_WeaponToken",4)
			RW00 MNO 2
			goto ReadyActual

		Fire:
			RW00 B 0 A_JumpIfInventory("RTCW_ColtDualWield",1,"FireDual")
			RW00 B 0 A_JumpIfInventory("SamsaraReloadMode",2,"FireReloadable")
		FireNonReloadable:
			RW00 B 0 A_JumpIfInventory("PistolModeOn",1,1)
			goto FireStart
			RW00 B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart")
			RW00 B 0 A_JumpIfInventory("RTCW_AlliedAmmo1",1,"FireStart")
			goto FireDry

		FireReloadable:
			RW00 B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireStart")
			RW00 B 0 A_JumpIfInventory("RTCW_ColtMagazine",1,"FireStart")
		FireReloadableDryCheck:
			RW00 B 0 A_JumpIfInventory("PistolModeOn",1,1)
			goto ReloadStart
			RW00 B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReloadStart")
			RW00 B 0 A_JumpIfInventory("RTCW_AlliedAmmo1",1,"ReloadStart")
			goto FireDry

		FireDry:
			RW00 A 0 A_ClearReFire
			RW00 A 0 A_GiveInventory("SamsaraRTCWWeaponDrySoundPlayer")
			RW00 A 0 A_JumpIfInventory("RTCW_ColtDualWield",1,"FireDualDry")
			RW00 A 17 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
		FireDryFinish:
			RW00 A 0 A_SelectWeapon("RTCW_Luger")
			goto Deselect

		FireStart:
			RW00 B 0 A_GiveInventory("RTCW_FiringToken",1)
			RW00 B 0 A_GiveInventory("RTCW_BlazkoWeaponAlert",1)
			RW00 B 0 A_GunFlash("FireFlash")
			RW00 B 0 A_GiveInventory("SamsaraRTCWColtRightAttackHandler")
			RW00 BCDEFGA 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto ReadyActual

		FireFlash:
			TNT1 A 0 A_Jump(256,1,2)
			RW03 B 2 Bright
			stop
			RW08 B 2 Bright
			stop

		Reload:
		ReloadCheck:
			RW00 A 0 A_JumpIfInventory("RTCW_ColtDualWield",1,"ReloadCheckDual")
			RW00 A 0 A_JumpIfInventory("RTCW_ColtMagazine",0,"ReadyActual")
			RW00 A 0 A_JumpIfInventory("PistolModeOn",1,1)
			goto ReloadStart
			RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
			RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReloadStart")
			RW00 A 0 A_JumpIfInventory("RTCW_AlliedAmmo1",1,"ReloadStart")
			goto ReadyActual

		ReloadStart:
			RW00 Q 0 A_GiveInventory("SamsaraRTCWColtSingleReloadSoundPlayer1")
			//RW00 Q 0 A_JumpIfInventory("RTCW_ColtMagazine",1,1)
			goto ReloadStartAmmoless
			RW00 "QRSTUVWXYZ[\]" 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW01 ABCDEFGHIJKLM 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW00 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"ReloadAmmo")
		ReloadFinish:
			RW00 A 0
			goto ReadyActual

		ReloadAmmo:
			RW00 A 0 A_JumpIfInventory("PistolModeOn",1,"ReloadAmmoReserve")
			RW00 A 0 A_JumpIfInventory("RTCW_ColtMagazine",0,"ReloadFinish")
			RW00 A 0 A_GiveInventory("RTCW_ColtMagazine")
			loop

		ReloadAmmoReserve:
			RW00 A 0 A_JumpIfInventory("RTCW_ColtMagazine",0,"ReloadFinish")
			RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
			RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
			RW00 A 0 A_JumpIfInventory("RTCW_AlliedAmmo1",1,1)
			goto ReloadFinish
			RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
			RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
			RW00 A 0 A_TakeInventory("RTCW_AlliedAmmo1",1)
			RW00 A 0 A_GiveInventory("RTCW_ColtMagazine")
			goto ReloadAmmo

		ReloadStartAmmoless:
			RW00 "QRSTUVWXYZ[\]" 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW01 ABCDEFGH 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW01 IJKLM 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW01 N 0 A_GiveInventory("SamsaraRTCWColtSingleReloadSoundPlayer2")
			RW01 NOPQR 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW01 S 0 A_JumpIfInventory("SamsaraReloadMode",2,"ReloadAmmoAmmoless")
		ReloadFinishAmmoless:
			RW01 STUVWXYZ 2 A_WeaponReady
			goto ReadyActual

		ReloadAmmoAmmoless:
			RW01 S 0 A_JumpIfInventory("PistolModeOn",1,"ReloadAmmoAmmolessReserve")
			RW01 S 0 A_JumpIfInventory("RTCW_ColtMagazine",0,"ReloadFinishAmmoless")
			RW01 S 0 A_GiveInventory("RTCW_ColtMagazine")
			loop

		ReloadAmmoAmmolessReserve:
			RW01 S 0 A_JumpIfInventory("RTCW_ColtMagazine",0,"ReloadFinishAmmoless")
			RW01 S 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
			RW01 S 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
			RW01 S 0 A_JumpIfInventory("RTCW_AlliedAmmo1",1,1)
			goto ReloadFinishAmmoless
			RW01 S 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
			RW01 S 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
			RW01 S 0 A_TakeInventory("RTCW_AlliedAmmo1",1)
			RW01 S 0 A_GiveInventory("RTCW_ColtMagazine")
			goto ReloadAmmoAmmoless

		Kicking:
			RW00 A 0 A_GunFlash("KickingFoot")
		KickingDelay:
			RW00 A 0 A_JumpIfInventory("RTCW_ColtDualWield",1,"KickingDelayDual")
			RW00 A 18 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
		KickingDelayFinish:
			RW00 A 0 A_TakeInventory("RTCW_Kicking",1)
			goto ReadyActual

		// Dual Init
		AltFire:
			RW01 Z 0 A_GiveInventory("SamsaraRTCWWeaponSwitchSoundPlayer")
			RW01 Z 0 A_JumpIfInventory("RTCW_ColtDualWield",1,"AltFireSingle")
		AltFireDual:
			RW01 Z 0 A_GiveInventory("RTCW_ColtDualWield")
			RW01 Z 0 A_TakeInventory("RTCW_WeaponToken",256)
			RW01 Z 0 A_GiveInventory("RTCW_WeaponToken",5)
			RW01 "Z[\]" 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW06 "[\]" 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW07 AB 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto ReadyActual

		AltFireSingle:
			RW06 R 0 A_TakeInventory("RTCW_ColtDualWield")
			RW06 R 0 A_TakeInventory("RTCW_WeaponToken",256)
			RW06 R 0 A_GiveInventory("RTCW_WeaponToken",4)
			RW06 RSTU 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			TNT1 A 0
			RW02 JKL 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto ReadyActual

		// Dual
		ReadyActualDualNonReloadable:
			RW04 A 1 A_WeaponReady
			goto ReadyActualAmmoCheck

		ReadyActualDualReloadable:
			RW04 A 1 A_WeaponReady(WRF_ALLOWRELOAD)
			goto ReadyActualAmmoCheck

		ReadyActualAmmoCheckDual:
			RW04 A 0 A_JumpIfInventory("RTCW_ColtMagazine2",1,"ReadyActual")
			goto ReloadCheck

		DeselectDual:
			RW04 NOPQR 2
			goto DeselectInstant

		ReadyDual:
			RW04 S 2 A_GiveInventory("RTCW_WeaponToken",5)
			RW04 TUVW 2
			goto ReadyActual

		FireDual:
			RW04 B 0 A_JumpIfInventory("SamsaraReloadMode",2,"FireDualReloadable")
		FireDualNonReloadable:
			RW04 B 0 A_JumpIfInventory("PistolModeOn",1,1)
			goto FireDualStart
			RW04 B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireDualStart")
			RW04 B 0 A_JumpIfInventory("RTCW_AlliedAmmo1",1,"FireDualStart")
			goto FireDry

		FireDualReloadable:
			RW04 B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireDualStart")
			RW04 B 0 A_JumpIfInventory("RTCW_ColtMagazine",1,"FireDualStart")
			RW04 B 0 A_JumpIfInventory("RTCW_ColtMagazine2",1,"FireDualStart")
		FireDualReloadableDryCheck:
			RW04 B 0 A_JumpIfInventory("PistolModeOn",1,1)
			goto ReloadStartDual
			RW04 B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReloadStartDual")
			RW04 B 0 A_JumpIfInventory("RTCW_AlliedAmmo1",1,"ReloadStartDual")
			goto FireDry

		FireDualDry:
			RW04 A 17 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto FireDryFinish

		FireDualStart:
			RW04 B 0 A_JumpIfInventory("RTCW_ColtFiredLeft",1,"FireDualCheckRight")
			goto FireDualCheckLeft

		FireDualCheckLeft:
			RW04 B 0 A_JumpIfInventory("SamsaraReloadMode",2,1)
			goto FireDualStartLeft
			RW04 B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireDualStartLeft")
			RW04 B 0 A_JumpIfInventory("RTCW_ColtMagazine2",1,"FireDualStartLeft")
			goto FireDualStartRight

		FireDualStartLeft:
			RW04 B 0 A_JumpIfInventory("SamsaraRTCWColtDualFireState",1,2)
			RW04 B 0 A_GiveInventory("SamsaraRTCWColtDualFireState")
			goto FireDualStartLeftAttack
			RW04 B 0 A_TakeInventory("SamsaraRTCWColtDualFireState")
			goto FireDualStartLeftAttack

		FireDualStartLeftAttack:
			RW04 B 0 A_GiveInventory("RTCW_FiringToken",1)
			RW04 B 0 A_GiveInventory("RTCW_BlazkoWeaponAlert",1)
			RW04 B 0 A_GiveInventory("RTCW_ColtFiredLeft")
			RW04 B 0 A_GunFlash("FireFlashDualLeft")
			RW04 B 0 A_GiveInventory("SamsaraRTCWColtLeftAttackHandler")
			RW04 B 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW04 C 0 A_JumpIfInventory("SamsaraRTCWColtDualFireState",1,"FireDualStartLeft1")
			goto FireDualStartLeft2

		FireDualStartLeft1:
			RW04 C 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW04 D 0 A_JumpIfInventory("SamsaraHasDoubleFiringSpeed",1,2)
			RW04 DE 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW04 F 0 A_JumpIfInventory("SamsaraHasDoubleFiringSpeed",1,2)
			RW04 FG 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW04 H 0 A_JumpIfInventory("SamsaraReloadMode",2,"FireDualFinishLeftReloadable")
			goto FireDualFinishLeftNonReloadable

		FireDualStartLeft2:
			RW04 C 0 A_JumpIfInventory("SamsaraHasDoubleFiringSpeed",1,2)
			RW04 CD 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW04 E 0 A_JumpIfInventory("SamsaraHasDoubleFiringSpeed",1,2)
			RW04 EF 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW04 G 0 A_JumpIfInventory("SamsaraHasDoubleFiringSpeed",1,2)
			RW04 G 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW04 H 0 A_JumpIfInventory("SamsaraReloadMode",2,"FireDualFinishLeftReloadable")
			goto FireDualFinishLeftNonReloadable

		FireDualFinishLeftNonReloadable:
			RW04 H 0 A_JumpIfInventory("PistolModeOn",1,1)
			goto FireDualFinishLeftReFire
			RW04 H 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireDualFinishLeftReFire")
			RW04 H 0 A_JumpIfInventory("RTCW_AlliedAmmo1",1,"FireDualFinishLeftReFire")
			RW04 H 0 A_JumpIfInventory("SamsaraRTCWColtDualFireState",1,"FireDualFinishLeft1")
			goto FireDualFinishLeft2

		FireDualFinishLeftReloadable:
			RW04 H 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireDualFinishLeftReFire")
			RW04 H 0 A_JumpIfInventory("RTCW_ColtMagazine",1,"FireDualFinishLeftReFire") // have magazine ammo in the right colt?
			RW04 H 0 A_JumpIfInventory("SamsaraRTCWColtDualFireState",1,"FireDualFinishLeft1")
			goto FireDualFinishLeft2

		FireDualFinishLeftReFire:
			RW04 H 0 A_ReFire
			RW04 H 0 A_JumpIfInventory("SamsaraRTCWColtDualFireState",1,"FireDualFinishLeft1")
			goto FireDualFinishLeft2

		FireDualFinishLeft1:
			RW04 H 0 A_JumpIfInventory("SamsaraHasDoubleFiringSpeed",1,2)
			RW04 H 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW04 A 6 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto ReadyActual

		FireDualFinishLeft2:
			RW04 H 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW04 A 6 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto ReadyActual

		FireFlashDualLeft:
			TNT1 A 0 A_Jump(256,1,2)
			RW03 C 2 Bright
			stop
			RW08 C 2 Bright
			stop

		FireDualCheckRight:
			RW04 I 0 A_JumpIfInventory("SamsaraReloadMode",2,1)
			goto FireDualStartRight
			RW04 I 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireDualStartRight")
			RW04 I 0 A_JumpIfInventory("RTCW_ColtMagazine",1,"FireDualStartRight")
			goto FireDualStartLeft

		FireDualStartRight:
			RW04 I 0 A_JumpIfInventory("SamsaraRTCWColtDualFireState",1,2)
			RW04 I 0 A_GiveInventory("SamsaraRTCWColtDualFireState")
			goto FireDualStartRightAttack
			RW04 I 0 A_TakeInventory("SamsaraRTCWColtDualFireState")
			goto FireDualStartRightAttack

		FireDualStartRightAttack:
			RW04 I 0 A_GiveInventory("RTCW_FiringToken",1)
			RW04 I 0 A_GiveInventory("RTCW_BlazkoWeaponAlert",1)
			RW04 I 0 A_TakeInventory("RTCW_ColtFiredLeft")
			RW04 I 0 A_GunFlash("FireFlashDualRight")
			RW04 I 0 A_GiveInventory("SamsaraRTCWColtRightAttackHandler")
			RW04 I 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW04 J 0 A_JumpIfInventory("SamsaraRTCWColtDualFireState",1,"FireDualStartRight1")
			goto FireDualStartRight2

		FireDualStartRight1:
			RW04 J 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW04 K 0 A_JumpIfInventory("SamsaraHasDoubleFiringSpeed",1,2)
			RW04 KL 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW04 M 0 A_JumpIfInventory("SamsaraHasDoubleFiringSpeed",1,2)
			RW04 MN 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW04 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"FireDualFinishRightReloadable")
			goto FireDualFinishRightNonReloadable

		FireDualStartRight2:
			RW04 J 0 A_JumpIfInventory("SamsaraHasDoubleFiringSpeed",1,2)
			RW04 JK 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW04 L 0 A_JumpIfInventory("SamsaraHasDoubleFiringSpeed",1,2)
			RW04 LM 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW04 N 0 A_JumpIfInventory("SamsaraHasDoubleFiringSpeed",1,2)
			RW04 N 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW04 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"FireDualFinishRightReloadable")
			goto FireDualFinishRightNonReloadable

		FireDualFinishRightNonReloadable:
			RW04 A 0 A_JumpIfInventory("PistolModeOn",1,1)
			goto FireDualFinishRightReFire
			RW04 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireDualFinishRightReFire")
			RW04 A 0 A_JumpIfInventory("RTCW_AlliedAmmo1",1,"FireDualFinishRightReFire")
			RW04 A 0 A_JumpIfInventory("SamsaraRTCWColtDualFireState",1,"FireDualFinishRight1")
			goto FireDualFinishRight2

		FireDualFinishRightReloadable:
			RW04 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireDualFinishRightReFire")
			RW04 A 0 A_JumpIfInventory("RTCW_ColtMagazine2",1,"FireDualFinishRightReFire") // have magazine ammo in the left colt?
			RW04 A 0 A_JumpIfInventory("SamsaraRTCWColtDualFireState",1,"FireDualFinishRight1")
			goto FireDualFinishRight2

		FireDualFinishRightReFire:
			RW04 A 0 A_ReFire
			RW04 A 0 A_JumpIfInventory("SamsaraRTCWColtDualFireState",1,"FireDualFinishRight1")
			goto FireDualFinishRight2

		FireDualFinishRight1:
			RW04 A 0 A_JumpIfInventory("SamsaraHasDoubleFiringSpeed",1,2)
			RW04 A 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW04 A 6 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto ReadyActual

		FireDualFinishRight2:
			RW04 A 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW04 A 6 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto ReadyActual

		FireFlashDualRight:
			TNT1 A 0 A_Jump(256,1,2)
			RW03 D 2 Bright
			stop
			RW08 D 2 Bright
			stop

		ReloadCheckDual:
			RW04 A 0 A_JumpIfInventory("RTCW_ColtMagazine",0,1)
			goto ReloadCheckDualSuccess
			RW04 A 0 A_JumpIfInventory("RTCW_ColtMagazine2",0,"ReadyActual")
		ReloadCheckDualSuccess:
			RW04 A 0 A_JumpIfInventory("PistolModeOn",1,1)
			goto ReloadStartDual
			RW04 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
			RW04 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReloadStartDual")
			RW04 A 0 A_JumpIfInventory("RTCW_AlliedAmmo1",1,"ReloadStartDual")
			goto ReadyActual

		ReloadStartDual:
			RW04 Y 0 A_GiveInventory("SamsaraRTCWColtDualReloadSoundPlayer1")
			/*RW04 Y 0 A_JumpIfInventory("RTCW_ColtMagazine2",1,1)
			goto ReloadStartAmmolessDual
			RW04 Y 0 A_JumpIfInventory("RTCW_ColtMagazine",1,1)*/
			goto ReloadStartAmmolessDual
			RW04 "YZ[\]" 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW05 ABCDEFGHIJKLMNOPQRSTUV 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW04 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"ReloadAmmoDualLeft")
			goto ReloadFinishDual

		ReloadStartAmmolessDual:
			RW04 "YZ[\]" 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW05 ABCDEFGHIJKLMNOPQRSTUV 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW05 W 0 A_GiveInventory("SamsaraRTCWColtDualReloadSoundPlayer2")
			RW05 "WXYZ[\]" 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW06 ABCDEFGHIJKLMNO 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW04 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"ReloadAmmoDualLeft")
			goto ReloadFinishDual

		ReloadFinishDual:
			RW04 A 0
			goto ReadyActual

		ReloadAmmoDualLeft:
			RW04 A 0 A_JumpIfInventory("PistolModeOn",1,"ReloadAmmoReserveDualLeft")
			RW04 A 0 A_JumpIfInventory("RTCW_ColtMagazine2",0,"ReloadAmmoDualRight")
			RW04 A 0 A_GiveInventory("RTCW_ColtMagazine2")
			loop

		ReloadAmmoReserveDualLeft:
			RW04 A 0 A_JumpIfInventory("RTCW_ColtMagazine2",0,"ReloadAmmoDualRight")
			RW04 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
			RW04 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
			RW04 A 0 A_JumpIfInventory("RTCW_AlliedAmmo1",1,1)
			goto ReloadAmmoDualRight
			RW04 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
			RW04 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
			RW04 A 0 A_TakeInventory("RTCW_AlliedAmmo1",1)
			RW04 A 0 A_GiveInventory("RTCW_ColtMagazine2")
			goto ReloadAmmoDualLeft

		ReloadAmmoDualRight:
			RW04 A 0 A_JumpIfInventory("PistolModeOn",1,"ReloadAmmoReserveDualRight")
			RW04 A 0 A_JumpIfInventory("RTCW_ColtMagazine",0,"ReloadFinishDual")
			RW04 A 0 A_GiveInventory("RTCW_ColtMagazine")
			loop

		ReloadAmmoReserveDualRight:
			RW04 A 0 A_JumpIfInventory("RTCW_ColtMagazine",0,"ReloadFinishDual")
			RW04 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
			RW04 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
			RW04 A 0 A_JumpIfInventory("RTCW_AlliedAmmo1",1,1)
			goto ReloadFinishDual
			RW04 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
			RW04 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
			RW04 A 0 A_TakeInventory("RTCW_AlliedAmmo1",1)
			RW04 A 0 A_GiveInventory("RTCW_ColtMagazine")
			goto ReloadAmmoDualRight

		KickingDelayDual:
			RW04 A 18 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto KickingDelayFinish
	}
}

actor SamsaraRTCWColtRightAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"PickupReloadable")
      PickupNonReloadable:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto PickupAttack
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("RTCW_AlliedAmmo1",1,1)
        stop
        TNT1 A 0 A_TakeInventory("RTCW_AlliedAmmo1",1)
        goto PickupAttack

      PickupReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("RTCW_ColtMagazine",1,1)
        stop
        TNT1 A 0 A_TakeInventory("RTCW_ColtMagazine",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",2,"PickupAttackET")
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupAttackRTCWConsole")
      // Original sounds
      PickupAttackRTCWPC:
        TNT1 A 0 A_PlaySound("RTCW/Colt/Fire",CHAN_WEAPON)
        TNT1 A 0 A_SpawnItemEx("RTCW_Casing",4,-10*cos(-pitch),43+(10*sin(-pitch)),1,0,2*cos(-pitch),-random(85,95),SXF_TRANSFERPITCH)
        TNT1 A 0 A_FireBullets(0,0,1,0,"SamsaraBlankPuff",FBF_NORANDOM|FBF_NOFLASH)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackRTCWPCCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackRTCWPCCoop")
      PickupAttackRTCWPCDM:
        TNT1 A 0 A_FireCustomMissile("RTCW_ColtTracer",frandom(-2.5,2.5),false,4,9,0,frandom(-2.5,2.5))
        stop

      PickupAttackRTCWPCCoop:
        TNT1 A 0 A_FireCustomMissile("RTCW_ColtTracerCoop",frandom(-2.5,2.5),false,4,9,0,frandom(-2.5,2.5))
        stop

      // Console sounds
      PickupAttackRTCWConsole:
        TNT1 A 0 A_PlaySound("RTCWConsole/Colt/Fire",CHAN_WEAPON)
        TNT1 A 0 A_SpawnItemEx("RTCW_Casing",4,-10*cos(-pitch),43+(10*sin(-pitch)),1,0,2*cos(-pitch),-random(85,95),SXF_TRANSFERPITCH)
        TNT1 A 0 A_FireBullets(0,0,1,0,"SamsaraBlankPuff",FBF_NORANDOM|FBF_NOFLASH)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackRTCWConsoleCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackRTCWConsoleCoop")
      PickupAttackRTCWConsoleDM:
        TNT1 A 0 A_FireCustomMissile("RTCW_ColtTracerSoundConsole",frandom(-2.5,2.5),false,4,9,0,frandom(-2.5,2.5))
        stop

      PickupAttackRTCWConsoleCoop:
        TNT1 A 0 A_FireCustomMissile("RTCW_ColtTracerCoopSoundConsole",frandom(-2.5,2.5),false,4,9,0,frandom(-2.5,2.5))
        stop

      // Enemy Territory sounds
      PickupAttackET:
        TNT1 A 0 A_PlaySound("RTCWET/Colt/Fire",CHAN_WEAPON)
        TNT1 A 0 A_SpawnItemEx("RTCW_Casing",4,-10*cos(-pitch),43+(10*sin(-pitch)),1,0,2*cos(-pitch),-random(85,95),SXF_TRANSFERPITCH)
        TNT1 A 0 A_FireBullets(0,0,1,0,"SamsaraBlankPuff",FBF_NORANDOM|FBF_NOFLASH)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackETCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackETCoop")
      PickupAttackETDM:
        TNT1 A 0 A_FireCustomMissile("RTCW_ColtTracerSoundET",frandom(-2.5,2.5),false,4,9,0,frandom(-2.5,2.5))
        stop

      PickupAttackETCoop:
        TNT1 A 0 A_FireCustomMissile("RTCW_ColtTracerCoopSoundET",frandom(-2.5,2.5),false,4,9,0,frandom(-2.5,2.5))
        stop
    }
}

actor SamsaraRTCWColtLeftAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"PickupReloadable")
      PickupNonReloadable:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto PickupAttack
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("RTCW_AlliedAmmo1",1,1)
        stop
        TNT1 A 0 A_TakeInventory("RTCW_AlliedAmmo1",1)
        goto PickupAttack

      PickupReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("RTCW_ColtMagazine2",1,1)
        stop
        TNT1 A 0 A_TakeInventory("RTCW_ColtMagazine2",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",2,"PickupAttackET")
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupAttackRTCWConsole")
      // Original sounds
      PickupAttackRTCWPC:
        TNT1 A 0 A_PlaySound("RTCW/Colt/Fire",CHAN_WEAPON)
        TNT1 A 0 A_SpawnItemEx("RTCW_Casing",-4,-10*cos(-pitch),43+(10*sin(-pitch)),-1,0,2*cos(-pitch),-random(85,95),SXF_TRANSFERPITCH)
        TNT1 A 0 A_FireBullets(0,0,1,0,"SamsaraBlankPuff",FBF_NORANDOM|FBF_NOFLASH)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackRTCWPCCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackRTCWPCCoop")
      PickupAttackRTCWPCDM:
        TNT1 A 0 A_FireCustomMissile("RTCW_ColtTracer",frandom(-2.5,2.5),false,-4,9,0,frandom(-2.5,2.5))
        stop

      PickupAttackRTCWPCCoop:
        TNT1 A 0 A_FireCustomMissile("RTCW_ColtTracerCoop",frandom(-2.5,2.5),false,-4,9,0,frandom(-2.5,2.5))
        stop

      // Console sounds
      PickupAttackRTCWConsole:
        TNT1 A 0 A_PlaySound("RTCWConsole/Colt/Fire",CHAN_WEAPON)
        TNT1 A 0 A_SpawnItemEx("RTCW_Casing",-4,-10*cos(-pitch),43+(10*sin(-pitch)),-1,0,2*cos(-pitch),-random(85,95),SXF_TRANSFERPITCH)
        TNT1 A 0 A_FireBullets(0,0,1,0,"SamsaraBlankPuff",FBF_NORANDOM|FBF_NOFLASH)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackRTCWConsoleCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackRTCWConsoleCoop")
      PickupAttackRTCWConsoleDM:
        TNT1 A 0 A_FireCustomMissile("RTCW_ColtTracerSoundConsole",frandom(-2.5,2.5),false,-4,9,0,frandom(-2.5,2.5))
        stop

      PickupAttackRTCWConsoleCoop:
        TNT1 A 0 A_FireCustomMissile("RTCW_ColtTracerCoopSoundConsole",frandom(-2.5,2.5),false,-4,9,0,frandom(-2.5,2.5))
        stop

      // Enemy Territory sounds
      PickupAttackET:
        TNT1 A 0 A_PlaySound("RTCWET/Colt/Fire",CHAN_WEAPON)
        TNT1 A 0 A_SpawnItemEx("RTCW_Casing",-4,-10*cos(-pitch),43+(10*sin(-pitch)),-1,0,2*cos(-pitch),-random(85,95),SXF_TRANSFERPITCH)
        TNT1 A 0 A_FireBullets(0,0,1,0,"SamsaraBlankPuff",FBF_NORANDOM|FBF_NOFLASH)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackETCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackETCoop")
      PickupAttackETDM:
        TNT1 A 0 A_FireCustomMissile("RTCW_ColtTracerSoundET",frandom(-2.5,2.5),false,-4,9,0,frandom(-2.5,2.5))
        stop

      PickupAttackETCoop:
        TNT1 A 0 A_FireCustomMissile("RTCW_ColtTracerCoopSoundET",frandom(-2.5,2.5),false,-4,9,0,frandom(-2.5,2.5))
        stop
    }
}

actor SamsaraRTCWColtSingleReloadSoundPlayer1 : Trigger
{
    States
    {
      // Sound playing stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",2,"PickupET")
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupRTCWConsole")
      // Original sounds
      PickupRTCWPC:
        TNT1 A 0 A_PlaySound("RTCW/Colt/Reload",CHAN_6)
        stop

      // Console sounds
      PickupRTCWConsole:
        TNT1 A 0 A_PlaySound("RTCWConsole/Colt/Reload",CHAN_6)
        stop

      // Enemy Territory sounds
      PickupET:
        TNT1 A 0 A_PlaySound("RTCWET/Colt/Reload",CHAN_6)
        stop
    }
}

actor SamsaraRTCWColtSingleReloadSoundPlayer2 : Trigger
{
    States
    {
      // Sound playing stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",2,"PickupET")
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupRTCWConsole")
      // Original sounds
      PickupRTCWPC:
        TNT1 A 0 A_PlaySound("RTCW/Colt/Reload1_2",CHAN_6)
        stop

      // Console sounds
      PickupRTCWConsole:
        TNT1 A 0 A_PlaySound("RTCWConsole/Colt/Reload1_2",CHAN_6)
        stop

      // Enemy Territory sounds
      PickupET:
        TNT1 A 0 A_PlaySound("RTCWET/Colt/Reload1_2",CHAN_6)
        stop
    }
}

actor SamsaraRTCWColtDualReloadSoundPlayer1 : Trigger
{
    States
    {
      // Sound playing stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",2,"PickupET")
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupRTCWConsole")
      // Original sounds
      PickupRTCWPC:
        TNT1 A 0 A_PlaySound("RTCW/Colt/Reload2",CHAN_6)
        stop

      // Console sounds
      PickupRTCWConsole:
        TNT1 A 0 A_PlaySound("RTCWConsole/Colt/Reload2",CHAN_6)
        stop

      // Enemy Territory sounds
      PickupET:
        TNT1 A 0 A_PlaySound("RTCWET/Colt/Reload2",CHAN_6)
        stop
    }
}

actor SamsaraRTCWColtDualReloadSoundPlayer2 : Trigger
{
    States
    {
      // Sound playing stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",2,"PickupET")
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupRTCWConsole")
      // Original sounds
      PickupRTCWPC:
        TNT1 A 0 A_PlaySound("RTCW/Colt/Reload2_2",CHAN_6)
        stop

      // Console sounds
      PickupRTCWConsole:
        TNT1 A 0 A_PlaySound("RTCWConsole/Colt/Reload2_2",CHAN_6)
        stop

      // Enemy Territory sounds
      PickupET:
        TNT1 A 0 A_PlaySound("RTCWET/Colt/Reload2_2",CHAN_6)
        stop
    }
}

actor RTCW_ColtMagazine : Ammo
{
	Inventory.MaxAmount 8
	Tag "Colt Magazine (Right)"
	+IGNORESKILL
}

actor RTCW_ColtMagazine2 : Ammo
{
	Inventory.MaxAmount 8
	Tag "Colt Magazine (Left)"
	+IGNORESKILL
}

//Was 8 damage, now 16
Actor RTCW_ColtTracer : RTCW_Tracer
{
	States
	{
		Crash:
		Death:
			TNT1 A 0 A_Explode(CallACS("RTCW_BulletDamage", 20, 0),8,!XF_HURTSOURCE,0,8)
			Goto Super::Death

		XDeath:
			TNT1 A 0 A_Explode(CallACS("RTCW_BulletDamage", 20, 0),8,!XF_HURTSOURCE,0,8)
			Goto Super::XDeath
	}
}

Actor RTCW_ColtTracerCoop : RTCW_ColtTracer { +THRUSPECIES Species "Player" }

Actor RTCW_ColtTracerSoundConsole : RTCW_TracerSoundConsole
{
	States
	{
		Crash:
		Death:
			TNT1 A 0 A_Explode(CallACS("RTCW_BulletDamage", 20, 1),8,!XF_HURTSOURCE,0,8)
			Goto Super::Death

		XDeath:
			TNT1 A 0 A_Explode(CallACS("RTCW_BulletDamage", 20, 1),8,!XF_HURTSOURCE,0,8)
			Goto Super::XDeath
	}
}

Actor RTCW_ColtTracerCoopSoundConsole : RTCW_ColtTracerSoundConsole { +THRUSPECIES Species "Player" }

Actor RTCW_ColtTracerSoundET : RTCW_TracerSoundET
{
	States
	{
		Crash:
		Death:
			TNT1 A 0 A_Explode(CallACS("RTCW_BulletDamage", 20, 2),8,!XF_HURTSOURCE,0,8)
			Goto Super::Death

		XDeath:
			TNT1 A 0 A_Explode(CallACS("RTCW_BulletDamage", 20, 2),8,!XF_HURTSOURCE,0,8)
			Goto Super::XDeath
	}
}

Actor RTCW_ColtTracerCoopSoundET : RTCW_ColtTracerSoundET { +THRUSPECIES Species "Player" }
