actor RTCW_BARAlt : Boolean {}
actor RTCW_BARFireFrame : Counter { Inventory.MaxAmount 3 }

Actor RTCW_BAR : RTCW_Weapon
{
	Weapon.SlotNumber 4
	Weapon.SlotPriority 2
	Weapon.SelectionOrder 300
	Weapon.AmmoType1 "RTCW_BARMagazine"
	Weapon.AmmoUse1 1
	Weapon.AmmoType2 "RTCW_AlliedAmmo1"
	Weapon.AmmoGive2 10
	Obituary "%o was killed (BAR) by %k."
	Inventory.PickupMessage "Browning Automatic Rifle"
	Tag "Browning Automatic Rifle"
	States
	{
		Spawn:
			RW98 A -1
			stop

		ReadyActual:
			RW00 A 0 A_TakeInventory("RTCW_BARFireFrame")
			RW00 A 0 A_TakeInventory("RTCW_FG42Spread")
			RW00 A 0 A_JumpIfInventory("RTCW_Kicking",1,"Kicking")
			RW00 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"ReadyActualReloadable")
		ReadyActualNonReloadable:
			RW00 A 1 A_WeaponReady
			goto ReadyActualAmmoCheck

		ReadyActualReloadable:
			RW00 A 1 A_WeaponReady(WRF_ALLOWRELOAD)
			goto ReadyActualAmmoCheck

		ReadyActualAmmoCheck:
			RW00 A 0 A_JumpIfInventory("SamsaraReloadMode",2,1)
			goto ReadyActual
			RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"ReadyActual")
			RW00 A 0 A_JumpIfInventory("RTCW_BARMagazine",1,"ReadyActual")
			goto ReloadCheck

		Deselect:
			RW00 U 0 A_TakeInventory("RTCW_BARFireFrame")
			RW00 U 0 A_TakeInventory("RTCW_FG42Spread")
			RW00 U 0 A_JumpIfHealthLower(1,"DeselectInstant")
			RW00 U 1 A_GiveInventory("SamsaraRTCWWeaponSwitchSoundPlayer")
			RW00 "VWXYZ[\]" 1
			RW01 A 1
			goto DeselectInstant

		Ready: // our "Select" state
			RW00 A 0 A_TakeInventory("RTCW_WeaponToken",256)
			RW00 A 0 A_GiveInventory("RTCW_WeaponToken",19)
			RW00 A 0 A_TakeInventory("RTCW_BARFireFrame")
			RW01 B 1 A_TakeInventory("RTCW_FG42Spread")
			RW01 CEFGHJKLMOPQSTUW 1
			goto ReadyActual

		Fire:
			RW00 A 0 A_TakeInventory("RTCW_BARAlt")
		FireAmmoCheck:
			RW00 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"FireAmmoCheckReloadable")
		FireAmmoCheckNonReloadable:
			RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart")
			RW00 A 0 A_JumpIfInventory("RTCW_AlliedAmmo1",1,"FireStart")
			goto FireDry

		FireAmmoCheckReloadable:
			RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireStart")
			RW00 A 0 A_JumpIfInventory("RTCW_BARMagazine",1,"FireStart")
		FireAmmoCheckReloadableDryCheck:
			RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReloadStart")
			RW00 A 0 A_JumpIfInventory("RTCW_AlliedAmmo1",1,"ReloadStart")
			goto FireDry

		FireDry:
			RW00 A 0 A_ClearReFire
			RW00 A 0 A_GiveInventory("SamsaraRTCWWeaponDrySoundPlayer")
			RW00 A 17 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW00 A 0 A_SelectWeapon("RTCW_Luger")
			goto Deselect

		FireUnderwater:
			RW00 A 0 A_ClearReFire
			RW00 A 0 A_GiveInventory("SamsaraRTCWWeaponUnderwaterSoundPlayer")
			RW00 A 17 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto ReadyActual

		FireStart:
			RW00 A 0 A_JumpIfInventory("SamsaraCurrentWaterLevel",0,"FireUnderwater")
			RW00 A 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW00 B 0 A_GiveInventory("RTCW_BlazkoWeaponAlert",1)
			RW00 B 0 A_JumpIfInventory("SamsaraReloadMode",2,"FireStartReloadable")
		FireStartNonReloadable:
			RW00 B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireAttack")
			RW00 B 0 A_JumpIfInventory("RTCW_AlliedAmmo1",1,"FireAttack")
			goto FireContinue

		FireStartReloadable:
			RW00 B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireAttack")
			RW00 B 0 A_JumpIfInventory("RTCW_BARMagazine",1,"FireAttack")
			goto FireContinue

		FireAttack:
			RW00 B 0 A_GiveInventory("RTCW_FiringToken",1)
			RW00 B 0 A_SpawnItemEx("RTCW_Casing",4,-10*cos(-pitch),43+(10*sin(-pitch)),1,0,2*cos(-pitch),-random(85,95),SXF_TRANSFERPITCH)
			RW00 B 0 A_JumpIfInventory("RTCW_BARFireFrame",0,"FireAttack4")
			RW00 B 0 A_JumpIfInventory("RTCW_BARFireFrame",2,"FireAttack3")
			RW00 B 0 A_JumpIfInventory("RTCW_BARFireFrame",1,"FireAttack2")
			goto FireAttack1

		FireAttack1:
			RW00 B 0 A_GunFlash("FireFlash1")
			goto FireContinue

		FireAttack2:
			RW00 B 0 A_GunFlash("FireFlash2")
			goto FireContinue

		FireAttack3:
			RW00 B 0 A_GunFlash("FireFlash3")
			goto FireContinue

		FireAttack4:
			RW00 B 0 A_GunFlash("FireFlash4")
			goto FireContinue

		FireContinue:
			RW00 B 0 A_JumpIfInventory("RTCW_BARAlt",0,"AltFireContinue")
			RW00 B 0 A_JumpIfInventory("RTCW_BARFireFrame",0,"FireContinue4")
			RW00 B 0 A_JumpIfInventory("RTCW_BARFireFrame",2,"FireContinue3")
			RW00 B 0 A_JumpIfInventory("RTCW_BARFireFrame",1,"FireContinue2")
			goto FireContinue1

		FireContinue1:
			RW00 B 0 A_GiveInventory("SamsaraRTCWBARAttackHandler")
			RW00 BC 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto FireFinish

		FireContinue2:
			RW00 D 0 A_GiveInventory("SamsaraRTCWBARAttackHandler")
			RW00 DE 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto FireFinish

		FireContinue3:
			RW00 F 0 A_GiveInventory("SamsaraRTCWBARAttackHandler")
			RW00 FG 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto FireFinish

		FireContinue4:
			RW00 H 0 A_GiveInventory("SamsaraRTCWBARAttackHandler")
			RW00 HI 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto FireFinish

		FireFinish:
			RW00 A 0 A_JumpIfInventory("RTCW_BARAlt",1,"FireFinishReFire")
			RW00 A 0 A_JumpIfInventory("RTCW_BARFireFrame",0,2)
			RW00 A 0 A_GiveInventory("RTCW_BARFireFrame")
			goto FireFinishReFire
			RW00 A 0 A_TakeInventory("RTCW_BARFireFrame")
			goto FireFinishReFire

		FireFinishReFire:
			RW00 A 0 A_ReFire
			goto ReadyActual

		AltFire:
			RW00 A 0 A_GiveInventory("RTCW_BARAlt")
			goto FireAmmoCheck

		AltFireContinue:
			RW00 B 0 A_JumpIfInventory("RTCW_BARFireFrame",0,"AltFireContinue4")
			RW00 B 0 A_JumpIfInventory("RTCW_BARFireFrame",2,"AltFireContinue3")
			RW00 B 0 A_JumpIfInventory("RTCW_BARFireFrame",1,"AltFireContinue2")
		AltFireContinue1:
			RW00 B 0 A_GiveInventory("SamsaraRTCWBARAttackHandler")
			RW00 BC 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto FireFinish

		AltFireContinue2:
			RW00 D 0 A_GiveInventory("SamsaraRTCWBARAttackHandler")
			RW00 DE 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto FireFinish

		AltFireContinue3:
			RW00 F 0 A_GiveInventory("SamsaraRTCWBARAttackHandler")
			RW00 FG 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto FireFinish

		AltFireContinue4:
			RW00 H 0 A_GiveInventory("SamsaraRTCWBARAttackHandler")
			RW00 HI 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto FireFinish

		FireFlash1:
			TNT1 A 0 A_Jump(256,1,2)
			RW04 B 2 Bright
			stop
			RW05 B 2 Bright
			stop

		FireFlash2:
			TNT1 A 0 A_Jump(256,1,2)
			RW04 D 2 Bright
			stop
			RW05 D 2 Bright
			stop

		FireFlash3:
			TNT1 A 0 A_Jump(256,1,2)
			RW04 F 2 Bright
			stop
			RW05 F 2 Bright
			stop

		FireFlash4:
			TNT1 A 0 A_Jump(256,1,2)
			RW04 H 2 Bright
			stop
			RW05 H 2 Bright
			stop

		Reload:
		ReloadCheck:
			RW00 A 0 A_JumpIfInventory("RTCW_BARMagazine",0,"ReadyActual")
			RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
			RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReloadStart")
			RW00 A 0 A_JumpIfInventory("RTCW_AlliedAmmo1",1,"ReloadStart")
			goto ReadyActual

		ReloadStart:
			RW01 "YZ[\" 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW01 "]" 0 A_GiveInventory("SamsaraRTCWBARReloadSoundPlayer1")
			RW01 "]" 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW02 ABCDEFG 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW02 H 0 A_GiveInventory("SamsaraRTCWBARReloadSoundPlayer2")
			RW02 "HIJKLMNOPQRSTUVWXYZ[\]" 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW03 AB 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW00 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"ReloadAmmo")
		ReloadFinish:
			RW00 A 0
			goto ReadyActual

		ReloadAmmo:
			RW00 A 0 A_JumpIfInventory("RTCW_BARMagazine",0,"ReloadFinish")
			RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
			RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
			RW00 A 0 A_JumpIfInventory("RTCW_AlliedAmmo1",1,1)
			goto ReloadFinish
			RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
			RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
			RW00 A 0 A_TakeInventory("RTCW_AlliedAmmo1",1)
			RW00 A 0 A_GiveInventory("RTCW_BARMagazine")
			loop

		Kicking:
			RW00 A 0 A_GunFlash("KickingFoot")
		KickingDelay:
			RW00 A 18 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW00 A 0 A_TakeInventory("RTCW_Kicking",1)
			goto ReadyActual
	}
}

actor SamsaraRTCWBARAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"PickupReloadable")
      PickupNonReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("RTCW_AlliedAmmo1",1,1)
        stop
        TNT1 A 0 A_TakeInventory("RTCW_AlliedAmmo1",1)
        goto PickupAttack

      PickupReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("RTCW_BARMagazine",1,1)
        stop
        TNT1 A 0 A_TakeInventory("RTCW_BARMagazine",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("RTCW/BAR/Fire",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",2,"PickupAttackET")
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupAttackRTCWConsole")
      // Return To Castle Wolfenstein (Original) sounds
      PickupAttackRTCWPC:
        TNT1 A 0 A_JumpIfInventory("RTCW_BARAlt",1,"PickupAttackRTCWPCSecondary")
      // Primary
      PickupAttackRTCWPCPrimary:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackRTCWPCPrimaryCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackRTCWPCPrimaryCoop")
      PickupAttackRTCWPCPrimaryDM:
        TNT1 A 0 A_FireCustomMissile("RTCW_BARTracer",CallACS("RTCW_Decorate", 1, 1)*frandom(-0.5,0.5),false,4,9,0,CallACS("RTCW_Decorate", 1, 1)*frandom(-0.5,0.5))
        goto PickupFinishPrimary

      PickupAttackRTCWPCPrimaryCoop:
        TNT1 A 0 A_FireCustomMissile("RTCW_BARTracerCoop",CallACS("RTCW_Decorate", 1, 1)*frandom(-0.5,0.5),false,4,9,0,CallACS("RTCW_Decorate", 1, 1)*frandom(-0.5,0.5))
        goto PickupFinishPrimary

      // Secondary
      PickupAttackRTCWPCSecondary:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackRTCWPCSecondaryCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackRTCWPCSecondaryCoop")
      PickupAttackRTCWPCSecondaryDM:
        TNT1 A 0 A_FireCustomMissile("RTCW_BARTracer",CallACS("RTCW_Decorate", 1, 1)*frandom(-1.0,1.0),false,4,9,0,CallACS("RTCW_Decorate", 1, 1)*frandom(-1.0,1.0))
        goto PickupFinishSecondary

      PickupAttackRTCWPCSecondaryCoop:
        TNT1 A 0 A_FireCustomMissile("RTCW_BARTracerCoop",CallACS("RTCW_Decorate", 1, 1)*frandom(-1.0,1.0),false,4,9,0,CallACS("RTCW_Decorate", 1, 1)*frandom(-1.0,1.0))
        goto PickupFinishSecondary

      // Return To Castle Wolfenstein (Console) sounds
      PickupAttackRTCWConsole:
        TNT1 A 0 A_JumpIfInventory("RTCW_BARAlt",1,"PickupAttackRTCWConsoleSecondary")
      // Primary
      PickupAttackRTCWConsolePrimary:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackRTCWConsolePrimaryCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackRTCWConsolePrimaryCoop")
      PickupAttackRTCWConsolePrimaryDM:
        TNT1 A 0 A_FireCustomMissile("RTCW_BARTracerSoundConsole",CallACS("RTCW_Decorate", 1, 1)*frandom(-0.5,0.5),false,4,9,0,CallACS("RTCW_Decorate", 1, 1)*frandom(-0.5,0.5))
        goto PickupFinishPrimary

      PickupAttackRTCWConsolePrimaryCoop:
        TNT1 A 0 A_FireCustomMissile("RTCW_BARTracerCoopSoundConsole",CallACS("RTCW_Decorate", 1, 1)*frandom(-0.5,0.5),false,4,9,0,CallACS("RTCW_Decorate", 1, 1)*frandom(-0.5,0.5))
        goto PickupFinishPrimary

      // Secondary
      PickupAttackRTCWConsoleSecondary:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackRTCWConsoleSecondaryCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackRTCWConsoleSecondaryCoop")
      PickupAttackRTCWConsoleSecondaryDM:
        TNT1 A 0 A_FireCustomMissile("RTCW_BARTracerSoundConsole",CallACS("RTCW_Decorate", 1, 1)*frandom(-1.0,1.0),false,4,9,0,CallACS("RTCW_Decorate", 1, 1)*frandom(-1.0,1.0))
        goto PickupFinishSecondary

      PickupAttackRTCWConsoleSecondaryCoop:
        TNT1 A 0 A_FireCustomMissile("RTCW_BARTracerCoopSoundConsole",CallACS("RTCW_Decorate", 1, 1)*frandom(-1.0,1.0),false,4,9,0,CallACS("RTCW_Decorate", 1, 1)*frandom(-1.0,1.0))
        goto PickupFinishSecondary

      // Wolfenstein: Enemy Territory sounds
      PickupAttackET:
        TNT1 A 0 A_JumpIfInventory("RTCW_BARAlt",1,"PickupAttackETSecondary")
      // Primary
      PickupAttackETPrimary:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackETPrimaryCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackETPrimaryCoop")
      PickupAttackETPrimaryDM:
        TNT1 A 0 A_FireCustomMissile("RTCW_BARTracerSoundET",CallACS("RTCW_Decorate", 1, 1)*frandom(-0.5,0.5),false,4,9,0,CallACS("RTCW_Decorate", 1, 1)*frandom(-0.5,0.5))
        goto PickupFinishPrimary

      PickupAttackETPrimaryCoop:
        TNT1 A 0 A_FireCustomMissile("RTCW_BARTracerCoopSoundET",CallACS("RTCW_Decorate", 1, 1)*frandom(-0.5,0.5),false,4,9,0,CallACS("RTCW_Decorate", 1, 1)*frandom(-0.5,0.5))
        goto PickupFinishPrimary

      // Secondary
      PickupAttackETSecondary:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackETSecondaryCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackETSecondaryCoop")
      PickupAttackETSecondaryDM:
        TNT1 A 0 A_FireCustomMissile("RTCW_BARTracerSoundET",CallACS("RTCW_Decorate", 1, 1)*frandom(-1.0,1.0),false,4,9,0,CallACS("RTCW_Decorate", 1, 1)*frandom(-1.0,1.0))
        goto PickupFinishSecondary

      PickupAttackETSecondaryCoop:
        TNT1 A 0 A_FireCustomMissile("RTCW_BARTracerCoopSoundET",CallACS("RTCW_Decorate", 1, 1)*frandom(-1.0,1.0),false,4,9,0,CallACS("RTCW_Decorate", 1, 1)*frandom(-1.0,1.0))
        goto PickupFinishSecondary

      // Primary finishing stuff
      PickupFinishPrimary:
        TNT1 A 0 A_GiveInventory("RTCW_FG42Spread")
        TNT1 A 0 ACS_NamedExecuteWithResult("RTCW_Recoil", 1)
        stop

      // Secondary finishing stuff
      PickupFinishSecondary:
        TNT1 A 0 A_GiveInventory("RTCW_FG42Spread")
        TNT1 A 0 ACS_NamedExecuteWithResult("RTCW_Recoil", 5)
        stop
    }
}

actor SamsaraRTCWBARReloadSoundPlayer1 : Trigger
{
    States
    {
      // Sound playing stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",2,"PickupET")
      // Return To Castle Wolfenstein sounds
      PickupRTCW:
        TNT1 A 0 A_PlaySound("RTCW/BAR/Reload",CHAN_6)
        stop

      // Wolfenstein: Enemy Territory sounds
      PickupET:
        TNT1 A 0 A_PlaySound("RTCWET/BAR/Reload",CHAN_6)
        stop
    }
}

actor SamsaraRTCWBARReloadSoundPlayer2 : Trigger
{
    States
    {
      // Sound playing stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",2,"PickupET")
      // Return To Castle Wolfenstein sounds
      PickupRTCW:
        TNT1 A 0 A_PlaySound("RTCW/BAR/Reload2",CHAN_6)
        stop

      // Wolfenstein: Enemy Territory sounds
      PickupET:
        TNT1 A 0 A_PlaySound("RTCWET/BAR/Reload2",CHAN_6)
        stop
    }
}

actor RTCW_BARMagazine : Ammo
{
	Inventory.MaxAmount 20
	Tag "Browning Automatic Rifle Magazine"
	+IGNORESKILL
}

//Was 19 damage
Actor RTCW_BARTracer : RTCW_Tracer
{
	States
	{
		Crash:
		Death:
			TNT1 A 0 A_Explode(CallACS("RTCW_BulletDamage", 36, 0),8,!XF_HURTSOURCE,0,8)
			Goto Super::Death

		XDeath:
			TNT1 A 0 A_Explode(CallACS("RTCW_BulletDamage", 36, 0),8,!XF_HURTSOURCE,0,8)
			Goto Super::XDeath
	}
}

Actor RTCW_BARTracerCoop : RTCW_BARTracer { +THRUSPECIES Species "Player" }

Actor RTCW_BARTracerSoundConsole : RTCW_TracerSoundConsole
{
	States
	{
		Crash:
		Death:
			TNT1 A 0 A_Explode(CallACS("RTCW_BulletDamage", 36, 1),8,!XF_HURTSOURCE,0,8)
			Goto Super::Death

		XDeath:
			TNT1 A 0 A_Explode(CallACS("RTCW_BulletDamage", 36, 1),8,!XF_HURTSOURCE,0,8)
			Goto Super::XDeath
	}
}

Actor RTCW_BARTracerCoopSoundConsole : RTCW_BARTracerSoundConsole { +THRUSPECIES Species "Player" }

Actor RTCW_BARTracerSoundET : RTCW_TracerSoundET
{
	States
	{
		Crash:
		Death:
			TNT1 A 0 A_Explode(CallACS("RTCW_BulletDamage", 36, 2),8,!XF_HURTSOURCE,0,8)
			Goto Super::Death

		XDeath:
			TNT1 A 0 A_Explode(CallACS("RTCW_BulletDamage", 36, 2),8,!XF_HURTSOURCE,0,8)
			Goto Super::XDeath
	}
}

Actor RTCW_BARTracerCoopSoundET : RTCW_BARTracerSoundET { +THRUSPECIES Species "Player" }
