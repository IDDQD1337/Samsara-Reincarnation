actor RTCW_StG44Alt : Boolean {}
actor RTCW_StG44RateOfFire : Boolean {}

Actor RTCW_StG44 : RTCW_Weapon
{
	Weapon.SlotNumber 4
	Weapon.SlotPriority 1
	Weapon.SelectionOrder 500
	Weapon.AmmoType1 "RTCW_StG44Magazine"
	Weapon.AmmoUse1 1
	Weapon.AmmoType2 "Clip"
	Weapon.AmmoGive2 15
	Obituary "%o was killed (StG44) by %k."
	Inventory.PickupMessage "StG44"
    Inventory.Icon "CWCMPW07"
	Tag "StG44"
	States
	{
		Spawn:
			RW98 A -1
			stop

		ReadyActual:
			RW00 A 0 A_TakeInventory("RTCW_FG42Spread")
			RW00 A 0 A_JumpIfInventory("RTCW_Kicking",1,"Kicking")
			RW00 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"ReadyActualReloadable")
		ReadyActualNonReloadable:
			RW00 A 1 A_WeaponReady
			goto ReadyActualAmmoCheck

		ReadyActualReloadable:
			RW00 A 1 A_WeaponReady(WRF_ALLOWRELOAD)
			goto ReadyActualAmmoCheck

		ReadyActualAmmoCheck:
			RW00 A 0 A_JumpIfInventory("SamsaraReloadMode",2,1)
			goto ReadyActual
			RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"ReadyActual")
			RW00 A 0 A_JumpIfInventory("RTCW_StG44Magazine",1,"ReadyActual")
			goto ReloadCheck

		Deselect:
			RW00 G 0 A_TakeInventory("RTCW_FG42Spread")
			RW00 G 0 A_JumpIfHealthLower(1,"DeselectInstant")
			RW00 G 2 A_GiveInventory("SamsaraRTCWWeaponSwitchSoundPlayer")
			RW00 HIJK 2
			goto DeselectInstant

		Ready: // our "Select" state
			RW00 A 0 A_TakeInventory("RTCW_WeaponToken",256)
			RW00 A 0 A_GiveInventory("RTCW_WeaponToken",18)
			RW00 L 2 A_TakeInventory("RTCW_FG42Spread")
			RW00 MNO 2
			goto ReadyActual

		Fire:
		FireAmmoCheck:
			RW00 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"FireAmmoCheckReloadable")
		FireAmmoCheckNonReloadable:
			RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart")
			RW00 A 0 A_JumpIfInventory("Clip",1,"FireStart")
			goto FireDry

		FireAmmoCheckReloadable:
			RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireStart")
			RW00 A 0 A_JumpIfInventory("RTCW_StG44Magazine",1,"FireStart")
		FireAmmoCheckReloadableDryCheck:
			RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReloadStart")
			RW00 A 0 A_JumpIfInventory("Clip",1,"ReloadStart")
			goto FireDry

		FireDry:
			RW00 A 0 A_ClearReFire
			RW00 A 0 A_GiveInventory("SamsaraRTCWWeaponDrySoundPlayer")
			RW00 A 17 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW00 A 0 A_SelectWeapon("RTCW_Luger")
			goto Deselect

		FireUnderwater:
			RW00 A 0 A_ClearReFire
			RW00 A 0 A_GiveInventory("SamsaraRTCWWeaponUnderwaterSoundPlayer")
			RW00 A 17 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto ReadyActual

		FireStart:
			RW00 A 0 A_JumpIfInventory("SamsaraCurrentWaterLevel",0,"FireUnderwater")
			RW00 A 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW00 B 0 A_GiveInventory("RTCW_BlazkoWeaponAlert",1)
			RW00 B 0 A_JumpIfInventory("SamsaraReloadMode",2,"FireStartReloadable")
		FireStartNonReloadable:
			RW00 B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireAttack")
			RW00 B 0 A_JumpIfInventory("Clip",1,"FireAttack")
			goto FireContinue

		FireStartReloadable:
			RW00 B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireAttack")
			RW00 B 0 A_JumpIfInventory("RTCW_StG44Magazine",1,"FireAttack")
			goto FireContinue

		FireAttack:
			RW00 B 0 A_GiveInventory("RTCW_FiringToken",1)
			RW00 B 0 A_GunFlash("FireFlash")
		FireContinue:
			RW00 B 0 A_GiveInventory("SamsaraRTCWStG44AttackHandler")
			RW00 B 0 A_JumpIfInventory("RTCW_StG44Alt",1,"AltFireContinue")
			RW00 B 0 A_JumpIfInventory("RTCW_StG44RateOfFire",1,3)
			RW00 B 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW00 A 0 A_GiveInventory("RTCW_StG44RateOfFire",1)
			goto FireFinish
			RW00 B 0 A_TakeInventory("RTCW_StG44RateOfFire")
			RW00 B 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto FireFinish

		FireFinish:
			RW00 D 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
		FireFinishReFire:
			RW00 A 0 A_ReFire
			goto ReadyActual

		AltFire:
			RW00 A 0 A_JumpIfInventory("RTCW_StG44Alt",1,1)
			Goto SetSemiAuto
			RW00 A 0 A_TakeInventory("RTCW_StG44Alt")
			Goto SelectFireFinish
		SetSemiAuto:
			RW00 A 0 A_GiveInventory("RTCW_StG44Alt")
		SelectFireFinish:
			RW00 A 0 A_GiveInventory("SamsaraRTCWStG44ModeSwitchSoundPlayer")
			RW00 A 5 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto AltFireHold

		AltFireContinue:
			RW00 B 0 A_TakeInventory("RTCW_StG44RateOfFire")
			RW00 B 0 ACS_NamedExecuteWithResult("SamsaraSemiAuto")
			RW00 B 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW00 A 0 A_JumpIfInventory("SamsaraFiringFullAuto",1,1)
			goto FireFinishReFire
		AltFireHold:
			RW00 A 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW00 A 0 A_Refire("AltFireHold")
			goto FireFinishReFire
			
		FireFlash:
			TNT1 A 0 A_Jump(256,1,2)
			RW03 B 2 Bright
			stop
			RW04 B 2 Bright
			stop

		Reload:
		ReloadCheck:
			RW00 A 0 A_JumpIfInventory("RTCW_StG44Magazine",0,"ReadyActual")
			RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
			RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReloadStart")
			RW00 A 0 A_JumpIfInventory("Clip",1,"ReloadStart")
			goto ReadyActual

		ReloadStart:
			RW00 Q 0 A_GiveInventory("SamsaraRTCWStG44ReloadSoundPlayer1")
			//RW00 Q 0 A_JumpIfInventory("RTCW_StG44Magazine",1,1)
			goto ReloadStartAmmoless
			RW00 "QRSTUVWXYZ[\]" 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW01 ABCDEFGHIJKLMNOPQRSTU 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW02 EFG 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW00 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"ReloadAmmo")
			goto ReloadFinish

		ReloadStartAmmoless:
			RW00 "QRSTUVWXYZ[\]" 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW01 ABCDEFGHIJKLMNOPQR 2  A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW01 STU 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW01 V 0 A_GiveInventory("SamsaraRTCWStG44ReloadSoundPlayer2")
			RW01 "VWXYZ[\]" 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW02 ABCDEFG 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW00 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"ReloadAmmo")
			goto ReloadFinish

		ReloadFinish:
			RW00 A 0
			goto ReadyActual

		ReloadAmmo:
			RW00 A 0 A_JumpIfInventory("RTCW_StG44Magazine",0,"ReloadFinish")
			RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
			RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
			RW00 A 0 A_JumpIfInventory("Clip",1,1)
			goto ReloadFinish
			RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
			RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
			RW00 A 0 A_TakeInventory("Clip",1)
			RW00 A 0 A_GiveInventory("RTCW_StG44Magazine")
			loop

		Kicking:
			RW00 A 0 A_GunFlash("KickingFoot")
		KickingDelay:
			RW00 A 18 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW00 A 0 A_TakeInventory("RTCW_Kicking",1)
			goto ReadyActual
	}
}

actor SamsaraRTCWStG44AttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"PickupReloadable")
      PickupNonReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",1)
        goto PickupAttack

      PickupReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("RTCW_StG44Magazine",1,1)
        stop
        TNT1 A 0 A_TakeInventory("RTCW_StG44Magazine",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("RTCW/StG44/Fire",CHAN_WEAPON)
        TNT1 A 0 A_SpawnItemEx("RTCW_Casing",4,-10*cos(-pitch),43+(10*sin(-pitch)),1,0,2*cos(-pitch),-random(85,95),SXF_TRANSFERPITCH)
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",2,"PickupAttackET")
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupAttackRTCWConsole")
      // Original sounds
      PickupAttackRTCWPC:
        TNT1 A 0 A_JumpIfInventory("RTCW_StG44Alt",1,"PickupAttackRTCWPCSecondary")
      // Primary
      PickupAttackRTCWPCPrimary:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackRTCWPCPrimaryCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackRTCWPCPrimaryCoop")
      PickupAttackRTCWPCPrimaryDM:
        TNT1 A 0 A_FireCustomMissile("RTCW_StG44Tracer",CallACS("RTCW_Decorate", 1, 1)*frandom(-1.0,1.0),false,4,9,0,CallACS("RTCW_Decorate", 1, 1)*frandom(-1.0,1.0))
        goto PickupFinish

      PickupAttackRTCWPCPrimaryCoop:
        TNT1 A 0 A_FireCustomMissile("RTCW_StG44TracerCoop",CallACS("RTCW_Decorate", 1, 1)*frandom(-1.0,1.0),false,4,9,0,CallACS("RTCW_Decorate", 1, 1)*frandom(-1.0,1.0))
        goto PickupFinish

      // Secondary
      PickupAttackRTCWPCSecondary:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackRTCWPCSecondaryCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackRTCWPCSecondaryCoop")
      PickupAttackRTCWPCSecondaryDM:
        TNT1 A 0 A_FireCustomMissile("RTCW_StG44Tracer",CallACS("RTCW_Decorate", 1, 1)*frandom(-0.25,0.25),false,4,9,0,CallACS("RTCW_Decorate", 1, 1)*frandom(-0.25,0.25))
        goto PickupFinish

      PickupAttackRTCWPCSecondaryCoop:
        TNT1 A 0 A_FireCustomMissile("RTCW_StG44TracerCoop",CallACS("RTCW_Decorate", 1, 1)*frandom(-0.25,0.25),false,4,9,0,CallACS("RTCW_Decorate", 1, 1)*frandom(-0.25,0.25))
        goto PickupFinish

      // Console sounds
      PickupAttackRTCWConsole:
        TNT1 A 0 A_JumpIfInventory("RTCW_StG44Alt",1,"PickupAttackRTCWConsoleSecondary")
      // Primary
      PickupAttackRTCWConsolePrimary:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackRTCWConsolePrimaryCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackRTCWConsolePrimaryCoop")
      PickupAttackRTCWConsolePrimaryDM:
        TNT1 A 0 A_FireCustomMissile("RTCW_StG44TracerSoundConsole",CallACS("RTCW_Decorate", 1, 1)*frandom(-1.0,1.0),false,4,9,0,CallACS("RTCW_Decorate", 1, 1)*frandom(-1.0,1.0))
        goto PickupFinish

      PickupAttackRTCWConsolePrimaryCoop:
        TNT1 A 0 A_FireCustomMissile("RTCW_StG44TracerCoopSoundConsole",CallACS("RTCW_Decorate", 1, 1)*frandom(-1.0,1.0),false,4,9,0,CallACS("RTCW_Decorate", 1, 1)*frandom(-1.0,1.0))
        goto PickupFinish

      // Secondary
      PickupAttackRTCWConsoleSecondary:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackRTCWConsoleSecondaryCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackRTCWConsoleSecondaryCoop")
      PickupAttackRTCWConsoleSecondaryDM:
        TNT1 A 0 A_FireCustomMissile("RTCW_StG44TracerSoundConsole",CallACS("RTCW_Decorate", 1, 1)*frandom(-0.25,0.25),false,4,9,0,CallACS("RTCW_Decorate", 1, 1)*frandom(-0.25,0.25))
        goto PickupFinish

      PickupAttackRTCWConsoleSecondaryCoop:
        TNT1 A 0 A_FireCustomMissile("RTCW_StG44TracerCoopSoundConsole",CallACS("RTCW_Decorate", 1, 1)*frandom(-0.25,0.25),false,4,9,0,CallACS("RTCW_Decorate", 1, 1)*frandom(-0.25,0.25))
        goto PickupFinish

      // Enemy Territory sounds
      PickupAttackET:
        TNT1 A 0 A_JumpIfInventory("RTCW_StG44Alt",1,"PickupAttackETSecondary")
      // Primary
      PickupAttackETPrimary:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackETPrimaryCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackETPrimaryCoop")
      PickupAttackETPrimaryDM:
        TNT1 A 0 A_FireCustomMissile("RTCW_StG44TracerSoundET",CallACS("RTCW_Decorate", 1, 1)*frandom(-1.0,1.0),false,4,9,0,CallACS("RTCW_Decorate", 1, 1)*frandom(-1.0,1.0))
        goto PickupFinish

      PickupAttackETPrimaryCoop:
        TNT1 A 0 A_FireCustomMissile("RTCW_StG44TracerCoopSoundET",CallACS("RTCW_Decorate", 1, 1)*frandom(-1.0,1.0),false,4,9,0,CallACS("RTCW_Decorate", 1, 1)*frandom(-1.0,1.0))
        goto PickupFinish

      // Secondary
      PickupAttackETSecondary:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackETSecondaryCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackETSecondaryCoop")
      PickupAttackETSecondaryDM:
        TNT1 A 0 A_FireCustomMissile("RTCW_StG44TracerSoundET",CallACS("RTCW_Decorate", 1, 1)*frandom(-0.25,0.25),false,4,9,0,CallACS("RTCW_Decorate", 1, 1)*frandom(-0.25,0.25))
        goto PickupFinish

      PickupAttackETSecondaryCoop:
        TNT1 A 0 A_FireCustomMissile("RTCW_StG44TracerCoopSoundET",CallACS("RTCW_Decorate", 1, 1)*frandom(-0.25,0.25),false,4,9,0,CallACS("RTCW_Decorate", 1, 1)*frandom(-0.25,0.25))
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 A_GiveInventory("RTCW_FG42Spread")
        TNT1 A 0 ACS_NamedExecuteWithResult("RTCW_Recoil", 1)
        stop
    }
}

actor SamsaraRTCWStG44ModeSwitchSoundPlayer : Trigger
{
    States
    {
      // Sound playing stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",2,"PickupET")
      // Original/Console sounds
      PickupRTCW:
        TNT1 A 0 A_PlaySound("RTCW/StG44/ModeSwitch",CHAN_6)
        stop

      // Enemy Territory sounds
      PickupET:
        TNT1 A 0 A_PlaySound("RTCWET/StG44/ModeSwitch",CHAN_6)
        stop
    }
}

actor SamsaraRTCWStG44ReloadSoundPlayer1 : Trigger
{
    States
    {
      // Sound playing stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",2,"PickupET")
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupRTCWConsole")
      // Original sounds
      PickupRTCWPC:
        TNT1 A 0 A_PlaySound("RTCW/StG44/Reload",CHAN_6)
        stop

      // Console sounds
      PickupRTCWConsole:
        TNT1 A 0 A_PlaySound("RTCWConsole/StG44/Reload",CHAN_6)
        stop

      // Enemy Territory sounds
      PickupET:
        TNT1 A 0 A_PlaySound("RTCWET/StG44/Reload",CHAN_6)
        stop
    }
}

actor SamsaraRTCWStG44ReloadSoundPlayer2 : Trigger
{
    States
    {
      // Sound playing stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",2,"PickupET")
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupRTCWConsole")
      // Original sounds
      PickupRTCWPC:
        TNT1 A 0 A_PlaySound("RTCW/StG44/Reload2",CHAN_6)
        stop

      // Console sounds
      PickupRTCWConsole:
        TNT1 A 0 A_PlaySound("RTCWConsole/StG44/Reload2",CHAN_6)
        stop

      // Enemy Territory sounds
      PickupET:
        TNT1 A 0 A_PlaySound("RTCWET/StG44/Reload2",CHAN_6)
        stop
    }
}

actor RTCW_StG44Magazine : Ammo
{
	Inventory.MaxAmount 30
	Tag "StG44 Magazine"
	+IGNORESKILL
}

//Was 19 damage
Actor RTCW_StG44Tracer : RTCW_Tracer
{
	+THRUGHOST
	States
	{
		Crash:
		Death:
			TNT1 A 0 A_Explode(CallACS("RTCW_BulletDamage", 20, 0),8,!XF_HURTSOURCE,0,8)
			Goto Super::Death

		XDeath:
			TNT1 A 0 A_Explode(CallACS("RTCW_BulletDamage", 20, 0),8,!XF_HURTSOURCE,0,8)
			Goto Super::XDeath
	}
}

Actor RTCW_StG44TracerCoop : RTCW_StG44Tracer { +THRUSPECIES Species "Player" }

Actor RTCW_StG44TracerSoundConsole : RTCW_TracerSoundConsole
{
	+THRUGHOST
	States
	{
		Crash:
		Death:
			TNT1 A 0 A_Explode(CallACS("RTCW_BulletDamage", 20, 1),8,!XF_HURTSOURCE,0,8)
			Goto Super::Death

		XDeath:
			TNT1 A 0 A_Explode(CallACS("RTCW_BulletDamage", 20, 1),8,!XF_HURTSOURCE,0,8)
			Goto Super::XDeath
	}
}

Actor RTCW_StG44TracerCoopSoundConsole : RTCW_StG44TracerSoundConsole { +THRUSPECIES Species "Player" }

Actor RTCW_StG44TracerSoundET : RTCW_TracerSoundET
{
	+THRUGHOST
	States
	{
		Crash:
		Death:
			TNT1 A 0 A_Explode(CallACS("RTCW_BulletDamage", 20, 2),8,!XF_HURTSOURCE,0,8)
			Goto Super::Death

		XDeath:
			TNT1 A 0 A_Explode(CallACS("RTCW_BulletDamage", 20, 2),8,!XF_HURTSOURCE,0,8)
			Goto Super::XDeath
	}
}

Actor RTCW_StG44TracerCoopSoundET : RTCW_StG44TracerSoundET { +THRUSPECIES Species "Player" }
