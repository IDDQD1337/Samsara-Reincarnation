Actor RTCW_Panzerfaust : RTCW_Weapon
{
	Weapon.SlotNumber 5
	Weapon.SlotPriority 0
	Weapon.SelectionOrder 1500
	Weapon.AmmoType2 "RocketAmmo"
	Weapon.AmmoType1 "RTCW_PanzerfaustMagazine"
	Weapon.AmmoUse1 1
	Weapon.AmmoGive2 3
	Obituary "%o was killed (rl) by %k."
	Inventory.PickupMessage "Panzerfaust"
	Tag "Panzerfaust"
	+EXPLOSIVE
	States
	{
		Spawn:
			RW98 A -1
			stop

		ReadyActual:
			RW02 C 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReadyActualReloadable")
		ReadyActualNonReloadable:
			RW02 C 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReadyActualNonReloadableAmmo")
			RW02 C 0 A_JumpIfInventory("RocketAmmo",1,"ReadyActualNonReloadableAmmo")
			goto ReadyActualNonReloadableNoAmmo

		ReadyActualNonReloadableNoAmmo:
			TNT1 A 0 A_JumpIfInventory("RTCW_Kicking",1,"KickingNoAmmo")
			TNT1 A 0 A_GunFlash("ReadyActualNoAmmoFlash",GFF_NOEXTCHANGE)
			TNT1 A 1 A_WeaponReady
			goto ReadyActualNoAmmoCheck

		ReadyActualNonReloadableAmmo:
			RW02 C 0 A_JumpIfInventory("RTCW_Kicking",1,"Kicking")
			RW02 C 0 A_GunFlash("ReadyActualAmmoFlash",GFF_NOEXTCHANGE)
			RW02 C 1 A_WeaponReady
			goto ReadyActualAmmoCheck

		ReadyActualReloadable:
			RW02 C 0 A_JumpIfInventory("SamsaraReloadOffPlus",1,"ReadyActualReloadableAmmo")
			RW02 C 0 A_JumpIfInventory("RTCW_PanzerfaustMagazine",1,"ReadyActualReloadableAmmo")
			goto ReadyActualReloadableNoAmmo

		ReadyActualReloadableNoAmmo:
			TNT1 A 0 A_JumpIfInventory("RTCW_Kicking",1,"KickingNoAmmo")
			TNT1 A 0 A_GunFlash("ReadyActualNoAmmoFlash",GFF_NOEXTCHANGE)
			TNT1 A 1 A_WeaponReady(WRF_ALLOWRELOAD)
			goto ReadyActualAmmoCheck

		ReadyActualReloadableAmmo:
			RW02 C 0 A_JumpIfInventory("RTCW_Kicking",1,"Kicking")
			RW02 C 0 A_GunFlash("ReadyActualAmmoFlash",GFF_NOEXTCHANGE)
			RW02 C 1 A_WeaponReady(WRF_ALLOWRELOAD)
			goto ReadyActualAmmoCheck

		ReadyActualNoAmmoFlash:
			TNT1 A 0
			stop

		ReadyActualAmmoFlash:
			RW07 C 2
			stop

		ReadyActualNoAmmoCheck:
			TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReadyActualAmmoCheck")
			TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReloadStart")
			TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,"ReloadStart")
			goto ReadyActual

		ReadyActualAmmoCheck:
			RW02 C 0 A_JumpIfInventory("SamsaraReloadMode",1,1)
			goto ReadyActual
			RW02 C 0 A_JumpIfInventory("SamsaraReloadOffPlus",1,"ReadyActual")
			RW02 C 0 A_JumpIfInventory("RTCW_PanzerfaustMagazine",1,"ReadyActual")
			goto ReloadCheck

		Deselect:
			RW02 C 0 A_JumpIfHealthLower(1,"DeselectInstant")
			RW02 C 0 A_PlaySound("RTCW/Change",CHAN_7)
			RW02 C 0 A_JumpIfInventory("SamsaraReloadMode",1,"DeselectReloadable")
		DeselectNonReloadable:
			RW02 C 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"DeselectAmmo")
			RW02 C 0 A_JumpIfInventory("RocketAmmo",1,"DeselectAmmo")
			goto DeselectNoAmmo

		DeselectReloadable:
			RW02 C 0 A_JumpIfInventory("SamsaraReloadOffPlus",1,"DeselectAmmo")
			RW02 C 0 A_JumpIfInventory("RTCW_PanzerfaustMagazine",1,"DeselectAmmo")
			goto DeselectNoAmmo

		DeselectNoAmmo:
			TNT1 A 2 A_GunFlash("DeselectNoAmmoFlash")
			TNT1 AAAAAAAA 2
			TNT1 AAAAAAAAAAAA 2
			goto DeselectInstant

		DeselectNoAmmoFlash:
			TNT1 A 0
			stop

		DeselectAmmo:
			RW02 C 2 A_GunFlash("DeselectAmmoFlash",GFF_NOEXTCHANGE)
			RW00 "VWXYZ[\]" 2
			RW01 ABCDEFGHIJKL 2
			goto DeselectInstant

		DeselectAmmoFlash:
			RW07 C 2
			RW05 "VWXYZ[\]" 2
			RW06 ABCDEFGHIJKL 2
			stop

		DeselectInstant:
			TNT1 A 0 A_GunFlash("DeselectInstantFlash",GFF_NOEXTCHANGE)
			goto Super::DeselectInstant

		DeselectInstantFlash:
			TNT1 A 0
			stop

		Ready: // our "Select" state
			RW00 A 0 A_TakeInventory("RTCW_WeaponToken",256)
			RW00 A 0 A_GiveInventory("RTCW_WeaponToken",13)
			RW00 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReadyReloadable")
		ReadyNonReloadable:
			RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReadyAmmo")
			RW00 A 0 A_JumpIfInventory("RocketAmmo",1,"ReadyAmmo")
			goto ReadyNoAmmo

		ReadyReloadable:
			RW00 A 0 A_JumpIfInventory("SamsaraReloadOffPlus",1,"ReadyAmmo")
			RW00 A 0 A_JumpIfInventory("RTCW_PanzerfaustMagazine",1,"ReadyAmmo")
			goto ReadyNoAmmo

		ReadyNoAmmo:
			TNT1 A 2 A_GunFlash("ReadyNoAmmoFlash",GFF_NOEXTCHANGE)
			TNT1 AAAAAAAA 2
			TNT1 AA 2
			goto ReadyActual

		ReadyNoAmmoFlash:
			TNT1 A 0
			stop

		ReadyAmmo:
			RW01 M 2 A_GunFlash("ReadyAmmoFlash",GFF_NOEXTCHANGE)
			RW01 NOPQRSTU 2
			RW02 AB 2
			goto ReadyActual

		ReadyAmmoFlash:
			RW06 MNOPQRSTU 2
			RW07 AB 2
			stop

		Fire:
			RW00 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"FireReloadable")
		FireNonReloadable:
			RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart")
			RW00 A 0 A_JumpIfInventory("RocketAmmo",1,"FireStart")
			goto FireDry

		FireReloadable:
			RW00 A 0 A_JumpIfInventory("SamsaraReloadOffPlus",1,"FireStart")
			RW00 A 0 A_JumpIfInventory("RTCW_PanzerfaustMagazine",1,"FireStart")
		FireReloadableDryCheck:
			RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReloadStart")
			RW00 A 0 A_JumpIfInventory("RocketAmmo",1,"ReloadStart")
			goto FireDry

		FireDry:
			TNT1 A 0 A_ClearReFire
			TNT1 A 0 A_PlaySound("RTCW/NoAmmo",CHAN_5)
			TNT1 A 0 A_GunFlash("FireDryFlash",GFF_NOEXTCHANGE)
			TNT1 A 17 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW02 C 0 A_SelectWeapon("RTCW_Luger")
			goto Deselect

		FireDryFlash:
			TNT1 A 0
			stop

		FireUnderwater:
			RW09 A 0 A_ClearReFire
			RW09 A 0 A_PlaySound("RTCW/UnderwaterFire",CHAN_5)
			RW09 A 0 A_GunFlash("FireUnderwaterFlash",GFF_NOEXTCHANGE)
			RW09 A 17 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto ReadyActual

		FireUnderwaterFlash:
			RW07 C 17
			stop

		FireStart:
			RW00 A 0 A_JumpIfInventory("SamsaraCurrentWaterLevel",0,"FireUnderwater")
			RW00 A 0 A_GiveInventory("RTCW_FiringToken",1)
			RW00 A 0 A_GiveInventory("RTCW_BlazkoWeaponAlert",1)
			RW00 A 0 A_FireCustomMissile("RTCW_PanzerfaustShell",-random(85,105),false,8,4,0,-4)
		FireLastShotCalc:
			RW00 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"FireLastShotCalcReloadable")
		FireLastShotCalcNonReloadable:
			RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireContinueLast")
			RW00 A 0 A_JumpIfInventory("RocketAmmo",2,"FireContinueLast")
			goto FireContinueLastNoAmmo

		FireLastShotCalcReloadable:
			RW00 A 0 A_JumpIfInventory("SamsaraReloadOffPlus",1,"FireContinue")
			RW00 A 0 A_JumpIfInventory("RTCW_PanzerfaustMagazine",2,"FireContinue")
			RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireContinueLast")
			RW00 A 0 A_JumpIfInventory("RocketAmmo",1,"FireContinueLast")
			goto FireContinueLastNoAmmo

		FireContinue:
			RW02 C 0 A_GunFlash("FireFlash")
			RW02 C 0 A_GiveInventory("SamsaraRTCWPanzerfaustAttackHandler")
			RW02 CDEFGCCCCCCCCCCC 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE) // precautionary parity
			RW02 C 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReadyActual")
			goto ReadyActualNoAmmoCheck

		FireContinueLast:
			RW00 A 0 A_GunFlash("FireFlashLast")
			RW00 A 0 A_GiveInventory("SamsaraRTCWPanzerfaustAttackHandler")
			RW00 ABCDEFGHIJKLMNOP 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW00 P 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReadyActual")
			goto ReadyActualNoAmmoCheck

		FireContinueLastNoAmmo:
			RW02 C 0 A_PlaySound("RTCW/NoAmmo",CHAN_5)
			RW02 C 0 A_PlaySound("RTCW/Change",CHAN_7)
			RW02 C 0 A_GunFlash("FireFlashLastNoAmmo")
			RW02 C 0 A_GiveInventory("SamsaraRTCWPanzerfaustAttackHandler")
			RW02 CDEFGHIJK 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW02 L 0
			TNT1 AAAAAAAAAAAAAAAAAA 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE) // "LMNOPQRSTUVWXYZ[\]"
			TNT1 AAAAAAAAAAAAA 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE) // ABCDEFGHIJKLM
			TNT1 A 0 A_SelectWeapon("RTCW_Luger")
			goto DeselectInstant

		FireFlash:
		FireFlashLastNoAmmo:
			TNT1 A 0 A_Jump(256,1,2)
			RW04 B 2 Bright
			stop
			RW10 B 2 Bright
			stop

		FireFlashLast:
			TNT1 A 0 A_Jump(256,1,2)
			RW04 A 2 Bright
			stop
			RW10 A 2 Bright
			stop

		Reload:
		ReloadCheck:
			TNT1 A 0 A_JumpIfInventory("RTCW_PanzerfaustMagazine",0,"ReadyActual")
			TNT1 A 0 A_JumpIfInventory("SamsaraReloadOffPlus",1,2)
			TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReloadStart")
			TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,"ReloadStart")
			goto ReadyActual

		ReloadStart:
			RW01 G 0 A_GunFlash("ReloadFlash",GFF_NOEXTCHANGE)
			RW01 "GHIJKLMNOPQRSTUVWXYZ[\]" 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW02 AB 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW02 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReloadAmmo")
		ReloadFinish:
			RW02 A 0
			goto ReadyActual

		ReloadFlash:
			RW06 "GHIJKLMNOPQRSTUVWXYZ[\]" 2
			RW07 AB 2
			stop

		ReloadAmmo:
			RW02 A 0 A_JumpIfInventory("RTCW_PanzerfaustMagazine",0,"ReloadFinish")
			RW02 A 0 A_JumpIfInventory("SamsaraReloadOffPlus",1,2)
			RW02 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
			RW02 A 0 A_JumpIfInventory("RocketAmmo",1,1)
			goto ReloadFinish
			RW02 A 0 A_JumpIfInventory("SamsaraReloadOffPlus",1,2)
			RW02 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
			RW02 A 0 A_TakeInventory("RocketAmmo",1)
			RW02 A 0 A_GiveInventory("RTCW_PanzerfaustMagazine")
			loop

		KickingNoAmmo: // jumps to "with ammo" version for the time being
		Kicking:
			RW09 A 0 A_GunFlash("KickingFoot")
		KickingDelay:
			RW09 A 18 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW09 A 0 A_TakeInventory("RTCW_Kicking",1)
			goto ReadyActual
	}
}

actor SamsaraRTCWPanzerfaustAttackHandler : Trigger
{
	States
	{
	// Ammo checking/taking stuff
	Pickup:
		TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"PickupReloadable")
	PickupNonReloadable:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
		TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,1)
		stop
		TNT1 A 0 A_TakeInventory("RocketAmmo",1)
		goto PickupAttack
	PickupReloadable:
		TNT1 A 0 A_JumpIfInventory("SamsaraReloadOffPlus",1,"PickupAttack")
		TNT1 A 0 A_JumpIfInventory("RTCW_PanzerfaustMagazine",1,1)
		stop
		TNT1 A 0 A_TakeInventory("RTCW_PanzerfaustMagazine",1)
		goto PickupAttack

	// Attack stuff
	PickupAttack:
		TNT1 A 0 A_PlaySound("RTCW/Panzerfaust/Fire",CHAN_WEAPON)
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
	PickupAttackDM:
		TNT1 A 0 A_FireCustomMissile("RTCW_PanzerfaustRocket",0,false,8,5)
		goto PickupFinish
	PickupAttackCoop:
		TNT1 A 0 A_FireCustomMissile("RTCW_PanzerfaustRocketCoop",0,false,8,5)
		goto PickupFinish

	// Finishing stuff
	PickupFinish:
		TNT1 A 0 A_Quake(3,15,0,128,"")
		stop
	}
}

actor RTCW_PanzerfaustMagazine : Ammo
{
	Inventory.MaxAmount 1
	Tag "Panzerfaust Magazine"
	+IGNORESKILL
}

//Was 100 impact 120 splash, now 240 splash, 200 impact
Actor RTCW_PanzerfaustRocket
{
	Height 10
	Radius 5
	Damage (250)
	Speed 32
	Projectile
	+SKYEXPLODE
	+BRIGHT
	DamageType "RTCWExplosive"
	States
	{
		Spawn:
			RW00 A 0 NoDelay A_PlaySound("RTCW/Panzerfaust/Fly",CHAN_5,0.5,1)
			RW00 A 0 ACS_NamedExecuteWithResult("RTCW_SpawnRocketFlare")
		SpawnLoop:
			RW00 A 1
			Loop
		Crash:
		Death:
		XDeath:
			RW00 AAAA 0 A_SpawnItemEx("RTCW_PanzerfaustSmokeExplosion",0,0,0,frandom(-0.5,0.5),frandom(-0.5,0.5),1)
			RW00 AAAA 0 A_SpawnItemEx("RTCW_ExplosionSmokeDebris")
			RW00 A 0 A_Quake(3,15,0,128,"")
			RW00 A 0 A_GiveInventory("RTCW_BlazkoWeaponAlert",1,AAPTR_Target)
			RW00 A 0 A_Explode(300,120,XF_HURTSOURCE,1,0)
			TNT1 A 15 A_SpawnItemEx("RTCW_PanzerfaustRocketExplosion")
			Stop
	}
}

Actor RTCW_PanzerfaustRocketCoop : RTCW_PanzerfaustRocket { +THRUSPECIES Species "Player" }

Actor RTCW_PanzerfaustShell : RTCW_Casing
{
	Scale 2.0
	BounceType "None"
	States
	{
		Spawn:
			RW00 A 0 NoDelay A_ChangeVelocity(frandom(-0.75,0.75),frandom(-0.75,0.75),0,CVF_RELATIVE)
			RW00 A 0 ThrustThingZ(0,random(8,12),0,1)
		SpawnLoop:
			RW00 A 5 A_SpawnItemEx("RTCW_SmokePuff",0,-16,0,0,0,frandom(0.25,0.5))
			Loop
		Death:
			RW00 AAAAAAAAAAAAAAAAAAAA 5 A_SpawnItemEx("RTCW_SmokePuff",0,-16,0,0,0,frandom(0.25,0.5))
			RW00 AAAAAAAA 1 A_Fadeout(0.1)
			Stop
	}
}

Actor RTCW_PanzerfaustRocketFlare
{
	Height 10
	Radius 5
	Renderstyle Add
	+NOINTERACTION
	+BRIGHT
	States
	{
		Spawn:
			RW00 A 5
		SpawnLoop:
			RW00 A 0 A_SpawnItemEx("RTCW_PanzerfaustSmoke",-16*cos(velz/24),0,-16*sin(velz/24))
			RW00 A 0 A_SpawnItemEx("RTCW_PanzerfaustSmoke",-8*cos(velz/24),0,-8*sin(velz/24))
			RW00 A 1 A_SpawnItemEx("RTCW_PanzerfaustSmoke")
			Loop
	}
}

Actor RTCW_ExplosionSmokeDebris
{
	Height 4
	Radius 2
	Gravity 0.33
	Damage (0)
	Projectile
	Scale 0.1
	BounceType "Doom"
	BounceCount 3
	-NOGRAVITY
	+THRUACTORS
	+CLIENTSIDEONLY
	States
	{
		Spawn:
			TNT1 A 0 NoDelay A_ChangeVelocity(((2*random(0,1))-1)*frandom(1.0,2.0),((2*random(0,1))-1)*frandom(1.0,2.0),frandom(3.0,5.0),CVF_REPLACE)
		SpawnLoop:
			TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("RTCW_PanzerfaustSmoke",0,0,0,0,0,0,0,SXF_TRANSFERSCALE)
			Stop
	}
}

actor RTCW_PanzerfaustRocketExplosion : RTCW_GrenadeExplosion
{
    States
    {
      Spawn:
        //We have to duplicate the sprites because of a zandronum bug
        RW06 A 0 Bright NoDelay A_Quake(3,15,0,256,"")
        TNT1 A 0 A_JumpIf(waterlevel > 0, 2)
        TNT1 A 0 A_PlaySound("RTCW/Panzerfaust/Explode",CHAN_VOICE)
        goto SpawnFinish
        TNT1 A 0 A_PlaySound("RTCW/Panzerfaust/ExplodeUnderwater",CHAN_VOICE)
        goto SpawnFinish
    }
}

Actor RTCW_PanzerfaustSmoke
{
	Radius 1
	Height 1
	Alpha 0.2
	Scale 0.33
	RenderStyle Shaded
	+NOINTERACTION
	+CLIENTSIDEONLY
	+FORCEXYBILLBOARD
	States
	{
		Spawn:
			RW05 ABCDEFGHIJKLMNOPQRSTUVWX 2
			Stop
	}
}

Actor RTCW_PanzerfaustSmokeExplosion : RTCW_PanzerfaustSmoke { Scale 2.0 Alpha 0.33 }
