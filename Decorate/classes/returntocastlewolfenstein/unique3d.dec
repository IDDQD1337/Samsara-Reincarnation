Actor RTCW_Snooper : RTCW_Weapon
{
	Weapon.SlotNumber 3
	Weapon.SlotPriority 1
	Weapon.SelectionOrder 1500
	Weapon.AmmoType1 "RTCW_SnooperMagazine"
	Weapon.AmmoUse1 1
	Weapon.AmmoType2 "RTCW_AlliedAmmo2"
	Weapon.AmmoGive2 5
	Obituary "%o was killed (snooper) by %k."
	Inventory.PickupMessage "Snooper Rifle"
	Tag "M1S Snooper"
	States
	{
		Spawn:
			RW98 A -1
			stop

		ReadyActual:
			RW00 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"ReadyActualReloadable")
		ReadyActualNonReloadable:
			RW00 A 0 A_JumpIfInventory("RTCW_Scoped",1,3)
			RW00 A 0 A_JumpIfInventory("RTCW_Kicking",1,"Kicking")
			RW00 A 1 A_WeaponReady(WRF_ALLOWZOOM)
			goto ReadyActualAmmoCheck
			TNT1 A 0 A_JumpIf(CallACS("RTCW_Decorate", 3) == 0, 2)
			TNT1 A 1 A_ZoomFactor(90.0/CallACS("RTCW_Decorate", 2, 2)*1.0,ZOOM_INSTANT)
			TNT1 A 0 A_JumpIfInventory("RTCW_Kicking",1,"Kicking")
			TNT1 A 1 A_WeaponReady(WRF_ALLOWZOOM)
			goto ReadyActualAmmoCheck

		ReadyActualReloadable:
			RW00 A 0 A_JumpIfInventory("RTCW_Scoped",1,3)
			RW00 A 0 A_JumpIfInventory("RTCW_Kicking",1,"Kicking")
			RW00 A 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
			goto ReadyActualAmmoCheck
			TNT1 A 0 A_JumpIf(CallACS("RTCW_Decorate", 3) == 0, 2)
			TNT1 A 1 A_ZoomFactor(90.0/CallACS("RTCW_Decorate", 2, 2)*1.0,ZOOM_INSTANT)
			TNT1 A 0 A_JumpIfInventory("RTCW_Kicking",1,"Kicking")
			TNT1 A 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
			goto ReadyActualAmmoCheck

		ReadyActualAmmoCheck:
			RW00 A 0 A_JumpIfInventory("SamsaraReloadMode",2,1)
			goto ReadyActual
			RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"ReadyActual")
			RW00 A 0 A_JumpIfInventory("RTCW_SnooperMagazine",1,"ReadyActual")
			goto ReloadCheck

		Deselect:
			RW00 G 0 A_JumpIfInventory("RTCW_Scoped",1,1)
			goto DeselectStart
			RW00 G 0 A_TakeInventory("RTCW_ZoomFactor")
			RW00 G 0 A_TakeInventory("RTCW_Scoped")
			RW00 G 0 A_ZoomFactor(1,ZOOM_INSTANT)
			goto DeselectStart

		DeselectStart:
			RW00 G 0 A_JumpIfHealthLower(1,"DeselectInstant")
			RW00 G 2 A_GiveInventory("SamsaraRTCWWeaponSwitchSoundPlayer")
			RW00 HIJKLM 2
			goto DeselectInstant

		Ready: // our "Select" state
			RW00 A 0 A_TakeInventory("RTCW_WeaponToken",256)
			RW00 A 0 A_GiveInventory("RTCW_WeaponToken",11)
			RW00 M 0 A_JumpIfInventory("RTCW_Scoped",1,"ReadyZoomOut1") // map change hack
		ReadyStart1:
			RW00 M 1 A_JumpIfInventory("SamsaraHasDoubleFiringSpeed",1,1)
			RW00 M 0 A_JumpIfInventory("RTCW_Scoped",1,"ReadyZoomOut2") // map change hack
		ReadyStart2:
			RW00 M 1
			RW00 NOPQ 2
			goto ReadyActual

		ReadyZoomOut1:
			RW00 M 0 A_TakeInventory("RTCW_ZoomFactor")
			RW00 M 0 A_TakeInventory("RTCW_Scoped")
			RW00 M 0 A_ZoomFactor(1,ZOOM_INSTANT)
			goto ReadyStart1

		ReadyZoomOut2:
			RW00 M 0 A_TakeInventory("RTCW_ZoomFactor")
			RW00 M 0 A_TakeInventory("RTCW_Scoped")
			RW00 M 0 A_ZoomFactor(1,ZOOM_INSTANT)
			goto ReadyStart2

		Fire:
			RW00 B 0 A_JumpIfInventory("SamsaraReloadMode",2,"FireReloadable")
		FireNonReloadable:
			RW00 B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart")
			RW00 B 0 A_JumpIfInventory("RTCW_AlliedAmmo2",1,"FireStart")
			goto FireDry

		FireReloadable:
			RW00 B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireStart")
			RW00 B 0 A_JumpIfInventory("RTCW_SnooperMagazine",1,"FireStart")
		FireReloadableDryCheck:
			RW00 B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReloadScopeCheck")
			RW00 B 0 A_JumpIfInventory("RTCW_AlliedAmmo2",1,"ReloadScopeCheck")
			goto FireDry

		FireDry:
			RW00 A 0 A_ClearReFire
			RW00 A 0 A_GiveInventory("SamsaraRTCWWeaponDrySoundPlayer")
			RW00 A 0 A_JumpIfInventory("RTCW_Scoped",1,2)
			RW00 A 17 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto FireDryFinish
			TNT1 A 17 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto FireDryFinish

		FireDryFinish:
			RW00 A 0 A_SelectWeapon("RTCW_Luger")
			goto Deselect

		FireUnderwater:
			RW00 A 0 A_ClearReFire
			RW00 A 0 A_GiveInventory("SamsaraRTCWWeaponUnderwaterSoundPlayer")
			RW00 A 0 A_JumpIfInventory("RTCW_Scoped",1,2)
			RW00 A 17 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto ReadyActual
			TNT1 A 17 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto ReadyActual

		FireStart:
			RW00 B 0 A_JumpIfInventory("SamsaraCurrentWaterLevel",0,"FireUnderwater")
			//RW00 B 0 A_GiveInventory("RTCW_FiringToken",1)
			RW00 B 0 A_SpawnItemEx("RTCW_Casing",4,-6*cos(-pitch),43+(6*sin(-pitch)),1,0,2*cos(-pitch),-random(85,95),SXF_TRANSFERPITCH)
			RW00 B 0 A_JumpIfInventory("RTCW_Scoped",1,"FireScoped")
			RW00 B 0 A_GunFlash("FireFlash")
			RW00 B 0 A_GiveInventory("SamsaraRTCWSnooperAttackHandler")
			RW00 BC 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW00 A 33 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto ReadyActual

		FireScoped:
			TNT1 A 0 A_GiveInventory("SamsaraRTCWSnooperAttackHandler")
			TNT1 AA 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			TNT1 A 33 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto ReadyActual

		FireFlash:
			TNT1 A 0 A_Jump(256,1,2)
			RW03 B 2 Bright
			stop
			RW04 B 2 Bright
			stop

		AltFire:
		Zoom:
			RW00 A 0 A_GiveInventory("SamsaraRTCWWeaponSwitchSoundPlayer")
			RW00 A 0 A_JumpIfInventory("RTCW_Scoped",1,"ZoomUnscope")
		ZoomScope:
			TNT1 A 0 A_GiveInventory("RTCW_ZoomFactor",40)
			TNT1 A 0 A_GiveInventory("RTCW_Scoped",1)
			TNT1 A 0 ACS_NamedExecuteWithResult("RTCW_Scope", 2)
			TNT1 A 0 A_ZoomFactor(90.0/40.0,ZOOM_INSTANT)
			TNT1 A 10 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto ReadyActual

		ZoomUnscope:
			RW00 A 0 A_TakeInventory("RTCW_ZoomFactor",90)
			RW00 A 0 A_TakeInventory("RTCW_Scoped",1)
			RW00 A 0 A_ZoomFactor(1,ZOOM_INSTANT)
			RW00 M 0 A_JumpIfInventory("SamsaraHasDoubleFiringSpeed",1,2)
			RW00 MM 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW00 NOPQ 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto ReadyActual

		Reload:
		ReloadCheck:
			RW00 A 0 A_JumpIfInventory("RTCW_SnooperMagazine",0,"ReadyActual")
			RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
			RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReloadScopeCheck")
			RW00 A 0 A_JumpIfInventory("RTCW_AlliedAmmo2",1,"ReloadScopeCheck")
			goto ReadyActual

		ReloadScopeCheck:
			RW00 A 0 A_JumpIfInventory("RTCW_Scoped",1,1)
			goto ReloadStart
			RW00 A 0 A_GiveInventory("SamsaraRTCWWeaponSwitchSoundPlayer")
			RW00 A 0 A_TakeInventory("RTCW_ZoomFactor",90)
			RW00 A 0 A_TakeInventory("RTCW_Scoped",1)
			RW00 A 0 A_ZoomFactor(1,ZOOM_INSTANT)
			RW00 M 0 A_JumpIfInventory("SamsaraHasDoubleFiringSpeed",1,2)
			RW00 MM 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW00 NOPQ 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto ReloadCheck

		ReloadStart:
			RW00 Q 0 A_GiveInventory("SamsaraRTCWSnooperReloadSoundPlayer")
			RW00 "QRSTUVWXYZ[\]" 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW01 "ABCDEFGHIJKLMNOPQRSTUVWXYZ[\]" 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW02 ABCDEFG 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW00 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"ReloadAmmo")
		ReloadFinish:
			RW00 A 0
			goto ReadyActual

		ReloadAmmo:
			RW00 A 0 A_JumpIfInventory("RTCW_SnooperMagazine",0,"ReloadFinish")
			RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
			RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
			RW00 A 0 A_JumpIfInventory("RTCW_AlliedAmmo2",1,1)
			goto ReloadFinish
			RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
			RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
			RW00 A 0 A_TakeInventory("RTCW_AlliedAmmo2",1)
			RW00 A 0 A_GiveInventory("RTCW_SnooperMagazine")
			loop

		Kicking:
			RW00 A 0 A_GunFlash("KickingFoot")
		KickingDelay:
			RW00 A 0 A_JumpIfInventory("RTCW_Scoped",1,2)
			RW00 A 18 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto KickingDelayFinish
			TNT1 A 18 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE) // not really supposed to see anything, even the kick
			goto KickingDelayFinish

		KickingDelayFinish:
			RW00 A 0 A_TakeInventory("RTCW_Kicking",1)
			goto ReadyActual
	}
}

actor SamsaraRTCWSnooperAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"PickupReloadable")
      PickupNonReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupPlaySound")
        TNT1 A 0 A_JumpIfInventory("RTCW_AlliedAmmo2",1,1)
        stop
        TNT1 A 0 A_TakeInventory("RTCW_AlliedAmmo2",1)
        goto PickupPlaySound

      PickupReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"PickupPlaySound")
        TNT1 A 0 A_JumpIfInventory("RTCW_SnooperMagazine",1,1)
        stop
        TNT1 A 0 A_TakeInventory("RTCW_SnooperMagazine",1)
        goto PickupPlaySound

      // Sound stuff
      PickupPlaySound:
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",2,"PickupPlaySoundRTCWPC")
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupPlaySoundRTCWConsole")
      // Return To Castle Wolfenstein (Original) sounds
      PickupPlaySoundRTCWPC:
        TNT1 A 0 A_PlaySound("RTCW/Snooper/Fire",CHAN_WEAPON)
        goto PickupAttack

      // Return To Castle Wolfenstein (Console) sounds
      PickupPlaySoundRTCWConsole:
        TNT1 A 0 A_PlaySound("RTCWConsole/Snooper/Fire",CHAN_WEAPON)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",2,"PickupAttackET")
      // Return To Castle Wolfenstein sounds
      PickupAttackRTCW:
        TNT1 A 0 A_JumpIfInventory("RTCW_Scoped",1,"PickupAttackRTCWScoped")
      // Unscoped
      PickupAttackRTCWUnscoped:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackRTCWUnscopedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackRTCWUnscopedCoop")
      PickupAttackRTCWUnscopedDM:
        TNT1 A 0 A_FireCustomMissile("RTCW_SnooperTracer",frandom(-1.5,1.5),false,4,9,0,frandom(-1.5,1.5))
        goto PickupFinishUnscoped

      PickupAttackRTCWUnscopedCoop:
        TNT1 A 0 A_FireCustomMissile("RTCW_SnooperTracerCoop",frandom(-1.5,1.5),false,4,9,0,frandom(-1.5,1.5))
        goto PickupFinishUnscoped

      // Scoped
      PickupAttackRTCWScoped:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackRTCWScopedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackRTCWScopedCoop")
      PickupAttackRTCWScopedDM:
        TNT1 A 0 A_FireCustomMissile("RTCW_SnooperTracer",0,false,0,8)
        goto PickupFinishScoped

      PickupAttackRTCWScopedCoop:
        TNT1 A 0 A_FireCustomMissile("RTCW_SnooperTracerCoop",0,false,0,8)
        goto PickupFinishScoped

      // Wolfenstein: Enemy Territory sounds
      PickupAttackET:
        TNT1 A 0 A_JumpIfInventory("RTCW_Scoped",1,"PickupAttackETScoped")
      // Unscoped
      PickupAttackETUnscoped:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackETUnscopedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackETUnscopedCoop")
      PickupAttackETUnscopedDM:
        TNT1 A 0 A_FireCustomMissile("RTCW_SnooperTracerSoundET",frandom(-1.5,1.5),false,4,9,0,frandom(-1.5,1.5))
        goto PickupFinishUnscoped

      PickupAttackETUnscopedCoop:
        TNT1 A 0 A_FireCustomMissile("RTCW_SnooperTracerCoopSoundET",frandom(-1.5,1.5),false,4,9,0,frandom(-1.5,1.5))
        goto PickupFinishUnscoped

      // Scoped
      PickupAttackETScoped:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackETScopedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackETScopedCoop")
      PickupAttackETScopedDM:
        TNT1 A 0 A_FireCustomMissile("RTCW_SnooperTracerSoundET",0,false,0,8)
        goto PickupFinishScoped

      PickupAttackETScopedCoop:
        TNT1 A 0 A_FireCustomMissile("RTCW_SnooperTracerCoopSoundET",0,false,0,8)
        goto PickupFinishScoped

      // Finishing stuff
      // Unscoped
      PickupFinishUnscoped:
        TNT1 A 0 ACS_NamedExecuteWithResult("RTCW_Recoil", 2)
        stop

      // Scoped
      PickupFinishScoped:
        TNT1 A 0 A_GiveInventory("RTCW_AimSpread",16711680)
        TNT1 A 0 A_SetAngle(angle+frandom(-1.0,1.0))
        TNT1 A 0 ACS_NamedExecuteWithResult("RTCW_Recoil", 4)
        stop
    }
}

actor SamsaraRTCWSnooperReloadSoundPlayer : Trigger
{
    States
    {
      // Sound playing stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",2,"PickupET")
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupRTCWConsole")
      // Return To Castle Wolfenstein (Original) sounds
      PickupRTCWPC:
        TNT1 A 0 A_PlaySound("RTCW/Snooper/Reload",CHAN_6)
        stop

      // Return To Castle Wolfenstein (Console) sounds
      PickupRTCWConsole:
        TNT1 A 0 A_PlaySound("RTCWConsole/Snooper/Reload",CHAN_6)
        stop

      // Wolfenstein: Enemy Territory sounds
      PickupET:
        TNT1 A 0 A_PlaySound("RTCWET/Snooper/Reload",CHAN_6)
        stop
    }
}

actor RTCW_SnooperMagazine : Ammo
{
	Inventory.MaxAmount 5
	Tag "M1S Snooper Magazine"
	+IGNORESKILL
}

Actor RTCW_SnooperTracer : RTCW_Tracer
{
	+THRUGHOST
	States
	{
		Crash:
		Death:
			TNT1 A 0 A_Explode(CallACS("RTCW_BulletDamage", 100, 0),8,!XF_HURTSOURCE,0,8)
			Goto Super::Death

		XDeath:
			TNT1 A 0 A_Explode(CallACS("RTCW_BulletDamage", 100, 0),8,!XF_HURTSOURCE,0,8)
			Goto Super::XDeath
	}
}

Actor RTCW_SnooperTracerCoop : RTCW_SnooperTracer { +THRUSPECIES Species "Player" }

Actor RTCW_SnooperTracerDM : RTCW_Tracer
{
	States
	{
		Crash:
		Death:
			TNT1 A 0 A_Explode(CallACS("RTCW_BulletDamage", 70, 0),8,!XF_HURTSOURCE,0,8)
			Goto Super::Death

		XDeath:
			TNT1 A 0 A_Explode(CallACS("RTCW_BulletDamage", 70, 0),8,!XF_HURTSOURCE,0,8)
			Goto Super::XDeath
	}
}

Actor RTCW_SnooperTracerSoundET : RTCW_TracerSoundET
{
	+THRUGHOST
	States
	{
		Crash:
		Death:
			TNT1 A 0 A_Explode(CallACS("RTCW_BulletDamage", 100, 2),8,!XF_HURTSOURCE,0,8)
			Goto Super::Death

		XDeath:
			TNT1 A 0 A_Explode(CallACS("RTCW_BulletDamage", 100, 2),8,!XF_HURTSOURCE,0,8)
			Goto Super::XDeath
	}
}

Actor RTCW_SnooperTracerCoopSoundET : RTCW_SnooperTracerSoundET { +THRUSPECIES Species "Player" }

Actor RTCW_SnooperTracerDMSoundET : RTCW_TracerSoundET
{
	States
	{
		Crash:
		Death:
			TNT1 A 0 A_Explode(CallACS("RTCW_BulletDamage", 70, 2),8,!XF_HURTSOURCE,0,8)
			Goto Super::Death

		XDeath:
			TNT1 A 0 A_Explode(CallACS("RTCW_BulletDamage", 70, 2),8,!XF_HURTSOURCE,0,8)
			Goto Super::XDeath
	}
}

actor RTCW_SnooperVision : PowerLightAmp { Powerup.Color InverseMap }
