ACTOR "Subestron Arm" : Weapon
{
  Weapon.SelectionOrder 600
  Weapon.SlotNumber 7
  Weapon.BobStyle Inverse
  Weapon.BobRangeX 0.85
  Weapon.BobSpeed 1.1  
  Weapon.AmmoUse 8
  Weapon.AmmoGive 0
  Weapon.AmmoType "Cell"
  Inventory.PickupSound "RMR/pickup" 	  
  Inventory.PickupMessage "You got a Mechanical Weapon Arm!"
  Inventory.RestrictedTo "RebelMoonCommando"
  Tag "Subestron Arm"
  States
  {
  Ready:
    RM2X E 1 A_WeaponReady
    Loop
  Deselect:
	TNT1 A 0 A_Lower
    RM2X E 1 A_Lower
	Loop
  Select:
	TNT1 A 0 A_Raise  
    RM2X E 1 A_Raise
    Loop
  Fire:   
    TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopFire")  
    TNT1 A 0 A_PlaySound("RMR/RPlasma")
	RM2X F 10 bright A_FireCustomMissile("RMR-RedPlasma",0,1,0,0)
	RM2X G 10 bright
 	RM2X G 1 bright A_Refire
    Goto Ready
  CoopFire:
    TNT1 A 0 A_PlaySound("RMR/RPlasma")
	RM2X F 10 bright A_FireCustomMissile("RMR-RedPlasmaCoop",0,1,0,0)
	RM2X G 10 bright 	
 	RM2X G 1 bright A_Refire	
	Goto Ready
  Spawn:
	RM2X H 0 Bright
	TNT1 A 1 Bright A_SpawnItemEx("SamsaraSlotSevenPickup")
	Stop
  }
}

Actor RMR-RedPlasma : FastProjectile
{
    Damage (180)
    Radius 5
    Height 5
    Speed 115
    Projectile
	+WINDTHRUST
	+THRUACTORS
//	+NOBLOOD
	-SKYEXPLODE
	+FORCERADIUSDMG
//	+BLOODLESSIMPACT	
	DeathSound "RMR/RPlasmaB"
    DamageType "RMR-BigPlasma"		
	Obituary "%k killed %o with spicy doritos."
	Scale 0.50
    States
    {
      Spawn:
		TNT1 A 0
        RMZ3 GH 1 bright A_GiveInventory("RMR-RedPlasma_Predictor")
        loop
      Death:
        RMZ3 IJK 4 bright
		RMZ3 L 3 bright A_FadeOut
        stop
	  XDeath:
		TNT1 A 0 A_Explode(400,60,!XF_HURTSOURCE,1,60)
        RMZ3 IJK 4 bright
		RMZ3 L 3 bright A_FadeOut
        stop		
    }
}

Actor RMR-RedPlasma_Predictor : Trigger
{
	States
	{
		Pickup:
			TNT1 A 0 A_SetArg(0,sqrt(velx*velx + vely*vely)*1000/sqrt(velx*velx + vely*vely + velz*velz))
			TNT1 A 0 A_SetArg(1,velz*1000/sqrt(velx*velx + vely*vely + velz*velz))
			TNT1 A 0 A_SetArg(2,sqrt(velx*velx + vely*vely + velz*velz)/2)
			TNT1 A 0 A_SetMass(-args[2])
		PickupLoop:
			TNT1 A 0 A_JumpIf(mass >= args[2],"PickupFinish")
			TNT1 A 0 A_SpawnItemEx("RMR-RedPlasma_Explosion",args[0]/1000.0*mass,0,args[1]/1000.0*mass,0,0,0,0,SXF_TRANSFERPOINTERS)
			TNT1 A 0 A_SetMass(mass+radius)
			Loop
		PickupFinish:
			TNT1 A 0
			Stop
	}
}

Actor RMR-RedPlasma_Explosion
{
	Projectile
	Height 3
	Radius 3
	Speed 0
	DamageType "RMR-BigPlasma"	
	Obituary "%k killed %o with spicy doritos."
	+NOINTERACTION
	+FORCERADIUSDMG
	+NODAMAGETHRUST
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_Explode(Random(10,18),5,!XF_HURTSOURCE,0,5)
			TNT1 A 1
			Stop
	}
}

Actor RMR-RedPlasmaCoop : RMR-RedPlasma { Species "Player" +THRUSPECIES }