actor "ACR Laser Rifle" : SamsaraRMRWeapon
{
    Inventory.PickupMessage "Autofiring Combat Laser"
    Weapon.SlotNumber 2
    Weapon.SelectionOrder 1500
    Weapon.AmmoType "Shell"
    Weapon.AmmoGive 10
    Weapon.AmmoUse 1
    Tag "ACR-5A2 Automatic Combat Laser Rifle"
    States
    {
      Spawn:
        RM3Z E -1
        stop

      Ready:
        RM3Z A 1 A_WeaponReady
        loop

      Deselect:
        TNT1 A 0 A_Lower
        RM3Z A 1 A_Lower
        loop

      Select:
        TNT1 A 0 A_Raise
        RM3Z A 1 A_Raise
        loop

      Fire:
        TNT1 A 0 A_GiveInventory("SamsaraRMRACRAttackHandler")
        RM3Z B 4 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        RM3Z CD 4 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto Ready
    }
}

actor SamsaraRMRACRAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Shell",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Shell",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupAttackRM")
      // Rebel Moon Rising sounds
      PickupAttackRMR:
        TNT1 A 0 A_PlaySound("RMR/ACRSHOT",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackRMRCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackRMRCoop")
      PickupAttackRMRDM:
        TNT1 A 0 A_FireCustomMissile("RMRLaser2",0,false)
        stop

      PickupAttackRMRCoop:
        TNT1 A 0 A_FireCustomMissile("RMRLaser2Coop",0,false)
        stop

      // Rebel Moon sounds
      PickupAttackRM:
        TNT1 A 0 A_PlaySound("RebelMoon/ACRSHOT",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackRMCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackRMCoop")
      PickupAttackRMDM:
        TNT1 A 0 A_FireCustomMissile("RMRLaser2SoundRM",0,false)
        stop

      PickupAttackRMCoop:
        TNT1 A 0 A_FireCustomMissile("RMRLaser2CoopSoundRM",0,false)
        stop
    }
}

actor RMRLaser2 : FastProjectile
{
    Radius 5
    Height 4
    Speed 175
    Damage (40)
    DamageType "RMR-YellowPlasma"
    Scale 0.3
    DeathSound "RMR/LaserPuff2"
    Obituary "%k blasted %o with the autofiring combat laser."
    +BLOODSPLATTER
    +FORCEXYBILLBOARD
    +NOEXTREMEDEATH
    States
    {
      Spawn:
        RMZ2 G -1 Bright Light("RMRACRPROJ")
        stop

      Death:
        RMZ2 H 3 Bright Light("RMRACRPROJEXP")
        RMZ2 I 2 Bright Light("RMRACRPROJEXP") A_FadeOut(0.2)
        wait

      XDeath:
      Crash:
        TNT1 A 1 A_SpawnItemEx("RMRPlasmaSplash_2",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
        stop
    }
}

actor RMRLaser2Coop : RMRLaser2
{
    Damage (60)
    Species "Player"
    +THRUSPECIES
}

actor RMRLaser2SoundRM : RMRLaser2 { DeathSound "RebelMoon/LaserPuff2" }

actor RMRLaser2CoopSoundRM : RMRLaser2Coop { DeathSound "RebelMoon/LaserPuff2" }
