actor "Cyclops Particle Accelerator" : SamsaraRMRWeapon
{
    Inventory.PickupMessage "Cyclops Mark VI Particle Accelerator"
    Weapon.SelectionOrder 300
    Weapon.SlotNumber 4
    Weapon.AmmoType "Clip"
    Weapon.AmmoGive 25
    Weapon.AmmoUse 1
    Tag "Cyclops Particle Accelerator"
    States
    {
      Spawn:
        RM7T E -1
        stop

      Ready:
        RM7T A 1 A_WeaponReady
        loop

      Deselect:
        TNT1 A 0 A_Lower
        RM7T A 1 A_Lower
        loop

      Select:
        TNT1 A 0 A_Raise
        RM7T A 1 A_Raise
        loop

      Fire:
        TNT1 A 0 A_GiveInventory("SamsaraRMRParticleAcceleratorAttackHandler")
        RM7T B 1 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        RM7T CD 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto Ready
    }
}

actor SamsaraRMRParticleAcceleratorAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupAttackRM")
      // Rebel Moon Rising sounds
      PickupAttackRMR:
        TNT1 A 0 A_PlaySound("RMR/SpinningP",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackRMRCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackRMRCoop")
      PickupAttackRMRDM:
        TNT1 A 0 A_FireCustomMissile("RMRSpinningPlasma",0,false)
        stop

      PickupAttackRMRCoop:
        TNT1 A 0 A_FireCustomMissile("RMRSpinningPlasmaCoop",0,false)
        stop

      // Rebel Moon sounds
      PickupAttackRM:
        TNT1 A 0 A_PlaySound("RebelMoon/SpinningP",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackRMCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackRMCoop")
      PickupAttackRMDM:
        TNT1 A 0 A_FireCustomMissile("RMRSpinningPlasmaSoundRM",0,false)
        stop

      PickupAttackRMCoop:
        TNT1 A 0 A_FireCustomMissile("RMRSpinningPlasmaCoopSoundRM",0,false)
        stop
    }
}

actor RMRSpinningPlasma : FastProjectile
{
    Radius 5
    Height 5
    Speed 140
    Damage (14)
    DamageType "RMR-CyanPlasma"
    Scale 0.5
    DeathSound "RMR/SpinningB"
    Obituary "%k lit %o up with the particle accelerator."
    +BLOODLESSIMPACT
    +NOEXTREMEDEATH
    +WINDTHRUST
    States
    {
      Spawn:
        RMZ2 PQ 1 Bright Light("RMRCYANPLASMA")
        loop

      Death:
      Crash:
        RMZ2 RST 4 Bright Light("RMRCYANPLASMABOOM")
        RMZ2 U 3 Bright Light("RMRCYANPLASMABOOM") A_FadeOut
        stop

      XDeath:
        TNT1 A 1 A_SpawnItemEx("RMRPlasmaSplash",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
        stop
    }
}

actor RMRSpinningPlasmaCoop : RMRSpinningPlasma
{
    Speed 180
    Damage (18)
    Species "Player"
    +THRUSPECIES
}

actor RMRSpinningPlasmaSoundRM : RMRSpinningPlasma { DeathSound "RebelMoon/Impact3" }

actor RMRSpinningPlasmaCoopSoundRM : RMRSpinningPlasmaCoop { DeathSound "RebelMoon/Impact3" }
