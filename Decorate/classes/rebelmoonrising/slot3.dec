actor "ACR ADD-ON" : SamsaraRMRWeapon
{
    Inventory.PickupMessage "Recoilless ACL 3X Power"
    Weapon.SlotNumber 3
    Weapon.SelectionOrder 30
    Weapon.AmmoType "Shell"
    Weapon.AmmoGive 18
    Weapon.AmmoUse 3
    Tag "ACR Add-On"
    States
    {
      Spawn:
        RM2X I -1
        loop

      Ready:
        RM2X A 1 A_WeaponReady
        loop

      Deselect:
        TNT1 A 0 A_Lower
        RM2X A 1 A_Lower
        loop

      Select:
        TNT1 A 0 A_Raise
        RM2X A 1 A_Raise
        loop

      Fire:
        TNT1 A 0 A_GiveInventory("SamsaraRMRACRAddonAttackHandler")
        RM2X D 11 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        RM2X B 12 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto Ready
    }
}

actor SamsaraRMRACRAddonAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Shell",3,1)
        stop
        TNT1 A 0 A_TakeInventory("Shell",3)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupAttackRM")
      // Rebel Moon Rising sounds
      PickupAttackRMR:
        TNT1 A 0 A_PlaySound("RMR/ACRSHOT2",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackRMRCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackRMRCoop")
      PickupAttackRMRDM:
        TNT1 A 0 A_FireCustomMissile("RMRLaser3",0,false)
        TNT1 A 0 A_FireCustomMissile("RMRLaser3",0,false,-8)
        TNT1 A 0 A_FireCustomMissile("RMRLaser3",0,false,8)
        stop

      PickupAttackRMRCoop:
        TNT1 A 0 A_FireCustomMissile("RMRLaser3Coop",0,false)
        TNT1 A 0 A_FireCustomMissile("RMRLaser3Coop",0,false,-8)
        TNT1 A 0 A_FireCustomMissile("RMRLaser3Coop",0,false,8)
        stop

      // Rebel Moon sounds
      PickupAttackRM:
        TNT1 A 0 A_PlaySound("RebelMoon/ACRSHOT2",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackRMCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackRMCoop")
      PickupAttackRMDM:
        TNT1 A 0 A_FireCustomMissile("RMRLaser3SoundRM",0,false)
        TNT1 A 0 A_FireCustomMissile("RMRLaser3SoundRM",0,false,-8)
        TNT1 A 0 A_FireCustomMissile("RMRLaser3SoundRM",0,false,8)
        stop

      PickupAttackRMCoop:
        TNT1 A 0 A_FireCustomMissile("RMRLaser3CoopSoundRM",0,false)
        TNT1 A 0 A_FireCustomMissile("RMRLaser3CoopSoundRM",0,false,-8)
        TNT1 A 0 A_FireCustomMissile("RMRLaser3CoopSoundRM",0,false,8)
        stop
    }
}

actor RMRLaser3 : RMRLaser2
{
    Radius 7
    Height 6
    Damage (45)
    DamageType "RMR-YellowPlasma2"
    Scale 0.4
    Obituary "%k splayed %o with the recoilless ACL."
    States
    {
      Death:
        RMZ2 H 3 Bright Light("RMRACRPROJEXP")
        RMZ2 I 2 Bright Light("RMRACRPROJEXP") A_FadeOut(0.4)
        wait
    }
}

actor RMRLaser3Coop : RMRLaser3
{
    Damage (70)
    Species "Player"
    +THRUSPECIES
}

actor RMRLaser3SoundRM : RMRLaser3 { DeathSound "RebelMoon/LaserPuff2" }

actor RMRLaser3CoopSoundRM : RMRLaser3Coop { DeathSound "RebelMoon/LaserPuff2" }
