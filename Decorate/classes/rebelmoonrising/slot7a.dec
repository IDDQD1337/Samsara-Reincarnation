actor " RMR Plasma Cannon " : SamsaraRMRWeapon
{
    Inventory.PickupMessage "DEWY Mark II Plasma Cannon"
    Weapon.SelectionOrder 100
    Weapon.SlotNumber 7
    Weapon.AmmoType "Cell"
    Weapon.AmmoGive 40
    Weapon.AmmoUse 8
    Tag "Fusion Dynamics PC-12/2 Plasma Cannon"
    +BFG
    States
    {
      Spawn:
        RM7V E -1
        stop

      Ready:
        RM7V A 1 A_WeaponReady
        loop

      Deselect:
        TNT1 A 0 A_Lower
        RM7V A 1 A_Lower
        loop

      Select:
        TNT1 A 0 A_Raise
        RM7V A 1 A_Raise
        loop

      Fire:
        TNT1 A 0 A_GiveInventory("SamsaraRMRPlasmaCannonAttackHandler")
        RM7V CD 6 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        RM7V B 7 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto Ready
    }
}

actor SamsaraRMRPlasmaCannonAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Cell",8,1)
        stop
        TNT1 A 0 A_TakeInventory("Cell",8)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupAttackRM")
      // Rebel Moon Rising sounds
      PickupAttackRMR:
        TNT1 A 0 A_PlaySound("RMR/PlasmaC",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackRMRCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackRMRCoop")
      PickupAttackRMRDM:
        TNT1 A 0 A_FireCustomMissile("RMR-BigPlasma",0,false)
        stop

      PickupAttackRMRCoop:
        TNT1 A 0 A_FireCustomMissile("RMR-BigPlasmaCoop",0,false)
        stop

      // Rebel Moon sounds
      PickupAttackRM:
        TNT1 A 0 A_PlaySound("RebelMoon/PlasmaC",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackRMCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackRMCoop")
      PickupAttackRMDM:
        TNT1 A 0 A_FireCustomMissile("RMR-BigPlasmaSoundRM",0,false)
        stop

      PickupAttackRMCoop:
        TNT1 A 0 A_FireCustomMissile("RMR-BigPlasmaCoopSoundRM",0,false)
        stop
    }
}

actor RMR-BigPlasma : FastProjectile
{
    Radius 5
    Height 5
    Speed 70
    Damage (250)
    DamageType "RMR-BigPlasma"
    Scale 0.5
    DeathSound "RMR/PlasmaCB"
    Obituary "%k overcharged %o with the plasma cannon."
    +FORCEXYBILLBOARD
    +WINDTHRUST
    States
    {
      Spawn:
        RMZ3 AB 1 Bright Light("RMRPURPLEPLASMA")
        loop

      Death:
        RMZ3 C 4 Bright Light("RMRPURPLEPLASMABOOM") A_Explode(500,64,0)
        RMZ3 DE 4 Bright Light("RMRPURPLEPLASMABOOM")
        RMZ3 F 3 Bright Light("RMRPURPLEPLASMABOOM") A_FadeOut
        stop
    }
}

actor RMR-BigPlasmaCoop : RMR-BigPlasma
{
    Speed 100
    Damage (1000)
    Species "Player"
    +THRUSPECIES
}

actor RMR-BigPlasmaSoundRM : RMR-BigPlasma { DeathSound "RebelMoon/PlasmaCB" }

actor RMR-BigPlasmaCoopSoundRM : RMR-BigPlasmaCoop { DeathSound "RebelMoon/PlasmaCB" }
