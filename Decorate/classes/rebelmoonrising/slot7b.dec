actor "Subestron Arm" : SamsaraRMRWeapon
{
    Inventory.PickupMessage "Mechanical Weapon Arm"
    Weapon.SelectionOrder 600
    Weapon.SlotNumber 7
    Weapon.AmmoType "Cell"
    //Weapon.AmmoGive 24
    Weapon.AmmoUse 8
    Tag "Subestron Arm"
    +BFG
    States
    {
      Spawn:
        RM2X H 0
        TNT1 A 1 A_SpawnItemEx("SamsaraSlotSevenPickup")
        stop

      Ready:
        RM2X E 1 A_WeaponReady
        loop

      Deselect:
        TNT1 A 0 A_Lower
        RM2X E 1 A_Lower
        loop

      Select:
        TNT1 A 0 A_Raise
        RM2X E 1 A_Raise
        loop

      Fire:
        TNT1 A 0 A_GiveInventory("SamsaraRMRSubestronArmAttackHandler")
        RM2X FG 10 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto Ready
    }
}

actor SamsaraRMRSubestronArmAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Cell",8,1)
        stop
        TNT1 A 0 A_TakeInventory("Cell",8)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("RMR/RPlasma",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("RMR-RedPlasma",0,false)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("RMR-RedPlasmaCoop",0,false)
        stop
    }
}

actor RMR-RedPlasma : FastProjectile
{
    Radius 5
    Height 5
    Speed 115
    Damage (180)
    DamageType "RMR-BigPlasma"
    Scale 0.5
    DeathSound "RMR/RPlasmaB"
    Obituary "%k vaporized %o with the mechanical weapon arm."
    +FORCERADIUSDMG
    +THRUACTORS
    +WINDTHRUST
    States
    {
      Spawn:
        RMZ3 G 0 Bright Light("RMRREDPLASMA")
        RMZ3 GH 1 Bright Light("RMRREDPLASMA") A_GiveInventory("RMR-RedPlasma_Predictor")
        loop

      Death:
        RMZ3 IJK 4 Bright Light("RMRREDPLASMASPLASH")
        RMZ3 L 3 Bright Light("RMRREDPLASMASPLASH") A_FadeOut
        stop

      XDeath:
        TNT1 A 0 A_Explode(400,60,0,false,60)
        goto Death
    }
}

actor RMR-RedPlasma_Predictor : Trigger
{
    States
    {
      Pickup:
        TNT1 A 0 A_SetArg(0,sqrt(velx*velx+vely*vely)*1000/sqrt(velx*velx+vely*vely+velz*velz))
        TNT1 A 0 A_SetArg(1,velz*1000/sqrt(velx*velx+vely*vely+velz*velz))
        TNT1 A 0 A_SetArg(2,sqrt(velx*velx+vely*vely+velz*velz)/2)
        TNT1 A 0 A_SetMass(-args[2])
      PickupLoop:
        TNT1 A 0 A_JumpIf(mass >= args[2], "PickupStop")
        TNT1 A 0 A_SpawnItemEx("RMR-RedPlasma_Explosion",args[0]/1000.0*mass,0,args[1]/1000.0*mass,0,0,0,0,SXF_TRANSFERPOINTERS)
        TNT1 A 0 A_SetMass(mass+radius)
        loop
    }
}

Actor RMR-RedPlasma_Explosion
{
    Projectile
    Radius 3
    Height 3
    DamageType "RMR-BigPlasma"
    Obituary "%k vaporized %o with the mechanical weapon arm."
    +DONTBLAST
    +DONTREFLECT
    +DONTSPLASH
    +FORCERADIUSDMG
    +NODAMAGETHRUST
    +NOINTERACTION
    +NOTIMEFREEZE
    States
    {
      Spawn:
        TNT1 A 0
        TNT1 A 1 A_Explode(random(10,18),5,0,false,5)
        stop
    }
}

actor RMR-RedPlasmaCoop : RMR-RedPlasma
{
    Species "Player"
    +THRUSPECIES
}
