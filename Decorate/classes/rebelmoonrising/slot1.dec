actor "Dirtshark" : SamsaraRMRWeapon
{
    Inventory.PickupMessage "MolyCarbide Power Mining Drill"
    Weapon.SlotNumber 1
    Weapon.SelectionOrder 2200
    Weapon.Kickback 0
    Obituary "%k drilled %o a deep hole."
    Tag "\"Dirtshark\" MolyCarbide Power Mining Drill"
    +MELEEWEAPON
    States
    {
      Spawn:
        RMZ6 P -1
        stop

      Ready:
        RM0G A 1 A_WeaponReady
        loop

      Deselect:
        TNT1 A 0 A_Lower
        RM0G A 1 A_Lower
        loop

      Select:
        TNT1 A 0 A_Raise
        RM0G A 1 A_Raise
        loop

      Fire:
        TNT1 A 0 A_GiveInventory("SamsaraRMRDirtsharkAttackHandler")
        RM0G BCD 4 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto Ready
    }
}

actor SamsaraRMRDirtsharkAttackHandler : Trigger
{
    States
    {
      // Attack stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupRM")
      // Rebel Moon Rising sounds
      PickupRMR:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupRMRCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupRMRCoop")
      PickupRMRDM:
        TNT1 A 0 A_Saw("rmr/drillfull","rmr/drillfull",30,"RMRDirtsharkPuff",SF_NORANDOM|SF_NOUSEAMMO)
        stop

      PickupRMRCoop:
        TNT1 A 0 A_Saw("rmr/drillfull","rmr/drillfull",30,"RMRDirtsharkPuffCoop",SF_NORANDOM|SF_NOUSEAMMO)
        stop

      // Rebel Moon sounds
      PickupRM:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupRMCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupRMCoop")
      PickupRMDM:
        TNT1 A 0 A_Saw("rebelmoon/drillfull","rebelmoon/drillfull",30,"RMRDirtsharkPuffSoundRM",SF_NORANDOM|SF_NOUSEAMMO)
        stop

      PickupRMCoop:
        TNT1 A 0 A_Saw("rebelmoon/drillfull","rebelmoon/drillfull",30,"RMRDirtsharkPuffCoopSoundRM",SF_NORANDOM|SF_NOUSEAMMO)
        stop
    }
}

actor RMRDirtsharkPuff : RMRPuff
{
    +NOTIMEFREEZE
    +PUFFONACTORS
    States
    {
      Spawn:
      Crash:
        RMZ9 A 0 Bright Light("RMRPUFF4") NoDelay A_PlaySound("rmr/drillhit")
      SpawnStart:
        TNT1 A 0 A_ChangeFlag("NOTIMEFREEZE",false)
        goto Puff

      XDeath:
        TNT1 A 1 A_PlaySound("rmr/drillhit")
        stop
    }
}

actor RMRDirtsharkPuffCoop : RMRDirtsharkPuff { +MTHRUSPECIES }

actor RMRDirtsharkPuffSoundRM : RMRDirtsharkPuff
{
    States
    {
      Spawn:
      Crash:
        RMZ9 A 0 Bright Light("RMRPUFF4") NoDelay A_PlaySound("rebelmoon/drillhit")
        goto SpawnStart

      XDeath:
        TNT1 A 1 A_PlaySound("rebelmoon/drillhit")
        stop
    }
}

actor RMRDirtsharkPuffCoopSoundRM : RMRDirtsharkPuffCoop
{
    States
    {
      Spawn:
      Crash:
        RMZ9 A 0 Bright Light("RMRPUFF4") NoDelay A_PlaySound("rebelmoon/drillhit")
        goto SpawnStart

      XDeath:
        TNT1 A 1 A_PlaySound("rebelmoon/drillhit")
        stop
    }
}
