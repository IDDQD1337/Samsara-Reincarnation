actor SamsaraRMRRailgunHold : Boolean {}

actor "RMR Railgun" : SamsaraRMRWeapon
{
    Inventory.PickupMessage "Railgun"
    Weapon.SelectionOrder 400
    Weapon.SlotNumber 6
    Weapon.AmmoType "Cell"
    Weapon.AmmoGive 36
    Weapon.AmmoUse 1
    Obituary "%k murdered %o with the railgun."
    Tag "Railgun"
    +AMMO_OPTIONAL // don't want A_ReFire to auto switch on running out of ammo
    +NOALERT
    States
    {
      Spawn:
        RM2W E -1
        stop

      Ready:
        RM2W A 1 A_WeaponReady
        loop

      Deselect:
        TNT1 A 0 A_Lower
        RM2W A 1 A_Lower
        loop

      Select:
        TNT1 A 0 A_Raise
        RM2W A 1 A_Raise
        loop

      Fire:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Cell",1,1)
        goto NoAmmoSwitch
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_TakeInventory("SamsaraRMRRailgunHold")
        goto FireStart

      FireHold:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Cell",1,1)
        goto NoAmmoSwitch
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_GiveInventory("SamsaraRMRRailgunHold")
      FireStart:
        TNT1 A 0 A_GiveInventory("SamsaraRMRRailgunAttackHandler")
        RM2W B 1 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        RM2W C 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        RM2W D 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_ReFire("FireHold")
        goto Ready

      // "No ammo" switch
      NoAmmoSwitch:
        TNT1 A 0 A_ClearReFire
        TNT1 A 0 A_SelectWeapon(" Laser Pistol ")
        goto Deselect
    }
}

actor SamsaraRMRRailgunAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Cell",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Cell",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SamsaraRMRRailgunHold",1,"PickupAttackHold")
      // First shot (Fire)
      PickupAttackFire:
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupAttackFireRM")
      // Rebel Moon Rising sounds
      PickupAttackFireRMR:
        TNT1 A 0 A_PlaySound("RMR/RLGUNSHOT",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackFireRMRCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackFireRMRCoop")
      PickupAttackFireRMRDM:
        TNT1 A 0 A_FireCustomMissile("RMRBullet",0,false)
        stop

      PickupAttackFireRMRCoop:
        TNT1 A 0 A_FireCustomMissile("RMRBulletCoop",0,false)
        stop

      // Rebel Moon sounds
      PickupAttackFireRM:
        TNT1 A 0 A_PlaySound("RebelMoon/RLGUNSHOT",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackFireRMCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackFireRMCoop")
      PickupAttackFireRMDM:
        TNT1 A 0 A_FireCustomMissile("RMRBulletSoundRM",0,false)
        stop

      PickupAttackFireRMCoop:
        TNT1 A 0 A_FireCustomMissile("RMRBulletCoopSoundRM",0,false)
        stop

      // Subsequent shots (Hold)
      PickupAttackHold:
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupAttackHoldRM")
      // Rebel Moon Rising sounds
      PickupAttackHoldRMR:
        TNT1 A 0 A_PlaySound("RMR/RLGUNSHOT",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackHoldRMRCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackHoldRMRCoop")
      PickupAttackHoldRMRDM:
        TNT1 A 0 A_FireCustomMissile("RMRBullet",frandom(-1.5,1.5),false,0,frandom(-2,2))
        stop

      PickupAttackHoldRMRCoop:
        TNT1 A 0 A_FireCustomMissile("RMRBulletCoop",frandom(-1.5,1.5),false,0,frandom(-2,2))
        stop

      // Rebel Moon sounds
      PickupAttackHoldRM:
        TNT1 A 0 A_PlaySound("RebelMoon/RLGUNSHOT",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackHoldRMCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackHoldRMCoop")
      PickupAttackHoldRMDM:
        TNT1 A 0 A_FireCustomMissile("RMRBulletSoundRM",frandom(-1.5,1.5),false,0,frandom(-2,2))
        stop

      PickupAttackHoldRMCoop:
        TNT1 A 0 A_FireCustomMissile("RMRBulletCoopSoundRM",frandom(-1.5,1.5),false,0,frandom(-2,2))
        stop
    }
}

actor RMRBullet : FastProjectile
{
    Radius 1
    Height 1
    Speed 275
    Damage (24)
    DamageType "RMR-Bullet"
    DeathSound "RMR/LaserPuff"
    Obituary "%k murdered %o with the railgun."
    +BLOODSPLATTER
    +NOEXTREMEDEATH
    +NOTIMEFREEZE
    +THRUGHOST
    States
    {
      Spawn:
        TNT1 A -1
        stop

      Death:
      Crash:
        TNT1 A 1 A_SpawnItemEx("RMRPuff",0,frandom(frandom(5.6,6.6)*frandom(1,frandom(1.5,2)),frandom(-6.6,-5.6)*frandom(1,frandom(1.5,2))),frandom(frandom(5.6,6.6)*frandom(1,frandom(1.5,2)),frandom(-6.6,-5.6)*frandom(1,frandom(1.5,2))))
        stop

      XDeath:
        TNT1 A 1 A_SpawnItemEx("RMRPlasmaSplash",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
        stop
    }
}

actor RMRBulletCoop : RMRBullet
{
    Damage (32)
    Species "Player"
    +THRUSPECIES
}

actor RMRBulletSoundRM : RMRBullet { DeathSound "RebelMoon/SpinningB" }

actor RMRBulletCoopSoundRM : RMRBulletCoop { DeathSound "RebelMoon/SpinningB" }
