actor SamsaraDescentSmartMineAttackHandler : Trigger
{
	States
	{
	// Ammo checking/taking stuff
	Pickup:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
		TNT1 A 0 A_JumpIfInventory("Shell",1,1)
		stop
		TNT1 A 0 A_TakeInventory("Shell",1)
		goto PickupAttack

	// Attack stuff
	PickupAttack:
		TNT1 A 0 A_PlaySound("Descent/Mine/Arm",CHAN_WEAPON)
		TNT1 A 0 ACS_NamedExecuteWithResult("DescentMinePlace", 1)
		stop
	}
}

actor DescentSmartMine
{
	Height 32
	Radius 16
	Health 100
	Mass 65536
	+NOBLOOD
	+SHOOTABLE
	+FRIENDLY
	-COUNTKILL
	+NOGRAVITY
	+LOOKALLAROUND
	+FORCEXYBILLBOARD
	+DONTFALL
	+FORCERADIUSDMG
	States
	{
		Spawn:
			DS16 A 0 NoDelay A_CopyFriendliness(AAPTR_MASTER)
			DS16 A 35
		SpawnLoop:
			DS16 ABCDE 2 A_LookEx(0,0,64)
			loop

		See:
			TNT1 A 0 A_Die
		Death:
			TNT1 A 0 A_Jump(256,1,2,3,4,5,6,7)
			TNT1 AAAAAAAAAAAA 0 A_CustomMissile("DescentSmartMineBomblet",0,0,random(0,359),CMF_AIMDIRECTION|CMF_TRACKOWNER,random(0,359))
			Goto DeathExplosion
		DeathExplosion:
			TNT1 A 0 A_PlaySound("Descent/Laser/Explode",CHAN_VOICE)
			TNT1 A 0 A_AlertMonsters
			TNT1 A 0 A_CustomMissile("DescentMineExplosion")
			TNT1 A 1 A_SpawnItemEx("DescentExplosion",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
			stop
	}
}

actor DescentSmartMineCoop : DescentSmartMine
{
	States
	{
		Death:
			TNT1 A 0 A_Jump(256,1,2,3,4,5,6,7)
			TNT1 AAAAAAAAAAAA 0 A_CustomMissile("DescentSmartMineBombletCoop",0,0,random(0,359),CMF_AIMDIRECTION|CMF_TRACKOWNER,random(0,359))
			goto DeathExplosion
	}
}

actor DescentMineExplosion
{
	Projectile
	Speed 0
	Damage (0)
	+BLOODLESSIMPACT
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 1 A_Explode(200,150)
			stop
	}
}

actor DescentSmartMineBomblet : DescentMissile
{
	Speed 8
	+SEEKERMISSILE
	-SCREENSEEKER
	States
	{
		Spawn:
			DS17 A 0
			DS17 A 0 A_TransferPointer(AAPTR_TARGET,AAPTR_DEFAULT,AAPTR_FRIENDPLAYER,AAPTR_TARGET)
			DS17 A 0 A_CopyFriendliness(AAPTR_TARGET)
			DS17 A 0 A_ReArrangePointers(AAPTR_TARGET,AAPTR_MASTER,AAPTR_NULL)
		SpawnLoop:
			DS17 ABCDEF 1 A_SeekerMissile(8,8,SMF_LOOK|SMF_PRECISE)
			loop

		Death:
			//DS18 A 0 A_SetScale(1,1)
			DS18 A 0 A_Explode(30,50,0)
			DS18 A 0 A_PlaySound("Descent/Laser/Explode",CHAN_VOICE)
			DS18 ABCDEFGH 2
			stop
	}
}

actor DescentSmartMineBombletCoop : DescentSmartMineBomblet { +THRUSPECIES Species "Player" }
