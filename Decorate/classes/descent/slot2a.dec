actor SamsaraDescentVulcanFiredCount : Counter { Inventory.MaxAmount 7 }
actor SamsaraDescentVulcanAttackSilentSound : Boolean {}

actor DescentVulcan : DescentWeaponPrimer
{
	Weapon.SlotNumber 2
	Weapon.AmmoType1 "Clip"
	Weapon.AmmoType2 "Shell"
	Weapon.AmmoUse1 1
	Weapon.AmmoUse2 1
	Weapon.AmmoGive1 10
	Weapon.AmmoGive2 4
	Inventory.PickupMessage "Vulcan Cannon and Concussion Missiles!"
	Tag "Vulcan Cannon and Concussion Missiles"
	Inventory.RestrictedTo "DescentPyroGX"
	+FLOATBOB
	States
	{
		Spawn:
			DS69 ABCDEFGHIJKLMNO 2
			loop

		SetPrimary:
			TNT1 A 0 A_TakeInventory("DescentPrimaryCounter")
			TNT1 A 0 A_GiveInventory("DescentPrimaryCounter",2)
			goto Super::SetPrimary

		SetSecondary:
			TNT1 A 0 A_TakeInventory("DescentMissileCamera")
			TNT1 A 0 A_TakeInventory("DescentSecondaryCounter")
			TNT1 A 0 A_GiveInventory("DescentSecondaryCounter",2)
			goto Super::SetSecondary
	}
}

actor SamsaraDescentVulcanAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        stop
        TNT1 A 0 A_JumpIfInventory("SamsaraDescentVulcanFiredCount",0,2)
        TNT1 A 0 A_GiveInventory("SamsaraDescentVulcanFiredCount")
        goto PickupAttack
        TNT1 A 0 A_TakeInventory("Clip",1)
        TNT1 A 0 A_TakeInventory("SamsaraDescentVulcanFiredCount")
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SamsaraDescentVulcanAttackSilentSound",1,2)
        TNT1 A 0 A_PlaySound("Descent/Vulcan/Fire",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireBullets(3.5,3.5,-1,4,"DescentVulcanPuff",FBF_NORANDOM|FBF_NOFLASH|FBF_NORANDOMPUFFZ)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireBullets(3.5,3.5,-1,6,"DescentVulcanPuffCoop",FBF_NORANDOM|FBF_NOFLASH|FBF_NORANDOMPUFFZ)
        stop
    }
}

actor DescentVulcanPuff : DescentPuff
{
	DamageType "DescentBullet"
	States
	{
		Spawn:
			DS00 ABCDE 2
			Stop
	}
}

actor DescentVulcanPuffCoop : DescentVulcanPuff { +MTHRUSPECIES }
