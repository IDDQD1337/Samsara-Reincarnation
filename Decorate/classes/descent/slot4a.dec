actor DescentSpreadFire : DescentWeaponPrimer
{
	Weapon.SlotNumber 4
	Weapon.AmmoType1 "Clip"
	Weapon.AmmoType2 "Shell"
	Weapon.AmmoUse1 2
	Weapon.AmmoUse2 1
	Weapon.AmmoGive1 30
	Weapon.AmmoGive2 4
	Inventory.PickupMessage "Spreadfire Cannon and Guided Missiles!"
	Tag "Spreadfire Cannon and Guided Missiles"
	Inventory.RestrictedTo "DescentPyroGX"
	+FLOATBOB
	States
	{
		Spawn:
			DS58 ABCDEFGHIJKLMNO 2
			loop

		SetPrimary:
			TNT1 A 0 A_TakeInventory("DescentPrimaryCounter")
			TNT1 A 0 A_GiveInventory("DescentPrimaryCounter",4)
			goto Super::SetPrimary

		SetSecondary:
			TNT1 A 0 A_TakeInventory("DescentMissileCamera")
			TNT1 A 0 A_TakeInventory("DescentSecondaryCounter")
			TNT1 A 0 A_GiveInventory("DescentSecondaryCounter",4)
			goto Super::SetSecondary
	}
}

actor SamsaraDescentSpreadFireAttackHandler : Trigger
{
	States
	{
	// Ammo checking/taking stuff
	Pickup:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
		TNT1 A 0 A_JumpIfInventory("Clip",2,1)
		stop
		TNT1 A 0 A_TakeInventory("Clip",2)
		goto PickupAttack

	// Attack stuff
	PickupAttack:
		TNT1 A 0 A_PlaySound("Descent/SpreadFire/Fire",CHAN_WEAPON)
		TNT1 A 0 A_JumpIfInventory("DescentPrimarySwitcher",1,"PickupAttackHorizontal")
	// Vertical
	PickupAttackVertical:
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackVerticalCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackVerticalCoop")
	PickupAttackVerticalDM:
		TNT1 A 0 A_FireCustomMissile("DescentSpreadFireProjectile",0,false,0,0,0,-4)
		TNT1 A 0 A_FireCustomMissile("DescentSpreadFireProjectile",0,false)
		TNT1 A 0 A_FireCustomMissile("DescentSpreadFireProjectile",0,false,0,0,0,4)
		goto PickupFinishVertical
	PickupAttackVerticalCoop:
		TNT1 A 0 A_FireCustomMissile("DescentSpreadFireProjectileCoop",0,false,0,0,0,-4)
		TNT1 A 0 A_FireCustomMissile("DescentSpreadFireProjectileCoop",0,false)
		TNT1 A 0 A_FireCustomMissile("DescentSpreadFireProjectileCoop",0,false,0,0,0,4)
		goto PickupFinishVertical

	// Horizontal
	PickupAttackHorizontal:
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackHorizontalCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackHorizontalCoop")
	PickupAttackHorizontalDM:
		TNT1 A 0 A_FireCustomMissile("DescentSpreadFireProjectile",-4,false)
		TNT1 A 0 A_FireCustomMissile("DescentSpreadFireProjectile",0,false)
		TNT1 A 0 A_FireCustomMissile("DescentSpreadFireProjectile",4,false)
		goto PickupFinishHorizontal
	PickupAttackHorizontalCoop:
		TNT1 A 0 A_FireCustomMissile("DescentSpreadFireProjectileCoop",-4,false)
		TNT1 A 0 A_FireCustomMissile("DescentSpreadFireProjectileCoop",0,false)
		TNT1 A 0 A_FireCustomMissile("DescentSpreadFireProjectileCoop",4,false)
		goto PickupFinishHorizontal

	// Finishing stuff
	// Vertical
	PickupFinishVertical:
		TNT1 A 0 A_GiveInventory("DescentPrimarySwitcher")
		stop

	// Horizontal
	PickupFinishHorizontal:
		TNT1 A 0 A_TakeInventory("DescentPrimarySwitcher")
		stop
	}
}

actor DescentSpreadFireProjectile : DescentLaserProjectile
{
	Damage (10)
	Scale 0.1
	+THRUGHOST
	States
	{
		Spawn:
			DS03 A -1
			stop

		Death:
			TNT1 A 0 A_PlaySound("Descent/Laser/Explode",CHAN_VOICE,0.5)
			TNT1 A 1 A_SpawnItemEx("DescentSpreadFirePuff",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
			stop
	}
}

actor DescentSpreadFireProjectileCoop : DescentSpreadFireProjectile { +THRUSPECIES Species "Player" }

actor DescentSpreadFirePuff : DescentPuff
{
	States
	{
		Spawn:
			DS03 BCDEF 2
			Stop
	}
}
