actor DescentPlasma : DescentWeaponPrimer
{
	Weapon.SlotNumber 8
	Weapon.AmmoType1 "Clip"
	Weapon.AmmoType2 "Shell"
	Weapon.AmmoUse1 1
	Weapon.AmmoUse2 1
	Weapon.AmmoGive1 50
	Weapon.AmmoGive2 4
	Inventory.PickupMessage "Plasma Cannon and Proximity Bombs!"
	Tag "Plasma Cannon and Proximity Bombs"
	Inventory.RestrictedTo "DescentPyroGX"
	+FLOATBOB
	States
	{
		Spawn:
			DS56 ABCDEFGHIJKLMNO 2
			loop

		SetPrimary:
			TNT1 A 0 A_TakeInventory("DescentPrimaryCounter")
			TNT1 A 0 A_GiveInventory("DescentPrimaryCounter",8)
			goto Super::SetPrimary

		SetSecondary:
			TNT1 A 0 A_TakeInventory("DescentMissileCamera")
			TNT1 A 0 A_TakeInventory("DescentSecondaryCounter")
			TNT1 A 0 A_GiveInventory("DescentSecondaryCounter",8)
			goto Super::SetSecondary
	}
}

actor SamsaraDescentPlasmaAttackHandler : Trigger
{
	States
	{
	// Ammo checking/taking stuff
	Pickup:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
		TNT1 A 0 A_JumpIfInventory("Clip",1,1)
		stop
		TNT1 A 0 A_TakeInventory("Clip",1)
		goto PickupAttack

	// Attack stuff
	PickupAttack:
		TNT1 A 0 A_PlaySound("Descent/Plasma/Fire",CHAN_WEAPON)
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
	PickupAttackDM:
		TNT1 A 0 A_FireCustomMissile("DescentPlasmaProjectile",0,false,-8)
		TNT1 A 0 A_FireCustomMissile("DescentPlasmaProjectile",0,false,8)
		stop
	PickupAttackCoop:
		TNT1 A 0 A_FireCustomMissile("DescentPlasmaProjectileCoop",0,false,-8)
		TNT1 A 0 A_FireCustomMissile("DescentPlasmaProjectileCoop",0,false,8)
		stop
	}
}

actor DescentPlasmaProjectile : DescentLaserProjectile
{
	Damage (15)
	Scale 0.25
	+THRUGHOST
	States
	{
		Spawn:
			DS07 ABCDEF 1
			loop

		Death:
			TNT1 A 0 A_PlaySound("Descent/Laser/Explode",CHAN_VOICE)
			TNT1 A 1 A_SpawnItemEx("DescentPlasmaPuff",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
			stop
	}
}

actor DescentPlasmaProjectileCoop : DescentPlasmaProjectile { +THRUSPECIES Species "Player" }
