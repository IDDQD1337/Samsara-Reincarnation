actor DescentGuidedController : DescentCounter {}

actor SamsaraDescentGuidedMissileAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Shell",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Shell",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("Descent/Missile/Fire",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("DescentMissileSwitcher",1,"PickupAttackLeft")
      // Right
      PickupAttackRight:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackRightCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackRightCoop")
      PickupAttackRightDM:
        TNT1 A 0 A_FireCustomMissile("DescentGuidedMissile",0,false,8)
        goto PickupFinishRight

      PickupAttackRightCoop:
        TNT1 A 0 A_FireCustomMissile("DescentGuidedMissileCoop",0,false,8)
        goto PickupFinishRight

      // Left
      PickupAttackLeft:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackLeftCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackLeftCoop")
      PickupAttackLeftDM:
        TNT1 A 0 A_FireCustomMissile("DescentGuidedMissile",0,false,-8)
        goto PickupFinishLeft

      PickupAttackLeftCoop:
        TNT1 A 0 A_FireCustomMissile("DescentGuidedMissileCoop",0,false,-8)
        goto PickupFinishLeft

      // Finishing stuff
      // Right
      PickupFinishRight:
        TNT1 A 0 A_GiveInventory("DescentMissileSwitcher")
        stop

      // Left
      PickupFinishLeft:
        TNT1 A 0 A_TakeInventory("DescentMissileSwitcher")
        stop
    }
}

actor DescentGuidedMissile : DescentMissile 
{
	Speed 12
	+SCREENSEEKER
	+SEEKERMISSILE
	States
	{
		Spawn:
			DS00 A 0
			DS00 A 0 A_GiveToTarget("DescentMissileCamera")
			DS00 A 0 A_GiveToTarget("DescentGuidedController")
			DS00 A 0 ACS_NamedExecuteWithResult("DescentMissileCamera")
			DS00 A 0 ACS_NamedExecuteWithResult("DescentGuidedMissile")
			DS00 A 0 A_GiveInventory("DescentGuidedController")
		SpawnLoop:
			DS00 A 1
			DS00 A 0 A_JumpIfInTargetInventory("DescentGuidedController",1,"SpawnLoop")
		SpawnLoopHoming:
			DS00 A 0 A_TakeInventory("DescentGuidedController")
			DS00 A 1 A_SeekerMissile(2,2,SMF_LOOK|SMF_PRECISE)
			wait

		Death:
			TNT1 A 0 A_ChangeVelocity(0,0,0,CVF_REPLACE)
			TNT1 A 0 A_RemoveMaster
			TNT1 A 0 A_Explode(120,120,0)
			TNT1 A 0 A_GiveToTarget("DescentMissileStaticGiver")
			TNT1 A 0 A_TakeFromTarget("DescentMissileCamera",1)
			TNT1 A 0 A_PlaySound("Descent/Laser/Explode",CHAN_VOICE)
			TNT1 A 1 A_SpawnItemEx("DescentExplosion",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
			stop

		StopSky:
			TNT1 A 1
			stop
	}
}

actor DescentGuidedMissileCoop : DescentGuidedMissile { +THRUSPECIES Species "Player" }
