actor SamsaraDescentMercuryMissileAttackHandler : Trigger
{
	States
	{
	// Ammo checking/taking stuff
	Pickup:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
		TNT1 A 0 A_JumpIfInventory("Shell",2,1)
		stop
		TNT1 A 0 A_TakeInventory("Shell",2)
		goto PickupAttack

	// Attack stuff
	PickupAttack:
		TNT1 A 0 A_PlaySound("Descent/Mercury/Fire",CHAN_WEAPON)
		TNT1 A 0 A_JumpIfInventory("DescentMissileSwitcher",1,"PickupAttackLeft")
	// Right
	PickupAttackRight:
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackRightCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackRightCoop")
	PickupAttackRightDM:
		TNT1 A 0 A_FireCustomMissile("DescentMercuryMissile",0,false,8)
		goto PickupFinishRight
	PickupAttackRightCoop:
		TNT1 A 0 A_FireCustomMissile("DescentMercuryMissileCoop",0,false,8)
		goto PickupFinishRight

	// Left
	PickupAttackLeft:
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackLeftCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackLeftCoop")
	PickupAttackLeftDM:
		TNT1 A 0 A_FireCustomMissile("DescentMercuryMissile",0,false,-8)
		goto PickupFinishLeft
	PickupAttackLeftCoop:
		TNT1 A 0 A_FireCustomMissile("DescentMercuryMissileCoop",0,false,-8)
		goto PickupFinishLeft

	// Finishing stuff
	// Right
	PickupFinishRight:
		TNT1 A 0 A_GiveInventory("DescentMissileSwitcher")
		stop

	// Left
	PickupFinishLeft:
		TNT1 A 0 A_TakeInventory("DescentMissileSwitcher")
		stop
	}
}

actor DescentMercuryMissile : FastProjectile
{
	Damage (0)
	Radius 4
	Height 8
	Speed 80
	+FORCEXYBILLBOARD
	+FORCERADIUSDMG
	+SKYEXPLODE
	+BRIGHT
	States
	{
		Spawn:
			DS00 A 0
			DS00 A 0 A_GiveToTarget("DescentMissileCamera")
			DS00 A 0 ACS_NamedExecuteWithResult("DescentMissileCamera")
		SpawnLoop:
			DS00 A 1 A_SpawnItemEx("DescentMercuryMissileTrail",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
			loop

		Death:
			TNT1 A 0 A_RemoveMaster
			TNT1 A 0 A_Explode(110,64,0)
			TNT1 A 0 A_GiveToTarget("DescentMissileStaticGiver")
			TNT1 A 0 A_TakeFromTarget("DescentMissileCamera",1)
			TNT1 A 0 A_PlaySound("Descent/Laser/Explode",CHAN_VOICE)
			TNT1 A 1 A_SpawnItemEx("DescentExplosion",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
			stop
	}
}

actor DescentMercuryMissileCoop : DescentMercuryMissile { +THRUSPECIES Species "Player" }

actor DescentMercuryMissileTrail
{
	+NOINTERACTION
	+FORCEXYBILLBOARD
	+BRIGHT
	Scale 0.5
	States
	{
		Spawn:
			TNT1 A 2
			DS01 ABCDE 2
			Stop
	}
}
