actor SamsaraDescentSmartMissileAttackHandler : Trigger
{
	States
	{
	// Ammo checking/taking stuff
	Pickup:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
		TNT1 A 0 A_JumpIfInventory("Shell",2,1)
		stop
		TNT1 A 0 A_TakeInventory("Shell",2)
		goto PickupAttack

	// Attack stuff
	PickupAttack:
		TNT1 A 0 A_PlaySound("Descent/Missile/Fire",CHAN_WEAPON)
		TNT1 A 0 A_JumpIfInventory("DescentMissileSwitcher",1,"PickupAttackLeft")
	// Right
	PickupAttackRight:
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackRightCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackRightCoop")
	PickupAttackRightDM:
		TNT1 A 0 A_FireCustomMissile("DescentSmartMissile",0,false,8)
		goto PickupFinishRight
	PickupAttackRightCoop:
		TNT1 A 0 A_FireCustomMissile("DescentSmartMissileCoop",0,false,8)
		goto PickupFinishRight

	// Left
	PickupAttackLeft:
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackLeftCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackLeftCoop")
	PickupAttackLeftDM:
		TNT1 A 0 A_FireCustomMissile("DescentSmartMissile",0,false,-8)
		goto PickupFinishLeft
	PickupAttackLeftCoop:
		TNT1 A 0 A_FireCustomMissile("DescentSmartMissileCoop",0,false,-8)
		goto PickupFinishLeft

	// Finishing stuff
	// Right
	PickupFinishRight:
		TNT1 A 0 A_GiveInventory("DescentMissileSwitcher")
		stop

	// Left
	PickupFinishLeft:
		TNT1 A 0 A_TakeInventory("DescentMissileSwitcher")
		stop
	}
}

actor DescentSmartMissile : DescentMissile
{
	+SCREENSEEKER
	+SEEKERMISSILE
	States
	{
		Death:
			TNT1 A 0 A_RemoveMaster
			TNT1 A 0 A_Explode(50,50,0)
			TNT1 A 0 A_GiveToTarget("DescentMissileStaticGiver")
			TNT1 A 0 A_TakeFromTarget("DescentMissileCamera",1)
			TNT1 A 0 A_PlaySound("Descent/Laser/Explode",CHAN_VOICE)
			TNT1 AAA 0 A_CustomMissile("DescentSmartBomblet",0,0,random(0,359),CMF_AIMDIRECTION|CMF_TRACKOWNER,random(0,359))
			TNT1 A 1 A_SpawnItemEx("DescentExplosion",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
			stop
	}
}

actor DescentSmartMissileCoop : DescentSmartMissile
{
	+THRUSPECIES
	Species "Player"
	States
	{
		Death:
			TNT1 A 0 A_RemoveMaster
			TNT1 A 0 A_Explode(50,50,0)
			TNT1 A 0 A_GiveToTarget("DescentMissileStaticGiver")
			TNT1 A 0 A_TakeFromTarget("DescentMissileCamera",1)
			TNT1 A 0 A_PlaySound("Descent/Laser/Explode",CHAN_VOICE)
			TNT1 AAAAAA 0 A_CustomMissile("DescentSmartBombletCoop",0,0,random(0,359),CMF_AIMDIRECTION|CMF_TRACKOWNER,random(0,359))
			TNT1 A 1 A_SpawnItemEx("DescentExplosion",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
			stop
	}
}

actor DescentSmartBomblet : DescentMissile
{
	Scale 0.25
	Speed 8
	+SCREENSEEKER
	+SEEKERMISSILE
	States
	{
		Spawn:
			DS07 ABCDEF 1 A_SeekerMissile(8,8,SMF_LOOK|SMF_PRECISE)
			loop

		Death:
			TNT1 A 0 A_Explode(25,50,0)
			TNT1 A 0 A_PlaySound("Descent/Laser/Explode",CHAN_VOICE)
			TNT1 A 1 A_SpawnItemEx("DescentExplosion",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
			stop
	}
}

actor DescentSmartBombletCoop : DescentSmartBomblet { +THRUSPECIES Species "Player" }
