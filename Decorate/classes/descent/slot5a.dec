actor SamsaraDescentFusionChargeAmount : Counter { Inventory.MaxAmount 20 }
actor SamsaraDescentFusionSoundChannelCount : Counter { Inventory.MaxAmount 2 }

actor DescentFusion : DescentWeaponPrimer
{
	Weapon.SlotNumber 5
	Weapon.AmmoType1 "Cell"
	Weapon.AmmoType2 "RocketAmmo"
	Weapon.AmmoUse1 5
	Weapon.AmmoUse2 1
	Weapon.AmmoGive1 50
	Weapon.AmmoGive2 3
	Inventory.PickupMessage "Fusion Cannon and Mega Missiles!"
	Tag "Fusion Cannon and Mega Missiles"
	Inventory.RestrictedTo "DescentPyroGX"
	+EXPLOSIVE
	+FLOATBOB
	States
	{
		Spawn:
			DS50 ABCDEFGHIJKLMNO 2
			loop

		SetPrimary:
			TNT1 A 0 A_TakeInventory("DescentPrimaryCounter")
			TNT1 A 0 A_GiveInventory("DescentPrimaryCounter",5)
			goto Super::SetPrimary

		SetSecondary:
			TNT1 A 0 A_TakeInventory("DescentMissileCamera")
			TNT1 A 0 A_TakeInventory("DescentSecondaryCounter")
			TNT1 A 0 A_GiveInventory("DescentSecondaryCounter",5)
			goto Super::SetSecondary
	}
}

actor SamsaraDescentFusionAttackHandler : Trigger
{
    States
    {
      // Ammo taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_TakeInventory("Cell",5) // attack could be called with less than intended amount during charge
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("Descent/Fusion/Fire",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("DescentFusionProjectile",0,false,-12)
        TNT1 A 0 A_FireCustomMissile("DescentFusionProjectile",0,false,12)
        goto PickupFinish

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("DescentFusionProjectileCoop",0,false,-12)
        TNT1 A 0 A_FireCustomMissile("DescentFusionProjectileCoop",0,false,12)
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 A_GiveInventory("DescentRecoil",12)
        stop
    }
}

actor SamsaraDescentFusionChargeGiver : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraDescentFusionChargeAmount",0,"PickupGiveCharge")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupGiveCharge")
        TNT1 A 0 A_JumpIfInventory("Cell",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Cell",1)
        goto PickupGiveCharge

      // Charge giving stuff
      PickupGiveCharge:
        TNT1 A 0 A_JumpIfInventory("SamsaraRecoilIsDisabled",1,2)
        TNT1 A 0 A_SetPitch(pitch+frandom(-1.0,1.0))
        TNT1 A 0 A_SetAngle(angle+frandom(-1.0,1.0))
        TNT1 A 0 A_GiveInventory("SamsaraDescentFusionChargeSoundPlayer")
        TNT1 A 0 A_JumpIfInventory("SamsaraDescentFusionChargeAmount",0,"PickupDamage")
        TNT1 A 0 A_JumpIfInventory("WeaponFlashesAreDisabled",1,2)
        TNT1 A 0 A_SetBlend("Purple",0.33,70)
        TNT1 A 0 A_GiveInventory("SamsaraDescentFusionChargeAmount")
        stop

      // Damage stuff
      PickupDamage:
        TNT1 A 0 A_JumpIfInventory("WeaponFlashesAreDisabled",1,2)
        TNT1 A 0 A_SetBlend("Yellow",0.33,70)
        TNT1 A 0 Thing_Damage(0,5,0)
        stop
    }
}

actor SamsaraDescentFusionChargeSoundPlayer : Trigger
{
    States
    {
      // Sound stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraDescentFusionSoundChannelCount",0,"Pickup7")
        TNT1 A 0 A_JumpIfInventory("SamsaraDescentFusionSoundChannelCount",1,"Pickup6")
      Pickup5:
        TNT1 A 0 A_PlaySound("Descent/Fusion/Charge",CHAN_5)
        TNT1 A 0 A_GiveInventory("SamsaraDescentFusionSoundChannelCount")
        stop

      Pickup6:
        TNT1 A 0 A_PlaySound("Descent/Fusion/Charge",CHAN_6)
        TNT1 A 0 A_GiveInventory("SamsaraDescentFusionSoundChannelCount")
        stop

      Pickup7:
        TNT1 A 0 A_PlaySound("Descent/Fusion/Charge",CHAN_7)
        TNT1 A 0 A_TakeInventory("SamsaraDescentFusionSoundChannelCount")
        stop
    }
}

actor DescentFusionProjectile
{
	Damage (50)
	Speed 32
	Projectile
	RenderStyle Add
	+FORCEXYBILLBOARD
	+FORCERADIUSDMG
	+BRIGHT
	+NOTIMEFREEZE
	States
	{
        Spawn:
            DS00 A 0
            DS00 A 0 A_JumpIfInTargetInventory("SamsaraDescentFusionChargeAmount",1,1)
            goto SpawnInitDone
            DS00 A 0 A_GiveInventory("SamsaraDescentFusionChargeAmount",CallACS("DescentTransferFusionCharge", 1))
            goto SpawnInitDone

        SpawnInitDone:
            DS00 A 0 A_GiveInventory("SamsaraProjectileSpawnInit")
            DS00 A 0 A_ChangeFlag("NOTIMEFREEZE",false)
        SpawnLoop:
			DS00 A -1
			stop

        Death:
            TNT1 A 0 A_JumpIfInventory("SamsaraProjectileSpawnInit",1,"DeathStart")
            TNT1 A 0 A_JumpIfInTargetInventory("SamsaraDescentFusionChargeAmount",1,1)
            goto DeathInitDone
            TNT1 A 0 A_GiveInventory("SamsaraDescentFusionChargeAmount",CallACS("DescentTransferFusionCharge", 1))
            goto DeathInitDone

        DeathInitDone:
            TNT1 A 0 A_GiveInventory("SamsaraProjectileSpawnInit")
        DeathStart:
			TNT1 A 0 A_PlaySound("Descent/Laser/Explode",CHAN_VOICE)
			TNT1 A 0 A_Explode(50+(CallACS("DescentTransferFusionCharge")*10),120)
			TNT1 A 1 A_SpawnItemEx("DescentFusionEffect")
			stop
	}
}

actor DescentFusionProjectileCoop : DescentFusionProjectile 
{ 
	+THRUSPECIES 
	Species "Player" 
	States
	{
        Death:
            TNT1 A 0 A_JumpIfInventory("SamsaraProjectileSpawnInit",1,"DeathStart")
            TNT1 A 0 A_JumpIfInTargetInventory("SamsaraDescentFusionChargeAmount",1,1)
            goto DeathInitDone
            TNT1 A 0 A_GiveInventory("SamsaraDescentFusionChargeAmount",CallACS("DescentTransferFusionCharge", 1))
            goto DeathInitDone

        DeathInitDone:
            TNT1 A 0 A_GiveInventory("SamsaraProjectileSpawnInit")
        DeathStart:
			TNT1 A 0 A_PlaySound("Descent/Laser/Explode",CHAN_VOICE)
			TNT1 A 0 A_Explode(75+(CallACS("DescentTransferFusionCharge")*25),120)
			TNT1 A 1 A_SpawnItemEx("DescentFusionEffect")
			stop
	}	
}

actor DescentFusionEffect
{
	+NOINTERACTION
	+NOGRAVITY
	+FORCEXYBILLBOARD
	+BRIGHT
	Scale 2
	States
	{
		Spawn:
			DS05 ABCDEFGHIJ 2
			Stop
	}
}
