actor DescentHelixRotation : Counter { Inventory.MaxAmount 3600 }

actor DescentHelix : DescentWeaponPrimer
{
	Weapon.SlotNumber 6
	Weapon.AmmoType1 "Clip"
	Weapon.AmmoType2 "Shell"
	Weapon.AmmoUse1 4
	Weapon.AmmoUse2 2
	Weapon.AmmoGive1 50
	Weapon.AmmoGive2 2
	Inventory.PickupMessage "Helix Cannon and Mercury Missiles!"
	Tag "Helix Cannon and Mercury Missiles"
	Inventory.RestrictedTo "DescentPyroGX"
	+FLOATBOB
	States
	{
		Spawn:
			DS52 ABCDEFGHIJKLMNOP 2
			loop

		SetPrimary:
			TNT1 A 0 A_TakeInventory("DescentPrimaryCounter")
			TNT1 A 0 A_GiveInventory("DescentPrimaryCounter",6)
			goto Super::SetPrimary

		SetSecondary:
			TNT1 A 0 A_TakeInventory("DescentMissileCamera")
			TNT1 A 0 A_TakeInventory("DescentSecondaryCounter")
			TNT1 A 0 A_GiveInventory("DescentSecondaryCounter",6)
			goto Super::SetSecondary
	}
}

actor SamsaraDescentHelixAttackHandler : Trigger
{
	States
	{
	// Ammo checking/taking stuff
	Pickup:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
		TNT1 A 0 A_JumpIfInventory("Clip",4,1)
		stop
		TNT1 A 0 A_TakeInventory("Clip",4)
		goto PickupAttack

	// Attack stuff
	PickupAttack:
		TNT1 A 0 A_PlaySound("Descent/Helix/Fire",CHAN_WEAPON)
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
	PickupAttackDM:
		TNT1 A 0 A_FireCustomMissile("DescentHelixProjectile",5*cos(CallACS("DescentHelixRotation")/10),false,0,0,0,5*sin(CallACS("DescentHelixRotation")/10))
		TNT1 A 0 A_FireCustomMissile("DescentHelixProjectile",2.5*cos(CallACS("DescentHelixRotation")/10),false,0,0,0,2.5*sin(CallACS("DescentHelixRotation")/10))
		TNT1 A 0 A_FireCustomMissile("DescentHelixProjectile",0,false)
		TNT1 A 0 A_FireCustomMissile("DescentHelixProjectile",-2.5*cos(CallACS("DescentHelixRotation")/10),false,0,0,0,-2.5*sin(CallACS("DescentHelixRotation")/10))
		TNT1 A 0 A_FireCustomMissile("DescentHelixProjectile",-5*cos(CallACS("DescentHelixRotation")/10),false,0,0,0,-5*sin(CallACS("DescentHelixRotation")/10))
		goto PickupFinish
	PickupAttackCoop:
		TNT1 A 0 A_FireCustomMissile("DescentHelixProjectileCoop",5*cos(CallACS("DescentHelixRotation")/10),false,0,0,0,5*sin(CallACS("DescentHelixRotation")/10))
		TNT1 A 0 A_FireCustomMissile("DescentHelixProjectileCoop",2.5*cos(CallACS("DescentHelixRotation")/10),false,0,0,0,2.5*sin(CallACS("DescentHelixRotation")/10))
		TNT1 A 0 A_FireCustomMissile("DescentHelixProjectileCoop",0,false)
		TNT1 A 0 A_FireCustomMissile("DescentHelixProjectileCoop",-2.5*cos(CallACS("DescentHelixRotation")/10),false,0,0,0,-2.5*sin(CallACS("DescentHelixRotation")/10))
		TNT1 A 0 A_FireCustomMissile("DescentHelixProjectileCoop",-5*cos(CallACS("DescentHelixRotation")/10),false,0,0,0,-5*sin(CallACS("DescentHelixRotation")/10))
		goto PickupFinish

	// Finishing stuff
	PickupFinish:
		TNT1 A 0 A_GiveInventory("DescentHelixRotation",225)
		TNT1 A 0 A_JumpIfInventory("DescentHelixRotation",0,1)
		stop
		TNT1 A 0 A_TakeInventory("DescentHelixRotation")
		stop
	}
}

actor DescentHelixProjectile : DescentLaserProjectile
{
	Damage (10)
	Scale 0.1
	States
	{
		Spawn:
			DS08 ABC 1
			loop

		Death:
			TNT1 A 0 A_PlaySound("Descent/Laser/Explode",CHAN_VOICE,0.3)
			TNT1 A 1 A_SpawnItemEx("DescentHelixPuff")
			stop
	}
}

actor DescentHelixProjectileCoop : DescentHelixProjectile { Damage (18) +THRUSPECIES Species "Player" }
