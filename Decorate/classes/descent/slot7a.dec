actor SamsaraDescentOmegaSoundChannelCount : Counter { Inventory.MaxAmount 3 }

actor DescentOmega : DescentWeaponPrimer
{
	Weapon.SlotNumber 7
	Weapon.AmmoType1 "Cell"
	Weapon.AmmoType2 "RocketAmmo"
	Weapon.AmmoUse1 2
	Weapon.AmmoUse2 5
	Weapon.AmmoGive1 50
	Weapon.AmmoGive2 5
	Inventory.PickupMessage "Omega Cannon and Earthshaker Missiles!"
	Tag "Omega Cannon and Earthshaker Missiles"
	Inventory.RestrictedTo "DescentPyroGX"
	+BFG
	+FLOATBOB
	States
	{
		Spawn:
			DS54 ABCDEFGHIJKLMNO 2
			loop

		SetPrimary:
			TNT1 A 0 A_TakeInventory("DescentPrimaryCounter")
			TNT1 A 0 A_GiveInventory("DescentPrimaryCounter",7)
			goto Super::SetPrimary

		SetSecondary:
			TNT1 A 0 A_TakeInventory("DescentMissileCamera")
			TNT1 A 0 A_TakeInventory("DescentSecondaryCounter")
			TNT1 A 0 A_GiveInventory("DescentSecondaryCounter",7)
			goto Super::SetSecondary
	}
}

actor SamsaraDescentOmegaAttackHandler : Trigger
{
	States
	{
	// Ammo checking/taking stuff
	Pickup:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupPlaySound")
		TNT1 A 0 A_JumpIfInventory("Cell",2,1)
		stop
		TNT1 A 0 A_TakeInventory("Cell",2)
		goto PickupPlaySound

	// Sound stuff
	PickupPlaySound:
		TNT1 A 0 A_JumpIfInventory("SamsaraDescentOmegaSoundChannelCount",0,"PickupPlaySound7")
		TNT1 A 0 A_JumpIfInventory("SamsaraDescentOmegaSoundChannelCount",2,"PickupPlaySound6")
		TNT1 A 0 A_JumpIfInventory("SamsaraDescentOmegaSoundChannelCount",1,"PickupPlaySound5")
	PickupPlaySound1:
		TNT1 A 0 A_PlaySound("Descent/Omega/Fire",CHAN_WEAPON)
		TNT1 A 0 A_GiveInventory("SamsaraDescentOmegaSoundChannelCount")
		goto PickupAttack
	PickupPlaySound5:
		TNT1 A 0 A_PlaySound("Descent/Omega/Fire",CHAN_5)
		TNT1 A 0 A_GiveInventory("SamsaraDescentOmegaSoundChannelCount")
		goto PickupAttack
	PickupPlaySound6:
		TNT1 A 0 A_PlaySound("Descent/Omega/Fire",CHAN_6)
		TNT1 A 0 A_GiveInventory("SamsaraDescentOmegaSoundChannelCount")
		goto PickupAttack
	PickupPlaySound7:
		TNT1 A 0 A_PlaySound("Descent/Omega/Fire",CHAN_7)
		TNT1 A 0 A_TakeInventory("SamsaraDescentOmegaSoundChannelCount")
		goto PickupAttack

	// Attack stuff
	PickupAttack:
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
	PickupAttackDM:
		TNT1 A 0 A_FireCustomMissile("DescentOmegaProjectile",0,false,-12)
		stop
	PickupAttackCoop:
		TNT1 A 0 A_FireCustomMissile("DescentOmegaProjectileCoop",0,false,-12)
		stop
	}
}

actor DescentOmegaProjectile : FastProjectile
{
	Damage (30)
	Speed 256
	Projectile
	+SEEKERMISSILE
	+SCREENSEEKER
	+FORCEXYBILLBOARD
	+SKYEXPLODE
	+DONTBLAST
	+NOTIMEFREEZE
	States
	{
		Spawn:
			TNT1 AAA 1 A_SeekerMissile(90,90,SMF_PRECISE|SMF_LOOK,256,3)
			goto Death+1

		Death:
			TNT1 A 0 A_SpawnItemEx("DescentOmegaPuff")
			TNT1 A 1 ACS_NamedExecuteWithResult("DescentOmegaLaser")
			stop
	}
}

actor DescentOmegaProjectileCoop : DescentOmegaProjectile { Damage (70) +THRUSPECIES Species "Player" }

actor DescentOmegaEffect
{
	+NOINTERACTION
	+NOGRAVITY
	+FORCEXYBILLBOARD
	+BRIGHT
	Scale 0.25
	States
	{
		Spawn:
			DS10 ABC 2
			Stop
	}
}
