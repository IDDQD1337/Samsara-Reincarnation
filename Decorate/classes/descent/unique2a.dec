actor DescentPhoenix : DescentWeaponPrimer
{
	Weapon.SlotNumber 9
	Weapon.AmmoType1 "Cell"
	Weapon.AmmoType2 "Shell"
	Weapon.AmmoUse1 1
	Weapon.AmmoUse2 1
	Weapon.AmmoGive1 50
	Weapon.AmmoGive2 4
	Inventory.PickupMessage "Phoenix Cannon and Smart Mines!"
	Tag "Phoenix Cannon and Smart Mines"
	Inventory.RestrictedTo "DescentPyroGX"
	+WEAPON.AMMO_OPTIONAL
	+FLOATBOB
	States
	{
		Spawn:
			DS55 ABCDEFGHIJKLMNOP 2
			loop

		SetPrimary:
			TNT1 A 0 A_TakeInventory("DescentPrimaryCounter")
			TNT1 A 0 A_GiveInventory("DescentPrimaryCounter",9)
			goto Super::SetPrimary

		SetSecondary:
			TNT1 A 0 A_TakeInventory("DescentMissileCamera")
			TNT1 A 0 A_TakeInventory("DescentSecondaryCounter")
			TNT1 A 0 A_GiveInventory("DescentSecondaryCounter",9)
			goto Super::SetSecondary
	}
}

actor SamsaraDescentPhoenixAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Cell",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Cell",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("Descent/Phoenix/Fire",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("DescentPhoenixProjectile",0,false,-8)
        TNT1 A 0 A_FireCustomMissile("DescentPhoenixProjectile",0,false,8)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("DescentPhoenixProjectileCoop",0,false,-8)
        TNT1 A 0 A_FireCustomMissile("DescentPhoenixProjectileCoop",0,false,8)
        stop
    }
}

actor DescentPhoenixProjectile : DescentLaserProjectile
{
	Damage (15)
	DamageType "Fire"
	BounceType "Doom"
	BounceCount 3
	Scale 0.25
	States
	{
		Spawn:
			DS14 ABC 1
			loop

		Death:
			TNT1 A 0 A_PlaySound("Descent/Laser/Explode",CHAN_VOICE)
			TNT1 A 1 A_SpawnItemEx("DescentGaussPuff")
			stop
	}
}

actor DescentPhoenixProjectileCoop : DescentPhoenixProjectile { +THRUSPECIES Species "Player" }
