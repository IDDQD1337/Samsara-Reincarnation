actor " Arachnicator " : SamsaraEradicatorWeapon
{
    Inventory.PickupMessage "Arachnicator"
    Weapon.SlotNumber 1
    Weapon.SlotPriority 0
    Weapon.SelectionOrder 2800
    Weapon.AmmoType "RocketAmmo"
    Weapon.AmmoGive 3
    Weapon.AmmoUse 1
    Obituary "%k didn't know %o was arachnophobic."
    Tag "Arachnicator"
    +EXPLOSIVE
    +NOALERT
    States
    {
      Spawn:
        SPPU AB 3 Bright
        loop

      Ready:
        TNT1 A 0 A_JumpIfInventory("EleenaReady",1,"IntroTaunt")
      ReadyStart:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReadyStartAmmo")
        TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,"ReadyStartAmmo")
        goto ReadyStartNoAmmo

      ReadyStartNoAmmo:
        TNT1 A 1 A_WeaponReady
        goto ReadyNoAmmoCheck

      ReadyStartAmmo:
        ESPM A 1 A_WeaponReady
        goto Ready

      ReadyNoAmmoCheck:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireFinish")
        TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,"FireFinish")
        goto Ready

      Deselect:
        TNT1 A 0 A_JumpIfHealthLower(1,"DeselectInstant")
        TNT1 A 0 A_PlaySound("Erad/wepswitch")
      DeselectLoop:
        TNT1 A 0 A_Lower
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"DeselectLoopAmmo")
        TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,"DeselectLoopAmmo")
        goto DeselectLoopNoAmmo

      DeselectLoopNoAmmo:
        TNT1 A 1 A_Lower
        goto DeselectLoop

      DeselectLoopAmmo:
        ESPM A 1 A_Lower
        goto DeselectLoop

      Select:
        TNT1 A 0 A_Raise
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"SelectAmmo")
        TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,"SelectAmmo")
        goto SelectNoAmmo

      SelectNoAmmo:
        TNT1 A 1 A_Raise
        goto Select

      SelectAmmo:
        ESPM A 1 A_Raise
        goto Select

      Fire:
        ESPM A 2 Offset(0,40)
        ESPM A 2 Offset(0,50)
        ESPM A 2 Offset(0,60)
        ESPM A 2 Offset(0,70)
        ESPM A 2 Offset(0,80)
        ESPM A 2 Offset(0,90)
        ESPM A 4 Offset(0,150)
      FireLastShotCalc:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireContinue")
        TNT1 A 0 A_JumpIfInventory("RocketAmmo",2,"FireContinue")
        goto FireContinueLast

      FireContinue:
        TNT1 A 0 A_GiveInventory("SamsaraEradicatorArachnicatorAttackHandler")
      FireFinish:
        ESPM A 1 Offset(0,100)
        ESPM A 1 Offset(0,90)
        ESPM A 1 Offset(0,80)
        ESPM A 1 Offset(0,70)
        ESPM A 1 Offset(0,60)
        ESPM A 1 Offset(0,50)
        ESPM A 1 Offset(0,40)
        ESPM A 1 Offset(0,32)
        goto Ready

      FireContinueLast:
        TNT1 A 0 A_GiveInventory("SamsaraEradicatorArachnicatorAttackHandler")
        goto Ready

      IntroTaunt:
        TNT1 A 0 A_GiveInventory("EleenaIntroTaunt")
        goto ReadyStart
    }
}

actor SamsaraEradicatorArachnicatorAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,1)
        stop
        TNT1 A 0 A_TakeInventory("RocketAmmo",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("Eleena/SpiderReady",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("SeekingSpider",0,false)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("CoopSeekingSpider",0,false)
        stop
    }
}

actor SeekingSpider
{
    Projectile
    Radius 5
    Height 5
    Speed 18
    Damage (60)
    DamageType "EradExplosive"
    Scale 0.65
    ActiveSound "Eleena/SpiderWalk"
    DeathSound "Eleena/SpiderDie"
    Obituary "%k didn't know %o was arachnophobic."
    +FLOORHUGGER
    +SEEKERMISSILE
    +STEPMISSILE
    +THRUGHOST
    States
    {
      Spawn:
        SPMB A 0 Light("ERADSPIDERSHIT__")
        TNT1 A 0 A_LoopActiveSound
      SpawnLoop:
        SPMB ABCDE 2 Light("ERADSPIDERSHIT__") A_SeekerMissile(90,90)
        loop

      Death:
        TNT1 A 0 A_SpawnDebris("SpiderGib1")
        TNT1 A 0 A_SpawnDebris("SpiderGib2")
        TNT1 A 0 A_SpawnDebris("SpiderGib3")
        TNT1 A 0 A_SpawnDebris("SpiderGib4")
        TNT1 A 0 A_SpawnDebris("SpiderGib5")
        GLBB A 2 Bright Light("ERADEXPLOSION_1__") A_Explode(175,55,XF_HURTSOURCE,true)
        GLBB B 2 Bright Light("ERADEXPLOSION_2__")
        GLBB C 2 Bright Light("ERADEXPLOSION_3__")
        GLBB D 2 Bright Light("ERADEXPLOSION_4__") A_FadeOut
        GLBB EF 2 Bright Light("ERADEXPLOSION_5__") A_FadeOut
        stop
    }
}

actor CoopSeekingSpider : SeekingSpider
{
    Species "Player"
    +THRUSPECIES
}

actor SpiderGib1
{
    Projectile
    Health 1
    Radius 1
    Height 1
    Speed 3
    Gravity 0.8 // 0.125
    BounceType "Doom"
    Scale 0.65
    +CLIENTSIDEONLY
    +FLOORCLIP
    +MOVEWITHSECTOR
    +NOBLOCKMONST
    -NOGRAVITY
    +WINDTHRUST
    States
    {
      Spawn:
        SPGB ABCDE 2
        loop

      Death:
        SPGB E 1 A_FadeOut
        stop
    }
}

actor SpiderGib2 : SpiderGib1
{
    States
    {
      Spawn:
        SPGB FGHIJ 2
        loop

      Death:
        SPGB J 1 A_FadeOut
        stop
    }
}

actor SpiderGib3 : SpiderGib1
{
    States
    {
      Spawn:
        SPGB KLMNO 2
        loop

      Death:
        SPGB O 1 A_FadeOut
        stop
    }
}

actor SpiderGib4 : SpiderGib1
{
    States
    {
      Spawn:
        SPGB PQRST 2
        loop

      Death:
        SPGB T 1 A_FadeOut
        stop
    }
}

actor SpiderGib5 : SpiderGib1
{
    States
    {
      Spawn:
        SPGB UVWXY 2
        loop

      Death:
        SPGB Y 1 A_FadeOut
        stop
    }
}
