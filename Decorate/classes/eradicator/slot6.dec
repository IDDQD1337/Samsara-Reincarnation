actor "Napalm Charge" : SamsaraEradicatorWeapon
{
    Inventory.PickupMessage "Napalm Charge"
    Weapon.SlotNumber 6
    Weapon.SelectionOrder 1350
    Weapon.AmmoType1 "Cell"
    Weapon.AmmoType2 "Cell"
    Weapon.AmmoGive 48
    Weapon.AmmoUse1 6
    Weapon.AmmoUse2 12
    Obituary "%k made a new scar on %o's face."
    Tag "Napalm Charge"
    +NOAUTOAIM
    States
    {
      Spawn:
        ENC6 C -1
        stop

      Ready:
        TNT1 A 0 A_JumpIfInventory("EleenaReady",1,"IntroTaunt")
      ReadyStart:
        TNT1 A 0 A_JumpIfInventory("PowerEradEnWP",1,3)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,2)
        ENC6 A 1 A_WeaponReady(WRF_NOSECONDARY)
        goto Ready
        ENC6 A 1 A_WeaponReady
        goto Ready

      Deselect:
        TNT1 A 0 A_JumpIfHealthLower(1,"DeselectInstant")
        TNT1 A 0 A_PlaySound("Erad/wepswitch")
      DeselectLoop:
        TNT1 A 0 A_Lower
        ENC6 A 1 A_Lower
        loop

      Select:
        TNT1 A 0 A_Raise
        ENC6 A 1 A_Raise
        loop

      Fire:
        TNT1 A 0 A_GunFlash
        ENC6 A 2 Bright A_GiveInventory("SamsaraEradicatorNapalmChargePrimaryAttackHandler")
        ENC6 A 1 Offset(2,33)
        ENC6 A 1 Offset(3,34)
        ENC6 A 1 Offset(7,39)
        ENC6 A 1 Offset(8,43)
        ENC6 A 1 Offset(7,49)
        ENC6 A 2 Offset(6,45)
        ENC6 A 2 Offset(5,41)
        ENC6 A 3 Offset(4,39)
        ENC6 A 3 Offset(3,37)
        ENC6 A 3 Offset(2,35)
        ENC6 A 3 Offset(1,33)
        ENC6 A 7 Offset(0,32)
        ENC6 A 3 A_ReFire
        goto Ready

      AltFire:
        TNT1 A 0 A_GunFlash
        ENC6 A 2 Bright A_GiveInventory("SamsaraEradicatorNapalmChargeSecondaryAttackHandler")
        ENC6 A 1 Offset(2,33)
        ENC6 A 1 Offset(3,34)
        ENC6 A 1 Offset(7,39)
        ENC6 A 1 Offset(8,43)
        ENC6 A 1 Offset(7,49)
        ENC6 A 2 Offset(6,45)
        ENC6 A 2 Offset(5,41)
        ENC6 A 3 Offset(4,39)
        ENC6 A 3 Offset(3,37)
        ENC6 A 3 Offset(2,35)
        ENC6 A 3 Offset(1,33)
        ENC6 A 7 Offset(0,32)
        ENC6 A 3 A_ReFire
        goto Ready

      Flash:
        ENC6 B 2 Bright //A_Light0 // I feel like this was supposed to be a different value. I honestly don't know
        goto LightDone

      IntroTaunt:
        TNT1 A 0 A_GiveInventory("EleenaIntroTaunt")
        goto ReadyStart
    }
}

actor SamsaraEradicatorNapalmChargePrimaryAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Cell",6,1)
        stop
        TNT1 A 0 A_TakeInventory("Cell",6)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("GrenadeB/FireB",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("NapalmChargeGlass",0,false,0,0,0,6) //+range
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("NapalmChargeCoopGlass",0,false,0,0,0,6) //+range
        stop
    }
}

actor SamsaraEradicatorNapalmChargeSecondaryAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Cell",12,1)
        stop
        TNT1 A 0 A_TakeInventory("Cell",12)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("GrenadeB/FireB",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("NapalmChargeBrokenGlass",0,false,0,0,0,6)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("NapalmChargeBrokenGlassCoop",0,false,0,0,0,6)
        stop
    }
}

actor NapalmChargeGlass
{
    Projectile
    Radius 13
    Height 13
    Speed 15
    Damage (random(24,45))
    DamageType "EradExplosive2"
    Mass 20
    Gravity 0.125
    Scale 0.6
    DeathSound "Erad/Glassbrk"
    Obituary "%k made a new scar on %o's face."
    -NOGRAVITY
    +WINDTHRUST
    States
    {
      Spawn:
        ERP3 A 0 Bright Light("CATGREENBALLZ__")
        TNT1 A 0 ThrustThingZ(0,3,0,1)
      SpawnLoop:
        ERP3 A 6 Bright Light("CATGREENBALLZ__") A_Recoil(-1)
        ERP3 BCDEF 6 Bright Light("CATGREENBALLZ__")
        loop

      Death:
        TNT1 A 0 A_SpawnDebris("GlassPiece1")
        TNT1 A 0 A_SpawnDebris("GlassPiece2")
        TNT1 A 0 A_SpawnDebris("GlassPiece3")
        TNT1 A 0 A_SpawnDebris("GlassPiece4")
        TNT1 A 0 A_SpawnDebris("GlassPiece5")
        TNT1 AAAAAA 0 A_SpawnItemEx("EradGreenFire",0,0,0,random(-30,30)/10.00,random(-30,30)/10.00,random(50,150)/10.00)
        TNT1 A 1 A_Explode(50,60)
        stop
    }
}

actor NapalmChargeCoopGlass : NapalmChargeGlass
{
    DamageType "EradCoopExplosive2"
    Species "Player"
    +NODAMAGETHRUST
    +THRUSPECIES
    States
    {
      Death:
        TNT1 A 0 A_SpawnDebris("GlassPiece1")
        TNT1 A 0 A_SpawnDebris("GlassPiece2")
        TNT1 A 0 A_SpawnDebris("GlassPiece3")
        TNT1 A 0 A_SpawnDebris("GlassPiece4")
        TNT1 A 0 A_SpawnDebris("GlassPiece5")
        TNT1 AAAAAA 0 A_SpawnItemEx("EradCoopGreenFire",0,0,0,random(-30,30)/10.00,random(-30,30)/10.00,random(50,150)/10.00)
        TNT1 A 1 A_Explode(50,60)
        stop
    }
}

actor NapalmChargeBrokenGlass : NapalmChargeGlass
{
    States
    {
      Spawn:
        ERP3 A 0 Bright Light("CATGREENBALLZ__")
        TNT1 A 0 ThrustThingZ(0,3,0,1)
      SpawnLoop:
        ERP3 A 6 Bright Light("CATGREENBALLZ__") A_Recoil(-1)
        ERP3 BC 6 Bright Light("CATGREENBALLZ__")
        ERP3 D 6 Bright Light("CATGREENBALLZ__") A_SpawnItemEx("EradGreenDrippedFire")
        ERP3 EF 6 Bright Light("CATGREENBALLZ__")
        TNT1 A 0 A_SpawnItemEx("EradGreenDrippedFire")
        loop
    }
}

actor NapalmChargeBrokenGlassCoop : NapalmChargeCoopGlass
{
    States
    {
      Spawn:
        ERP3 A 0 Bright Light("CATGREENBALLZ__")
        TNT1 A 0 ThrustThingZ(0,3,0,1)
      SpawnLoop:
        ERP3 A 6 Bright Light("CATGREENBALLZ__") A_Recoil(-1)
        ERP3 BC 6 Bright Light("CATGREENBALLZ__")
        ERP3 D 6 Bright Light("CATGREENBALLZ__") A_SpawnItemEx("EradGreenDrippedFireCoop")
        ERP3 EF 6 Bright Light("CATGREENBALLZ__")
        TNT1 A 0 A_SpawnItemEx("EradGreenDrippedFireCoop")
        loop
    }
}

actor EradGreenFire
{
    Health 2
    Radius 1
    Height 1
    Damage 8
    DamageType "EradGreenFire"
    //ReactionTime 8 // default
    Scale 0.6
    Obituary "%o didn't know that %k's green fire actually burns, too!"
    +DONTSPLASH
    +DROPOFF
    +FORCERADIUSDMG
    +MOVEWITHSECTOR
    +NOBLOCKMAP
    +NOBLOCKMONST
    +NOBOSSRIP
    +NODAMAGETHRUST
    +NOTELEPORT
    +RANDOMIZE
    +RIPPER
    +WINDTHRUST
    States
    {
      Spawn:
        ERP2 B 0 Bright Light("CATGREENBALLZ2__")
        TNT1 A 0 A_Jump(200,"SpawnLoop")
        TNT1 A 0 A_ChangeFlag("PAINLESS",true)
      SpawnLoop:
        TNT1 A 0 A_Countdown
        ERP2 BCD 3 Bright Light("CATGREENBALLZ2__") A_Explode(1,25)
        TNT1 A 0 A_ChangeFlag("FORCERADIUSDMG",false)
        ERP2 E 3 Bright Light("CATGREENBALLZ2__") A_Explode(1,25)
        TNT1 A 0 A_Countdown
        ERP2 FABC 3 Bright Light("CATGREENBALLZ2__") A_Explode(1,25)
        TNT1 A 0 A_ChangeFlag("FORCERADIUSDMG",true)
        ERP2 DEF 3 Bright Light("CATGREENBALLZ2__") A_Explode(1,25)
        loop

      Death:
        ERP2 FEDCBA 3 Bright Light("CATGREENBALLZ2__") A_FadeOut
        stop
    }
}

actor EradCoopGreenFire : EradGreenFire
{
    DamageType "EradCoopGreenFire"
    Species "Player"
    +CANNOTPUSH
    +THRUSPECIES
    States
    {
      Spawn:
        ERP2 B 0 Bright Light("CATGREENBALLZ2__")
        TNT1 A 0 A_Jump(200,"SpawnLoop")
        TNT1 A 0 A_ChangeFlag("PAINLESS",true)
      SpawnLoop:
        TNT1 A 0 A_Countdown
        ERP2 BCD 3 Bright Light("CATGREENBALLZ2__") A_Explode(2,25,0)
        TNT1 A 0 A_ChangeFlag("FORCERADIUSDMG",false)
        ERP2 E 3 Bright Light("CATGREENBALLZ2__") A_Explode(2,25,0)
        TNT1 A 0 A_Countdown
        ERP2 FABC 3 Bright Light("CATGREENBALLZ2__") A_Explode(2,25,0)
        TNT1 A 0 A_ChangeFlag("FORCERADIUSDMG",true)
        ERP2 DEF 3 Bright Light("CATGREENBALLZ2__") A_Explode(2,25,0)
        loop
    }
}

actor EradGreenDrippedFire : EradGreenFire
{
    ReactionTime 10
    States
    {
      Spawn:
        ERP3 G 0 Bright
        TNT1 A 0 A_Jump(200,"SpawnLoop")
        TNT1 A 0 A_ChangeFlag("PAINLESS",true)
      SpawnLoop:
        TNT1 A 0 A_Countdown
        ERP3 GHI 3 Bright A_Explode(1,18)
        TNT1 A 0 A_ChangeFlag("FORCERADIUSDMG",false)
        ERP3 G 3 Bright A_Explode(1,18)
        TNT1 A 0 A_Countdown
        ERP3 HIGH 3 Bright A_Explode(1,18)
        TNT1 A 0 A_ChangeFlag("FORCERADIUSDMG",true)
        ERP3 I 3 Bright A_Explode(1,18)
        loop

      Death:
        ERP3 GHIGHI 3 Bright A_FadeOut
        stop
    }
}

actor EradGreenDrippedFireCoop : EradCoopGreenFire
{
    ReactionTime 10
    States
    {
      Spawn:
        ERP3 G 0 Bright
        TNT1 A 0 A_Jump(200,"SpawnLoop")
        TNT1 A 0 A_ChangeFlag("PAINLESS",true)
      SpawnLoop:
        TNT1 A 0 A_Countdown
        ERP3 GHI 3 Bright A_Explode(1,18,0)
        TNT1 A 0 A_ChangeFlag("FORCERADIUSDMG",false)
        ERP3 G 3 Bright A_Explode(1,18,0)
        TNT1 A 0 A_Countdown
        ERP3 HIGH 3 Bright A_Explode(1,18,0)
        TNT1 A 0 A_ChangeFlag("FORCERADIUSDMG",true)
        ERP3 I 3 Bright A_Explode(1,18,0)
        loop

      Death:
        ERP3 GHIGHI 3 Bright A_FadeOut
        stop
    }
}

actor GlassPiece1
{
    Projectile
    Health 2
    Radius 1
    Height 1
    Speed 3
    Gravity 0.8 // 0.125
    BounceType "Doom"
    Scale 0.68
    +CLIENTSIDEONLY
    +EXPLODEONWATER
    +MOVEWITHSECTOR
    +NOBLOCKMONST
    -NOGRAVITY
    +WINDTHRUST
    States
    {
      Spawn:
        ERG7 ABCDE 2
        loop

      Death:
        ERG7 O 1 A_FadeOut
        stop
    }
}

actor GlassPiece2 : GlassPiece1
{
    States
    {
      Spawn:
        ERG7 FGHIJKLMN 2
        loop

      Death:
        ERG7 O 1 A_FadeOut
        stop
    }
}

actor GlassPiece3 : GlassPiece1
{
    States
    {
      Spawn:
        ERG7 PQRSTU 2
        loop

      Death:
        ERG7 O 1 A_FadeOut
        stop
    }
}

actor GlassPiece4 : GlassPiece1
{
    States
    {
      Spawn:
        ERG7 VWXY 2
        loop

      Death:
        ERG7 O 1 A_FadeOut
        stop
    }
}

actor GlassPiece5 : GlassPiece1
{
    States
    {
      Spawn:
        ERG7 LMN 2
        loop

      Death:
        ERG7 O 1 A_FadeOut
        stop
    }
}
