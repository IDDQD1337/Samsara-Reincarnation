actor "  Dart Cannon  " : SamsaraEradicatorWeapon
{
    Inventory.PickupMessage "Death Dart"
    Weapon.SlotNumber 4
    Weapon.SelectionOrder 1300
    Weapon.AmmoType1 "Clip"
    Weapon.AmmoType2 "Clip"
    Weapon.AmmoGive 18
    Weapon.AmmoUse1 1
    Weapon.AmmoUse2 2
    Obituary "%o was pierced by %k's death darts."
    Tag "Dart Cannon"
    States
    {
      Spawn:
        DZ8P A -1
        stop

      Ready:
        TNT1 A 0 A_JumpIfInventory("EleenaReady",1,"IntroTaunt")
      ReadyStart:
        TNT1 A 0 A_JumpIfInventory("PowerEradEnWP",1,3)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,2)
        ELDC A 1 A_WeaponReady(WRF_NOSECONDARY)
        goto Ready
        ELDC A 1 A_WeaponReady
        goto Ready

      Deselect:
        TNT1 A 0 A_JumpIfHealthLower(1,"DeselectInstant")
        TNT1 A 0 A_PlaySound("Erad/wepswitch")
      DeselectLoop:
        TNT1 A 0 A_Lower
        ELDC A 1 A_Lower
        loop

      Select:
        TNT1 A 0 A_Raise
        ELDC A 1 A_Raise
        loop

      Fire:
        TNT1 A 0 A_GunFlash
        TNT1 A 0 A_GiveInventory("SamsaraEradicatorDeathDartPrimaryAttackHandler")
        ELDC A 3 Bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
        ELDC C 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
        ELDC D 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
        TNT1 A 0 A_ReFire
        ELDC E 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
        goto Ready

      AltFire:
        TNT1 A 0 A_GunFlash
        TNT1 A 0 A_GiveInventory("SamsaraEradicatorDeathDartSecondaryAttackHandler")
        ELDC A 3 Bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
        ELDC CD 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
        TNT1 A 0 A_ReFire
        ELDC E 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
        goto Ready

      Flash:
        ELDC B 3 Bright A_Light1
        goto LightDone

      IntroTaunt:
        TNT1 A 0 A_GiveInventory("EleenaIntroTaunt")
        goto ReadyStart
    }
}

actor SamsaraEradicatorDeathDartPrimaryAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("eleena/dartfire",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("ImpalingDart",frandom(3.00,7.00),false,6)
        TNT1 A 0 A_FireCustomMissile("ImpalingDart",frandom(-7.00,-3.00),false,-6)
        TNT1 A 0 A_FireCustomMissile("ImpalingDart",frandom(-2.00,2.00),false)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("CoopImpalingDart",frandom(3.00,7.00),false,6)
        TNT1 A 0 A_FireCustomMissile("CoopImpalingDart",frandom(-7.00,-3.00),false,-6)
        TNT1 A 0 A_FireCustomMissile("CoopImpalingDart",frandom(-2.00,2.00),false)
        stop
    }
}

actor SamsaraEradicatorDeathDartSecondaryAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",2,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",2)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("eleena/dartfire2",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("ImpalingDart",0,false,8)
        TNT1 A 0 A_FireCustomMissile("ImpalingDart",0,false,-8)
        TNT1 A 0 A_FireCustomMissile("ImpalingDart",0,false)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("CoopImpalingDart",0,false,8)
        TNT1 A 0 A_FireCustomMissile("CoopImpalingDart",0,false,-8)
        TNT1 A 0 A_FireCustomMissile("CoopImpalingDart",0,false)
        stop
    }
}

actor ImpalingDart
{
    Projectile
    Radius 3
    Height 3
    Speed 40
    Damage (7)
    DamageType "EradPiercingDart"
    Obituary "%o was pierced by %k's death darts."
    +BLOODSPLATTER
    +PIERCEARMOR
    +THRUGHOST
    +WINDTHRUST
    States
    {
      Spawn:
        DS95 J -1
        stop

      Death:
        DS95 J 6 A_PlaySound("Dart/PierceWall",CHAN_VOICE)
        stop

      XDeath:
      Crash:
        TNT1 A 1
        stop
    }
}

actor CoopImpalingDart : ImpalingDart
{
    Species "Player"
    +THRUSPECIES
}
