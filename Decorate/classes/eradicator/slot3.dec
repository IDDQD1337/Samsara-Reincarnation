actor EradAmmoShit : Counter { Inventory.MaxAmount 3 }

actor "Pellet Bomb" : SamsaraEradicatorWeapon
{
    Inventory.PickupMessage "Pellet Bomb"
    Weapon.SlotNumber 3
    Weapon.SelectionOrder 1400
    Weapon.AmmoType1 "Shell"
    Weapon.AmmoType2 "Shell"
    Weapon.AmmoGive 12
    Weapon.AmmoUse1 2
    Weapon.AmmoUse2 1
    Obituary "%k threw some balls at %o's face."
    Tag "Pellet Bomb"
    States
    {
      Spawn:
        EPE2 F -1
        stop

      Ready:
        TNT1 A 0 A_JumpIfInventory("EleenaReady",1,"IntroTaunt")
      ReadyStart:
        TNT1 A 0 A_JumpIfInventory("PowerEradEnWP",1,3)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,2)
        EPE2 G 1 A_WeaponReady(WRF_NOSECONDARY)
        goto Ready
        EPE2 G 1 A_WeaponReady
        goto Ready

      Deselect:
        TNT1 A 0 A_JumpIfHealthLower(1,"DeselectInstant")
        TNT1 A 0 A_PlaySound("Erad/wepswitch")
      DeselectLoop:
        TNT1 A 0 A_Lower
        EPE2 G 1 A_Lower
        loop

      Select:
        TNT1 A 0 A_Raise
        EPE2 G 1 A_Raise
        loop

      Fire:
        TNT1 A 0 A_GunFlash
        EPE2 G 2 Bright A_GiveInventory("SamsaraEradicatorPelletBombPrimaryAttackHandler")
        EPE2 G 1 Offset(0,39)
        EPE2 G 1 Offset(0,45)
        EPE2 G 1 Offset(0,59)
        EPE2 G 1 Offset(0,55)
        EPE2 G 1 Offset(0,52)
        EPE2 G 2 Offset(0,49)
        EPE2 G 2 Offset(0,46)
        EPE2 G 2 Offset(0,44)
        EPE2 G 2 Offset(0,40)
        EPE2 G 2 Offset(0,38)
        EPE2 G 2 Offset(0,36)
        EPE2 G 2 Offset(0,34)
        EPE2 G 9 Offset(0,32)
        EPE2 G 3 A_ReFire
        goto Ready

      AltFire:
        TNT1 A 0 A_GunFlash
        EPE2 G 2 Bright A_GiveInventory("SamsaraEradicatorPelletBombSecondaryAttackHandler")
        EPE2 G 2
        EPE2 G 1 A_ReFire
        goto Ready

      Flash:
        EPE2 H 2 Bright A_Light1
        goto LightDone

      IntroTaunt:
        TNT1 A 0 A_GiveInventory("EleenaIntroTaunt")
        goto ReadyStart
    }
}

actor SamsaraEradicatorPelletBombPrimaryAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Shell",2,1)
        stop
        TNT1 A 0 A_TakeInventory("Shell",2)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("GrenadeA/FireA",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("EradPelletBomb",random(-1,1),false,random(-3,3),random(-2,3),0,2) //+random(-1,2))
        TNT1 A 0 A_FireCustomMissile("EradPelletBomba",random(-2,2)+random(-3,3),false,random(-3,3),random(-3,3),0,2) //+random(-2,1))
        TNT1 A 0 A_FireCustomMissile("EradPelletBombb",random(-2,2)+random(-5,5),false,random(-3,3),random(-3,4),0,2) //+random(-3,4))
        TNT1 A 0 A_FireCustomMissile("EradPelletBombc",random(-4,4)+random(-7,7),false,random(-3,3),random(-4,4),0,2) //+random(-4,3))
        TNT1 A 0 A_FireCustomMissile("EradPelletBombd",random(-4,4)+random(-9,9),false,random(-3,3),random(-4,5),0,2) //+random(-6,5))
        TNT1 A 0 A_FireCustomMissile("EradPelletBombe",random(-6,6)+random(-11,11),false,random(-3,3),random(-5,5),0,2) //+random(-5,6))
        TNT1 A 0 A_FireCustomMissile("EradPelletBombf",random(-6,6)+random(-13,13),false,random(-3,3),random(-5,6),0,2) //+random(-6,7))
        TNT1 A 0 A_FireCustomMissile("EradPelletBombg",random(-8,8)+random(-14,14),false,random(-3,3),random(-6,6),0,2) //+random(-7,6)) - added ,0,2 to retain some range with lower speed
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("CEradPelletBomb",random(-1,1),false,random(-3,3),random(-2,3),0,2) //+random(-1,2))
        TNT1 A 0 A_FireCustomMissile("CEradPelletBomba",random(-2,2)+random(-3,3),false,random(-3,3),random(-3,3),0,2) //+random(-2,1))
        TNT1 A 0 A_FireCustomMissile("CEradPelletBombb",random(-2,2)+random(-5,5),false,random(-3,3),random(-3,4),0,2) //+random(-3,4))
        TNT1 A 0 A_FireCustomMissile("CEradPelletBombc",random(-4,4)+random(-7,7),false,random(-3,3),random(-4,4),0,2) //+random(-4,3))
        TNT1 A 0 A_FireCustomMissile("CEradPelletBombd",random(-4,4)+random(-9,9),false,random(-3,3),random(-4,5),0,2) //+random(-6,5))
        TNT1 A 0 A_FireCustomMissile("CEradPelletBombe",random(-6,6)+random(-11,11),false,random(-3,3),random(-5,5),0,2) //+random(-5,6))
        TNT1 A 0 A_FireCustomMissile("CEradPelletBombf",random(-6,6)+random(-13,13),false,random(-3,3),random(-5,6),0,2) //+random(-6,7))
        TNT1 A 0 A_FireCustomMissile("CEradPelletBombg",random(-8,8)+random(-14,14),false,random(-3,3),random(-6,6),0,2) //+random(-7,6)) - added ,0,2 to retain some range with lower speed
        stop
    }
}

actor SamsaraEradicatorPelletBombSecondaryAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Shell",1,1)
        stop
        TNT1 A 0 A_JumpIfInventory("EradAmmoShit",0,2)
        TNT1 A 0 A_GiveInventory("EradAmmoShit")
        goto PickupAttack
        TNT1 A 0 A_TakeInventory("Shell",1)
        TNT1 A 0 A_TakeInventory("EradAmmoShit")
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("GrenadeA/FireA",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("EradPelletBomb",0,false,0,0,0,2)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("CEradPelletBomb",0,false,0,0,0,2)
        stop
    }
}

actor EradPelletBomb
{
    Projectile
    Radius 3
    Height 3
    Speed 22
    Damage (random(12,14))
    DamageType "EradExplosive2"
    ReactionTime 16
    Mass 20
    Gravity 0.125 // 0.2
    BounceType "Hexen"
    Scale 0.6
    DeathSound "GrenadeA/Boom"
    Obituary "%k threw some balls at %o's face."
    +FORCERADIUSDMG
    +MOVEWITHSECTOR
    -NOGRAVITY
    //+WINDTHRUST
    States
    {
      Spawn:
        EPE2 BCDE 6 Bright A_Countdown
        loop

      Death:
        TNT1 A 0 A_NoGravity
        TNT1 A 0 A_Explode(8,27)
      DeathFinish:
        HSD5 CDEF 3 Bright
        HSD5 GH 3 Bright A_FadeOut
        stop
    }
}

actor EradPelletBomba : EradPelletBomb
{
    //Damage (random(2,4)+6*2)
    ReactionTime 17
}

actor EradPelletBombb : EradPelletBomb
{
    //Damage (random(2,4)+7*2)
    ReactionTime 18
}

actor EradPelletBombc : EradPelletBomb
{
    //Damage (random(2,4)+6*2)
    ReactionTime 19
}

actor EradPelletBombd : EradPelletBomb
{
    //Damage (random(2,4)+8*2)
    ReactionTime 20
}

actor EradPelletBombe : EradPelletBomb
{
    //Damage (random(2,4)+6*2)
    ReactionTime 21
}

actor EradPelletBombf : EradPelletBomb
{
    //Damage (random(2,4)+7*2)
    ReactionTime 22
}

actor EradPelletBombg : EradPelletBomb
{
    //Damage (random(2,4)+9*2)
    ReactionTime 23
}

actor CEradPelletBomb : EradPelletBomb
{
    Species "Player"
    +THRUSPECIES
    States
    {
      Death:
        TNT1 A 0 A_NoGravity
        TNT1 A 0 A_Explode(8,27,0)
        goto DeathFinish
    }
}

actor CEradPelletBomba : CEradPelletBomb { ReactionTime 17 }
actor CEradPelletBombb : CEradPelletBomb { ReactionTime 18 }
actor CEradPelletBombc : CEradPelletBomb { ReactionTime 19 }
actor CEradPelletBombd : CEradPelletBomb { ReactionTime 20 }
actor CEradPelletBombe : CEradPelletBomb { ReactionTime 21 }
actor CEradPelletBombf : CEradPelletBomb { ReactionTime 22 }
actor CEradPelletBombg : CEradPelletBomb { ReactionTime 23 }
