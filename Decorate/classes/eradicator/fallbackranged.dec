actor SamsaraEradicatorRipperDiscAltAttackAmount : Counter { Inventory.MaxAmount 4 }
actor SamsaraEradicatorRipperDiscAltAttackSilentSound : Boolean {}
actor SamsaraEradicatorLaserAltAttackSwitch : TemporaryBoolean {}
actor SamsaraEradicatorPhotonPulseAttackSwitch : TemporaryBoolean {}

actor "  Ripper Disc  " : SamsaraEradicatorWeapon
{
    Inventory.PickupMessage "Ripper Disc"
    Weapon.SlotNumber 1
    Weapon.SlotPriority 1
    Weapon.SelectionOrder 1800
    Obituary "%o was ripped by %k's discs."
    Tag "Ripper Disc"
    +NOALERT
    +WIMPY_WEAPON
    States
    {
      Spawn:
        EDAP A -1
        stop

      Ready:
        TNT1 A 0 A_JumpIfInventory("EleenaReady",1,"IntroTaunt")
	  ReadyStart:
        TNT1 A 0 A_JumpIfInventory("EradMode",3,"ReadyStartMarine")
        TNT1 A 0 A_JumpIfInventory("EradMode",2,"ReadyStartDan")
        TNT1 A 0 A_JumpIfInventory("EradMode",1,"ReadyStartKamchak")
      ReadyStartEleena:
        TNT1 A 0 A_JumpIfInventory("PowerEradEnWP",1,3)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,2)
        ELED A 1 A_WeaponReady(WRF_NOSECONDARY)
        goto Ready
        ELED A 1 A_WeaponReady
        goto Ready

      Deselect:
        TNT1 A 0 A_JumpIfHealthLower(1,"DeselectInstant")
        TNT1 A 0 A_PlaySound("Erad/wepswitch")
	  DeselectLoop:
        TNT1 A 0 A_JumpIfInventory("EradMode",3,"DeselectLoopMarine")
        TNT1 A 0 A_JumpIfInventory("EradMode",2,"DeselectLoopDan")
        TNT1 A 0 A_JumpIfInventory("EradMode",1,"DeselectLoopKamchak")
      DeselectLoopEleena:
        TNT1 A 0 A_Lower
        ELED A 1 A_Lower
        loop

      Select:
		TNT1 A 0 A_JumpIfInventory("EradMode",3,"SelectMarine")
        TNT1 A 0 A_JumpIfInventory("EradMode",2,"SelectDan")
        TNT1 A 0 A_JumpIfInventory("EradMode",1,"SelectKamchak")
        TNT1 A 0 A_Raise
        ELED A 1 A_Raise
        loop

      Fire:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto FireStart
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"FireStart")
        goto NoAmmoSwitch

      FireStart:
		TNT1 A 0 A_JumpIfInventory("EradMode",3,"FireStartMarine")
        TNT1 A 0 A_JumpIfInventory("EradMode",2,"FireStartDan")
        TNT1 A 0 A_JumpIfInventory("EradMode",1,"FireStartKamchak")
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_GunFlash
        TNT1 A 0 A_GiveInventory("SamsaraEradicatorRipperDiscPrimaryAttackHandler")
        ELED A 1 Bright
		ELED A 1 Offset(-2,34)
		ELED A 1 Offset(-4,36)
		ELED A 2 Offset(-6,38)
		ELED A 3 Offset(-6,39)
		ELED A 2 Offset(-6,38)
		ELED A 1 Offset(-4,36)
		ELED A 1 Offset(-2,34)
      FireFinish:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto FireFinishReFire
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireFinishReFire")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"FireFinishReFire")
        goto NoAmmoSwitch

      FireFinishReFire:
        TNT1 A 0 A_ReFire
        goto Ready

      AltFire:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto AltFireStart
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"AltFireStart")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"AltFireStart")
        goto NoAmmoSwitch

      AltFireStart:
		TNT1 A 0 A_TakeInventory("SamsaraEradicatorRipperDiscAltAttackSilentSound")
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_JumpIfInventory("EradMode",3,"AltFireStartMarine")
        TNT1 A 0 A_JumpIfInventory("EradMode",2,"AltFireStartDan")
        TNT1 A 0 A_JumpIfInventory("EradMode",1,"AltFireStartKamchak")
        TNT1 A 0 A_TakeInventory("SamsaraEradicatorRipperDiscAltAttackAmount") // reset
      AltFireStartLoop: // despite firing on the first tic, ammo checks are here due to the loop system
        TNT1 A 0 A_GiveInventory("SamsaraEradicatorRipperDiscAltAttackAmount")
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto AltFireAttack
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"AltFireAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"AltFireAttack")
        goto AltFireContinue

      AltFireAttack:
        TNT1 A 0 A_GunFlash
      AltFireContinue:
        TNT1 A 0 A_GiveInventory("SamsaraEradicatorRipperDiscSecondaryAttackHandler")
        ELED A 1 Bright
        ELED A 1 Offset(-2,34)
        TNT1 A 0 A_JumpIfInventory("SamsaraEradicatorRipperDiscAltAttackAmount",0,"AltFireFinish")
        goto AltFireStartLoop

      AltFireFinish:
	    ELED A 1 Offset(-4,36)
		ELED A 1 Offset(-6,38)
		ELED A 1 Offset(-6,39)
		ELED A 1 Offset(-6,38)
		ELED A 1 Offset(-4,36)
		ELED A 1 Offset(-2,34)
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto AltFireFinishReFire
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"AltFireFinishReFire")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"AltFireFinishReFire")
        goto NoAmmoSwitch

      AltFireFinishReFire:
        TNT1 A 0 A_ReFire
        goto Ready

      Flash:
	    TNT1 A 0 A_JumpIfInventory("EradMode",3,"FlashMarine")
        TNT1 A 0 A_JumpIfInventory("EradMode",2,"FlashDan")
        TNT1 A 0 A_JumpIfInventory("EradMode",1,"FlashKamchak")
	  FlashEleena:
        ELED B 1 Bright
        goto LightDone

      IntroTaunt:
        TNT1 A 0 A_GiveInventory("EleenaIntroTaunt")
        goto ReadyStart

      // "No ammo" switch
      NoAmmoSwitch:
        TNT1 A 0 A_ClearReFire
        TNT1 A 0 A_SelectWeapon("  Claw  ")
        goto Deselect
		
      // Kamchak (Photon Bolt)
      ReadyStartKamchak:
        TNT1 A 0 A_JumpIfInventory("PowerEradEnWP",1,3)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,2)
        ER04 A 1 A_WeaponReady(WRF_NOSECONDARY)
        goto Ready
        ER04 A 1 A_WeaponReady
        goto Ready

      DeselectLoopKamchak:
        TNT1 A 0 A_Lower
        ER04 A 1 A_Lower
        goto DeselectLoop

      SelectKamchak:
        TNT1 A 0 A_Raise
        ER04 A 1 A_Raise
        goto Select
	  
	  FireStartKamchak:
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_GunFlash
        TNT1 A 0 A_GiveInventory("SamsaraEradicatorPhotonPulsePrimaryAttackHandler")
        ER04 A 1 Bright
		ER04 A 1 Offset(2,35)
		ER04 A 1 Offset(4,38)
		ER04 A 2 Offset(6,41)
		ER04 A 3 Offset(8,44)
		ER04 A 1 Offset(6,41)
		ER04 A 1 Offset(4,38)
		ER04 A 1 Offset(2,35)
	  FireFinishKamchak:
	    TNT1 A 0 A_JumpIfInventory("SamsaraEradicatorPhotonPulseAttackSwitch",1,2)
		TNT1 A 0 A_GiveInventory("SamsaraEradicatorPhotonPulseAttackSwitch")
		Goto FireFinishKamchakContinue
		TNT1 A 0 A_TakeInventory("SamsaraEradicatorPhotonPulseAttackSwitch")
      FireFinishKamchakContinue:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto FireFinishReFire
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireFinishReFire")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"FireFinishReFire")
        goto NoAmmoSwitch
		
		
	  AltFireStartKamchak:
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_GunFlash
        TNT1 A 0 A_GiveInventory("SamsaraEradicatorPhotonPulseSecondaryAttackHandler")
        ER04 A 1 Bright
		ER04 A 1 Offset(2,35)
		ER04 A 1 Offset(4,38)
		ER04 A 2 Offset(6,41)
		ER04 A 3 Offset(8,44)
		ER04 A 1 Offset(6,41)
		ER04 A 1 Offset(4,38)
		ER04 A 1 Offset(2,35)
      AltFireFinishKamchak:
	    TNT1 A 0 A_JumpIfInventory("SamsaraEradicatorPhotonPulseAttackSwitch",1,2)
		TNT1 A 0 A_GiveInventory("SamsaraEradicatorPhotonPulseAttackSwitch")
		Goto AltFireFinishContinueKamchak
		TNT1 A 0 A_TakeInventory("SamsaraEradicatorPhotonPulseAttackSwitch")
	  AltFireFinishContinueKamchak:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto AltFireFinishReFire
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"AltFireFinishReFire")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"AltFireFinishReFire")
        goto NoAmmoSwitch
	  
	  FlashKamchak:
		TNT1 A 0 A_JumpIfInventory("SamsaraEradicatorPhotonPulseAttackSwitch",1,2)
        ER04 B 1 Bright
        goto LightDone
		ER04 C 1 Bright
        goto LightDone
		
	  // Dan Blaze (Strident Laser)
      ReadyStartDan:
        TNT1 A 0 A_JumpIfInventory("PowerEradEnWP",1,3)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,2)
        ER03 A 1 A_WeaponReady(WRF_NOSECONDARY)
        goto Ready
        ER03 A 1 A_WeaponReady
        goto Ready

      DeselectLoopDan:
        TNT1 A 0 A_Lower
        ER03 A 1 A_Lower
        goto DeselectLoop

      SelectDan:
        TNT1 A 0 A_Raise
        ER03 A 1 A_Raise
        goto Select
	  
	  FireStartDan:
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_GunFlash
        TNT1 A 0 A_GiveInventory("SamsaraEradicatorLaserPrimaryAttackHandler")
        ER03 A 1 Bright
		ER03 A 1 Offset(-2,35)
		ER03 A 1 Offset(-4,38)
		ER03 A 2 Offset(-6,41)
		ER03 A 3 Offset(-8,44)
		ER03 A 1 Offset(-6,41)
		ER03 A 1 Offset(-4,38)
		ER03 A 1 Offset(-2,35)
      FireFinishDan:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto FireFinishReFire
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireFinishReFire")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"FireFinishReFire")
        goto NoAmmoSwitch
		
	  AltFireStartDan:
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_GunFlash
        TNT1 A 0 A_GiveInventory("SamsaraEradicatorLaserSecondaryAttackHandler")
        ER03 A 1 Bright
		ER03 A 1 Offset(-2,35)
		ER03 A 1 Offset(-4,38)
		ER03 A 2 Offset(-6,41)
		ER03 A 3 Offset(-8,44)
		ER03 A 1 Offset(-6,41)
		ER03 A 1 Offset(-4,38)
		ER03 A 1 Offset(-2,35)
      AltFireFinishDan:
	    TNT1 A 0 A_JumpIfInventory("SamsaraEradicatorLaserAltAttackSwitch",1,2)
		TNT1 A 0 A_GiveInventory("SamsaraEradicatorLaserAltAttackSwitch")
		Goto AltFireFinishContinueDan
		TNT1 A 0 A_TakeInventory("SamsaraEradicatorLaserAltAttackSwitch")
	  AltFireFinishContinueDan:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto AltFireFinishReFire
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"AltFireFinishReFire")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"AltFireFinishReFire")
        goto NoAmmoSwitch
	  
	  FlashDan:
        ER03 B 1 Bright
        goto LightDone
		
	  // Alliance Commando (Photon Bolt)
      ReadyStartMarine:
        TNT1 A 0 A_JumpIfInventory("PowerEradEnWP",1,3)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,2)
        ER05 A 1 A_WeaponReady(WRF_NOSECONDARY)
        goto Ready
        ER05 A 1 A_WeaponReady
        goto Ready

      DeselectLoopMarine:
        TNT1 A 0 A_Lower
        ER05 A 1 A_Lower
        goto DeselectLoop

      SelectMarine:
        TNT1 A 0 A_Raise
        ER05 A 1 A_Raise
        goto Select
	  
	  FireStartMarine:
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_GunFlash
        TNT1 A 0 A_GiveInventory("SamsaraEradicatorPhotonPulsePrimaryAttackHandler")
        ER05 A 1 Bright
		ER05 A 1 Offset(2,35)
		ER05 A 1 Offset(4,38)
		ER05 A 2 Offset(6,41)
		ER05 A 3 Offset(8,44)
		ER05 A 1 Offset(6,41)
		ER05 A 1 Offset(4,38)
		ER05 A 1 Offset(2,35)
      FireFinishMarine:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto FireFinishReFire
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireFinishReFire")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"FireFinishReFire")
        goto NoAmmoSwitch
		
	  AltFireStartMarine:
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_GunFlash
        TNT1 A 0 A_GiveInventory("SamsaraEradicatorPhotonPulseSecondaryAttackHandler")
        ER05 A 1 Bright
		ER05 A 1 Offset(2,35)
		ER05 A 1 Offset(4,38)
		ER05 A 2 Offset(6,41)
		ER05 A 3 Offset(8,44)
		ER05 A 1 Offset(6,41)
		ER05 A 1 Offset(4,38)
		ER05 A 1 Offset(2,35)
	  AltFireFinishMarine:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto AltFireFinishReFire
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"AltFireFinishReFire")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"AltFireFinishReFire")
        goto NoAmmoSwitch
	  
	  FlashMarine:
        ER05 B 1 Bright
        goto LightDone
    }
}

actor SamsaraEradicatorRipperDiscPrimaryAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto PickupAttack
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("eleena/discfire",CHAN_WEAPON)
        TNT1 A 0 A_FireBullets(0,0,1,0,"SamsaraBlankPuff",FBF_NORANDOM|FBF_NOFLASH)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("RippingDisc",0,false,-7)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("RippingCoopDisc",0,false,-7)
        stop
    }
}

actor SamsaraEradicatorRipperDiscSecondaryAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto PickupAttack
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        goto PickupFinish
        TNT1 A 0 A_TakeInventory("Clip",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SamsaraEradicatorRipperDiscAltAttackSilentSound",1,2)
        TNT1 A 0 A_PlaySound("eleena/fastdisc",CHAN_WEAPON)
        TNT1 A 0 A_FireBullets(0,0,1,0,"SamsaraBlankPuff",FBF_NORANDOM|FBF_NOFLASH)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("RippingDisc",0,false,-7)
        goto PickupFinish

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("RippingCoopDisc",0,false,-7)
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 A_GiveInventory("SamsaraEradicatorRipperDiscAltAttackSilentSound")
        stop
    }
}

actor SamsaraEradicatorLaserPrimaryAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto PickupAttack
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("erad/laser1",CHAN_WEAPON)
        TNT1 A 0 A_FireBullets(0,0,1,0,"SamsaraBlankPuff",FBF_NORANDOM|FBF_NOFLASH)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("EradDanLaserProjectile",0,false,-10)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("EradDanLaserProjectileCoop",0,false,-10)
        stop
    }
}

actor SamsaraEradicatorLaserSecondaryAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto PickupAttack
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        goto PickupFinish
        TNT1 A 0 A_TakeInventory("Clip",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SamsaraEradicatorRipperDiscAltAttackSilentSound",1,2)
        TNT1 A 0 A_PlaySound("erad/laserp",CHAN_WEAPON)
        TNT1 A 0 A_FireBullets(0,0,1,0,"SamsaraBlankPuff",FBF_NORANDOM|FBF_NOFLASH)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
		TNT1 A 0 A_FireCustomMissile("EradDanLaserProjectile",0,false,-10)
		TNT1 A 0 A_JumpIfInventory("SamsaraEradicatorLaserAltAttackSwitch",1,"PickupAttackDM2")
		TNT1 A 0 A_FireCustomMissile("EradDanLaserProjectile",2,false,-10,-5)
		TNT1 A 0 A_FireCustomMissile("EradDanLaserProjectile",-2,false,-10,5)
        goto PickupFinish
	  PickupAttackDM2:
		TNT1 A 0 A_FireCustomMissile("EradDanLaserProjectile",-2,false,-10,-5)
		TNT1 A 0 A_FireCustomMissile("EradDanLaserProjectile",2,false,-10,5)
		goto PickupFinish
      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("EradDanLaserProjectileCoop",0,false,-10)
		TNT1 A 0 A_JumpIfInventory("SamsaraEradicatorLaserAltAttackSwitch",1,"PickupAttackCoop2")
		TNT1 A 0 A_FireCustomMissile("EradDanLaserProjectileCoop",2,false,-10,-5)
		TNT1 A 0 A_FireCustomMissile("EradDanLaserProjectileCoop",-2,false,-10,5)
        goto PickupFinish
	  PickupAttackCoop2:
		TNT1 A 0 A_FireCustomMissile("EradDanLaserProjectileCoop",-2,false,-10,-5)
		TNT1 A 0 A_FireCustomMissile("EradDanLaserProjectileCoop",2,false,-10,5)
		goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 A_GiveInventory("SamsaraEradicatorRipperDiscAltAttackSilentSound")
        stop
    }
}

actor SamsaraEradicatorPhotonPulsePrimaryAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto PickupAttack
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("erad/hsdfire",CHAN_WEAPON)
        TNT1 A 0 A_FireBullets(0,0,1,0,"SamsaraBlankPuff",FBF_NORANDOM|FBF_NOFLASH)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("EradKamchakPhotonPulseProjectile",0,false,7)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("EradKamchakPhotonPulseProjectileCoop",0,false,7)
        stop
    }
}

actor SamsaraEradicatorPhotonPulseSecondaryAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto PickupAttack
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("erad/hsdfire",CHAN_WEAPON)
        TNT1 A 0 A_FireBullets(0,0,1,0,"SamsaraBlankPuff",FBF_NORANDOM|FBF_NOFLASH)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("EradKamchakPhotonPulseWeaveProjectile1",0,false,7)
		TNT1 A 0 A_FireCustomMissile("EradKamchakPhotonPulseWeaveProjectile2",0,false,7)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("EradKamchakPhotonPulseWeaveProjectile1Coop",0,false,7)
		TNT1 A 0 A_FireCustomMissile("EradKamchakPhotonPulseWeaveProjectile2Coop",0,false,7)
        stop
    }
}

actor RippingDisc
{
    Projectile
    Radius 6
    Height 5
    Speed 70
    Damage (Random(10,15))
    DamageType "EradDisc"
    BounceCount 6
    BounceFactor 1
    Scale 0.5
    DeathSound "eleena/discdead"
    Obituary "%o was ripped by %k's discs."
    +BOUNCEONCEILINGS
    +BOUNCEONFLOORS
    +WINDTHRUST
    States
    {
      Spawn:
        EDAM A -1 Bright Light("ERADDISC1__")
        stop

      Death:
        EDAM B 2 Bright Light("ERADDISC2__")
        EDAM C 2 Bright Light("ERADDISC3__")
        EDAM D 2 Bright Light("ERADDISC4__")
        EDAM E 2 Bright Light("ERADDISC5__")
        stop
    }
}

actor RippingCoopDisc : RippingDisc
{
    Speed 80
    BounceCount 8
    Species "Player"
    +THRUSPECIES
}

actor EradDanLaserProjectile
{
    Projectile
    Radius 6
    Height 5
    Speed 70
    Damage (Random(10,15))
    DamageType "EradDisc"
    Scale 0.5
    DeathSound "Erad/Rico2"
    Obituary "%o was killed by %k's laser."
    +WINDTHRUST
    States
    {
      Spawn:
        ER03 C -1 Bright Light("ERADDISC1__")
        stop

      Death:
        EDAM B 2 Bright Light("ERADDISC2__")
        EDAM C 2 Bright Light("ERADDISC3__")
        EDAM D 2 Bright Light("ERADDISC4__")
        EDAM E 2 Bright Light("ERADDISC5__")
        stop
    }
}

actor EradDanLaserProjectileCoop : EradDanLaserProjectile
{
    Species "Player"
    +THRUSPECIES
}

actor EradKamchakPhotonPulseProjectile
{
    Projectile
    Radius 6
    Height 5
    Speed 70
    Damage (Random(10,15))
    DamageType "EradDisc"
    Scale 0.5
    DeathSound "GrenadeA/Boom"
    Obituary "%o was killed by %k's photon pulse."
    +WINDTHRUST
    States
    {
      Spawn:
        HSD5 IJ 3 Bright Light("ERADPHOTON1__")
        Loop

      Death:
        LSD_ B 2 Bright Light("ERADPHOTON2__")
        LSD_ C 2 Bright Light("ERADPHOTON3__")
        LSD_ D 2 Bright Light("ERADPHOTON4__")
        LSD_ E 2 Bright Light("ERADPHOTON5__")
        stop
    }
}

actor EradKamchakPhotonPulseProjectileCoop : EradKamchakPhotonPulseProjectile
{
    Species "Player"
    +THRUSPECIES
}

actor EradKamchakPhotonPulseWeaveProjectile1 : EradKamchakPhotonPulseProjectile
{
    States
    {
      Spawn:
		TNT1 A 0
	  SpawnLoop:
        HSD5 IIIIII 0 A_Weave(1,1,1.0,1.0)
		HSD5 I 1 Bright Light("ERADPHOTON1__")
		HSD5 JJJJJJ 0 A_Weave(1,1,1.0,1.0)
		HSD5 J 1 Bright Light("ERADPHOTON1__")
        Loop
    }
}

actor EradKamchakPhotonPulseWeaveProjectile2 : EradKamchakPhotonPulseWeaveProjectile1
{
	WeaveIndexXY 32
    WeaveIndexZ 32
}

actor EradKamchakPhotonPulseWeaveProjectile1Coop : EradKamchakPhotonPulseWeaveProjectile1
{
    Species "Player"
    +THRUSPECIES
}

actor EradKamchakPhotonPulseWeaveProjectile2Coop : EradKamchakPhotonPulseWeaveProjectile2
{
    Species "Player"
    +THRUSPECIES
}