actor SamsaraEradicatorRipperDiscAltAttackAmount : Counter { Inventory.MaxAmount 4 }
actor SamsaraEradicatorRipperDiscAltAttackSilentSound : Boolean {}

actor "  Ripper Disc  " : SamsaraEradicatorWeapon
{
    Inventory.PickupMessage "Ripper Disc"
    Weapon.SlotNumber 1
    Weapon.SlotPriority 1
    Weapon.SelectionOrder 1800
    Obituary "%o was ripped by %k's discs."
    Tag "Ripper Disc"
    +NOALERT
    +WIMPY_WEAPON
    States
    {
      Spawn:
        EDAP A -1
        stop

      Ready:
        TNT1 A 0 A_JumpIfInventory("EleenaReady",1,"IntroTaunt")
      ReadyStart:
        TNT1 A 0 A_JumpIfInventory("PowerEradEnWP",1,3)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,2)
        ELED A 1 A_WeaponReady(WRF_NOSECONDARY)
        goto Ready
        ELED A 1 A_WeaponReady
        goto Ready

      Deselect:
        TNT1 A 0 A_JumpIfHealthLower(1,"DeselectInstant")
        TNT1 A 0 A_PlaySound("Erad/wepswitch")
      DeselectLoop:
        TNT1 A 0 A_Lower
        ELED A 1 A_Lower
        loop

      Select:
        TNT1 A 0 A_Raise
        ELED A 1 A_Raise
        loop

      Fire:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto FireStart
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"FireStart")
        goto NoAmmoSwitch

      FireStart:
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_GunFlash
        TNT1 A 0 A_GiveInventory("SamsaraEradicatorRipperDiscPrimaryAttackHandler")
        ELED A 1 Bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
        ELED CD 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
        ELED E 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
        ELED F 3 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
        ELED EDC 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
      FireFinish:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto FireFinishReFire
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireFinishReFire")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"FireFinishReFire")
        goto NoAmmoSwitch

      FireFinishReFire:
        TNT1 A 0 A_ReFire
        goto Ready

      AltFire:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto AltFireStart
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"AltFireStart")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"AltFireStart")
        goto NoAmmoSwitch

      AltFireStart:
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_TakeInventory("SamsaraEradicatorRipperDiscAltAttackAmount") // reset
        TNT1 A 0 A_TakeInventory("SamsaraEradicatorRipperDiscAltAttackSilentSound")
      AltFireStartLoop: // despite firing on the first tic, ammo checks are here due to the loop system
        TNT1 A 0 A_GiveInventory("SamsaraEradicatorRipperDiscAltAttackAmount")
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto AltFireAttack
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"AltFireAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"AltFireAttack")
        goto AltFireContinue

      AltFireAttack:
        TNT1 A 0 A_GunFlash
      AltFireContinue:
        TNT1 A 0 A_GiveInventory("SamsaraEradicatorRipperDiscSecondaryAttackHandler")
        ELED A 1 Bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
        ELED C 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
        TNT1 A 0 A_JumpIfInventory("SamsaraEradicatorRipperDiscAltAttackAmount",0,"AltFireFinish")
        goto AltFireStartLoop

      AltFireFinish:
        ELED DEFEDC 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto AltFireFinishReFire
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"AltFireFinishReFire")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"AltFireFinishReFire")
        goto NoAmmoSwitch

      AltFireFinishReFire:
        TNT1 A 0 A_ReFire
        goto Ready

      Flash:
        ELED B 1 Bright
        goto LightDone

      IntroTaunt:
        TNT1 A 0 A_GiveInventory("EleenaIntroTaunt")
        goto ReadyStart

      // "No ammo" switch
      NoAmmoSwitch:
        TNT1 A 0 A_ClearReFire
        TNT1 A 0 A_SelectWeapon("  Claw  ")
        goto Deselect
    }
}

actor SamsaraEradicatorRipperDiscPrimaryAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto PickupAttack
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("eleena/discfire",CHAN_WEAPON)
        TNT1 A 0 A_FireBullets(0,0,1,0,"SamsaraBlankPuff",FBF_NORANDOM|FBF_NOFLASH)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("RippingDisc",0,false)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("RippingCoopDisc",0,false)
        stop
    }
}

actor SamsaraEradicatorRipperDiscSecondaryAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto PickupAttack
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        goto PickupFinish
        TNT1 A 0 A_TakeInventory("Clip",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SamsaraEradicatorRipperDiscAltAttackSilentSound",1,2)
        TNT1 A 0 A_PlaySound("eleena/fastdisc",CHAN_WEAPON)
        TNT1 A 0 A_FireBullets(0,0,1,0,"SamsaraBlankPuff",FBF_NORANDOM|FBF_NOFLASH)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("RippingDisc",0,false)
        goto PickupFinish

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("RippingCoopDisc",0,false)
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 A_GiveInventory("SamsaraEradicatorRipperDiscAltAttackSilentSound")
        stop
    }
}

actor RippingDisc
{
    Projectile
    Radius 6
    Height 5
    Speed 70
    Damage (Random(10,15))
    DamageType "EradDisc"
    BounceCount 6
    BounceFactor 1
    Scale 0.5
    DeathSound "eleena/discdead"
    Obituary "%o was ripped by %k's discs."
    +BOUNCEONCEILINGS
    +BOUNCEONFLOORS
    +WINDTHRUST
    States
    {
      Spawn:
        EDAM A -1 Bright Light("ERADDISC1__")
        stop

      Death:
        EDAM B 2 Bright Light("ERADDISC2__")
        EDAM C 2 Bright Light("ERADDISC3__")
        EDAM D 2 Bright Light("ERADDISC4__")
        EDAM E 2 Bright Light("ERADDISC5__")
        stop
    }
}

actor RippingCoopDisc : RippingDisc
{
    Speed 80
    BounceCount 8
    Species "Player"
    +THRUSPECIES
}
