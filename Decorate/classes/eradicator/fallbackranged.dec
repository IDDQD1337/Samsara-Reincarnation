actor SamsaraEradicatorRipperDiscAltAttackAmount : Counter { Inventory.MaxAmount 4 }
actor SamsaraEradicatorRipperDiscAltAttackSilentSound : Boolean {}
actor SamsaraEradicatorLaserEnhancedAttackSwitch : Boolean {}
actor SamsaraEradicatorKamchakPhotonPulseAttackSwitch : Boolean {}

actor "  Ripper Disc  " : SamsaraEradicatorWeapon
{
    Inventory.PickupMessage "Ripper Disc"
    Weapon.SlotNumber 1
    Weapon.SlotPriority 1
    Weapon.SelectionOrder 1800
    Obituary "%o was ripped by %k's discs."
    Tag "Ripper Disc"
    +NOALERT
    +WIMPY_WEAPON
    States
    {
      Spawn:
        EDAP A -1
        stop

      // Eleena Brynstaarl
      Ready:
        TNT1 A 0 A_JumpIfInventory("EleenaReady",1,"IntroTaunt")
      ReadyStart:
        TNT1 A 0 A_JumpIfInventory("EradMode",3,"ReadyStartMarine")
        TNT1 A 0 A_JumpIfInventory("EradMode",2,"ReadyStartDan")
        TNT1 A 0 A_JumpIfInventory("EradMode",1,"ReadyStartKamchak")
      ReadyStartEleena:
        ELED A 1 A_WeaponReady
        goto Ready

      Deselect:
        TNT1 A 0 A_JumpIfHealthLower(1,"DeselectInstant")
        TNT1 A 0 A_PlaySound("Erad/wepswitch")
      DeselectLoop:
        TNT1 A 0 A_JumpIfInventory("EradMode",3,"DeselectLoopMarine")
        TNT1 A 0 A_JumpIfInventory("EradMode",2,"DeselectLoopDan")
        TNT1 A 0 A_JumpIfInventory("EradMode",1,"DeselectLoopKamchak")
      DeselectLoopEleena:
        TNT1 A 0 A_Lower
        ELED A 1 A_Lower
        goto DeselectLoop

      Select:
        TNT1 A 0 A_JumpIfInventory("EradMode",3,"SelectMarine")
        TNT1 A 0 A_JumpIfInventory("EradMode",2,"SelectDan")
        TNT1 A 0 A_JumpIfInventory("EradMode",1,"SelectKamchak")
      SelectEleena:
        TNT1 A 0 A_Raise
        ELED A 1 A_Raise
        goto Select

      // Unenhanced
      Fire:
        TNT1 A 0 A_JumpIfInventory("PowerEradEnWP",1,"FireEnhanced")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"FireEnhanced")
      FireUnenhanced:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto FireStartUnenhanced
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStartUnenhanced")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"FireStartUnenhanced")
        goto NoAmmoSwitch

      FireStartUnenhanced:
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_JumpIfInventory("EradMode",3,"FireStartUnenhancedMarine")
        TNT1 A 0 A_JumpIfInventory("EradMode",2,"FireStartUnenhancedDan")
        TNT1 A 0 A_JumpIfInventory("EradMode",1,"FireStartUnenhancedKamchak")
      FireStartUnenhancedEleena:
        TNT1 A 0 A_GunFlash("FlashEleena")
        ELED A 1 Bright A_GiveInventory("SamsaraEradicatorRipperDiscUnenhancedAttackHandler")
        ELED A 1 Offset(-2,34)
        ELED A 1 Offset(-4,36)
        ELED A 2 Offset(-6,38)
        ELED A 3 Offset(-6,39)
        ELED A 2 Offset(-6,38)
        ELED A 1 Offset(-4,36)
        ELED A 1 Offset(-2,34)
      FireFinish:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto FireFinishReFire
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireFinishReFire")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"FireFinishReFire")
        goto NoAmmoSwitch

      FireFinishReFire:
        TNT1 A 0 A_ReFire
        goto Ready

      // Enhanced
      FireEnhanced:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto FireStartEnhanced
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStartEnhanced")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"FireStartEnhanced")
        goto NoAmmoSwitch

      FireStartEnhanced:
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_JumpIfInventory("EradMode",3,"FireStartEnhancedMarine")
        TNT1 A 0 A_JumpIfInventory("EradMode",2,"FireStartEnhancedDan")
        TNT1 A 0 A_JumpIfInventory("EradMode",1,"FireStartEnhancedKamchak")
      FireStartEnhancedEleena:
        TNT1 A 0 A_TakeInventory("SamsaraEradicatorRipperDiscAltAttackSilentSound")
        TNT1 A 0 A_TakeInventory("SamsaraEradicatorRipperDiscAltAttackAmount") // reset
      FireStartLoopEnhancedEleena: // despite firing on the first tic, ammo checks are here due to the loop system
        TNT1 A 0 A_GiveInventory("SamsaraEradicatorRipperDiscAltAttackAmount")
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto FireAttackEnhancedEleena
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireAttackEnhancedEleena")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"FireAttackEnhancedEleena")
        goto FireContinueEnhancedEleena

      FireAttackEnhancedEleena:
        TNT1 A 0 A_GunFlash("FlashEleena")
      FireContinueEnhancedEleena:
        ELED A 1 Bright A_GiveInventory("SamsaraEradicatorRipperDiscEnhancedAttackHandler")
        ELED A 1 Offset(-2,34)
        TNT1 A 0 A_JumpIfInventory("SamsaraEradicatorRipperDiscAltAttackAmount",0,1)
        goto FireStartLoopEnhancedEleena
        ELED A 1 Offset(-4,36)
        ELED A 1 Offset(-6,38)
        ELED A 1 Offset(-6,39)
        ELED A 1 Offset(-6,38)
        ELED A 1 Offset(-4,36)
        ELED A 1 Offset(-2,34)
        goto FireFinish

      FlashEleena:
        ELED B 1 Bright
        goto LightDone

      IntroTaunt:
        TNT1 A 0 A_GiveInventory("EleenaIntroTaunt")
        goto ReadyStart

      // Kamchak (Photon Bolt)
      ReadyStartKamchak:
        ER04 A 1 A_WeaponReady
        goto Ready

      DeselectLoopKamchak:
        TNT1 A 0 A_Lower
        ER04 A 1 A_Lower
        goto DeselectLoop

      SelectKamchak:
        TNT1 A 0 A_Raise
        ER04 A 1 A_Raise
        goto Select

      // Unenhanced
      FireStartUnenhancedKamchak:
        TNT1 A 0 A_GunFlash("FlashKamchak")
        TNT1 A 0 A_GiveInventory("SamsaraEradicatorPhotonPulseUnenhancedAttackHandler")
        goto FireContinueKamchak

      // Enhanced
      FireStartEnhancedKamchak:
        TNT1 A 0 A_GunFlash("FlashKamchak")
        TNT1 A 0 A_GiveInventory("SamsaraEradicatorPhotonPulseEnhancedAttackHandler")
        goto FireContinueKamchak

      FireContinueKamchak:
        ER04 A 1 Bright
        ER04 A 1 Offset(2,35)
        ER04 A 1 Offset(4,38)
        ER04 A 2 Offset(6,41)
        ER04 A 3 Offset(8,44)
        ER04 A 1 Offset(6,41)
        ER04 A 1 Offset(4,38)
        ER04 A 1 Offset(2,35)
        goto FireFinish

      FlashKamchak:
        TNT1 A 0 A_JumpIfInventory("SamsaraEradicatorKamchakPhotonPulseAttackSwitch",1,"FlashKamchak2")
      FlashKamchak1:
        ER04 B 1 Bright
        goto LightDone

      FlashKamchak2:
        ER04 C 1 Bright
        goto LightDone

      // Dan Blaze (Strident Laser)
      ReadyStartDan:
        ER03 A 1 A_WeaponReady
        goto Ready

      DeselectLoopDan:
        TNT1 A 0 A_Lower
        ER03 A 1 A_Lower
        goto DeselectLoop

      SelectDan:
        TNT1 A 0 A_Raise
        ER03 A 1 A_Raise
        goto Select

      // Unenhanced
      FireStartUnenhancedDan:
        TNT1 A 0 A_GunFlash("FlashDan")
        TNT1 A 0 A_GiveInventory("SamsaraEradicatorLaserUnenhancedAttackHandler")
        goto FireContinueDan

      // Enhanced
      FireStartEnhancedDan:
        TNT1 A 0 A_GunFlash("FlashDan")
        TNT1 A 0 A_GiveInventory("SamsaraEradicatorLaserEnhancedAttackHandler")
        goto FireContinueDan

      FireContinueDan:
        ER03 A 1 Bright
        ER03 A 1 Offset(-2,35)
        ER03 A 1 Offset(-4,38)
        ER03 A 2 Offset(-6,41)
        ER03 A 3 Offset(-8,44)
        ER03 A 1 Offset(-6,41)
        ER03 A 1 Offset(-4,38)
        ER03 A 1 Offset(-2,35)
        goto FireFinish

      FlashDan:
        ER03 B 1 Bright
        goto LightDone

      // Alliance Commando (Photon Bolt)
      ReadyStartMarine:
        ER05 A 1 A_WeaponReady
        goto Ready

      DeselectLoopMarine:
        TNT1 A 0 A_Lower
        ER05 A 1 A_Lower
        goto DeselectLoop

      SelectMarine:
        TNT1 A 0 A_Raise
        ER05 A 1 A_Raise
        goto Select

      // Unenhanced
      FireStartUnenhancedMarine:
        TNT1 A 0 A_GunFlash("FlashMarine")
        TNT1 A 0 A_GiveInventory("SamsaraEradicatorPhotonPulseUnenhancedAttackHandler")
        goto FireContinueMarine

      // Enhanced
      FireStartEnhancedMarine:
        TNT1 A 0 A_GunFlash("FlashMarine")
        TNT1 A 0 A_GiveInventory("SamsaraEradicatorPhotonPulseEnhancedAttackHandler")
        goto FireContinueMarine

      FireContinueMarine:
        ER05 A 1 Bright
        ER05 A 1 Offset(2,35)
        ER05 A 1 Offset(4,38)
        ER05 A 2 Offset(6,41)
        ER05 A 3 Offset(8,44)
        ER05 A 1 Offset(6,41)
        ER05 A 1 Offset(4,38)
        ER05 A 1 Offset(2,35)
        goto FireFinish

      FlashMarine:
        ER05 B 1 Bright
        goto LightDone

      // "No ammo" switch
      NoAmmoSwitch:
        TNT1 A 0 A_ClearReFire
        TNT1 A 0 A_SelectWeapon("  Claw  ")
        goto Deselect
    }
}

// Eleena Brynstaarl

actor SamsaraEradicatorRipperDiscUnenhancedAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto PickupAttack
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("eleena/discfire",CHAN_WEAPON)
        TNT1 A 0 A_FireBullets(0,0,1,0,"SamsaraBlankPuff",FBF_NORANDOM|FBF_NOFLASH)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("RippingDisc",0,false,-7)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("RippingCoopDisc",0,false,-7)
        stop
    }
}

actor SamsaraEradicatorRipperDiscEnhancedAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto PickupAttack
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        goto PickupFinish
        TNT1 A 0 A_TakeInventory("Clip",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SamsaraEradicatorRipperDiscAltAttackSilentSound",1,2)
        TNT1 A 0 A_PlaySound("eleena/fastdisc",CHAN_WEAPON)
        TNT1 A 0 A_FireBullets(0,0,1,0,"SamsaraBlankPuff",FBF_NORANDOM|FBF_NOFLASH)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("RippingDisc",0,false,-7)
        goto PickupFinish

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("RippingCoopDisc",0,false,-7)
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 A_GiveInventory("SamsaraEradicatorRipperDiscAltAttackSilentSound")
        stop
    }
}

actor RippingDisc
{
    Projectile
    Radius 6
    Height 5
    Speed 70
    Damage (random(10,15))
    DamageType "EradDisc"
    BounceCount 6
    BounceFactor 1
    Scale 0.5
    DeathSound "eleena/discdead"
    Obituary "%o was ripped by %k's discs."
    +BOUNCEONCEILINGS
    +BOUNCEONFLOORS
    +WINDTHRUST
    States
    {
      Spawn:
        EDAM A -1 Bright Light("ERADDISC1__")
        stop

      Death:
        EDAM B 2 Bright Light("ERADDISC2__")
        EDAM C 2 Bright Light("ERADDISC3__")
        EDAM D 2 Bright Light("ERADDISC4__")
        EDAM E 2 Bright Light("ERADDISC5__")
        stop
    }
}

actor RippingCoopDisc : RippingDisc
{
    Speed 80
    BounceCount 8
    Species "Player"
    +THRUSPECIES
}

// Kamchak/Alliance Commando

actor SamsaraEradicatorPhotonPulseUnenhancedAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto PickupAttack
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("erad/hsdfire",CHAN_WEAPON)
        TNT1 A 0 A_FireBullets(0,0,1,0,"SamsaraBlankPuff",FBF_NORANDOM|FBF_NOFLASH)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("EradKamchakPhotonPulseProjectile",0,false,7)
        goto PickupFinish

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("EradKamchakPhotonPulseProjectileCoop",0,false,7)
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 A_JumpIfInventory("EradMode",2,"PickupStop")
        TNT1 A 0 A_JumpIfInventory("EradMode",1,1) // only give animation token for kamchak
        stop
        TNT1 A 0 A_JumpIfInventory("SamsaraEradicatorKamchakPhotonPulseAttackSwitch",1,2)
        TNT1 A 0 A_GiveInventory("SamsaraEradicatorKamchakPhotonPulseAttackSwitch")
        stop
        TNT1 A 0 A_TakeInventory("SamsaraEradicatorKamchakPhotonPulseAttackSwitch")
        stop
    }
}

actor SamsaraEradicatorPhotonPulseEnhancedAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto PickupAttack
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("erad/hsdfire",CHAN_WEAPON)
        TNT1 A 0 A_FireBullets(0,0,1,0,"SamsaraBlankPuff",FBF_NORANDOM|FBF_NOFLASH)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("EradKamchakPhotonPulseWeaveProjectile1",0,false,7)
        TNT1 A 0 A_FireCustomMissile("EradKamchakPhotonPulseWeaveProjectile2",0,false,7)
        goto PickupFinish

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("EradKamchakPhotonPulseWeaveProjectile1Coop",0,false,7)
        TNT1 A 0 A_FireCustomMissile("EradKamchakPhotonPulseWeaveProjectile2Coop",0,false,7)
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 A_JumpIfInventory("EradMode",2,"PickupStop")
        TNT1 A 0 A_JumpIfInventory("EradMode",1,1) // only give animation token for kamchak
        stop
        TNT1 A 0 A_JumpIfInventory("SamsaraEradicatorKamchakPhotonPulseAttackSwitch",1,2)
        TNT1 A 0 A_GiveInventory("SamsaraEradicatorKamchakPhotonPulseAttackSwitch")
        stop
        TNT1 A 0 A_TakeInventory("SamsaraEradicatorKamchakPhotonPulseAttackSwitch")
        stop
    }
}

actor EradKamchakPhotonPulseProjectile
{
    Projectile
    Radius 6
    Height 5
    Speed 70
    Damage (random(10,15))
    DamageType "EradDisc"
    Scale 0.5
    DeathSound "GrenadeA/Boom"
    Obituary "%o was killed by %k's photon pulse."
    +WINDTHRUST
    States
    {
      Spawn:
        HSD5 IJ 3 Bright Light("ERADPHOTON1__")
        loop

      Death:
        LSD_ B 2 Bright Light("ERADPHOTON2__")
        LSD_ C 2 Bright Light("ERADPHOTON3__")
        LSD_ D 2 Bright Light("ERADPHOTON4__")
        LSD_ E 2 Bright Light("ERADPHOTON5__")
        stop
    }
}

actor EradKamchakPhotonPulseProjectileCoop : EradKamchakPhotonPulseProjectile
{
    Species "Player"
    +THRUSPECIES
}

actor EradKamchakPhotonPulseWeaveProjectile1 : EradKamchakPhotonPulseProjectile
{
    States
    {
      Spawn:
        HSD5 I 0 Bright Light("ERADPHOTON1__")
      SpawnLoop:
        TNT1 AAAAAA 0 A_Weave(1,1,1,1)
        HSD5 I 1 Bright Light("ERADPHOTON1__")
        TNT1 AAAAAA 0 A_Weave(1,1,1,1)
        HSD5 J 1 Bright Light("ERADPHOTON1__")
        loop
    }
}

actor EradKamchakPhotonPulseWeaveProjectile2 : EradKamchakPhotonPulseWeaveProjectile1
{
    WeaveIndexXY 32
    WeaveIndexZ 32
}

actor EradKamchakPhotonPulseWeaveProjectile1Coop : EradKamchakPhotonPulseWeaveProjectile1
{
    Species "Player"
    +THRUSPECIES
}

actor EradKamchakPhotonPulseWeaveProjectile2Coop : EradKamchakPhotonPulseWeaveProjectile2
{
    Species "Player"
    +THRUSPECIES
}

// Dan Blaze

actor SamsaraEradicatorLaserUnenhancedAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto PickupAttack
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("erad/laser1",CHAN_WEAPON)
        TNT1 A 0 A_FireBullets(0,0,1,0,"SamsaraBlankPuff",FBF_NORANDOM|FBF_NOFLASH)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("EradDanLaserProjectile",0,false,-10)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("EradDanLaserProjectileCoop",0,false,-10)
        stop
    }
}

actor SamsaraEradicatorLaserEnhancedAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto PickupAttack
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("erad/laserp",CHAN_WEAPON)
        TNT1 A 0 A_FireBullets(0,0,1,0,"SamsaraBlankPuff",FBF_NORANDOM|FBF_NOFLASH)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_JumpIfInventory("SamsaraEradicatorLaserEnhancedAttackSwitch",1,"PickupAttackDM2")
      PickupAttackDM1:
        TNT1 A 0 A_FireCustomMissile("EradDanLaserProjectile",0,false,-10)
        TNT1 A 0 A_FireCustomMissile("EradDanLaserProjectile",2,false,-10,-5)
        TNT1 A 0 A_FireCustomMissile("EradDanLaserProjectile",-2,false,-10,5)
        goto PickupFinish

      PickupAttackDM2:
        TNT1 A 0 A_FireCustomMissile("EradDanLaserProjectile",0,false,-10)
        TNT1 A 0 A_FireCustomMissile("EradDanLaserProjectile",-2,false,-10,-5)
        TNT1 A 0 A_FireCustomMissile("EradDanLaserProjectile",2,false,-10,5)
        goto PickupFinish

      PickupAttackCoop:
        TNT1 A 0 A_JumpIfInventory("SamsaraEradicatorLaserEnhancedAttackSwitch",1,"PickupAttackCoop2")
      PickupAttackCoop1:
        TNT1 A 0 A_FireCustomMissile("EradDanLaserProjectileCoop",0,false,-10)
        TNT1 A 0 A_FireCustomMissile("EradDanLaserProjectileCoop",2,false,-10,-5)
        TNT1 A 0 A_FireCustomMissile("EradDanLaserProjectileCoop",-2,false,-10,5)
        goto PickupFinish

      PickupAttackCoop2:
        TNT1 A 0 A_FireCustomMissile("EradDanLaserProjectileCoop",0,false,-10)
        TNT1 A 0 A_FireCustomMissile("EradDanLaserProjectileCoop",-2,false,-10,-5)
        TNT1 A 0 A_FireCustomMissile("EradDanLaserProjectileCoop",2,false,-10,5)
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 A_JumpIfInventory("SamsaraEradicatorLaserEnhancedAttackSwitch",1,2)
        TNT1 A 0 A_GiveInventory("SamsaraEradicatorLaserEnhancedAttackSwitch")
        stop
        TNT1 A 0 A_TakeInventory("SamsaraEradicatorLaserEnhancedAttackSwitch")
        stop
    }
}

actor EradDanLaserProjectile
{
    Projectile
    Radius 6
    Height 5
    Speed 70
    Damage (random(10,15))
    DamageType "EradDisc"
    Scale 0.5
    DeathSound "Erad/Rico2"
    Obituary "%o was killed by %k's laser."
    +WINDTHRUST
    States
    {
      Spawn:
        ER03 C -1 Bright Light("ERADDISC1__")
        stop

      Death:
        EDAM B 2 Bright Light("ERADDISC2__")
        EDAM C 2 Bright Light("ERADDISC3__")
        EDAM D 2 Bright Light("ERADDISC4__")
        EDAM E 2 Bright Light("ERADDISC5__")
        stop
    }
}

actor EradDanLaserProjectileCoop : EradDanLaserProjectile
{
    Species "Player"
    +THRUSPECIES
}
