actor " Plasma Ball " : SamsaraEradicatorWeapon
{
    Inventory.PickupMessage "Death Sphere"
    Weapon.SlotNumber 7
    Weapon.SelectionOrder 2500
    Weapon.AmmoType "Cell"
    Weapon.AmmoGive 100
    Weapon.AmmoUse 50
    Obituary "%o was vaporized by %k's big balls."
    Tag "Death Sphere"
    +BFG
    +FLOATBOB
    States
    {
      Spawn:
        EPL9 TUVWX 5 Bright
        loop

      Ready:
        TNT1 A 0 A_JumpIfInventory("EleenaReady",1,"IntroTaunt")
      ReadyStart:
        ER14 A 1 A_WeaponReady
        goto Ready

      Deselect:
        TNT1 A 0 A_JumpIfHealthLower(1,"DeselectInstant")
        TNT1 A 0 A_PlaySound("Erad/wepswitch")
      DeselectLoop:
        TNT1 A 0 A_Lower
        ER14 A 1 A_Lower
        loop

      Select:
        TNT1 A 0 A_Raise
        ER14 A 1 A_Raise
        loop

      Fire:
        TNT1 A 0 A_PlaySound("Eleena/BoltShootA",CHAN_WEAPON)
        ER14 A 26 Bright A_GunFlash
        ER14 A 1 Bright A_GiveInventory("SamsaraEradicatorDeathSphereAttackHandler")
        //ER14 A 1 Bright Offset(2,33)
        ER14 A 1 Bright Offset(6,37)
        //ER14 A 1 Bright Offset(8,39)
        ER14 A 1 Bright Offset(12,43)
        ER14 A 1 Offset(24,65)
        ER14 A 1 Offset(32,73)
        ER14 A 1 Offset(28,69)
        ER14 A 1 Offset(24,65)
        ER14 A 1 Offset(12,43)
        ER14 A 1 Offset(8,39)
        ER14 A 1 Offset(6,37)
        ER14 A 1 Offset(2,33)
        ER14 A 13 Offset(0,32)
        goto Ready

      Flash:
		ER14 BCDE 4 Bright
        ER14 F 7 Bright
        ER14 GHI 1 Bright
        ER14 J 1 Bright A_Light1
        ER14 K 1 Bright A_Light2
        goto LightDone

      IntroTaunt:
        TNT1 A 0 A_GiveInventory("EleenaIntroTaunt")
        goto ReadyStart
    }
}

actor SamsaraEradicatorDeathSphereAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Cell",50,1)
        stop
        TNT1 A 0 A_TakeInventory("Cell",50)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("Eleena/BoltShoot2",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("EradPlasmaBall",0,false)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("CoopEradPlasmaBall",0,false)
        stop
    }
}

actor EradPlasmaBall
{
    Projectile
    Radius 8
    Height 6
    Speed 10
    Damage (0)
    DamageType "EradNova"
    ReactionTime 36
    BounceFactor 1.1
    Scale 0.65
    ActiveSound "Eleena/EBuddy3"
    DeathSound "Eleena/BigBoom"
    Obituary "%o was vaporized by %k big balls."
    +BOUNCEONCEILINGS
    +BOUNCEONFLOORS
    +BOUNCEONWALLS
    +CANBOUNCEWATER
    +DONTREFLECT
    +EXTREMEDEATH
    +FORCERADIUSDMG
    +LOOKALLAROUND
    +NODAMAGETHRUST
    //+RIPPER
    +SEEKERMISSILE
    +STEPMISSILE
    +THRUACTORS
    States
    {
      Spawn:
        EPL9 ABCDEABCDE 3 Bright Light("ERADBALLSHIT__")
        TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_SeekerMissile(90,90)
        TNT1 A 0 A_Countdown
      SpawnSeek:
        TNT1 A 0 A_Explode(CallACS("EleenaDeathBallDamageTier"),12,0,false,12)
        EPL9 AAA 1 Bright Light("ERADBALLSHIT__") A_SpawnItemEx("EradElectricTrail",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
        TNT1 A 0 A_Explode(CallACS("EleenaDeathBallDamageTier"),12,0,false,12)
        TNT1 A 0 A_SeekerMissile(90,90,SMF_LOOK|SMF_PRECISE,256)
        EPL9 BBB 1 Bright Light("ERADBALLSHIT__") A_SpawnItemEx("EradElectricTrail",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
        TNT1 A 0 A_Explode(CallACS("EleenaDeathBallDamageTier"),12,0,false,12)
        TNT1 A 0 A_SeekerMissile(90,90,SMF_LOOK|SMF_PRECISE,256)
        EPL9 CCC 1 Bright Light("ERADBALLSHIT__") A_SpawnItemEx("EradElectricTrail",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
        TNT1 A 0 A_Explode(CallACS("EleenaDeathBallDamageTier"),12,0,false,12)
        TNT1 A 0 A_SeekerMissile(90,90,SMF_LOOK|SMF_PRECISE,256)
        EPL9 DDD 1 Bright Light("ERADBALLSHIT__") A_SpawnItemEx("EradElectricTrail",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
        TNT1 A 0 A_Explode(CallACS("EleenaDeathBallDamageTier"),12,0,false,12)
        TNT1 A 0 A_SeekerMissile(90,90,SMF_LOOK|SMF_PRECISE,256)
        EPL9 EEE 1 Bright Light("ERADBALLSHIT__") A_SpawnItemEx("EradElectricTrail",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
        TNT1 A 0 A_Explode(CallACS("EleenaDeathBallDamageTier"),12,0,false,12)
        TNT1 A 0 A_SeekerMissile(90,90,SMF_LOOK|SMF_PRECISE,256)
        TNT1 A 0 A_Countdown
        loop

      Death:
        TNT1 A 0 A_Explode(600+random(100,200)/random(1,2),211,0)
      DeathFinish:
        TNT1 A 1 A_SpawnItemEx("EradPlasmaExplosion2",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
        stop
  }
}

actor CoopEradPlasmaBall : EradPlasmaBall
{
    Speed 15
    ReactionTime 40
    Species "Player"
    +THRUSPECIES
    States
    {
      Death:
        TNT1 AAA 0 A_Explode(284/random(1,2),211,0)
        goto DeathFinish
  }
}

actor EradElectricTrail
{
    Radius 1
    Height 1
    RenderStyle "Translucent"
    Alpha 0.35
    +CLIENTSIDEONLY
    +NOCLIP
    +NOGRAVITY
    +RANDOMIZE
    Scale 0.65
    States
    {
      Spawn:
        TNT1 AA 1
        BNFR A 1 Bright Light("ERADTRAILBLUESHIT__1")
        TNT1 AA 1
        BNFR B 1 Bright Light("ERADTRAILBLUESHIT__2")
        TNT1 AA 1
        BNFR C 1 Bright Light("ERADTRAILBLUESHIT__3")
        TNT1 AA 1
        BNFR D 1 Bright Light("ERADTRAILBLUESHIT__4")
        TNT1 AA 1
        BNFR E 1 Bright Light("ERADTRAILBLUESHIT__5")
        TNT1 AA 1
        BNFR F 1 Bright Light("ERADTRAILBLUESHIT__6")
        stop
    }
}

actor EradPlasmaExplosion2 : EradPlasmaExplosion1
{
    Scale 1.5
    States
    {
      Spawn:
        EPL9 F 4 Bright Light("ERADEXPLOSION1__")
        EPL9 G 4 Bright Light("ERADEXPLOSION2__")
        EPL9 H 4 Bright Light("ERADEXPLOSION3__")
        EPL9 I 4 Bright Light("ERADEXPLOSION4__")
        EPL9 J 4 Bright Light("ERADEXPLOSION5__") A_FadeOut
        stop
    }
}

actor EradPlasmaExplosion3 : EradPlasmaExplosion1
{
    States
    {
      Spawn:
        EPL9 KLMNOPQR 3 Bright
        EPL9 S 2 Bright A_FadeOut
        stop
    }
}
