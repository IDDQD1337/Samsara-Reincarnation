Actor DeusEx_10mmPistol : DeusExWeapon
{
	Weapon.SelectionOrder 1700
	Weapon.SlotNumber 1
	Weapon.SlotPriority 1
	Weapon.AmmoType "DeusEx_GlockMagazine"
	Weapon.AmmoUse 1
	Tag "Pistol"
	Inventory.PickupMessage "You found a Pistol."
	Obituary "%o was gatted with %k's 10mm Pistol."
    +WIMPY_WEAPON
	States
	{
		ReadyActual:
            DX00 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReadyActualReloadable")
        ReadyActualNonReloadable:
			DX00 A 1 A_WeaponReady
            goto ReadyActualJump

        ReadyActualReloadable:
			DX00 A 1 A_WeaponReady(WRF_ALLOWRELOAD)
            goto ReadyActualJump

        ReadyActualJump:
            DX00 A 0 A_Jump(3,"ReadyActualIdle1","ReadyActualIdle2","ReadyActualIdle3")
			goto ReadyActualAmmoCheck

		ReadyActualIdle1:
            DX00 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReadyActualIdle1Reloadable")
        ReadyActualIdle1NonReloadable:
            DX00 A 5 A_WeaponReady
			DX01 "WXYZ[\]" 5 A_WeaponReady
			DX02 A 5 A_WeaponReady
			goto ReadyActual

        ReadyActualIdle1Reloadable:
            DX00 A 5 A_WeaponReady(WRF_ALLOWRELOAD)
			DX01 "WXYZ[\]" 5 A_WeaponReady(WRF_ALLOWRELOAD)
			DX02 A 5 A_WeaponReady(WRF_ALLOWRELOAD)
			goto ReadyActual

		ReadyActualIdle2:
            DX00 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReadyActualIdle2Reloadable")
        ReadyActualIdle2NonReloadable:
            DX00 A 5 A_WeaponReady
			DX02 BCDEFGHIJKLM 5 A_WeaponReady
			goto ReadyActual

        ReadyActualIdle2Reloadable:
            DX00 A 5 A_WeaponReady(WRF_ALLOWRELOAD)
			DX02 BCDEFGHIJKLM 5 A_WeaponReady(WRF_ALLOWRELOAD)
			goto ReadyActual

		ReadyActualIdle3:
            DX00 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReadyActualIdle3Reloadable")
        ReadyActualIdle3NonReloadable:
            DX00 A 5 A_WeaponReady
			DX02 NOPQRSTUVWXY 5 A_WeaponReady
			goto ReadyActual

        ReadyActualIdle3Reloadable:
            DX00 A 5 A_WeaponReady(WRF_ALLOWRELOAD)
			DX02 NOPQRSTUVWXY 5 A_WeaponReady(WRF_ALLOWRELOAD)
			goto ReadyActual

        ReadyActualAmmoCheck:
            DX00 A 0 A_JumpIfInventory("SamsaraReloadMode",1,1)
            goto ReadyActual
            DX00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"ReadyActual")
            DX00 A 0 A_JumpIfInventory("DeusEx_GlockMagazine",1,"ReadyActual")
            goto ReloadCheck

		Deselect:
			DX01 U 0 A_TakeInventory("DeusEx_GlockSpread")
			DX01 U 0 A_GiveInventory("DeusEx_Reloading")
			DX01 U 2 A_JumpIfHealthLower(1,"DeathDeselect")
			DX01 V 2
            goto DeselectFinish

        DeathDeselect:
            TNT1 AA 2
            goto DeselectFinish

        DeselectFinish:
			TNT1 A 0 A_TakeInventory("DeusEx_GlockEquipped")
			goto DeselectInstant

		Ready: // our "Select" state
            DX00 B 0 A_PlaySound("DeusEx/10mm/Select")
			DX00 B 1 A_GiveInventory("DeusEx_GlockEquipped")
			DX00 CDEFGHIJKLMNOP 1
			DX00 A 0 A_TakeInventory("DeusEx_Reloading")
			goto ReadyActual

		Fire:
            DX00 Q 0 A_JumpIfInventory("SamsaraReloadMode",1,"FireReloadable")
        FireNonReloadable:
            DX00 Q 0 A_JumpIfInventory("PistolModeOn",1,1)
            goto FireStart
            DX00 Q 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart")
            DX00 Q 0 A_JumpIfInventory("Clip",1,"FireStart")
            goto FireDry

        FireReloadable:
            DX00 Q 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireStart")
            DX00 Q 0 A_JumpIfInventory("DeusEx_GlockMagazine",1,"FireStart")
        FireReloadableDryCheck:
            DX00 Q 0 A_JumpIfInventory("PistolModeOn",1,1)
            goto ReloadStart
            DX00 Q 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReloadStart")
            DX00 Q 0 A_JumpIfInventory("Clip",1,"ReloadStart")
            goto FireDry

		FireDry:
			DX00 A 0 A_PlaySound("DeusEx/DryFire",CHAN_6)
			DX00 A 0 A_ClearReFire
            DX00 A 4 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
            goto ReadyActualAmmoCheck

		FireStart:
			DX00 Q 0 A_GiveInventory("DeusEx_Firing")
			DX00 Q 0 A_AlertMonsters(1000)
			DX00 Q 0 A_SpawnItemEx("DeusEx_ShellCasing1",8,-16,39,random(1,3),0,3,-90,SXF_NOCHECKPOSITION|SXF_MULTIPLYSPEED|SXF_TRANSFERPITCH)
            DX00 Q 0 A_GiveInventory("SamsaraDeusExGlockAttackHandler")
			DX00 Q 3 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX00 RS 4 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX00 TU 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
            DX00 V 0 A_ReFire
			DX00 V 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto ReadyActual

        Reload:
        ReloadCheck:
            DX00 A 0 A_JumpIfInventory("DeusEx_GlockMagazine",0,"ReadyActual")
            DX00 A 0 A_JumpIfInventory("PistolModeOn",1,1)
            goto ReloadStart
            DX00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
            DX00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReloadStart")
            DX00 A 0 A_JumpIfInventory("Clip",1,"ReloadStart")
            goto ReadyActual

		ReloadStart:
			DX00 Y 0 A_GiveInventory("DeusEx_Reloading")
			DX00 Y 0 A_PlaySound("DeusEx/10mm/Reload")
			DX00 "YZ[\]" 2 A_WeaponReady(WRF_NOFIRE)
			DX01 ABCDEFGHIJK 2 A_WeaponReady(WRF_NOFIRE)
			DX01 L 0 A_JumpIfInventory("SPModeOn",1,2)
			DX01 L 0 A_JumpIfInventory("CoopModeOn",1,1)
            goto ReloadContinueAdvanced
			DX01 L 0 A_JumpIfInventory("DeusEx_PistolSkill",0,"ReloadContinueMaster")
			DX01 L 0 A_JumpIfInventory("DeusEx_PistolSkill",3,"ReloadContinueAdvanced")
			DX01 L 0 A_JumpIfInventory("DeusEx_PistolSkill",2,"ReloadContinueProficient")
			DX01 L 0 A_JumpIfInventory("DeusEx_PistolSkill",1,"ReloadContinueTrained")
        ReloadContinueUntrained:
			DX01 LMNOP 4 A_WeaponReady(WRF_NOFIRE)
        ReloadContinueTrained:
			DX01 LMNOP 4 A_WeaponReady(WRF_NOFIRE)
        ReloadContinueProficient:
            DX01 LMNOP 4 A_WeaponReady(WRF_NOFIRE)
        ReloadContinueAdvanced:
            DX01 LMNOP 4 A_WeaponReady(WRF_NOFIRE)
        ReloadContinueMaster:
			DX01 Q 0 A_TakeInventory("DeusEx_Reloading")
			DX01 QRS 2 A_WeaponReady(WRF_NOFIRE)
            DX00 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReloadAmmo")
            goto ReloadDone

        ReloadDone:
            DX00 A 0
			goto ReadyActual

        ReloadAmmo:
            DX00 A 0 A_JumpIfInventory("PistolModeOn",1,"ReloadAmmoReserve")
            DX00 A 0 A_JumpIfInventory("DeusEx_GlockMagazine",0,"ReloadDone")
            DX00 A 0 A_GiveInventory("DeusEx_GlockMagazine")
            loop

        ReloadAmmoReserve:
            DX00 A 0 A_JumpIfInventory("DeusEx_GlockMagazine",0,"ReloadDone")
            DX00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
            DX00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
            DX00 A 0 A_JumpIfInventory("Clip",1,1)
            goto ReloadDone
            DX00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
            DX00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
            DX00 A 0 A_TakeInventory("Clip",1)
            DX00 A 0 A_GiveInventory("DeusEx_GlockMagazine")
            goto ReloadAmmo
	}
}

actor SamsaraDeusExGlockAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"PickupReloadable")
      PickupNonReloadable:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto PickupAttack
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",1)
        goto PickupAttack

      PickupReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("DeusEx_GlockMagazine",1,1)
        stop
        TNT1 A 0 A_TakeInventory("DeusEx_GlockMagazine",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("DeusEx/10mm/Fire",CHAN_WEAPON)
        TNT1 A 0 A_FireBullets(0,0,1,0,"SamsaraBlankPuff",FBF_NORANDOM|FBF_NOFLASH)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("DeusEx_GlockBullet",CallACS("DeusEx_Decorate", 5, 0, 1)*frandom(-1.0,1.0),false,4,16,1,CallACS("DeusEx_Decorate", 5, 0, 1)*frandom(-0.5,0.5))
        goto PickupFinish

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("DeusEx_GlockBulletCoop",CallACS("DeusEx_Decorate", 5, 0, 1)*frandom(-1.0,1.0),false,4,16,1,CallACS("DeusEx_Decorate", 5, 0, 1)*frandom(-0.5,0.5))
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 A_GiveInventory("DeusEx_GlockSpread",8)
        TNT1 A 0 ACS_NamedExecuteWithResult("DeusExRecoil", 18, 3, 1)
        stop
    }
}

Actor DeusEx_GlockMagazine : Ammo
{
    Inventory.MaxAmount 4
    Tag "Pistol Magazine"
    +IGNORESKILL
}

Actor DeusEx_GlockSpread : Ammo
{
    Inventory.MaxAmount 120
    Inventory.InterHubAmount 0
    +IGNORESKILL
    +UNDROPPABLE
}

Actor DeusEx_GlockBullet : DeusExBullet
{
	Damage(CallACS("DeusEx_WeaponSkillDamage", 1, 14))
	DamageType "DeusExPistol"
	States
	{
		Spawn:
			TNT1 A 0 NODELAY //A_SetUserVar(user_damage, CallACS("DeusEx_WeaponSkillDamage", 1, 14))
			TNT1 A 1 //A_SetUserVar(user_damage, CallACS("DeusEx_WeaponSkillDamage", 1, 14))
			Goto Spawn2
		Crash:
			TNT1 A 0 A_PlaySound("DeusEx/Bullet/HitRobot", CHAN_BODY)
			TNT1 A 1 A_Explode(CallACS("DeusEx_DamageModifiers",CallACS("DeusEx_WeaponSkillDamage", 1, 14)),8,!XF_HURTSOURCE,0,8)
			stop
		XDeath:
			//TNT1 A 0 A_PlaySound("DeusEx/Bullet/HitFlesh", CHAN_BODY)
			TNT1 A 0 A_Explode(CallACS("DeusEx_DamageModifiers",CallACS("DeusEx_WeaponSkillDamage", 1, 14)),8,!XF_HURTSOURCE,0,8)
			TNT1 A 0 ACS_NamedExecuteWithResult("DeusEx_BloodGenerator_Offsets")
			TNT1 A 1 A_GiveInventory("DeusEx_BloodGenerator",1,AAPTR_TRACER)
			stop
	}
}

Actor DeusEx_GlockBulletCoop : DeusEx_GlockBullet { +THRUSPECIES Species "Player" }
