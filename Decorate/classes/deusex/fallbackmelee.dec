Actor DeusEx_CombatKnife : DeusExWeapon
{
	Weapon.SelectionOrder 2000
	Weapon.SlotNumber 0
	Weapon.SlotPriority 1
	+WEAPON.MELEEWEAPON
	+WEAPON.WIMPY_WEAPON
	Tag "Combat Knife"
	Inventory.PickupMessage "You found a Combat Knife."
	Obituary "%o was shanked with %o's Combat Knife."
	States
	{
		Spawn:
			DX99 A -1
			stop

		ReadyActual:
			DX00 A 1 A_WeaponReady
			goto ReadyActualJump

		ReadyActualJump:
			DX00 A 0 A_Jump(4,"ReadyActualIdle1","ReadyActualIdle2","ReadyActualIdle3")
			goto ReadyActual

		ReadyActualIdle1:
			DX01 IJKL 5 A_WeaponReady
			goto ReadyActual

		ReadyActualIdle2:
			DX01 MNOPQRST 5 A_WeaponReady
			goto ReadyActual

		ReadyActualIdle3:
			DX01 UVWX 5 A_WeaponReady
			goto ReadyActual

		Deselect:
			DX01 D 0 A_GiveInventory("DeusEx_Reloading")
			DX01 D 2 A_JumpIfHealthLower(1,"DeathDeselect")
			DX01 EFGH 2
			goto DeselectFinish

		DeathDeselect:
			TNT1 AAAAA 2
			goto DeselectFinish

		DeselectFinish:
			TNT1 A 0 A_TakeInventory("DeusEx_KnifeEquipped")
			goto DeselectInstant

		Ready: // our "Select" state
			DX00 B 0 A_PlaySound("DeusEx/Knife/Select")
			DX00 B 5 A_GiveInventory("DeusEx_KnifeEquipped")
			DX00 CD 5
			DX00 A 0 A_TakeInventory("DeusEx_Reloading")
			goto ReadyActual

		Fire:
			DX00 A 0 A_GiveInventory("DeusEx_Firing")
			DX00 A 0 A_PlaySound("DeusEx/Knife/Fire",CHAN_WEAPON)
			DX00 A 0 A_Jump(256,"Fire1","Fire2","Fire3")
		Fire1:
			DX00 E 0 A_JumpIfInventory("DeusEx_LowTechSkill",0,"Fire1Master")
			DX00 E 0 A_JumpIfInventory("DeusEx_LowTechSkill",3,"Fire1Advanced")
			DX00 E 0 A_JumpIfInventory("DeusEx_LowTechSkill",2,"Fire1Proficient")
			DX00 E 0 A_JumpIfInventory("DeusEx_LowTechSkill",1,"Fire1Trained")
		Fire1Untrained:
			DX00 EFGHI 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX00 J 0 A_GiveInventory("SamsaraDeusExKnifeAttackHandler")
			DX00 JKL 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto FireFinish

		Fire1Trained:
			DX00 EF 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX00 G 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX00 HI 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX00 J 0 A_GiveInventory("SamsaraDeusExKnifeAttackHandler")
			DX00 J 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX00 K 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX00 L 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto FireFinish

		Fire1Proficient:
			DX00 E 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX00 FG 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX00 HI 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX00 J 0 A_GiveInventory("SamsaraDeusExKnifeAttackHandler")
			DX00 J 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX00 KL 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto FireFinish

		Fire1Advanced:
			DX00 EFG 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX00 HI 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX00 J 0 A_GiveInventory("SamsaraDeusExKnifeAttackHandler")
			DX00 JKL 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto FireFinish

		Fire1Master:
			DX00 EFGHI 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX00 J 0 A_GiveInventory("SamsaraDeusExKnifeAttackHandler")
			DX00 JKL 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto FireFinish

		Fire2:
			DX00 M 0 A_JumpIfInventory("DeusEx_LowTechSkill",0,"Fire2Master")
			DX00 M 0 A_JumpIfInventory("DeusEx_LowTechSkill",3,"Fire2Advanced")
			DX00 M 0 A_JumpIfInventory("DeusEx_LowTechSkill",2,"Fire2Proficient")
			DX00 M 0 A_JumpIfInventory("DeusEx_LowTechSkill",1,"Fire2Trained")
		Fire2Untrained:
			DX00 MNOPQRS 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX00 U 0 A_GiveInventory("SamsaraDeusExKnifeAttackHandler")
			DX00 UVWX 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto FireFinish

		Fire2Trained:
			DX00 MNO 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX00 P 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX00 QRS 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX00 U 0 A_GiveInventory("SamsaraDeusExKnifeAttackHandler")
			DX00 UV 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX00 W 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX00 X 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto FireFinish

		Fire2Proficient:
			DX00 MN 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX00 OPQ 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX00 RS 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX00 U 0 A_GiveInventory("SamsaraDeusExKnifeAttackHandler")
			DX00 U 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX00 VW 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX00 X 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto FireFinish

		Fire2Advanced:
			DX00 NOPQ 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX00 RS 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX00 U 0 A_GiveInventory("SamsaraDeusExKnifeAttackHandler")
			DX00 UVW 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX00 X 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto FireFinish

		Fire2Master:
			DX00 NOPQRS 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX00 U 0 A_GiveInventory("SamsaraDeusExKnifeAttackHandler")
			DX00 UVWX 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto FireFinish

		Fire3:
			DX00 Y 0 A_JumpIfInventory("DeusEx_LowTechSkill",0,"Fire3Master")
			DX00 Y 0 A_JumpIfInventory("DeusEx_LowTechSkill",3,"Fire3Advanced")
			DX00 Y 0 A_JumpIfInventory("DeusEx_LowTechSkill",2,"Fire3Proficient")
			DX00 Y 0 A_JumpIfInventory("DeusEx_LowTechSkill",1,"Fire3Trained")
		Fire3Untrained:
			DX00 "YZ[\]" 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX01 A 0 A_GiveInventory("SamsaraDeusExKnifeAttackHandler")
			DX01 ABC 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto FireFinish

		Fire3Trained:
			DX00 Y 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX00 Z 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX00 "[\]" 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX01 A 0 A_GiveInventory("SamsaraDeusExKnifeAttackHandler")
			DX01 A 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX01 B 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX01 C 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto FireFinish

		Fire3Proficient:
			DX00 YZ 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX00 "[\]" 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX01 A 0 A_GiveInventory("SamsaraDeusExKnifeAttackHandler")
			DX01 A 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX01 BC 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto FireFinish

		Fire3Advanced:
			DX00 "YZ[" 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX00 "\]" 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX01 A 0 A_GiveInventory("SamsaraDeusExKnifeAttackHandler")
			DX01 ABC 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto FireFinish

		Fire3Master:
			DX00 "YZ[\]" 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX01 A 0 A_GiveInventory("SamsaraDeusExKnifeAttackHandler")
			DX01 ABC 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto FireFinish

		FireFinish:
			DX00 A 0
			goto ReadyActual
	}
}

actor SamsaraDeusExKnifeAttackHandler : Trigger
{
	States
	{
	// Attack stuff
	Pickup:
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupCoop")
	PickupDM:
		TNT1 A 0 A_FireCustomMissile("DeusEx_KnifeMissile",0,false)
		stop
	PickupCoop:
		TNT1 A 0 A_FireCustomMissile("DeusEx_KnifeMissileCoop",0,false)
		stop
	}
}

Actor DeusEx_KnifeMissile : DeusExBullet
{
	Damage(CallACS("DeusEx_WeaponSkillDamage", 2, 10))
	Speed 32
	Decal none
	DamageType "DeusExMelee"
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 1
			Stop
		Crash:
			TNT1 A 0 A_PlaySound("DeusEx/Knife/HitSoft", CHAN_BODY)
			TNT1 A 1 A_Explode(CallACS("DeusEx_BackDamage",CallACS("DeusEx_WeaponSkillDamage", 2, 10)),8,!XF_HURTSOURCE,0,8)
			Stop
		Death:
			TNT1 A 1 A_PlaySound("DeusEx/Knife/HitHard", CHAN_BODY)
			Stop
		XDeath:
			TNT1 A 0 A_PlaySound("DeusEx/Knife/HitFlesh", CHAN_BODY)
			TNT1 A 0 A_Explode(CallACS("DeusEx_BackDamage",CallACS("DeusEx_WeaponSkillDamage", 2, 10)),8,!XF_HURTSOURCE,0,8)
			TNT1 A 0 ACS_NamedExecuteWithResult("DeusEx_BloodGenerator_Offsets")
			TNT1 A 1 A_GiveInventory("DeusEx_BloodGenerator",1,AAPTR_TRACER)
			stop
	}
}

Actor DeusEx_KnifeMissileCoop : DeusEx_KnifeMissile { Species "Player" +THRUSPECIES }
