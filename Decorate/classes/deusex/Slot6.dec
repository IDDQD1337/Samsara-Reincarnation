actor SamsaraDeusExFlamethrowerAttackSilentSound : Boolean {}

Actor DeusEx_FlameThrower : DeusExWeapon // horrifically overpowered. will have to rethink later.
{
	Weapon.SelectionOrder 1600
	Weapon.SlotNumber 6
	Weapon.SlotPriority 1
	Weapon.AmmoType1 "DeusEx_FlameThrowerMagazine"
	Weapon.AmmoType2 "Cell"
	Weapon.AmmoUse1 1
	Weapon.AmmoGive2 100
	Tag "Flamethrower"
	Inventory.PickupMessage "You found a Flamethrower."
	Obituary "%o was burned to a crisp with %k's Flamethrower."
	States
	{
		Spawn:
			DX99 A -1
			stop

		ReadyActual:
			DX00 A 0 A_GunFlash("ReadyActualFlame",GFF_NOEXTCHANGE)
			DX00 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"ReadyActualReloadable")
		ReadyActualNonReloadable:
			DX00 AAAAAAAA 3 A_WeaponReady
			goto ReadyActualJump

		ReadyActualReloadable:
			DX00 AAAAAAAA 3 A_WeaponReady(WRF_ALLOWRELOAD)
			goto ReadyActualJump

		ReadyActualFlame:
			DX02 ABCDEFGH 3 Bright
			stop

		ReadyActualJump:
			DX00 A 0 A_Jump(96,"ReadyActualIdle1","ReadyActualIdle2")
			goto ReadyActualAmmoCheck

		ReadyActualIdle1:
			DX00 A 0 A_GunFlash("ReadyActualIdle1Flame",GFF_NOEXTCHANGE)
			DX00 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"ReadyActualIdle1Reloadable")
		ReadyActualIdle1NonReloadable:
			DX00 A 6 A_WeaponReady
			DX01 IJKLMNOP 6 A_WeaponReady
			goto ReadyActual

		ReadyActualIdle1Reloadable:
			DX00 A 6 A_WeaponReady(WRF_ALLOWRELOAD)
			DX01 IJKLMNOP 6 A_WeaponReady(WRF_ALLOWRELOAD)
			goto ReadyActual

		ReadyActualIdle1Flame:
			DX02 A 6 Bright
			DX04 ABCDEFGH 6 Bright
			stop

		ReadyActualIdle2:
			DX00 A 0 A_GunFlash("ReadyActualIdle2Flame",GFF_NOEXTCHANGE)
			DX00 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"ReadyActualIdle2Reloadable")
		ReadyActualIdle2NonReloadable:
			DX00 A 6 A_WeaponReady
			DX01 QRSTUVWX 6 A_WeaponReady
			goto ReadyActual

		ReadyActualIdle2Reloadable:
			DX00 A 6 A_WeaponReady(WRF_ALLOWRELOAD)
			DX01 QRSTUVWX 6 A_WeaponReady(WRF_ALLOWRELOAD)
			goto ReadyActual

		ReadyActualIdle2Flame:
			DX02 A 6 Bright
			DX05 ABCDEFGH 6 Bright
			stop

		ReadyActualAmmoCheck:
			DX00 A 0 A_JumpIfInventory("SamsaraReloadMode",2,1)
			goto ReadyActual
			DX00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"ReadyActual")
			DX00 A 0 A_JumpIfInventory("DeusEx_FlameThrowerMagazine",1,"ReadyActual")
			goto ReloadCheck

		Deselect:
			DX00 A 0 A_GiveInventory("DeusEx_Reloading")
			DX00 A 0 A_JumpIfHealthLower(1,"DeathDeselect")
			DX00 A 4 A_GunFlash("DeselectFlame",GFF_NOEXTCHANGE)
			DX01 CDEFG 4
			DX01 H 0
			TNT1 A 4
			goto DeselectFinish

		DeselectFlame:
			DX02 A 4 Bright
			DX06 ABCDE 4 Bright
			DX06 F 0 Bright
			stop

		DeathDeselect:
			TNT1 A 4 A_GunFlash("DeathDeselectFlame",GFF_NOEXTCHANGE)
			TNT1 AAAAAA 4
			goto DeselectFinish

		DeathDeselectFlame:
			TNT1 A 0
			stop

		DeselectFinish:
			TNT1 A 0 A_TakeInventory("DeusEx_FlameThrowerEquipped")
			goto DeselectInstant

		Ready: // our "Select" state
			DX00 B 0 A_PlaySound("DeusEx/Flamethrower/Select")
			DX00 B 0 A_GiveInventory("DeusEX_FlameThrowerEquipped")
			DX00 B 3 A_GunFlash("ReadyFlame",GFF_NOEXTCHANGE)
			DX00 CDEFGHI 3
			DX00 A 0 A_TakeInventory("DeusEx_Reloading")
			goto ReadyActual

		ReadyFlame:
			DX03 ABCDEFGH 3 Bright
			stop

		Fire:
			DX00 J 0 A_JumpIfInventory("SamsaraReloadMode",2,"FireReloadable")
		FireNonReloadable:
			DX00 J 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireWaterCheck")
			DX00 J 0 A_JumpIfInventory("Cell",1,"FireWaterCheck")
			goto FireDry

		FireReloadable:
			DX00 J 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireWaterCheck")
			DX00 J 0 A_JumpIfInventory("DeusEx_FlameThrowerMagazine",1,"FireWaterCheck")
		FireReloadableDryCheck:
			DX00 J 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReloadStart")
			DX00 J 0 A_JumpIfInventory("Cell",1,"ReloadStart")
			goto FireDry

		FireWaterCheck:
			DX00 J 0 A_JumpIfInventory("SamsaraCurrentWaterLevel",0,"FireDry")
			goto FireStart

		FireDry:
			DX00 A 0 A_PlaySound("DeusEx/DryFire",CHAN_6)
			DX00 A 0 A_ClearReFire
			DX00 A 0 A_GunFlash("FireDryFlame",GFF_NOEXTCHANGE)
			DX00 A 4 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto ReadyActualAmmoCheck

		FireDryFlame:
			DX02 A 4 Bright
			stop

		FireStart:
			DX00 J 0 A_GiveInventory("DeusEx_Firing")
			DX00 J 0 A_AlertMonsters
			DX00 J 0 A_GunFlash("FireFlame",GFF_NOEXTCHANGE)
			DX00 J 0 A_TakeInventory("SamsaraDeusExFlamethrowerAttackSilentSound")
			DX00 J 0 A_GiveInventory("SamsaraDeusExFlamethrowerAttackHandler")
			DX00 J 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
		FireStart2:
			DX00 K 0 A_AlertMonsters
			DX00 K 0 A_GiveInventory("SamsaraDeusExFlamethrowerAttackSilentSound")
			DX00 K 0 A_GiveInventory("SamsaraDeusExFlamethrowerAttackHandler")
			DX00 K 4 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
		FireStart3:
			DX00 L 0 A_AlertMonsters
			DX00 L 0 A_GiveInventory("SamsaraDeusExFlamethrowerAttackSilentSound")
			DX00 L 0 A_GiveInventory("SamsaraDeusExFlamethrowerAttackHandler")
			DX00 L 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
		FireStart4:
			DX00 M 0 A_AlertMonsters
			DX00 M 0 A_GiveInventory("SamsaraDeusExFlamethrowerAttackSilentSound")
			DX00 M 0 A_GiveInventory("SamsaraDeusExFlamethrowerAttackHandler")
			DX00 M 4 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
		FireStart5:
			DX00 N 0 A_AlertMonsters
			DX00 N 0 A_GiveInventory("SamsaraDeusExFlamethrowerAttackSilentSound")
			DX00 N 0 A_GiveInventory("SamsaraDeusExFlamethrowerAttackHandler")
			DX00 N 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
		FireStart6:
			DX00 O 0 A_AlertMonsters
			DX00 O 0 A_GiveInventory("SamsaraDeusExFlamethrowerAttackSilentSound")
			DX00 O 0 A_GiveInventory("SamsaraDeusExFlamethrowerAttackHandler")
			DX00 O 4 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
		FireStart7:
			DX00 P 0 A_AlertMonsters
			DX00 P 0 A_GiveInventory("SamsaraDeusExFlamethrowerAttackSilentSound")
			DX00 P 0 A_GiveInventory("SamsaraDeusExFlamethrowerAttackHandler")
			DX00 P 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
		FireFinish:
			DX00 Q 0 A_ReFire
			DX00 Q 4 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto ReadyActual

		FireFlame:
			DX07 A 3 Bright
			DX07 B 4 Bright
			DX07 C 3 Bright
			DX07 D 4 Bright
			DX07 E 3 Bright
			DX07 F 4 Bright
			DX07 G 3 Bright
			DX07 H 4 Bright
			stop

		Reload:
		ReloadCheck:
			DX00 A 0 A_JumpIfInventory("DeusEx_FlameThrowerMagazine",0,"ReadyActual")
			DX00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
			DX00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReloadStart")
			DX00 A 0 A_JumpIfInventory("Cell",1,"ReloadStart")
			goto ReadyActual

		ReloadStart:
			DX00 A 0 A_GiveInventory("DeusEx_Reloading")
			DX00 A 0 A_GunFlash("ReloadFlame",GFF_NOEXTCHANGE)
			DX00 A 0 A_PlaySound("DeusEx/Flamethrower/Reload1")
			DX00 ARST 4 A_WeaponReady(WRF_NOFIRE)
			DX00 U 0 A_JumpIfInventory("SPModeOn",1,2)
			DX00 U 0 A_JumpIfInventory("CoopModeOn",1,1)
			goto ReloadContinueAdvanced
			DX00 U 0 A_JumpIfInventory("DeusEx_HeavyWeaponSkill",0,"ReloadContinueMaster")
			DX00 U 0 A_JumpIfInventory("DeusEx_HeavyWeaponSkill",3,"ReloadContinueAdvanced")
			DX00 U 0 A_JumpIfInventory("DeusEx_HeavyWeaponSkill",2,"ReloadContinueProficient")
			DX00 U 0 A_JumpIfInventory("DeusEx_HeavyWeaponSkill",1,"ReloadContinueTrained")
		ReloadContinueUntrained:
			DX00 "UVWXYZ[" 4 A_WeaponReady(WRF_NOFIRE)
		ReloadContinueTrained:
			DX00 "UVWXYZ[" 4 A_WeaponReady(WRF_NOFIRE)
		ReloadContinueProficient:
			DX00 "UVWXYZ[" 4 A_WeaponReady(WRF_NOFIRE)
		ReloadContinueAdvanced:
			DX00 "UVWXYZ[" 4 A_WeaponReady(WRF_NOFIRE)
		ReloadContinueMaster:
			DX00 "\" 0 A_TakeInventory("DeusEx_Reloading")
			DX00 "\" 0 A_PlaySound("DeusEx/Flamethrower/Reload2")
			DX00 "\]" 4 A_WeaponReady(WRF_NOFIRE)
			DX01 AB 4 A_WeaponReady(WRF_NOFIRE)
			DX00 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"ReloadAmmo")
			goto ReloadDone

		ReloadDone:
			DX00 A 0
			goto ReadyActual

		ReloadAmmo:
			DX00 A 0 A_JumpIfInventory("DeusEx_FlameThrowerMagazine",0,"ReloadDone")
			DX00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
			DX00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
			DX00 A 0 A_JumpIfInventory("Cell",1,1)
			goto ReloadDone
			DX00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
			DX00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
			DX00 A 0 A_TakeInventory("Cell",1)
			DX00 A 0 A_GiveInventory("DeusEx_FlameThrowerMagazine")
			loop

		ReloadFlame:
			DX02 A 4 Bright
			DX08 ABC 4 Bright
			DX08 D 0 A_JumpIfInventory("SPModeOn",1,2)
			DX08 D 0 A_JumpIfInventory("CoopModeOn",1,1)
			goto ReloadContinueAdvancedFlame
			DX08 D 0 A_JumpIfInventory("DeusEx_HeavyWeaponSkill",0,"ReloadContinueMasterFlame")
			DX08 D 0 A_JumpIfInventory("DeusEx_HeavyWeaponSkill",3,"ReloadContinueAdvancedFlame")
			DX08 D 0 A_JumpIfInventory("DeusEx_HeavyWeaponSkill",2,"ReloadContinueProficientFlame")
			DX08 D 0 A_JumpIfInventory("DeusEx_HeavyWeaponSkill",1,"ReloadContinueTrainedFlame")
		ReloadContinueUntrainedFlame:
			DX08 DEFGHIJ 4 Bright
		ReloadContinueTrainedFlame:
			DX08 DEFGHIJ 4 Bright
		ReloadContinueProficientFlame:
			DX08 DEFGHIJ 4 Bright
		ReloadContinueAdvancedFlame:
			DX08 DEFGHIJ 4 Bright
		ReloadContinueMasterFlame:
			DX08 KLMN 4 Bright
			stop
	}
}

actor SamsaraDeusExFlamethrowerAttackHandler : Trigger
{
	States
	{
	// Ammo checking/taking stuff
	Pickup:
		TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"PickupReloadable")
	PickupNonReloadable:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
		TNT1 A 0 A_JumpIfInventory("Cell",1,1)
		stop
		TNT1 A 0 A_TakeInventory("Cell",1)
		goto PickupAttack
	PickupReloadable:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"PickupAttack")
		TNT1 A 0 A_JumpIfInventory("DeusEx_FlameThrowerMagazine",1,1)
		stop
		TNT1 A 0 A_TakeInventory("DeusEx_FlameThrowerMagazine",1)
		goto PickupAttack

	// Attack stuff
	PickupAttack:
		TNT1 A 0 A_JumpIfInventory("SamsaraDeusExFlamethrowerAttackSilentSound",1,2)
		TNT1 A 0 A_PlaySound("DeusEx/Flamethrower/Fire",CHAN_WEAPON)
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
	PickupAttackDM:
		TNT1 A 0 A_FireCustomMissile("DeusEx_FireballDM",frandom(-5.0,-3.5),false,16,16,0,frandom(-3.0,3.0))
		TNT1 A 0 A_FireCustomMissile("DeusEx_FireballDM",frandom(-3.0,3.0),false,16,16,0,frandom(-3.0,3.0))
		TNT1 A 0 A_FireCustomMissile("DeusEx_FireballDM",frandom(3.5,5.0),false,16,16,0,frandom(-3.0,3.0))
		stop
	PickupAttackCoop:
		TNT1 A 0 A_FireCustomMissile("DeusEx_FireballCoop",frandom(-5.0,-3.5),false,16,16,0,frandom(-3.0,3.0))
		TNT1 A 0 A_FireCustomMissile("DeusEx_FireballCoop",frandom(-3.0,3.0),false,16,16,0,frandom(-3.0,3.0))
		TNT1 A 0 A_FireCustomMissile("DeusEx_FireballCoop",frandom(3.5,5.0),false,16,16,0,frandom(-3.0,3.0))
		stop
	}
}

Actor DeusEx_FlameThrowerMagazine : Ammo
{
	Inventory.MaxAmount 100
	Tag "Flamethrower Magazine"
	+IGNORESKILL
}

Actor DeusEx_FlamethrowerFireballActive : Counter { Inventory.MaxAmount 2 }
Actor DeusEx_FlamethrowerIsOnFire : Boolean {}
Actor DeusEx_FlamethrowerOnFireCounter : Counter { Inventory.MaxAmount 1050 }

Actor DeusEx_FireBall
{
	PROJECTILE
	Radius 6
	Height 5
	Speed 12
	Damage (CallACS("DeusEx_WeaponSkillDamage", 5, 5))
	PoisonDamageType "DeusExFlamed"
	PoisonDamage 5, 30, 35 //can't scale up poison damage =(
	RenderStyle Add
	Alpha 0
	//+RIPPER
	+BLOODLESSIMPACT
	+FORCERADIUSDMG
	+HITTRACER
	+BRIGHT
	DamageType "DeusExFlamed"
	Scale 0.25
	States
	{
		Spawn:
			TNT1 AAAA 1 NoDelay A_JumpIf(waterlevel >= 2, "FuckWater")
			DX03 A 0 A_JumpIf(waterlevel >= 2, "FuckWater")
			DX03 A 0 A_SetScale(scaleX+0.125)
			DX03 A 0 A_FadeIn(0.25)
			DX03 A 1 A_GiveInventory("DeusEx_FlamethrowerFireballActive")
			DX03 A 1 A_JumpIf(waterlevel >= 2, "FuckWater")
			DX03 B 0 A_JumpIf(waterlevel >= 2, "FuckWater")
			DX03 B 0 A_SetScale(scaleX+0.125)
			DX03 B 0 A_FadeIn(0.25)
			DX03 B 1 A_GiveInventory("DeusEx_FlamethrowerFireballActive")
			DX03 B 1 A_JumpIf(waterlevel >= 2, "FuckWater")
			DX03 CCDDEEFFGGHH 1 A_JumpIf(waterlevel >= 2, "FuckWater")
			goto DeathStart

		XDeath:
			DX03 A 0 ACS_NamedExecuteWithResult("DeusExIgnite")
			DX03 A 0 A_Explode(CallACS("DeusEx_WeaponSkillDamage", 5, 5),12,0,0,12)
		Crash:
		Death:
			DX03 A 0 A_JumpIfInventory("DeusEx_FlamethrowerFireballActive",0,"DeathStart")
			DX03 A 0 A_SetScale(0.5)
			DX03 A 0 A_JumpIfInventory("DeusEx_FlamethrowerFireballActive",1,"Death2")
			goto Death1

		Death1:
			DX03 A 0 A_FadeIn(0.5)
			goto DeathStart

		Death2:
			DX03 A 0 A_FadeIn(0.25)
			goto DeathStart

		DeathStart:
			DX03 A 0 A_JumpIf(waterlevel >= 2, "FuckWater")
			DX03 A 1 A_FadeOut(0.03125)
			DX03 A 0 A_JumpIf(waterlevel >= 2, "FuckWater")
			DX03 A 1 A_FadeOut(0.03125)
			DX03 B 0 A_JumpIf(waterlevel >= 2, "FuckWater")
			DX03 B 1 A_FadeOut(0.03125)
			DX03 B 0 A_JumpIf(waterlevel >= 2, "FuckWater")
			DX03 B 1 A_FadeOut(0.03125)
			DX03 C 0 A_JumpIf(waterlevel >= 2, "FuckWater")
			DX03 C 1 A_FadeOut(0.03125)
			DX03 C 0 A_JumpIf(waterlevel >= 2, "FuckWater")
			DX03 C 1 A_FadeOut(0.03125)
			DX03 D 0 A_JumpIf(waterlevel >= 2, "FuckWater")
			DX03 D 1 A_FadeOut(0.03125)
			DX03 D 0 A_JumpIf(waterlevel >= 2, "FuckWater")
			DX03 D 1 A_FadeOut(0.03125)
			DX03 E 0 A_JumpIf(waterlevel >= 2, "FuckWater")
			DX03 E 1 A_FadeOut(0.03125)
			DX03 E 0 A_JumpIf(waterlevel >= 2, "FuckWater")
			DX03 E 1 A_FadeOut(0.03125)
			DX03 F 0 A_JumpIf(waterlevel >= 2, "FuckWater")
			DX03 F 1 A_FadeOut(0.03125)
			DX03 F 0 A_JumpIf(waterlevel >= 2, "FuckWater")
			DX03 F 1 A_FadeOut(0.03125)
			DX03 G 0 A_JumpIf(waterlevel >= 2, "FuckWater")
			DX03 G 1 A_FadeOut(0.03125)
			DX03 G 0 A_JumpIf(waterlevel >= 2, "FuckWater")
			DX03 G 1 A_FadeOut(0.03125)
			DX03 H 0 A_JumpIf(waterlevel >= 2, "FuckWater")
			DX03 H 1 A_FadeOut(0.03125)
			DX03 H 0 A_JumpIf(waterlevel >= 2, "FuckWater")
			DX03 H 1 A_FadeOut(0.03125)
			stop

		FuckWater:
			TNT1 A 0
			stop
	}
}

Actor DeusEx_FireBallCoop : DeusEx_FireBall { Species "Player" +THRUSPECIES }
Actor DeusEx_FireBallDM : DeusEx_FireBall { PoisonDamage 0, 0, 0 }

Actor DeusEx_BurnEffectSpawner
{
	Height 4
	Radius 2
	+NOGRAVITY
	+NOINTERACTION
	var int user_radius;
	var int user_height;
	var int user_angle;
	States
	{
		Spawn:
			TNT1 A 0 NoDelay A_PlaySound("DeusEx/Burn",CHAN_5,1.0,true)
			TNT1 A 0 A_SetUserVar("user_radius",CallACS("DeusEx_Decorate",8,1))
			TNT1 A 0 A_SetUserVar("user_height",CallACS("DeusEx_Decorate",8,2))
			TNT1 A 0 A_SetUserVar("user_angle",CallACS("DeusEx_Decorate",8,3)+180)
		SpawnLoop:
			TNT1 A 0 A_JumpIfInventory("DeusEx_FlamethrowerIsOnFire",1,1,AAPTR_MASTER)
			Goto Remove
			TNT1 A 2 A_SpawnItemEx("DeusEx_SmokeTrail",0,0,frandom(user_height*0.33,user_height*0.66),1.0,frandom(-0.25,0.25),2.0,user_angle)
			TNT1 A 2 A_SpawnItemEx("DeusEx_SmokeTrail",0,0,frandom(user_height*0.33,user_height*0.66),1.0,frandom(-0.25,0.25),2.0,user_angle)
			TNT1 A 0 A_SpawnItemEx("DeusEx_BurnEffect",frandom(-user_radius*0.8,user_radius*0.8),0,frandom(user_height*0.33,user_height*0.66),0,0,0,frandom(0,360),SXF_TRANSFERPOINTERS)
			TNT1 A 0 A_JumpIfInventory("DeusEx_FlamethrowerIsOnFire",1,1,AAPTR_MASTER)
			Goto Remove
			TNT1 A 2 A_SpawnItemEx("DeusEx_SmokeTrail",0,0,frandom(user_height*0.33,user_height*0.66),1.0,frandom(-0.25,0.25),2.0,user_angle)
			TNT1 A 2 A_SpawnItemEx("DeusEx_SmokeTrail",0,0,frandom(user_height*0.33,user_height*0.66),1.0,frandom(-0.25,0.25),2.0,user_angle)
			Loop
		Remove:
			TNT1 A 2 A_StopSound(CHAN_5)
			Stop
	}
}

Actor DeusEx_BurnEffect
{
	Height 4
	Radius 2
	Renderstyle Add
	+NOINTERACTION
	+BRIGHT
	States
	{
		Spawn:
			DX08 A 0
			DX08 ACEGI 2 A_SpawnItemEx("DeusEx_FlameSmokeCreator",0,0,0,frandom(-1.0,-0.25))
			DX08 KKLLMMNNOOPPQQRR 2 A_Fadeout(0.0625)
			Stop
	}
}

Actor DeusEx_FlameSmokeCreator
{
	Height 4
	Radius 2
	Renderstyle Add
	+NOINTERACTION
	+BRIGHT
	States
	{
		Spawn:
			DX09 A 0
			DX09 ABCDEFGH 2 A_Fadeout(0.125)
			Stop
	}
}
