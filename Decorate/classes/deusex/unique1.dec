Actor DeusEx_LAW : DeusExWeapon
{
	Weapon.SelectionOrder 2400
	Weapon.SlotNumber 8
	Weapon.AmmoType "DeusEx_LAWAmmo"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 1
	+WEAPON.BFG
	+WEAPON.EXPLOSIVE
	Tag "Light Anti-Tank Weapon"
	Inventory.PickupMessage "You found a Light Anti-Tank Weapon (LAW)."
	Obituary "%o fought %k's LAW and the LAW won."
	States
	{
		Spawn:
			DX99 A -1
			stop

		ReadyActual:
			DX00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReadyActualAmmo")
			DX00 A 0 A_JumpIfInventory("DeusEx_LAWAmmo",1,"ReadyActualAmmo")
            goto ReadyActualNoAmmo

		ReadyActualMapChangeHack:
			DX00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReadyActualAmmo")
			DX00 A 0 A_JumpIfInventory("DeusEx_LAWAmmo",1,"ReadyActualAmmo")
            goto ReadyActualNoAmmoMapChangeHack

		ReadyActualNoAmmo:
			TNT1 A 1 A_WeaponReady
            goto ReadyActualNoAmmoCheck

		ReadyActualNoAmmoMapChangeHack:
			TNT1 A 1 A_WeaponReady
            goto ReadyActualNoAmmoCheckMapChangeHack

		ReadyActualAmmo:
			DX00 A 1 A_WeaponReady
            goto ReadyActualJump

        ReadyActualJump:
			DX00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReadyActualJumpStart")
			DX00 A 0 A_JumpIfInventory("DeusEx_LAWAmmo",1,"ReadyActualJumpStart")
            goto NoAmmoSwitch

        ReadyActualJumpStart:
            DX00 A 0 A_Jump(3,"ReadyActualIdle1","ReadyActualIdle2","ReadyActualIdle3")
			goto ReadyActual

		ReadyActualIdle1:
            DX00 A 6 A_WeaponReady // interpolation hack
			DX01 "YZ[\]" 6 A_WeaponReady
			DX02 ABCDE 6 A_WeaponReady
			goto ReadyActual

		ReadyActualIdle2:
            DX00 A 6 A_WeaponReady // interpolation hack
			DX02 DGHIJKLMNO 6 A_WeaponReady
			goto ReadyActual

		ReadyActualIdle3:
			DX02 PQRSTUVWXY 6 A_WeaponReady
			goto ReadyActual

        ReadyActualNoAmmoCheck:
            TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReloadStart")
            TNT1 A 0 A_JumpIfInventory("DeusEx_LAWAmmo",1,"ReloadStart")
            goto NoAmmoSwitch

        ReadyActualNoAmmoCheckMapChangeHack:
            TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReadyAmmo")
            TNT1 A 0 A_JumpIfInventory("DeusEx_LAWAmmo",1,"ReadyAmmo")
            goto NoAmmoSwitch

		Deselect:
			DX01 O 0 A_GiveInventory("DeusEx_Reloading")
			DX01 O 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"DeselectAmmo")
			DX01 O 0 A_JumpIfInventory("DeusEx_LAWAmmo",1,"DeselectAmmo")
			goto DeselectNoAmmo

		DeselectNoAmmo:
			TNT1 A 0 A_JumpIfHealthLower(1,"DeathDeselect")
			goto DeselectFinish

        DeselectAmmo:
            DX01 O 4 A_JumpIfHealthLower(1,"DeathDeselect")
			DX01 PQRSTUVWX 4
			goto DeselectFinish

		DeathDeselect:
			TNT1 AAAAAAAAAA 4
			goto DeselectFinish

        DeselectFinish:
			TNT1 A 0 A_TakeInventory("DeusEx_LAWEquipped")
			goto DeselectInstant

		Ready: // our "Select" state
			DX00 B 0 A_GiveInventory("DeusEx_LAWEquipped")
			DX00 B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReadyAmmo")
			DX00 B 0 A_JumpIfInventory("DeusEx_LAWAmmo",1,"ReadyAmmo")
			goto ReadyNoAmmo

        ReadyNoAmmo:
			TNT1 A 0 A_TakeInventory("DeusEx_Reloading")
			goto ReadyActualMapChangeHack

        ReadyAmmo:
            DX00 B 4 A_PlaySound("DeusEx/LAW/Select")
			DX00 CDEFGHIJK 4
			DX00 A 0 A_TakeInventory("DeusEx_Reloading")
			goto ReadyActual

		Fire:
            DX00 L 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireWaterCheck")
            DX00 L 0 A_JumpIfInventory("DeusEx_LAWAmmo",1,"FireWaterCheck")
            goto FireDry

        FireWaterCheck:
            DX00 L 0 A_JumpIfInventory("SamsaraCurrentWaterLevel",0,"FireUnderwater")
            goto FireStart

		FireDry:
			TNT1 A 0 A_ClearReFire
            goto ReadyActualNoAmmoCheck

		FireUnderwater:
			DX00 A 0 A_PlaySound("DeusEx/DryFire",CHAN_6)
			DX00 A 0 A_ClearReFire
            DX00 A 4 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
            goto ReadyActual

		FireStart:
			DX00 L 0 A_GiveInventory("DeusEx_Firing")
            // [tv50] no A_AlertMonsters call here, interestingly enough
			DX00 L 0 A_GiveInventory("SamsaraDeusExLAWAttackHandler")
			DX00 "LMNOPQRSTUVWXYZ[\]" 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX01 ABCDEFGHIJKLM 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX01 N 0 A_SpawnItemEx("DeusEx_LAWDrop",8,-16,24,4,0,3,-90,SXF_NOCHECKPOSITION|SXF_MULTIPLYSPEED|SXF_TRANSFERPITCH)
            TNT1 A 7 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
            goto ReadyActualNoAmmoCheck

		ReloadStart:
			TNT1 A 0 A_GiveInventory("DeusEx_Reloading")
			TNT1 A 0 A_JumpIfInventory("SPModeOn",1,2)
			TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,1)
            goto ReloadContinueAdvanced
			TNT1 A 0 A_JumpIfInventory("DeusEx_HeavyWeaponSkill",0,"ReloadContinueMaster")
			TNT1 A 0 A_JumpIfInventory("DeusEx_HeavyWeaponSkill",3,"ReloadContinueAdvanced")
			TNT1 A 0 A_JumpIfInventory("DeusEx_HeavyWeaponSkill",2,"ReloadContinueProficient")
			TNT1 A 0 A_JumpIfInventory("DeusEx_HeavyWeaponSkill",1,"ReloadContinueTrained")
        ReloadContinueUntrained:
			TNT1 A 17 A_WeaponReady(WRF_NOFIRE)
        ReloadContinueTrained:
			TNT1 A 17 A_WeaponReady(WRF_NOFIRE)
        ReloadContinueProficient:
			TNT1 A 17 A_WeaponReady(WRF_NOFIRE)
        ReloadContinueAdvanced:
			TNT1 A 17 A_WeaponReady(WRF_NOFIRE)
		ReloadContinueMaster:
			DX00 B 0 A_TakeInventory("DeusEx_Reloading")
			DX00 B 0 A_PlaySound("DeusEx/LAW/Reload")
			DX00 BCDEFGHIJK 3 A_WeaponReady(WRF_NOFIRE)
        ReloadDone:
            DX00 A 0
			goto ReadyActual

        // "No ammo" switch
        NoAmmoSwitch:
            TNT1 A 0 A_ClearReFire
            TNT1 A 0 A_SelectWeapon("DeusEx_10mmPistol")
            goto Deselect
	}
}

actor SamsaraDeusExLAWAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("DeusEx_LAWAmmo",1,1)
        stop
        TNT1 A 0 A_TakeInventory("DeusEx_LAWAmmo",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("DeusEx/LAW/Fire",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("DeusEx_LAWMissile",random(-3,3),false,14,14)
        goto PickupFinish

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("DeusEx_LAWMissileCoop",random(-3,3),false,14,14)
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 ACS_NamedExecuteWithResult("DeusExRecoil", 24, 6, 3)
        stop
    }
}

Actor DeusEx_LAWAmmo : Ammo
{
    Inventory.MaxAmount 3
    Tag "Light Anti-Tank Weapons"
    +IGNORESKILL
}

Actor DeusEx_LAWMissile
{
	Projectile
	Radius 8
	Height 6
	Damage (0)
	Speed 15
	+FORCERADIUSDMG
	DamageType "DeusExExploded"
	//ActiveSound "DeusEx/LAW/Approach"
	Decal "DeusExExplosionDecal"
	var int user_rocketpitch;
	States
	{
		Spawn:
			DX00 A 0 NoDelay A_PlaySound("DeusEx/LAW/Approach",CHAN_VOICE,0.5,true)
			DX00 A 0 A_SetUserVar("user_rocketpitch",CallACS("SamsaraProjectileMovementPitch")/182.0)
			DX00 A 0 A_SpawnItemEx("DeusEx_RocketFlameTrail",(-5*cos(user_rocketpitch))+(2*sin(user_rocketpitch)),0,(-5*sin(user_rocketpitch))+(2*cos(user_rocketpitch)),0,0,0,0,SXF_CLIENTSIDE)
			DX00 A 0 A_SpawnItemEx("DeusEx_RocketFlameTrail",(-10*cos(user_rocketpitch))+(2*sin(user_rocketpitch)),0,-10*sin(user_rocketpitch)+(2*cos(user_rocketpitch)),0,0,0,0,SXF_CLIENTSIDE)
			DX00 A 0 A_SpawnItemEx("DeusEx_RocketFlameTrail",(-15*cos(user_rocketpitch))+(2*sin(user_rocketpitch)),0,-15*sin(user_rocketpitch)+(2*cos(user_rocketpitch)),0,0,0,0,SXF_CLIENTSIDE)
			DX00 A 0 A_SpawnItemEx("DeusEx_RocketSmoke",(-5*cos(user_rocketpitch))-(4*sin(user_rocketpitch)),0,(-5*sin(user_rocketpitch))-(4*cos(user_rocketpitch)),(0.25*cos(user_rocketpitch))+(0.25*sin(user_rocketpitch)),0,(0.25*sin(user_rocketpitch))+(0.25*cos(user_rocketpitch)),0,SXF_CLIENTSIDE)
			DX00 A 1 A_SpawnItemEx("DeusEx_RocketSmoke",(-15*cos(user_rocketpitch))-(4*sin(user_rocketpitch)),0,(-15*sin(user_rocketpitch))-(4*cos(user_rocketpitch)),(0.25*cos(user_rocketpitch))+(0.25*sin(user_rocketpitch)),0,(0.25*sin(user_rocketpitch))+(0.25*cos(user_rocketpitch)),0,SXF_CLIENTSIDE)
			loop

		Death:
			TNT1 A 0 A_CheckCeiling(1)
			Goto Death+2
			TNT1 A 0 A_SpawnItemEx("DeusEx_ExplosionCraterCeiling")
			TNT1 A 0 A_CheckFloor(1)
			Goto Death+4
			TNT1 A 0 A_SpawnItemEx("DeusEx_ExplosionCraterFloor")
			TNT1 A 0 A_PlaySound("DeusEx/LAW/Explode",CHAN_VOICE,1.0,false,ATTN_NONE)
			TNT1 A 0 A_SpawnItemEX("DeusEx_ExplosionLarge", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
			TNT1 A 0 A_SpawnItemEX("DeusEx_ShockWaveHugeSpawner", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
			TNT1 A 0 A_AlertMonsters(3000)
			TNT1 A 2 A_Explode(CallACS("DeusEx_WeaponSkillDamage", 5, 300), 76, 1, 1, 76)
			TNT1 A 2 A_Explode(CallACS("DeusEx_WeaponSkillDamage", 5, 300), 154, 1, 1, 154)
			TNT1 A 2 A_Explode(CallACS("DeusEx_WeaponSkillDamage", 5, 300), 232, !XF_HURTSOURCE, 1, 232)
			TNT1 A 2 A_Explode(CallACS("DeusEx_WeaponSkillDamage", 5, 300), 308, !XF_HURTSOURCE, 1, 308)
			TNT1 A 2 A_Explode(CallACS("DeusEx_WeaponSkillDamage", 5, 300), 384, !XF_HURTSOURCE, 1, 384)
			Stop
	}
}

Actor DeusEx_LAWMissileCoop : DeusEx_LAWMissile { Species "Player" +THRUSPECIES }
