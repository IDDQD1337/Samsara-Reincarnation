actor SamsaraDisruptorAMBlasterSide : Boolean {}
actor SamsaraDisruptorAMBlasterCharging : Counter { Inventory.MaxAmount 6 }
actor SamsaraDisruptorAMBlasterCanFireBoth : Boolean {}

actor " AM Blaster " : SamsaraDisruptorWeapon
{
    Inventory.PickupMessage "Picked up AM Blaster."
    Weapon.SlotNumber 3
    Weapon.SelectionOrder 400
    Weapon.AmmoType "Shell"
    Weapon.AmmoGive 8
    Weapon.AmmoUse 1
    Obituary "%o was destroyed by %k's AM Blaster."
    Tag "AM Blaster"
    +ALT_USES_BOTH
    +NOALERT
    States
    {
      Spawn:
        SQ72 V -1
        stop

      Ready:
        SQ69 OOOOPPPPQQQQRRRRSSSSTTTT 1 A_WeaponReady
        loop

      Deselect:
        TNT1 A 0 A_TakeInventory("SamsaraDisruptorAMBlasterSide")
        TNT1 A 0 A_Lower
        SQ69 T 1 A_Lower
        loop

      Select:
        TNT1 A 0 A_TakeInventory("SamsaraDisruptorAMBlasterSide")
        TNT1 A 0 A_Raise
        SQ69 T 1 A_Raise
        loop

      Fire:
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_JumpIfInventory("SamsaraDisruptorAMBlasterSide",1,"Fire2")
      Fire1:
        TNT1 A 0 A_GiveInventory("SamsaraDisruptorAMBlasterPrimaryAttackHandler")
        SQ69 W 7 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        SQ69 X 7 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto FireFinish

      Fire2:
        TNT1 A 0 A_GiveInventory("SamsaraDisruptorAMBlasterPrimaryAttackHandler")
        SQ69 U 7 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        SQ69 V 7 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto FireFinish

      FireFinish:
        TNT1 A 0 A_CheckReload
        SQ69 T 11 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto Ready

      AltFire:
        TNT1 A 0 A_TakeInventory("SamsaraDisruptorAMBlasterCharging")
        TNT1 A 0 A_TakeInventory("SamsaraDisruptorAMBlasterCanFireBoth")
        TNT1 A 0 A_PlaySound("Disruptor/Charge",CHAN_WEAPON)
        SQ69 T 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
      AltFireCharge:
        TNT1 A 0 A_JumpIfInventory("SamsaraDisruptorAMBlasterCharging",0,"AltFireChargeIncrement")
        SQ69 T 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_GiveInventory("SamsaraDisruptorAMBlasterCharging")
        TNT1 A 0 A_ReFire("AltFireCharge")
        TNT1 A 0 A_JumpIfInventory("SamsaraDisruptorAMBlasterCanFireBoth",1,1)
        goto Fire
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"AltFireStart")
        TNT1 A 0 A_JumpIfInventory("Shell",2,"AltFireStart")
        goto Fire

      AltFireChargeIncrement:
        TNT1 A 0 A_TakeInventory("SamsaraDisruptorAMBlasterCharging")
        TNT1 A 0 A_GiveInventory("SamsaraDisruptorAMBlasterCanFireBoth")
        TNT1 A 0 A_PlaySound("Disruptor/Beep",CHAN_WEAPON)
        SQ69 T 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto AltFireCharge

      AltFireStart:
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_GiveInventory("SamsaraDisruptorAMBlasterSecondaryAttackHandler")
        SQ69 Y 7 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        SQ69 Z 7 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto FireFinish
    }
}

actor SamsaraDisruptorAMBlasterPrimaryAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Shell",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Shell",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("Disruptor/SSG",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireBullets(5,4,6,random(12,15),"SamsaraDisruptorAMBlasterPuff",FBF_NORANDOM|FBF_NOFLASH)
        goto PickupRecoil

      PickupAttackCoop:
        TNT1 A 0 A_FireBullets(5,4,6,random(12,15),"SamsaraDisruptorAMBlasterPuffCoop",FBF_NORANDOM|FBF_NOFLASH)
        goto PickupRecoil

      // Recoil stuff
      PickupRecoil:
        TNT1 A 0 A_Recoil(2)
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 A_JumpIfInventory("SamsaraDisruptorAMBlasterSide",1,2)
        TNT1 A 0 A_GiveInventory("SamsaraDisruptorAMBlasterSide")
        stop
        TNT1 A 0 A_TakeInventory("SamsaraDisruptorAMBlasterSide")
        stop
    }
}

actor SamsaraDisruptorAMBlasterSecondaryAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Shell",2,1)
        stop
        TNT1 A 0 A_TakeInventory("Shell",2)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("Disruptor/SuperShoot",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0  A_FireBullets(5.5,4.5,12,random(12,15),"SamsaraDisruptorAMBlasterPuff",FBF_NORANDOM|FBF_NOFLASH)
        goto PickupRecoil

      PickupAttackCoop:
        TNT1 A 0  A_FireBullets(5.5,4.5,12,random(12,15),"SamsaraDisruptorAMBlasterPuffCoop",FBF_NORANDOM|FBF_NOFLASH)
        goto PickupRecoil

      // Recoil stuff
      PickupRecoil:
        TNT1 A 0 A_Recoil(5)
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 A_TakeInventory("SamsaraDisruptorAMBlasterSide")
        stop
    }
}

actor SamsaraDisruptorAMBlasterPuff : BulletPuff
{
    VSpeed 0
    DamageType "Disruptoruniversaldamageshit2"
    Mass 100
    RenderStyle "Add"
    Alpha 1
    Scale 0.54
    +CLIENTSIDEONLY
    +FORCEXYBILLBOARD
    +NOEXTREMEDEATH
    +PUFFONACTORS
    States
    {
      Spawn:
      Melee:
      Crash:
        SQ97 TUVWXYZ 3 Bright
        SQ98 P 3 Bright
        stop
    }
}

actor SamsaraDisruptorAMBlasterPuffCoop : SamsaraDisruptorAMBlasterPuff { +MTHRUSPECIES }

actor DisruptorBullet3 : FastProjectile // currently used for remixer
{
    Radius 1
    Height 1
    Speed 600
    Damage (random(12,15))
    DamageType "Disruptoruniversaldamageshit2"
    RenderStyle "Add"
    Scale 0.54
    +BLOODSPLATTER
    +FORCEXYBILLBOARD
    +NOEXTREMEDEATH
    +NOTIMEFREEZE
    States
    {
      Spawn:
        TNT1 A -1
        stop

      Death:
      XDeath:
      Crash:
        SQ97 TUVWXYZ 3 Bright
        SQ98 P 3 Bright
        stop
    }
}
