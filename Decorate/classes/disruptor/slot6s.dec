actor SamsaraDisruptorPlasmaLanceAngle : Counter { Inventory.MaxAmount 20 }
actor SamsaraDisruptorPlasmaLanceTurn : Boolean {}
actor SamsaraDisruptorPlasmaLanceTimer : Counter { Inventory.MaxAmount 27 }
actor SamsaraDisruptorPlasmaLanceFrame : Counter { Inventory.MaxAmount 3 }
actor SamsaraDisruptorPlasmaLanceFiringSoundCheck : Boolean {}

actor " Disruptor Plasmalance " : SamsaraDisruptorWeapon
{
    Inventory.PickupMessage "Picked up Plasma Lance."
    Weapon.SlotNumber 6
    Weapon.SlotPriority 1
    Weapon.SelectionOrder 600
    Weapon.AmmoType "Cell"
    Weapon.AmmoGive 40
    Weapon.AmmoUse 1
    Obituary "%o was disintegrated by %k's plasma lance."
    Tag "Plasma Lance"
    States
    {
      Spawn:
        SQ72 Y -1
        stop

      Ready:
        TNT1 A 0 A_TakeInventory("SamsaraDisruptorPlasmaLanceFiringSoundCheck")
        SQ84 AABBCCDDEEFF 1 A_WeaponReady
        loop

      Deselect:
        TNT1 A 0 A_GiveInventory("SamsaraDisruptorPlasmaLanceFiringSoundStopper")
        TNT1 A 0 A_Lower
        SQ84 A 1 A_Lower
        loop

      Select:
        TNT1 A 0 A_TakeInventory("SamsaraDisruptorPlasmaLanceFiringSoundCheck")
        TNT1 A 0 A_Raise
        SQ84 A 1 A_Raise
        loop

      Fire:
        TNT1 A 0 A_PlaySound("Disruptor/LanceFire",CHAN_WEAPON)
        TNT1 A 0 A_GiveInventory("SamsaraDisruptorPlasmaLanceFiringSoundCheck")
        TNT1 A 0 A_TakeInventory("SamsaraDisruptorPlasmaLanceAngle")
        TNT1 A 0 A_TakeInventory("SamsaraDisruptorPlasmaLanceTurn")
        TNT1 A 0 A_TakeInventory("SamsaraDisruptorPlasmaLanceTimer")
        TNT1 A 0 A_TakeInventory("SamsaraDisruptorPlasmaLanceFrame")
      FireHold:
        TNT1 A 0 A_JumpIfInventory("SamsaraDisruptorPlasmaLanceAngle",0,"FireTurnLeft")
        TNT1 A 0 A_JumpIfInventory("SamsaraDisruptorPlasmaLanceAngle",1,"FireAngle")
        goto FireTurnRight

      FireTurnLeft:
        TNT1 A 0 A_TakeInventory("SamsaraDisruptorPlasmaLanceTurn")
        goto FireAngle

      FireTurnRight:
        TNT1 A 0 A_GiveInventory("SamsaraDisruptorPlasmaLanceTurn")
        goto FireAngle

      FireAngle:
        TNT1 A 0 A_JumpIfInventory("SamsaraDisruptorPlasmaLanceTurn",1,2)
        TNT1 A 0 A_TakeInventory("SamsaraDisruptorPlasmaLanceAngle",1) // sub
        goto FireSetFrame
        TNT1 A 0 A_GiveInventory("SamsaraDisruptorPlasmaLanceAngle") // add
        goto FireSetFrame

      FireSetFrame:
        TNT1 A 0 A_GiveInventory("SamsaraDisruptorPlasmaLanceFrame")
        TNT1 A 0 A_JumpIfInventory("SamsaraDisruptorPlasmaLanceFrame",0,"FireFrame3")
        TNT1 A 0 A_JumpIfInventory("SamsaraDisruptorPlasmaLanceFrame",2,"FireFrame2")
      FireFrame1:
        TNT1 A 0 A_TakeInventory("SamsaraTakingAmmo")
        TNT1 A 0 A_GiveInventory("SamsaraDisruptorPlasmaLanceAttackHandler")
        SQ84 G 1 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto FireHold

      FireFrame2:
        TNT1 A 0 A_TakeInventory("SamsaraTakingAmmo")
        TNT1 A 0 A_GiveInventory("SamsaraDisruptorPlasmaLanceAttackHandler")
        SQ84 H 1 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto FireHold

      FireFrame3:
        TNT1 A 0 A_GiveInventory("SamsaraTakingAmmo")
        TNT1 A 0 A_GiveInventory("SamsaraDisruptorPlasmaLanceAttackHandler")
        SQ84 I 1 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto FireFinish

      FireFinish:
        TNT1 A 0 A_TakeInventory("SamsaraDisruptorPlasmaLanceFrame")
        TNT1 A 0 A_ReFire("FireHold")
        TNT1 A 0 A_GiveInventory("SamsaraDisruptorPlasmaLanceFiringSoundStopper")
        goto Ready
    }
}

actor SamsaraDisruptorPlasmaLanceAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupPlaySound")
        TNT1 A 0 A_JumpIfInventory("Cell",1,1)
        stop
        TNT1 A 0 A_JumpIfInventory("SamsaraTakingAmmo",1,1)
        goto PickupPlaySound
        TNT1 A 0 A_TakeInventory("Cell",1)
        goto PickupPlaySound

      // Sound stuff
      PickupPlaySound:
        TNT1 A 0 A_GiveInventory("SamsaraDisruptorPlasmaLanceTimer")
        TNT1 A 0 A_JumpIfInventory("SamsaraDisruptorPlasmaLanceTimer",0,1)
        goto PickupAttack
        TNT1 A 0 A_PlaySound("Disruptor/LanceSpin",CHAN_WEAPON,1.0,true)
        TNT1 A 0 A_GiveInventory("SamsaraDisruptorPlasmaLanceFiringSoundCheck")
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("DisruptorPlasmaLance_Projectile",0,false)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("DisruptorPlasmaLance_ProjectileCoop",0,false)
        stop
    }
}

actor SamsaraDisruptorPlasmaLanceFiringSoundStopper : Trigger
{
    States
    {
      // Sound stopping stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraDisruptorPlasmaLanceFiringSoundCheck",1,1)
        stop
        TNT1 A 0 A_PlaySound("silence",CHAN_WEAPON)
        TNT1 A 0 A_StopSound(CHAN_WEAPON)
        TNT1 A 0 A_TakeInventory("SamsaraDisruptorPlasmaLanceFiringSoundCheck")
        stop
    }
}

actor DisruptorPlasmaLance_Projectile
{
    var int user_angle;
    var int user_pitch;
    Projectile
    Radius 4
    Height 8
    Speed 12
    Damage (13)
    DamageType "Disruptoruniversaldamageshit2"
    RenderStyle "Add"
    Alpha 0.5
    //DeathSound "Disruptor/GreenL"
    +BLOODSPLATTER
    +FORCEXYBILLBOARD
    +NOTIMEFREEZE
    +SKYEXPLODE
    States
    {
      Spawn:
        TNT1 A 0
        TNT1 A 0 A_SetUserVar("user_angle",CallACS("DisruptorPlasmaLanceAngle"))
        TNT1 A 0 A_SetUserVar("user_pitch",CallACS("DisruptorPlasmaLancePitch")/182)
        TNT1 A 2 A_ChangeFlag("NOTIMEFREEZE",false)
        SQ84 J 3 Bright
        SQ84 L 16 Bright A_ChangeVelocity(12*cos(user_pitch),(user_angle-10)/2,velz,CVF_RELATIVE|CVF_REPLACE)
        SQ84 L 16 Bright A_ChangeVelocity(12*cos(user_pitch),-(user_angle-10)/2,velz,CVF_RELATIVE|CVF_REPLACE)
        TNT1 A 0 A_ChangeVelocity(12*cos(user_pitch),0,velz,CVF_RELATIVE|CVF_REPLACE)
        TNT1 AAA 0 A_SpawnItemEx("DisruptorPlasmaLance_ProjectileDeath",frandom(-4.0,4.0),frandom(-4.0,4.0),frandom(-4.0,4.0),0,0,0,0,SXF_NOCHECKPOSITION)
        stop

      Death:
        TNT1 AAA 0 A_SpawnItemEx("DisruptorPlasmaLance_ProjectileDeath",frandom(-4.0,4.0),frandom(-4.0,4.0),frandom(-4.0,4.0),0,0,0,0,SXF_NOCHECKPOSITION)
      DeathFinish:
        TNT1 A 1
        stop
    }
}

actor DisruptorPlasmaLance_ProjectileCoop : DisruptorPlasmaLance_Projectile
{
    Species "Player"
    +THRUSPECIES
}

actor DisruptorPlasmaLance_ProjectileDeath
{
    Radius 4
    Height 8
    RenderStyle "Add"
    Alpha 0.5
    +CLIENTSIDEONLY
    +FORCEXYBILLBOARD
    +NOINTERACTION
    States
    {
      Spawn:
        SQ84 M 0 Bright
        TNT1 A 0 A_SetScale(frandom(0.5,1.0))
        TNT1 A 0 A_Stop
        TNT1 A 0 A_ChangeVelocity(frandom(0.0,2.0),frandom(0.0,2.0),frandom(0.0,2.0),CVF_REPLACE)
        SQ84 MMNNOOPP 1 Bright A_Fadeout(0.05)
        stop
    }
}
