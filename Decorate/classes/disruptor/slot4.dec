actor SamsaraDisruptorPhaseRepeaterMode : Boolean {}
actor SamsaraDisruptorPhaseRepeaterSide : Counter { Inventory.MaxAmount 2 }

actor "Phase Repeater" : SamsaraDisruptorWeapon
{
    Inventory.PickupMessage "Picked up Phase Repeater."
    Weapon.SlotNumber 4
    Weapon.SelectionOrder 1700
    Weapon.AmmoType1 "Clip"
    Weapon.AmmoGive1 60
    Weapon.AmmoUse1 8 // originally took this total amount at once on fire, used here as a requirement to fire
    Weapon.AmmoType2 "DisruptorHiFreq"
    Weapon.AmmoUse2 4 // originally took this total amount at once on fire, used here as a requirement to fire
    Obituary "%o was phased by %k."
    Tag "Phase Repeater"
    +ALT_AMMO_OPTIONAL
    +AMMO_OPTIONAL
    +NOALERT
    States
    {
      Spawn:
        SQ72 U -1
        stop

      // Normal
      Ready:
        TNT1 A 0 A_JumpIfInventory("SamsaraDisruptorPhaseRepeaterMode",1,"ReadyHiFreq")
        SQ74 AAAABBBBCCCCDDDDEEEEFFFF 1 A_WeaponReady
        loop

      Deselect:
        TNT1 A 0 A_Lower
        TNT1 A 0 A_JumpIfInventory("SamsaraDisruptorPhaseRepeaterMode",1,"DeselectHiFreq")
        SQ74 A 1 A_Lower
        loop

      Select:
        TNT1 A 0 A_Raise
        TNT1 A 0 A_JumpIfInventory("SamsaraDisruptorPhaseRepeaterMode",1,"SelectHiFreq")
        SQ74 A 1 A_Raise
        loop

      Fire:
        TNT1 A 0 A_JumpIfInventory("SamsaraDisruptorPhaseRepeaterMode",1,"FireHiFreq")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Clip",8,1)
        goto NoAmmoSwitch
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_TakeInventory("SamsaraDisruptorPhaseRepeaterSide")
        TNT1 A 0 A_GiveInventory("SamsaraDisruptorPhaseRepeaterNormalAttackHandler")
        SQ74 GH 2 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_GiveInventory("SamsaraDisruptorPhaseRepeaterNormalAttackHandler")
        SQ74 I 1 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        SQ74 J 2 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_GiveInventory("SamsaraDisruptorPhaseRepeaterNormalAttackHandler")
        SQ74 KL 2 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_GiveInventory("SamsaraDisruptorPhaseRepeaterNormalAttackHandler")
        SQ74 I 1 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        SQ74 J 2 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"Ready")
        TNT1 A 0 A_JumpIfInventory("Clip",8,"Ready")
        goto NoAmmoSwitch

      // High Frequency
      ReadyHiFreq:
        SQ70 EEEEDDDDCCCCBBBBAAAALLLL 1 A_WeaponReady
        goto Ready

      DeselectHiFreq:
        SQ70 E 1 A_Lower
        goto Deselect

      SelectHiFreq:
        SQ70 E 1 A_Raise
        goto Select

      FireHiFreq:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("DisruptorHiFreq",4,1)
        goto NoAmmoSwitchHiFreq
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_TakeInventory("SamsaraDisruptorPhaseRepeaterSide")
        TNT1 A 0 A_GiveInventory("SamsaraDisruptorPhaseRepeaterHighFrequencyAttackHandler")
        SQ70 FG 2 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_GiveInventory("SamsaraDisruptorPhaseRepeaterHighFrequencyAttackHandler")
        SQ70 H 1 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        SQ70 I 2 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_GiveInventory("SamsaraDisruptorPhaseRepeaterHighFrequencyAttackHandler")
        SQ70 JK 2 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_GiveInventory("SamsaraDisruptorPhaseRepeaterHighFrequencyAttackHandler")
        SQ70 H 1 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        SQ70 I 2 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"Ready")
        TNT1 A 0 A_JumpIfInventory("DisruptorHiFreq",4,"Ready")
        goto NoAmmoSwitchHiFreq

      AltFire:
        TNT1 A 0 A_JumpIfInventory("SamsaraDisruptorPhaseRepeaterMode",1,2)
        SQ70 E 0 A_GiveInventory("SamsaraDisruptorPhaseRepeaterMode")
        goto AltFireFinish
        SQ74 A 0 A_TakeInventory("SamsaraDisruptorPhaseRepeaterMode")
        goto AltFireFinish

      AltFireFinish:
        "####" "#" 5 A_WeaponReady(WRF_NOFIRE)
        "####" "#" 10 A_WeaponReady(WRF_NOSECONDARY)
        goto Ready

      // "No ammo" switch
      NoAmmoSwitch:
        TNT1 A 0 A_ClearReFire
        TNT1 A 0 A_SelectWeapon(" 18mm Semi ")
        goto Deselect

      NoAmmoSwitchHiFreq:
        TNT1 A 0 A_ClearReFire
        goto AltFire
    }
}

// Normal

actor SamsaraDisruptorPhaseRepeaterNormalAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",2,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",2)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("Disruptor/GreenLaser",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SamsaraDisruptorPhaseRepeaterSide",0,"PickupAttackRight")
        TNT1 A 0 A_JumpIfInventory("SamsaraDisruptorPhaseRepeaterSide",1,"PickupAttackCenter")
      // Left
      PickupAttackLeft:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackLeftCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackLeftCoop")
      PickupAttackLeftDM:
        TNT1 A 0 A_FireCustomMissile("DisruptorGP_1",7,false)
        goto PickupFinishLeft

      PickupAttackLeftCoop:
        TNT1 A 0 A_FireCustomMissile("DisruptorGP_2",7,false)
        goto PickupFinishLeft

      // Center
      PickupAttackCenter:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCenterCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCenterCoop")
      PickupAttackCenterDM:
        TNT1 A 0 A_FireCustomMissile("DisruptorGP_1",0,false)
        goto PickupFinishCenter

      PickupAttackCenterCoop:
        TNT1 A 0 A_FireCustomMissile("DisruptorGP_2",0,false)
        goto PickupFinishCenter

      // Right
      PickupAttackRight:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackRightCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackRightCoop")
      PickupAttackRightDM:
        TNT1 A 0 A_FireCustomMissile("DisruptorGP_1",-7,false)
        goto PickupFinishRight

      PickupAttackRightCoop:
        TNT1 A 0 A_FireCustomMissile("DisruptorGP_2",-7,false)
        goto PickupFinishRight

      // Finishing stuff
      // Left
      PickupFinishLeft:
        TNT1 A 0 A_GiveInventory("SamsaraDisruptorPhaseRepeaterSide")
        stop

      // Center
      PickupFinishCenter:
        TNT1 A 0 A_GiveInventory("SamsaraDisruptorPhaseRepeaterSide")
        stop

      // Right
      PickupFinishRight:
        TNT1 A 0 A_TakeInventory("SamsaraDisruptorPhaseRepeaterSide",1) // let it jump back to center, refire resets it back to left
        stop
    }
}

// High Frequency

actor SamsaraDisruptorPhaseRepeaterHighFrequencyAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("DisruptorHiFreq",1,1)
        stop
        TNT1 A 0 A_TakeInventory("DisruptorHiFreq",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("Disruptor/GreenLaser",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SamsaraDisruptorPhaseRepeaterSide",0,"PickupAttackRight")
        TNT1 A 0 A_JumpIfInventory("SamsaraDisruptorPhaseRepeaterSide",1,"PickupAttackCenter")
      // Left
      PickupAttackLeft:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackLeftCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackLeftCoop")
      PickupAttackLeftDM:
        TNT1 A 0 A_FireCustomMissile("DisruptorGP_12",7,false)
        goto PickupFinishLeft

      PickupAttackLeftCoop:
        TNT1 A 0 A_FireCustomMissile("DisruptorGP_22",7,false)
        goto PickupFinishLeft

      // Center
      PickupAttackCenter:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCenterCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCenterCoop")
      PickupAttackCenterDM:
        TNT1 A 0 A_FireCustomMissile("DisruptorGP_12",0,false)
        goto PickupFinishCenter

      PickupAttackCenterCoop:
        TNT1 A 0 A_FireCustomMissile("DisruptorGP_22",0,false)
        goto PickupFinishCenter

      // Right
      PickupAttackRight:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackRightCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackRightCoop")
      PickupAttackRightDM:
        TNT1 A 0 A_FireCustomMissile("DisruptorGP_12",-7,false)
        goto PickupFinishRight

      PickupAttackRightCoop:
        TNT1 A 0 A_FireCustomMissile("DisruptorGP_22",-7,false)
        goto PickupFinishRight

      // Finishing stuff
      // Left
      PickupFinishLeft:
        TNT1 A 0 A_GiveInventory("SamsaraDisruptorPhaseRepeaterSide")
        stop

      // Center
      PickupFinishCenter:
        TNT1 A 0 A_GiveInventory("SamsaraDisruptorPhaseRepeaterSide")
        stop

      // Right
      PickupFinishRight:
        TNT1 A 0 A_TakeInventory("SamsaraDisruptorPhaseRepeaterSide",1) // let it jump back to center, refire resets it back to left
        stop
    }
}
