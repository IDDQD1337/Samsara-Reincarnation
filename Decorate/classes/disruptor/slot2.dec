actor SamsaraDisruptorPhaseRifleMode : Boolean {}

actor " Phase Rifle " : SamsaraDisruptorWeapon
{
    Inventory.PickupMessage "Picked up Phase Rifle."
    Weapon.SlotNumber 2
    Weapon.SelectionOrder 1550
    Weapon.AmmoType1 "Clip"
    Weapon.AmmoGive1 40
    Weapon.AmmoUse1 2
    Weapon.AmmoType2 "DisruptorHiFreq"
    Weapon.AmmoUse2 1
    Tag "Phase Rifle"
    +ALT_AMMO_OPTIONAL
    +AMMO_OPTIONAL
    +NOALERT
    States
    {
      Spawn:
        SQ72 R -1
        stop

      // Normal
      Ready:
        TNT1 A 0 A_JumpIfInventory("SamsaraDisruptorPhaseRifleMode",1,"ReadyHiFreq")
        SQ69 AAABBBCCCDDDEEE 1 A_WeaponReady
        loop

      Deselect:
        TNT1 A 0 A_Lower
        TNT1 A 0 A_JumpIfInventory("SamsaraDisruptorPhaseRifleMode",1,"DeselectHiFreq")
        SQ69 A 1 A_Lower
        loop

      Select:
        TNT1 A 0 A_Raise
        TNT1 A 0 A_JumpIfInventory("SamsaraDisruptorPhaseRifleMode",1,"SelectHiFreq")
        SQ69 A 1 A_Raise
        loop

      Fire:
        TNT1 A 0 A_JumpIfInventory("SamsaraDisruptorPhaseRifleMode",1,"FireHiFreq")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Clip",2,1)
        goto NoAmmoSwitch
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_GiveInventory("SamsaraDisruptorPhaseRifleNormalAttackHandler")
        SQ69 FGH 2 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Clip",2,1)
        goto NoAmmoSwitch
        SQ69 ABCDE 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto Ready

      // High Frequency
      ReadyHiFreq:
        SQ73 KKKLLLMMMNNNOOO 1 A_WeaponReady
        goto Ready

      DeselectHiFreq:
        SQ73 K 1 A_Lower
        goto Deselect

      SelectHiFreq:
        SQ73 K 1 A_Raise
        goto Select

      FireHiFreq:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("DisruptorHiFreq",1,1)
        goto NoAmmoSwitchHiFreq
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_GiveInventory("SamsaraDisruptorPhaseRifleHighFrequencyAttackHandler")
        SQ73 P 1 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        SQ73 QR 2 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("DisruptorHiFreq",1,1)
        goto NoAmmoSwitchHiFreq
        SQ73 KLMNO 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto Ready

      AltFire:
        TNT1 A 0 A_JumpIfInventory("SamsaraDisruptorPhaseRifleMode",1,2)
        SQ73 K 0 A_GiveInventory("SamsaraDisruptorPhaseRifleMode")
        goto AltFireFinish
        SQ69 A 0 A_TakeInventory("SamsaraDisruptorPhaseRifleMode")
        goto AltFireFinish

      AltFireFinish:
        "####" "#" 5 A_WeaponReady(WRF_NOFIRE)
        "####" "#" 10 A_WeaponReady(WRF_NOSECONDARY)
        goto Ready

      // "No ammo" switch
      NoAmmoSwitch:
        TNT1 A 0 A_ClearReFire
        TNT1 A 0 A_SelectWeapon(" 18mm Semi ")
        goto Deselect

      NoAmmoSwitchHiFreq:
        TNT1 A 0 A_ClearReFire
        goto AltFire
    }
}

// Normal

actor SamsaraDisruptorPhaseRifleNormalAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",2,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",2)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("Disruptor/GreenLaser",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("DisruptorGP_1",0,false)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("DisruptorGP_2",0,false)
        stop
    }
}

actor DisruptorGP_1 : FastProjectile
{
    Radius 2
    Height 1
    Speed 63
    Damage (random(30,40))
    DamageType "Disruptoruniversaldamageshit2"
    RenderStyle "Add"
    DeathSound "Disruptor/GreenL"
    Obituary "%o ate %k's lemon worms."
    +BLOODSPLATTER
    +FORCEXYBILLBOARD
    +NOEXTREMEDEATH
    +THRUGHOST
    States
    {
      Spawn:
        SQ97 G -1 Bright
        stop

      Death:
        SQ98 ABCDEFGHIJKL 3 Bright
        SQ98 MNO 2 Bright
        stop

      XDeath:
      Crash:
        SQ97 HIJKLMNOP 3 Bright
        SQ97 QRS 2 Bright
        stop
    }
}

actor DisruptorGP_2 : DisruptorGP_1
{
    Species "Player"
    +THRUSPECIES
}

// High Frequency

actor SamsaraDisruptorPhaseRifleHighFrequencyAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("DisruptorHiFreq",1,1)
        stop
        TNT1 A 0 A_TakeInventory("DisruptorHiFreq",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("Disruptor/GreenLaser",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("DisruptorGP_12",0,false)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("DisruptorGP_22",0,false)
        stop
    }
}

actor DisruptorHiFreq : Ammo
{
    Inventory.Amount 10
    Inventory.MaxAmount 150
    Ammo.BackpackAmount 10
    Ammo.BackpackMaxAmount 300
    Inventory.Icon "AMSMK0"
    Tag "High Frequency Ammo"
}

actor DisruptorGP_12 : DisruptorGP_1
{
    Radius 6
    Damage (0)
    Obituary "%o ate %k's spicy shit."
    +FORCERADIUSDMG
    +FORCEXYBILLBOARD
    //+SPECIALFIREDAMAGE //WHY NOT? No.
    +THRUACTORS
    States
    {
      Spawn:
        SQ99 D 0
        SQ99 DDEEFFGGHHIIJJKK 1 bright A_GiveInventory("DisruptorGP_12_Predictor") //ACS_NamedExecuteWithResult("Samsara_ProjectilePredictor", 1) //A_Explode(random(20,30),6,0,false,6)
        loop

      Death:
        SQ99 LMNOPQRSTUVW 3 Bright
        SQ99 XYZ 2 Bright
        stop

      XDeath:
      Crash:
        //TNT1 A 0 A_Explode(random(20,30),30,0,false,30)
        SQ98 QRSTUVWXYZ 3 Bright
        SQ99 ABC 2 Bright
        stop
    }
}

actor DisruptorGP_12_Predictor : Trigger
{
    States
    {
      Pickup:
        TNT1 A 0 A_SetArg(0,sqrt(velx*velx+vely*vely)*1000/sqrt(velx*velx+vely*vely+velz*velz))
        TNT1 A 0 A_SetArg(1,velz*1000/sqrt(velx*velx+vely*vely+velz*velz))
        TNT1 A 0 A_SetArg(2,sqrt(velx*velx+vely*vely+velz*velz)/2)
        TNT1 A 0 A_SetMass(-args[2])
      PickupLoop:
        TNT1 A 0 A_JumpIf(mass >= args[2], "PickupStop")
        TNT1 A 0 A_SpawnItemEx("DisruptorGP_12_Explosion",args[0]/1000.0*mass,0,args[1]/1000.0*mass,0,0,0,0,SXF_TRANSFERPOINTERS)
        TNT1 A 0 A_SetMass(mass+radius)
        loop
    }
}

actor DisruptorGP_12_Explosion
{
    Projectile
    Radius 1
    Height 2
    Obituary "%o ate %k's spicy shit."
    +DONTBLAST
    +DONTREFLECT
    +DONTSPLASH
    +FORCERADIUSDMG
    +NODAMAGETHRUST
    +NOINTERACTION
    +NOTIMEFREEZE
    States
    {
      Spawn:
        TNT1 A 0
        TNT1 A 1 A_Explode(random(1,2),6,0,false,6)
        stop
    }
}

actor DisruptorGP_22 : DisruptorGP_12
{
    Species "Player"
    +THRUSPECIES
}
