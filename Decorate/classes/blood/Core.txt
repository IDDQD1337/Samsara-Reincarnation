Actor CalebWeapon : Weapon
{
	Weapon.BobStyle Smooth
	Weapon.BobRangeX 0.2
	Weapon.BobRangeY 0.4
	Weapon.BobSpeed 0.5
	Inventory.RestrictedTo "Caleb"
	States
	{
	DeselectLoop:
		"####" "##" 0 A_Lower
		"####" "#" 1 A_Lower
		Loop
	SelectLoop:
		"####" "##" 0 A_Raise
		"####" "#" 1 A_Raise
		Loop
	DeselectLighter:
		TNT1 A 0 A_TakeInventory("CalebLighterReadyDummy")
		TNT1 A 0 A_JumpIfInventory("CalebUsingTNT",1,1)
		Goto DeselectLighterAnimation
		TNT1 A 0 A_TakeInventory("CalebUsingTNT")
		TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,"DeselectTNT")
		Goto DeselectLighterAnimation
	DeselectLighterAnimation:
		BLTR NO 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_PlayWeaponSound("TNT/Zip")
		BLTR PQRS 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		Goto DeselectLighterEnd
	DeselectLighterEnd:
		TNT1 A 0 Offset(0,128)
		TNT1 A 1 A_Jump(256,"Select")
		Wait
	DeselectTNT:
		BTNT OP 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_PlayWeaponSound("TNT/Zip")
		BTNT QRS 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		BLTR S 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		Goto DeselectLighterEnd
	IntroTaunt:
		TNT1 A 0 A_JumpIfInventory("CalebBallgag",1,2)
		TNT1 A 0 A_PlaySound("Caleb/Enter", CHAN_7)
		TNT1 A 0 A_TakeInventory("CalebReady", 1)
		Goto TauntCooldown
	WaitingTaunt:
		TNT1 A 0 A_JumpIfInventory("CalebIdleBallgag",1,"IdleBallgagStuff")
		TNT1 A 0 A_JumpIfInventory("CalebBallgag",1,2)
		TNT1 A 0 A_PlaySound("Caleb/Bored", CHAN_7)
		TNT1 A 0 A_TakeInventory("CalebWaiting",525)
		Goto TauntCooldown
	TauntCooldown:
		TNT1 A 0 A_GiveInventory("CalebTauntCooldown",5)
		TNT1 A 0 ACS_NamedExecuteAlways("CalebTauntCooldown", 0)
		TNT1 A 0 A_Jump(256,"Ready")
	IdleBallgagStuff:
		TNT1 A 0 A_TakeInventory("CalebWaiting",525)
		TNT1 A 0 A_Jump(256,"Ready")
	}
}