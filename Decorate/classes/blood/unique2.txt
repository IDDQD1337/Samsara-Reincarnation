actor SamsaraBloodGunsAkimboHUDDisplay : Counter { Inventory.MaxAmount 3 }
actor SamsaraBloodGunsAkimboCooldown : Counter {}
actor SamsaraBloodGunsAkimboToggleMemory : Boolean {}
actor SamsaraBloodGunsAkimboToggled : Boolean {}

actor SamsaraBloodGunsAkimboPickup : Trigger
{
    States
    {
      Pickup:
        TNT1 A 0 A_JumpIf(CallACS("SamsaraDecorate", 59) >= 3, "PickupToggle")
        TNT1 A 0 A_JumpIf(CallACS("SamsaraDecorate", 59) >= 1, "PickupInventory")
      PickupPowerup:
        TNT1 A 0 A_GiveInventory("SamsaraPowerBloodGunsAkimboGiver")
        stop

      PickupInventory:
        TNT1 A 0 A_GiveInventory("SamsaraBloodGunsAkimboInventory")
        TNT1 A 0 A_JumpIf(CallACS("SamsaraDecorate", 59) == 2, 1)
        stop
        TNT1 A 0 A_JumpIfInventory("SamsaraBloodGunsAkimboCooldown",1,1)
        stop
        TNT1 A 0 ACS_NamedExecuteWithResult("SamsaraBloodGunsAkimboCooldownDecrement")
        stop

      PickupToggle:
        TNT1 A 0 A_GiveInventory("SamsaraPowerBloodGunsAkimboQuadDamageGiver")
        stop
    }
}

actor SamsaraPowerBloodGunsAkimboGiver : SamsaraPowerupGiver { Powerup.Type "SamsaraBloodGunsAkimbo" }

actor PowerSamsaraBloodGunsAkimbo : Powerup { Powerup.Duration -35 }

actor SamsaraPowerBloodGunsAkimboQuadDamageGiver : SamsaraPowerupGiver { Powerup.Type "SamsaraBloodGunsAkimboQuadDamage" }

actor PowerSamsaraBloodGunsAkimboQuadDamage : PowerDamage
{
    Powerup.Duration -35
    DamageFactor "Normal", 4
}

actor SamsaraBloodGunsAkimboInventory : CustomInventory
{
    Inventory.UseSound ""
    Inventory.Icon "AKIMC0"
    Inventory.MaxAmount 1
    Inventory.InterHubAmount 1
    Inventory.RestrictedTo "Caleb"
    Tag "Guns Akimbo"
    +INVBAR
    +UNDROPPABLE
    States
    {
      Use:
        TNT1 A 0 A_JumpIf(CallACS("SamsaraDecorate", 59) >= 3, "UseToggle")
        TNT1 A 0 A_JumpIf(CallACS("SamsaraDecorate", 59) == 2, "UseCooldownCheck")
        goto UseContinue

      UseCooldownCheck:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasDisabledInventoryCooldown",1,"UseContinue")
        TNT1 A 0 A_JumpIfInventory("SamsaraBloodGunsAkimboCooldown",1,"UseFailCooldown")
        goto UseContinue

      UseContinue:
        TNT1 A 0 A_JumpIf(CallACS("SamsaraPowerupTimerActiveChecker", 15, 0), "UseFail")
        TNT1 A 0 A_PlaySound("Caleb/Powerup",CHAN_ITEM)
        TNT1 A 0 A_JumpIf(CallACS("SamsaraDecorate", 59) == 2, "UseFinishCooldown")
      UseFinish:
        TNT1 A 0 A_GiveInventory("SamsaraPowerBloodGunsAkimboGiver")
        stop

      UseFinishCooldown:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasDisabledInventoryCooldown",1,3)
        TNT1 A 0 A_GiveInventory("SamsaraBloodGunsAkimboCooldown",120)
        TNT1 A 0 ACS_NamedExecuteAlways("SamsaraBloodGunsAkimboCooldown")
        TNT1 A 0 A_GiveInventory("SamsaraPowerBloodGunsAkimboGiver")
        fail

      UseToggle:
        TNT1 A 0 A_JumpIfInventory("SamsaraBloodGunsAkimboToggleMemory",1,"UseToggleDeactivate")
      UseToggleActivate:
        TNT1 A 0 A_GiveInventory("SamsaraBloodGunsAkimboToggleMemory")
        fail

      UseToggleDeactivate:
        TNT1 A 0 A_TakeInventory("SamsaraBloodGunsAkimboToggleMemory")
        fail

      UseFail:
        TNT1 A 0
        fail

      UseFailCooldown:
        TNT1 A 0 ACS_NamedExecuteAlways("SamsaraClientDecorate", 0, 17, 8)
        fail
    }
}
