Actor "   Revolver   " : CalebWeapon
{
	Obituary "%o was shot by %k's revolver."
	AttackSound "weapons/bloodpistol"
	Weapon.SelectionOrder 1900
	Weapon.KickBack 100
	Weapon.AmmoUse 0
	Weapon.AmmoGive 0
	Weapon.AmmoType ""
	Inventory.PickupSound "PICKWEAP"
	Weapon.SlotNumber 1
	+NOALERT
	+WEAPON.WIMPY_WEAPON
	+INVENTORY.UNDROPPABLE
	Decal ZBloodBulletChip1
	Tag "Revolver"
	States
	{
	Spawn:
		TNT1 A 1
		Stop
	Ready:
		TNT1 A 0 A_TakeInventory("UsingPitchfork", 1)
		TNT1 A 0 A_JumpIfInventory("CalebReady",1,"IntroTaunt")
		TNT1 A 0 A_JumpIfInventory("CalebWaiting",60,"WaitingTaunt")
		TNT1 A 0 A_JumpIfInventory("GunsAkimboP", 1, "AkimboReady")
		BLRV A 1 A_WeaponReady
		Goto Ready+1
	AkimboReady:
		BLRV I 1 A_WeaponReady
		Goto Ready
	Deselect:
		BLRV A 0 A_JumpIfInventory("GunsAkimboP", 1, 1)
		Goto Super::DeselectLoop
		BLRV I 0
		Goto Super::DeselectLoop
	Select:
		TNT1 A 0 A_JumpIfInventory("CalebLighterReadyDummy",1,"DeselectLighter")
		BLRV A 0 A_JumpIfInventory("GunsAkimboP", 1, 1)
		Goto Super::SelectLoop
		BLRV I 0
		Goto Super::SelectLoop
	Fire:
		TNT1 A 0 A_JumpIfInventory("GunsAkimboP", 1, "AkimboFire")
		Goto ShouldCheckAmmo
	ShouldCheckAmmo:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"KeepFiring")
		TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,"CheckForAmmo")
		Goto KeepFiring
	CheckForAmmo:
		TNT1 A 0 A_JumpIfInventory("Clip",1,1)
		Goto OutOfAmmo
		TNT1 A 0 A_TakeInventory("Clip",1)
		Goto KeepFiring
	KeepFiring:
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GiveInventory("SamsaraBloodRevolverAttackHandler")
		BLRF ABC 2 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		BLRV EFG 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		BLRV H 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_ReFire
		Goto Ready
	Flash:
		BLRV B 2 A_Light2
		BLRV C 2 A_Light1
		BLRV D 2 A_Light0
		Stop
	AkimboFire:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"StartFiringAkimbo")
		TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,"AkimboCheckForAmmo")
		Goto StartFiringAkimbo
	AkimboHold:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"KeepFiringAkimbo")
		TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,"AkimboCheckForAmmoHold")
		Goto KeepFiringAkimbo
	AkimboCheckForAmmo:
		TNT1 A 0 A_JumpIfInventory("Clip",1,1)
		Goto OutOfAmmo
		TNT1 A 0 A_TakeInventory("Clip",1)
		Goto StartFiringAkimbo
	AkimboCheckForAmmoHold:
		TNT1 A 0 A_JumpIfInventory("Clip",1,1)
		Goto OutOfAmmo
		TNT1 A 0 A_TakeInventory("Clip",1)
		Goto KeepFiringAkimbo
	StartFiringAkimbo:
		TNT1 A 0 A_GunFlash("AkimboFlash")
		Goto AkimboFireContinue
	KeepFiringAkimbo:
		TNT1 A 0 A_GunFlash("AkimboFlashHold")
		Goto AkimboFireContinue
	AkimboFireContinue:
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GiveInventory("SamsaraBloodRevolverAttackHandler")
		BLRF DEF 2 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		BLRV MM 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"AkimboFireContinue2")
		TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,"AkimboCheckForAmmo2")
		Goto AkimboFireContinue2
	AkimboCheckForAmmo2:
		TNT1 A 0 A_JumpIfInventory("Clip",1,"AkimboTakeAmmo2")
		BLRV UVW 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		Goto OutOfAmmo
	AkimboTakeAmmo2:
		TNT1 A 0 A_TakeInventory("Clip",1)
		Goto AkimboFireContinue2
	AkimboFireContinue2:
		TNT1 A 0 A_GunFlash("AkimboFlash2")
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GiveInventory("SamsaraBloodRevolverAttackHandler")
		BLRF GHI 2 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		BLRV QQ 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_JumpIfInventory("GunsAkimboP", 1, 1)
		Goto AkimboFireEnd
		TNT1 A 0 A_ReFire("AkimboHold")
		Goto AkimboFireEnd
	AkimboFireEnd:
		BLRV XYZ 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_ReFire
		Goto Ready
	AkimboFlash:
		BLRV J 2 A_Light2
		BLRV K 2 A_Light1
		BLRV L 2 A_Light0
		Stop
	AkimboFlash2:
		BLRV N 2 A_Light2
		BLRV O 2 A_Light1
		BLRV P 2 A_Light0
		Stop
	AkimboFlashHold:
		BLRV R 2 A_Light2
		BLRV S 2 A_Light1
		BLRV T 2 A_Light0
		Stop
	OutOfAmmo:
		TNT1 A 0 A_SelectWeapon("Pitchfork")
		Goto Deselect
	}
}

Actor SamsaraBloodRevolverAttackHandler : Trigger
{
	States
	{
		Pickup:
			TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
			TNT1 A 0 A_FireBullets(2.6, 1.6, -1, 12, "ZBBulletPuff", FBF_NORANDOM|FBF_NOFLASH)
			Stop
		PickupAttackCoop:
			TNT1 A 0 A_FireBullets(2.6, 1.6, -1, 12, "ZBBulletPuffCoop", FBF_NORANDOM|FBF_NOFLASH)
			Stop
	}
}
