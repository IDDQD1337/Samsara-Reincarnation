Actor ProximityTNTSeamlessToken : TemporaryCounter { Inventory.MaxAmount 4 }
Actor BloodProximityTNT : CalebThrowableWeapon
{
	Inventory.PickupMessage "Picked up Proximity Detonator"
	Weapon.SelectionOrder 900
	Weapon.AmmoGive 5
	Weapon.AmmoType "RocketAmmo"
	Weapon.AmmoType2 "RocketAmmo"
	Obituary "%o got a faceful of %k's TNT!"
	Weapon.AmmoUse 1
	Weapon.AmmoUse2 1
	Weapon.SlotNumber 5
	Weapon.SlotPriority 1
	Inventory.PickupSound "Caleb/Weaponup"
	+WEAPON.EXPLOSIVE
	+WEAPON.NOALERT
	+WEAPON.NOAUTOAIM
	Tag "Proximity Detonator"
	States
	{
	Ready:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"RaisingDynamite")
		TNT1 A 0 A_JumpIfNoAmmo("ReadyAmmoless")
		Goto RaisingDynamite
	RaisingDynamite:
		BPRX ABCDI 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		Goto ReadyLoop+3
	ReadyLoop:
		TNT1 A 0 A_TakeInventory("ProximityTNTSeamlessToken")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
		TNT1 A 0 A_JumpIfNoAmmo("ReadyAmmoless")
		TNT1 A 0 A_JumpIfInventory("CalebReady",1,"IntroTaunt")
		TNT1 A 0 A_JumpIfInventory("CalebWaiting",60,"WaitingTaunt")
		BPRX E 3 A_WeaponReady
		TNT1 A 0 A_GiveInventory("ProximityTNTSeamlessToken")
		BPRX F 3 A_WeaponReady
		TNT1 A 0 A_GiveInventory("ProximityTNTSeamlessToken")
		BPRX G 3 A_WeaponReady
		TNT1 A 0 A_GiveInventory("ProximityTNTSeamlessToken")
		BPRX H 3 A_WeaponReady
		TNT1 A 0 A_GiveInventory("ProximityTNTSeamlessToken")
		BPRX I 3 A_WeaponReady
		Loop
	ReadyAmmoless:
		TNT1 A 0 A_JumpIfInventory("CalebReady",1,"IntroTaunt")
		TNT1 A 0 A_JumpIfInventory("CalebWaiting",60,"WaitingTaunt")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"RaisingDynamite")
		TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,"RaisingDynamite")
		TNT1 A 3 A_WeaponReady
		Loop
	Deselect:
		TNT1 A 0 A_TakeInventory("RemoteTNTDetonate")
		TNT1 A 0 A_TakeInventory("ProximityTNTSeamlessToken")
		TNT1 A 0 A_TakeInventory("CalebInterpolateBar")
		TNT1 A 0 A_TakeInventory("ThrowPowerBig")
		TNT1 A 0 A_TakeInventory("ThrowPower")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
		TNT1 A 0 A_JumpIfNoAmmo("DeselectLoop")
		BPRX EJKLM 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		Goto Super::DeselectLoop
	Select:
		TNT1 A 0 A_JumpIfInventory("CalebLighterReadyDummy",1,"DeselectLighter")
		TNT1 A 0 A_TakeInventory("CalebInterpolateBar")
		TNT1 A 0 A_TakeInventory("ThrowPowerBig")
		TNT1 A 0 A_TakeInventory("ThrowPower")
		TNT1 A 0 A_TakeInventory("UsingPitchfork", 1)
		Goto Super::SelectLoop
	Fire:
		TNT1 A 0 A_GiveInventory("CalebInterpolateBar")
		Goto Hold
	Hold:
		TNT1 A 0 A_JumpIfInventory("ProximityTNTSeamlessToken",4,"Hold5")
		TNT1 A 0 A_JumpIfInventory("ProximityTNTSeamlessToken",3,"Hold4")
		TNT1 A 0 A_JumpIfInventory("ProximityTNTSeamlessToken",2,"Hold3")
		TNT1 A 0 A_JumpIfInventory("ProximityTNTSeamlessToken",1,"Hold2")
		Goto Hold1
	Hold1:
		TNT1 A 0 A_GiveInventory("ProximityTNTSeamlessToken")
		BPRX E 3 A_GunFlash("Flash",GFF_NOEXTCHANGE)
		TNT1 A 0 A_ReFire("Hold2")
		Goto Throw
	Hold2:
		TNT1 A 0 A_GiveInventory("ProximityTNTSeamlessToken")
		BPRX F 3 A_GunFlash("Flash",GFF_NOEXTCHANGE)
		TNT1 A 0 A_ReFire("Hold3")
		Goto Throw
	Hold3:
		TNT1 A 0 A_GiveInventory("ProximityTNTSeamlessToken")
		BPRX G 3 A_GunFlash("Flash",GFF_NOEXTCHANGE)
		TNT1 A 0 A_ReFire("Hold4")
		Goto Throw
	Hold4:
		TNT1 A 0 A_GiveInventory("ProximityTNTSeamlessToken")
		BPRX H 3 A_GunFlash("Flash",GFF_NOEXTCHANGE)
		TNT1 A 0 A_ReFire("Hold5")
		Goto Throw
	Hold5:
		TNT1 A 0 A_TakeInventory("ProximityTNTSeamlessToken")
		BPRX I 3 A_GunFlash("Flash",GFF_NOEXTCHANGE)
		TNT1 A 0 A_ReFire("Hold1")
		Goto Throw
	Flash:
		TNT1 A 0 A_GiveInventory("ThrowPower", 5)
		TNT1 A 0 A_GiveInventory("ThrowPowerBig", 3)
		TNT1 A 0 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		Stop
	AltFire:
		TNT1 A 0 A_TakeInventory("CalebInterpolateBar")
		TNT1 A 0 A_TakeInventory("ThrowPower")
		BPRX EQR 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_GiveInventory("SamsaraBloodProximityAttackHandler")
		BPRX S 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		Goto ReadyAmmoless
	Spawn:
		DPRO A -1
		Stop
	Throw:
		BPRX NOP 1
		TNT1 A 2
		BTHR A 1
		TNT1 A 0 A_TakeInventory("CalebInterpolateBar")
		TNT1 A 0 A_PlayWeaponSound("TNT/Throw")
		BTHR B 1 A_GiveInventory("SamsaraBloodProximityAttackHandler")
		BTHR CDEFG 1
		Goto ReadyAmmoless
	}
}

Actor SamsaraBloodProximityAttackHandler : Trigger
{
	States
	{
		Pickup:
			TNT1 A 0
			TNT1 A 0 A_TakeInventory("RocketAmmo", 1, TIF_NOTAKEINFINITE)
			TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,2)
			TNT1 A 0 A_FireCustomMissile("PlayerProximityTNT",0,false,8,0)
			Stop
			TNT1 A 0 A_FireCustomMissile("PlayerProximityTNT_Coop",0,false,8,0)
			Stop
	}
}

Actor ProximityTNTDetonate : TemporaryBoolean {}

actor PlayerProximityTNT : PlayerTNT
{
	Damage (0)
	BounceFactor 0.5
	BounceType "Hexen"
	+BOUNCEONACTORS
	-EXPLODEONWATER
	-PUSHABLE
	States
	{
	Spawn:
		TTNT K 0
		TNT1 A 0 A_SetUserVar("user_strength",CallACS("CalebThrowStrength"))
		TNT1 A 0 ACS_NamedExecuteWithResult("CalebThrowDuration")
		TNT1 A 0 A_ChangeVelocity((5+(user_strength))*(user_strength>0)*(sqrt(velx*velx + vely*vely)/5.0),0,(((5+(user_strength/2))*(sqrt(velx*velx + vely*vely)/5.0))+((5+(user_strength))*(velz/5.0)))*(user_strength>0),CVF_RELATIVE|CVF_REPLACE)
		Goto See
	See:
		TTNT K 1 A_ScaleVelocity(0.9) TNT1 A 0 A_JumpIf(sqrt(velx*velx + vely*vely + velz*velz) < 2,"Stay")
		TTNT K 1 A_ScaleVelocity(0.9) TNT1 A 0 A_JumpIf(sqrt(velx*velx + vely*vely + velz*velz) < 2,"Stay")
		TTNT L 1 A_ScaleVelocity(0.9) TNT1 A 0 A_JumpIf(sqrt(velx*velx + vely*vely + velz*velz) < 2,"Stay")
		TTNT L 1 A_ScaleVelocity(0.9) TNT1 A 0 A_JumpIf(sqrt(velx*velx + vely*vely + velz*velz) < 2,"Stay")
		TTNT M 1 A_ScaleVelocity(0.9) TNT1 A 0 A_JumpIf(sqrt(velx*velx + vely*vely + velz*velz) < 2,"Stay")
		TTNT M 1 A_ScaleVelocity(0.9) TNT1 A 0 A_JumpIf(sqrt(velx*velx + vely*vely + velz*velz) < 2,"Stay")
		TTNT F 1 A_ScaleVelocity(0.9) TNT1 A 0 A_JumpIf(sqrt(velx*velx + vely*vely + velz*velz) < 2,"Stay")
		TTNT F 1 A_ScaleVelocity(0.9) TNT1 A 0 A_JumpIf(sqrt(velx*velx + vely*vely + velz*velz) < 2,"Stay")
		TTNT G 1 A_ScaleVelocity(0.9) TNT1 A 0 A_JumpIf(sqrt(velx*velx + vely*vely + velz*velz) < 2,"Stay")
		TTNT G 1 A_ScaleVelocity(0.9) TNT1 A 0 A_JumpIf(sqrt(velx*velx + vely*vely + velz*velz) < 2,"Stay")
		TTNT H 1 A_ScaleVelocity(0.9) TNT1 A 0 A_JumpIf(sqrt(velx*velx + vely*vely + velz*velz) < 2,"Stay")
		TTNT H 1 A_ScaleVelocity(0.9) TNT1 A 0 A_JumpIf(sqrt(velx*velx + vely*vely + velz*velz) < 2,"Stay")
		TTNT I 1 A_ScaleVelocity(0.9) TNT1 A 0 A_JumpIf(sqrt(velx*velx + vely*vely + velz*velz) < 2,"Stay")
		TTNT I 1 A_ScaleVelocity(0.9) TNT1 A 0 A_JumpIf(sqrt(velx*velx + vely*vely + velz*velz) < 2,"Stay")
		TTNT J 1 A_ScaleVelocity(0.9) TNT1 A 0 A_JumpIf(sqrt(velx*velx + vely*vely + velz*velz) < 2,"Stay")
		TTNT J 1 A_ScaleVelocity(0.9) TNT1 A 0 A_JumpIf(sqrt(velx*velx + vely*vely + velz*velz) < 2,"Stay")
		Loop
	Stay:
		TNT1 A 0 A_ScaleVelocity(0.0)
		TTNT ABCDEABC 5
		Goto Charge
	Charge:
		TNT1 A 0 A_PlaySound("TNT/Arm",CHAN_5)
		TNT1 A 0 ACS_NamedExecuteWithResult("CalebProximityLooker")
		Goto ChargeLoop
	ChargeLoop:
		TTNT DDDDDEEEEEAAAAABBBBBCCCCC 1 A_JumpIfInventory("ProximityTNTDetonate",1,"Explode")
		Loop
	Death:
	XDeath:
	Crash:
		"####" "#" 1 ACS_NamedExecuteWithResult("CalebTossableResolve",reactiontime,0,2)
		Wait
	Explode:
		TNT1 A 0 A_RemoveChildren(true)
		TNT1 A 0 ACS_NamedExecuteWithResult("CalebFloorDynamite")
		TNT1 A 0 A_SpawnItemEx("Kaboom",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
		Goto Super::BlowAway
	ExplodeFloor:
		Goto Super::Death
	}
}

actor PlayerProximityTNT_Coop : PlayerProximityTNT
{
	Species "Player"
	+THRUSPECIES
	+DONTHARMCLASS
	+DONTHARMSPECIES
	+MTHRUSPECIES
	DamageType "DynamiteCoop"
	States
	{
	Death:
		"####" "#" 1 ACS_NamedExecuteWithResult("CalebTossableResolve",reactiontime,0,1)
		Wait
	Explode:
		TNT1 A 0 A_RemoveChildren(true)
		TNT1 A 0 A_CheckFloor("ExplodeFloor")
		TNT1 A 0 A_SpawnItemEx("Kaboom",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
		Goto BlowAway
	ExplodeFloor:
		TNT1 A 0 A_SpawnItemEx("FloorKaboom",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
		Goto BlowAway
	BlowAway:
		TNT1 A 0 A_SpawnItemEx("BlowerCoop",0,0,-2,0,0,-10,0,SXF_NOCHECKPOSITION)
		Goto Super::ExplodeSound
	}
}

Actor PlayerProximityTNTLooker
{
	-COUNTKILL
	+ISMONSTER
	+NEVERTARGET
	+NOBLOOD
	Mass 65536
	Height 2
	Radius 2
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_CopyFriendliness(AAPTR_TRACER)
			Goto SpawnLoop
		SpawnLoop:
			TNT1 A 0 A_Warp(AAPTR_MASTER,0,0,0,0,WARPF_NOCHECKPOSITION)
			TNT1 A 1 A_LookEx(0,0,128,0,360,"See")
			Loop
		See:
			TNT1 A 1 A_GiveInventory("ProximityTNTDetonate",1,AAPTR_MASTER)
			Stop
	}
}