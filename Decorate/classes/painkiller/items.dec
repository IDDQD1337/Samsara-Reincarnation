Actor Painkiller_SoulTarget
{
	+ISMONSTER
	-COUNTKILL
	+THRUACTORS
	+NOGRAVITY
	+FLOAT
	+DROPOFF
	-NODROPOFF
	+NOBLOCKMONST
	+DONTBLAST
	Mass 100000000
	Height 4
	Radius 2
	States
	{
		Spawn:
			TNT1 A 1
			TNT1 A 0 A_LookEx(LOF_NOSOUNDCHECK,0,256,0,360)
			Loop
		See:
			TNT1 A 0 A_JumpIfInTargetInventory("Painkiller_SoulCatcher_Power",1,1)
			Goto Spawn
			TNT1 A 1 A_FaceTarget
			TNT1 A 0 A_JumpIfCloser(16,"GiveSoul")
			TNT1 A 0 A_SkullAttack(16)
			Loop
		GiveSoul:
			TNT1 A 1 A_GiveToTarget("Painkiller_GreenSoul",1)
			Stop
	}
}

Actor Painkiller_SoulTargetRed : Painkiller_SoulTarget
{
	States
	{
		GiveSoul:
			TNT1 A 1 A_GiveToTarget("Painkiller_RedSoul",1)
			Stop
	}
}

Actor Painkiller_GreenSoul : CustomInventory
{
	Inventory.RestrictedTo "DanielGarner"
	Renderstyle Add
	+NOGRAVITY
	+INVENTORY.QUIET
	+BRIGHT
	States
	{
		Spawn:
			PK00 A 0
			PK00 A 0 ACS_NamedExecuteWithResult("Painkiller_SoulCatcher")
			Goto SpawnChoose
		SpawnChoose:
			PK00 A 0 A_Jump(128,"Spawn2")
			Goto Spawn1
		Spawn1:
			PK00 ABCDEFGHIJKLMNOP 4
			PK00 ABCDEFGHIJKLMNOP 4
			PK00 ABCDEFGHIJKLMNOP 4
			PK00 ABCDEFGHIJKLMNOP 4
			PK00 ABCDEFGHIJKLMNOP 4
			PK00 ABCDEFGHIJKL 4 A_Fadeout(0.0833)
			Stop
		Spawn2:
			PK01 ABCDEFGHIJKLMNOP 4
			PK01 ABCDEFGHIJKLMNOP 4
			PK01 ABCDEFGHIJKLMNOP 4
			PK01 ABCDEFGHIJKLMNOP 4
			PK01 ABCDEFGHIJKLMNOP 4
			PK01 ABCDEFGHIJKL 4 A_Fadeout(0.0833)
			Stop
		Pickup:
			TNT1 A 0 ACS_NamedExecuteAlways("SamsaraDecorate", 0, 58)
			TNT1 A 0 A_GiveInventory("SamsaraHealthPickupBlend")
			TNT1 A 0 A_JumpIfInventory("Painkiller_BlockDemonMorph",1,2)
			TNT1 A 0 A_GiveInventory("Painkiller_SoulCount",1)
			TNT1 A 0 A_JumpIfInventory("Painkiller_SoulRedeemer_Power",1,2)
			TNT1 A 0 ACS_NamedExecuteWithResult("SamsaraPlayerHealer", 1, 200)
			stop
			TNT1 A 0 ACS_NamedExecuteWithResult("SamsaraPlayerHealer", 1 * 2, 200)
			stop
	}
}

Actor Painkiller_RedSoul : Painkiller_GreenSoul
{
	Alpha 0.5
	States
	{
		Spawn:
			PK00 A 0
			PK00 A 0 ACS_NamedExecuteWithResult("Painkiller_SoulCatcher",1)
			Goto Super::SpawnChoose
		Pickup:
			TNT1 A 0 ACS_NamedExecuteAlways("SamsaraDecorate", 0, 58)
			TNT1 A 0 A_GiveInventory("SamsaraHealthPickupBlend")
			TNT1 A 0 A_JumpIfInventory("Painkiller_BlockDemonMorph",1,2)
			TNT1 A 0 A_GiveInventory("Painkiller_SoulCount",1)
			TNT1 A 0 A_JumpIfInventory("Painkiller_SoulRedeemer_Power",1,2)
			TNT1 A 0 ACS_NamedExecuteWithResult("SamsaraPlayerHealer", 6, 200)
			stop
			TNT1 A 0 ACS_NamedExecuteWithResult("SamsaraPlayerHealer", 6 * 2, 200)
			stop
	}
}

Actor Painkiller_TossCoin : Trigger
{
	States
	{
		Pickup:
			TNT1 A 0 A_SpawnItemEx("Painkiller_Coin_Physical",0,0,random(0,CallACS("Painkiller_GibHeightCheck")),frandom(-3.0,3.0),0,frandom(2.0,4.0),random(0,359),SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)
			stop
	}
}

Actor Painkiller_Coin_Physical : SwitchableDecoration
{
	Projectile
	BounceType "Doom"
	Height 2
	Radius 1
	Gravity 0.75
	var int user_turndirection;
	Activation THINGSPEC_ThingTargets
	-NOGRAVITY
	//+THRUACTORS
	+DONTTRANSLATE
	+BUMPSPECIAL
	+CANBOUNCEWATER
	+DONTBLAST
	+DONTREFLECT
	+NOTRIGGER
	+SKYEXPLODE
	+BRIGHT
	States
	{
		Spawn:
			PK00 A 0
			PK00 A 0 A_SetUserVar("user_turndirection",((random(0,1)*2) - 1))
			PK00 A 0 ACS_NamedExecuteWithResult("Painkiller_SoulCatcher",2)
		SpawnLoop:
			PK00 A 0 A_SetPitch(pitch+user_turndirection*8)
			PK00 A 0 A_SpawnItemEx("Painkiller_CoinGlow")
			PK00 A 1 A_SetAngle(angle+user_turndirection*8)
			Loop
		Death:
			TNT1 A 1 A_SpawnItemEx("Painkiller_Coin")
			Stop
		Active:
			PK00 A 0 A_JumpIfInventory("PainkillerClass",1,1)
			Goto SpawnLoop
			PK00 A 0 A_GiveInventory("Painkiller_Coin",1)
			Stop
	}
}

Actor Painkiller_Coin : CustomInventory
{
	Inventory.RestrictedTo "DanielGarner"
	+INVENTORY.QUIET
	+BRIGHT
	States
	{
		Spawn:
			PK00 A 0 NoDelay ACS_NamedExecuteWithResult("Painkiller_SoulCatcher",2)
			PK00 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("Painkiller_CoinGlow")
			PK00 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("Painkiller_CoinGlow")
			PK00 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("Painkiller_CoinGlow")
			PK00 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("Painkiller_CoinGlow")
			PK00 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("Painkiller_CoinGlow")
			PK00 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("Painkiller_CoinGlow")
			PK00 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("Painkiller_CoinGlow")
			PK00 A 0 A_SpawnItemEx("Painkiller_CoinGlow2",0,0,1)
			PK00 A 1 A_Fadeout(0.05)
			Wait
		Pickup:
			PK00 A 0 A_PlaySound("Painkiller/Coin/Take",CHAN_ITEM|CHAN_MAYBE_LOCAL,0.5)
			PK00 A 0 A_GiveInventory("Painkiller_GoldAmount",1)
			Stop
	}
}

Actor Painkiller_CoinTarget : Painkiller_SoulTarget
{
	States
	{
		Spawn:
			TNT1 A 1
			TNT1 A 0 A_LookEx(LOF_NOSOUNDCHECK,0,256,0,360)
			Loop
		See:
			TNT1 A 0 A_JumpIfInTargetInventory("Painkiller_GoldMagnet_Power",1,1)
			Goto Spawn
			TNT1 A 1 A_FaceTarget
			TNT1 A 0 A_JumpIfCloser(16,"GiveCoin")
			TNT1 A 0 A_SkullAttack(16)
			Loop
		GiveCoin:
			TNT1 A 1 A_GiveToTarget("Painkiller_Coin",1)
			Stop
	}
}

Actor Painkiller_CoinGlow
{
	+NOINTERACTION
	+FORCEXYBILLBOARD
	+BRIGHT
	Renderstyle Translucent
	Height 2
	Radius 1
	Scale 0.5
	Alpha 0.5
	States
	{
		Spawn:
			PK01 F 2
			Stop
	}
}

Actor Painkiller_CoinGlow2 : Painkiller_CoinGlow
{
	States
	{
		Spawn:
			PK01 F 2 A_Fadeout(0.1)
			Wait
	}
}

Actor Painkiller_TossRing : Trigger
{
	States
	{
		Pickup:
			TNT1 A 0 A_SpawnItemEx("Painkiller_Ring_Physical",0,0,random(0,CallACS("Painkiller_GibHeightCheck")),frandom(-3.0,3.0),0,frandom(2.0,4.0),random(0,359),SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)
			stop
	}
}

Actor Painkiller_Ring_Physical : SwitchableDecoration
{
	Projectile
	BounceType "Doom"
	Height 2
	Radius 1
	Gravity 0.75
	var int user_turndirection;
	-NOGRAVITY
	//+THRUACTORS
	+DONTTRANSLATE
	+BUMPSPECIAL
	+CANBOUNCEWATER
	+DONTBLAST
	+DONTREFLECT
	+NOTRIGGER
	+SKYEXPLODE
	+BRIGHT
	States
	{
		Spawn:
			PK00 A 0
			PK00 A 0 ACS_NamedExecuteWithResult("Painkiller_SoulCatcher",3)
			PK00 A 0 A_SetUserVar("user_turndirection",((random(0,1)*2) - 1))
			PK00 A 0 A_SetMass(random(3,5))
			PK00 A 0 A_JumpIf(mass > 4,"Green")
			PK00 A 0 A_JumpIf(mass > 3,"Red")
		SpawnLoop:
			PK00 A 0 A_SetPitch(pitch+user_turndirection*8)
			PK00 A 0 A_SpawnItemEx("Painkiller_RingGlow")
			PK00 A 1 A_SetAngle(angle+user_turndirection*8)
			Loop
		Red:
			PK00 B 0 A_SetPitch(pitch+user_turndirection*8)
			PK00 B 0 A_SpawnItemEx("Painkiller_RingGlow")
			PK00 B 1 A_SetAngle(angle+user_turndirection*8)
			Loop
		Green:
			PK00 C 0 A_SetPitch(pitch+user_turndirection*8)
			PK00 C 0 A_SpawnItemEx("Painkiller_RingGlow")
			PK00 C 1 A_SetAngle(angle+user_turndirection*8)
			Loop
		Death:
			PK00 A 0 A_JumpIf(mass > 4,"DropGreen")
			PK00 A 0 A_JumpIf(mass > 3,"DropRed")
			TNT1 A 1 A_SpawnItemEx("Painkiller_Ring_Blue")
			Stop
		DropRed:
			TNT1 A 1 A_SpawnItemEx("Painkiller_Ring_Red")
			Stop
		DropGreen:
			TNT1 A 1 A_SpawnItemEx("Painkiller_Ring_Green")
			Stop
		Active:
			PK00 A 0 A_JumpIfInventory("PainkillerClass",1,1)
			Goto Spawn+3
			PK00 A 0 A_GiveInventory("Painkiller_Ring_Blue",1)
			Stop
	}
}

Actor Painkiller_Ring_Blue : CustomInventory
{
	Inventory.RestrictedTo "DanielGarner"
	+INVENTORY.QUIET
	+BRIGHT
	States
	{
		Spawn:
			PK00 A 0 NoDelay ACS_NamedExecuteWithResult("Painkiller_SoulCatcher",3)
			PK00 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("Painkiller_RingGlow")
			PK00 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("Painkiller_RingGlow")
			PK00 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("Painkiller_RingGlow")
			PK00 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("Painkiller_RingGlow")
			PK00 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("Painkiller_RingGlow")
			PK00 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("Painkiller_RingGlow")
			PK00 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("Painkiller_RingGlow")
			PK00 A 0 A_SpawnItemEx("Painkiller_RingGlow2",0,0,1)
			PK00 A 1 A_Fadeout(0.05)
			Wait
		Pickup:
			PK00 A 0 A_PlaySound("Painkiller/Ring/Take",CHAN_ITEM|CHAN_MAYBE_LOCAL,0.5)
			PK00 A 0 A_GiveInventory("Painkiller_GoldAmount",20)
			Stop
	}
}

Actor Painkiller_RingTarget : Painkiller_SoulTarget
{
	States
	{
		Spawn:
			TNT1 A 1
			TNT1 A 0 A_LookEx(LOF_NOSOUNDCHECK,0,256,0,360)
			Loop
		See:
			TNT1 A 0 A_JumpIfInTargetInventory("Painkiller_GoldMagnet_Power",1,1)
			Goto Spawn
			TNT1 A 1 A_FaceTarget
			TNT1 A 0 A_JumpIfCloser(16,"GiveCoin")
			TNT1 A 0 A_SkullAttack(16)
			Loop
		GiveCoin:
			TNT1 A 1 A_GiveToTarget("Painkiller_Ring_Blue",1)
			Stop
	}
}

Actor Painkiller_Ring_Red : Painkiller_Ring_Blue {}
Actor Painkiller_Ring_Green : Painkiller_Ring_Blue {}

Actor Painkiller_RingGlow : Painkiller_CoinGlow
{
	Scale 0.75
	Alpha 0.5
	States
	{
		Spawn:
			PK01 G 2
			Stop
	}
}

Actor Painkiller_RingGlow2 : Painkiller_CoinGlow
{
	Scale 0.75
	Alpha 0.5
	States
	{
		Spawn:
			PK01 G 2 A_Fadeout(0.1)
			Wait
	}
}

Actor Painkiller_CoinTosser
{
	Height 4
	Radius 2
	+NOINTERACTION
	+THRUACTORS
	-SOLID
	States
	{
		Spawn:
			TNT1 A 104
			TNT1 A 35 A_Warp(AAPTR_Master,0,0,32)
			Stop
	}
}
