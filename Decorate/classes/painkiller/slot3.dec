actor Painkiller_Boltgun : PainkillerWeapon
{
	Inventory.PickupMessage "Boltgun"
	Inventory.PickupSound "Painkiller/Boltgun/Pickup"
	Weapon.SlotNumber 3
	Weapon.SelectionOrder 700
	Weapon.BobRangeX 0
	Weapon.BobRangeY 1.2
	Weapon.AmmoType1 "Cell"
	Weapon.AmmoGive1 30
	Weapon.AmmoUse1 5 // originally took this total amount at once on fire, used here as a requirement to fire
	Weapon.AmmoType2 "Painkiller_HeaterAmmo"
	Weapon.AmmoGive2 20
	Weapon.AmmoUse2 10
	Obituary "%o was impaled by %k's boltgun."
	Tag "Boltgun/Heater"
	States
	{
		Spawn:
			PK99 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("Painkiller_Boltgun_Icon",0,0,40)
			PK99 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("Painkiller_Boltgun_Icon2",0,0,40)
			loop

		Ready:
			PK00 A 0 A_JumpIfInventory("Painkiller_Zoomed",1,"ReadyZoomed")
			PK00 A 0 A_JumpIfInventory("SamsaraHasDoubleFiringSpeed",1,2)
			PK00 A 1 A_WeaponReady(WRF_ALLOWZOOM)
			PK00 A 0 A_JumpIfInventory("Painkiller_Zoomed",1,"ReadyZoomOut1") // map change hack
		ReadyStart1:
			PK00 A 9 A_WeaponReady(WRF_ALLOWZOOM)
			PK00 BCDEFGHIJK 10 A_WeaponReady(WRF_ALLOWZOOM)
			goto Ready

		ReadyZoomOut1:
			PK00 A 0 A_TakeInventory("Painkiller_Zoomed")
			PK00 A 0 A_ZoomFactor(1,ZOOM_INSTANT)
			goto ReadyStart1

		ReadyZoomed:
			TNT1 A 1 A_WeaponReady(WRF_ALLOWZOOM)
			goto Ready

		Deselect:
			TNT1 A 0 A_JumpIfInventory("Painkiller_Zoomed",1,1)
			goto Super::Deselect
			TNT1 A 0 A_TakeInventory("Painkiller_Zoomed")
			TNT1 A 0 A_ZoomFactor(1,ZOOM_INSTANT)
			goto Super::Deselect

		Select:
			TNT1 A 0 A_TakeInventory("Painkiller_HoldingStake")
			TNT1 A 0 A_TakeInventory("Painkiller_HoldingShotgun")
			TNT1 A 0 A_GiveInventory("Painkiller_HoldingBolt")
			TNT1 A 0 A_TakeInventory("Painkiller_HoldingRifle")
			TNT1 A 0 A_TakeInventory("Painkiller_HoldingChaingun")
			TNT1 A 0 A_TakeInventory("Painkiller_HoldingElectro")
			TNT1 A 0 A_TakeInventory("Painkiller_HoldingDemon")
			TNT1 A 0 A_JumpIfInventory("Painkiller_Zoomed",1,1)
			goto Super::Select
			TNT1 A 0 A_TakeInventory("Painkiller_Zoomed")
			TNT1 A 0 A_ZoomFactor(1,ZOOM_INSTANT)
			goto Super::Select

		Fire:
			PK01 A 0 A_JumpIfInventory("Painkiller_Zoomed",1,"FireZoomed")
			PK01 A 2 A_GiveInventory("SamsaraPainkillerBoltgunPrimaryAttackHandler1")
			PK01 B 2
			PK01 C 2 A_GiveInventory("SamsaraPainkillerBoltgunPrimaryAttackHandler2")
			PK01 D 2
			PK01 E 2 A_GiveInventory("SamsaraPainkillerBoltgunPrimaryAttackHandler3")
			PK01 F 2
			PK02 A 2 A_PlaySound("Painkiller/BoltGun/BoltReload",CHAN_WEAPON)
			PK02 BCDFGHIKLMNOPQSTUVWXZ 2
			PK02 "[]" 2
			goto Ready

		FireZoomed:
			TNT1 A 2 A_GiveInventory("SamsaraPainkillerBoltgunPrimaryAttackHandler1")
			TNT1 A 2
			TNT1 A 2 A_GiveInventory("SamsaraPainkillerBoltgunPrimaryAttackHandler2")
			TNT1 A 2
			TNT1 A 2 A_GiveInventory("SamsaraPainkillerBoltgunPrimaryAttackHandler3")
			TNT1 A 2
			TNT1 A 2 A_PlaySound("Painkiller/BoltGun/BoltReload",CHAN_WEAPON)
			TNT1 AAAAAAAAAAAAAAAAAAAAA 2
			TNT1 AA 2
			goto Ready

		AltFire:
			PK03 A 0 A_JumpIfInventory("Painkiller_Zoomed",1,"AltFireZoomed")
			PK03 A 3 A_GiveInventory("SamsaraPainkillerBoltgunSecondaryAttackHandler")
			PK03 BCDEFGHIJKLM 3
			PK03 NOPQRST 3 A_WeaponReady
			PK03 U 2 A_WeaponReady
			goto Ready

		AltFireZoomed:
			TNT1 A 3 A_GiveInventory("SamsaraPainkillerBoltgunSecondaryAttackHandler")
			TNT1 AAAAAAAAAAAA 3
			TNT1 AAAAAAA 3 A_WeaponReady
			TNT1 A 2 A_WeaponReady
			goto Ready

		Zoom:
			TNT1 A 0 A_JumpIfInventory("Painkiller_Zoomed",1,"ZoomUnscope")
		ZoomScope:
			TNT1 A 0 A_PlaySound("Painkiller/BoltGun/ZoomIn",CHAN_WEAPON)
			TNT1 A 0 A_GiveInventory("Painkiller_Zoomed")
			TNT1 A 0 A_ZoomFactor(4)
			TNT1 A 8 ACS_NamedExecuteWithResult("Painkiller_BoltGunScopeGraphic")
			goto Ready

		ZoomUnscope:
			PK00 A 0 A_TakeInventory("Painkiller_Zoomed")
			PK00 A 8 A_ZoomFactor(1)
			goto Ready
	}
}

actor SamsaraPainkillerBoltgunPrimaryAttackHandler1 : Trigger
{
	States
	{
	Pickup:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"PickupMod")
		TNT1 A 0 A_JumpIfInventory("Painkiller_WeaponModifier_Token",1,"PickupMod")
	// Normal
	PickupNormal:
	// Ammo checking/taking stuff
	PickupAmmoCheckNormal:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttackNormal")
		TNT1 A 0 A_JumpIfInventory("Cell",1,1)
		stop
		TNT1 A 0 A_TakeInventory("Cell",1)
		goto PickupAttackNormal

	// Attack stuff
	PickupAttackNormal:
		TNT1 A 0 A_PlaySound("Painkiller/BoltGun/BoltShoot",CHAN_WEAPON)
		TNT1 A 0 A_JumpIf(CallACS("SamsaraDecorate", 48) == 1, "PickupAttackNormalPainkiller")
	// PainkillerStake damage type
	PickupAttackNormalPainkillerStake:
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackNormalPainkillerStakeCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackNormalPainkillerStakeCoop")
	PickupAttackNormalPainkillerStakeDM:
		TNT1 A 0 A_FireCustomMissile("Painkiller_BoltProjectile",0,false,0,2)
		stop
	PickupAttackNormalPainkillerStakeCoop:
		TNT1 A 0 A_FireCustomMissile("Painkiller_BoltProjectileCoop",0,false,0,2)
		stop

	// Painkiller (regular) damage type
	PickupAttackNormalPainkiller:
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackNormalPainkillerCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackNormalPainkillerCoop")
	PickupAttackNormalPainkillerDM:
		TNT1 A 0 A_FireCustomMissile("Painkiller_BoltProjectile_RegularDamageType",0,false,0,2)
		stop
	PickupAttackNormalPainkillerCoop:
		TNT1 A 0 A_FireCustomMissile("Painkiller_BoltProjectileCoop_RegularDamageType",0,false,0,2)
		stop

	// Weapon Modifier
	PickupMod:
	// Ammo checking/taking stuff
	PickupAmmoCheckMod:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttackMod")
		TNT1 A 0 A_JumpIfInventory("Cell",1,1)
		stop
		TNT1 A 0 A_TakeInventory("Cell",1)
		goto PickupAttackMod

	// Attack stuff
	PickupAttackMod:
		TNT1 A 0 A_PlaySound("Painkiller/BoltGun/BoltShoot",CHAN_WEAPON)
		TNT1 A 0 A_JumpIf(CallACS("SamsaraDecorate", 48) == 1, "PickupAttackModPainkiller")
	// PainkillerStake damage type
	PickupAttackModPainkillerStake:
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackModPainkillerStakeCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackModPainkillerStakeCoop")
	PickupAttackModPainkillerStakeDM:
		TNT1 A 0 A_FireCustomMissile("Painkiller_BoltProjectileMod",0,false,0,2)
		stop
	PickupAttackModPainkillerStakeCoop:
		TNT1 A 0 A_FireCustomMissile("Painkiller_BoltProjectileModCoop",0,false,0,2)
		stop

	// Painkiller (regular) damage type
	PickupAttackModPainkiller:
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackModPainkillerCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackModPainkillerCoop")
	PickupAttackModPainkillerDM:
		TNT1 A 0 A_FireCustomMissile("Painkiller_BoltProjectileMod_RegularDamageType",0,false,0,2)
		stop
	PickupAttackModPainkillerCoop:
		TNT1 A 0 A_FireCustomMissile("Painkiller_BoltProjectileModCoop_RegularDamageType",0,false,0,2)
		stop
	}
}

actor SamsaraPainkillerBoltgunPrimaryAttackHandler2 : Trigger
{
	States
	{
	Pickup:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"PickupMod")
		TNT1 A 0 A_JumpIfInventory("Painkiller_WeaponModifier_Token",1,"PickupMod")
	// Normal
	PickupNormal:
	// Ammo checking/taking stuff (right)
	PickupAmmoCheckNormalRight:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttackNormalRight")
		TNT1 A 0 A_JumpIfInventory("Cell",1,1)
		goto PickupAmmoCheckNormalLeft
		TNT1 A 0 A_TakeInventory("Cell",1)
		goto PickupAttackNormalRight

	// Attack stuff (right)
	PickupAttackNormalRight:
		TNT1 A 0 A_PlaySound("Painkiller/BoltGun/BoltShoot",CHAN_WEAPON)
		TNT1 A 0 A_JumpIf(CallACS("SamsaraDecorate", 48) == 1, "PickupAttackNormalRightPainkiller")
	// PainkillerStake damage type (right)
	PickupAttackNormalRightPainkillerStake:
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackNormalRightPainkillerStakeCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackNormalRightPainkillerStakeCoop")
	PickupAttackNormalRightPainkillerStakeDM:
		TNT1 A 0 A_FireCustomMissile("Painkiller_BoltProjectile",0,false,8,1)
		goto PickupAmmoCheckNormalLeft
	PickupAttackNormalRightPainkillerStakeCoop:
		TNT1 A 0 A_FireCustomMissile("Painkiller_BoltProjectileCoop",0,false,8,1)
		goto PickupAmmoCheckNormalLeft

	// Painkiller (regular) damage type (right)
	PickupAttackNormalRightPainkiller:
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackNormalRightPainkillerCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackNormalRightPainkillerCoop")
	PickupAttackNormalRightPainkillerDM:
		TNT1 A 0 A_FireCustomMissile("Painkiller_BoltProjectile_RegularDamageType",0,false,8,1)
		goto PickupAmmoCheckNormalLeft
	PickupAttackNormalRightPainkillerCoop:
		TNT1 A 0 A_FireCustomMissile("Painkiller_BoltProjectileCoop_RegularDamageType",0,false,8,1)
		goto PickupAmmoCheckNormalLeft

	// Ammo checking/taking stuff (left)
	PickupAmmoCheckNormalLeft:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttackNormalLeft")
		TNT1 A 0 A_JumpIfInventory("Cell",1,1)
		stop
		TNT1 A 0 A_TakeInventory("Cell",1)
		goto PickupAttackNormalLeft

	// Attack stuff (left)
	PickupAttackNormalLeft:
		TNT1 A 0 A_PlaySound("Painkiller/BoltGun/BoltShoot",CHAN_WEAPON)
		TNT1 A 0 A_JumpIf(CallACS("SamsaraDecorate", 48) == 1, "PickupAttackNormalLeftPainkiller")
	// PainkillerStake damage type (left)
	PickupAttackNormalLeftPainkillerStake:
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackNormalLeftPainkillerStakeCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackNormalLeftPainkillerStakeCoop")
	PickupAttackNormalLeftPainkillerStakeDM:
		TNT1 A 0 A_FireCustomMissile("Painkiller_BoltProjectile",0,false,-8,1)
		stop
	PickupAttackNormalLeftPainkillerStakeCoop:
		TNT1 A 0 A_FireCustomMissile("Painkiller_BoltProjectileCoop",0,false,-8,1)
		stop

	// Painkiller (regular) damage type (left)
	PickupAttackNormalLeftPainkiller:
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackNormalLeftPainkillerCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackNormalLeftPainkillerCoop")
	PickupAttackNormalLeftPainkillerDM:
		TNT1 A 0 A_FireCustomMissile("Painkiller_BoltProjectile_RegularDamageType",0,false,-8,1)
		stop
	PickupAttackNormalLeftPainkillerCoop:
		TNT1 A 0 A_FireCustomMissile("Painkiller_BoltProjectileCoop_RegularDamageType",0,false,-8,1)
		stop

	// Weapon Modifier
	PickupMod:
	// Ammo checking/taking stuff (right)
	PickupAmmoCheckModRight:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttackModRight")
		TNT1 A 0 A_JumpIfInventory("Cell",1,1)
		goto PickupAmmoCheckModLeft
		TNT1 A 0 A_TakeInventory("Cell",1)
		goto PickupAttackModRight

	// Attack stuff (right)
	PickupAttackModRight:
		TNT1 A 0 A_PlaySound("Painkiller/BoltGun/BoltShoot",CHAN_WEAPON)
		TNT1 A 0 A_JumpIf(CallACS("SamsaraDecorate", 48) == 1, "PickupAttackModRightPainkiller")
	// PainkillerStake damage type (right)
	PickupAttackModRightPainkillerStake:
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackModRightPainkillerStakeCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackModRightPainkillerStakeCoop")
	PickupAttackModRightPainkillerStakeDM:
		TNT1 A 0 A_FireCustomMissile("Painkiller_BoltProjectileMod",0,false,8,1)
		goto PickupAmmoCheckModLeft
	PickupAttackModRightPainkillerStakeCoop:
		TNT1 A 0 A_FireCustomMissile("Painkiller_BoltProjectileModCoop",0,false,8,1)
		goto PickupAmmoCheckModLeft

	// Painkiller (regular) damage type (right)
	PickupAttackModRightPainkiller:
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackModRightPainkillerCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackModRightPainkillerCoop")
	PickupAttackModRightPainkillerDM:
		TNT1 A 0 A_FireCustomMissile("Painkiller_BoltProjectileMod_RegularDamageType",0,false,8,1)
		goto PickupAmmoCheckModLeft
	PickupAttackModRightPainkillerCoop:
		TNT1 A 0 A_FireCustomMissile("Painkiller_BoltProjectileModCoop_RegularDamageType",0,false,8,1)
		goto PickupAmmoCheckModLeft

	// Ammo checking/taking stuff (left)
	PickupAmmoCheckModLeft:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttackModLeft")
		TNT1 A 0 A_JumpIfInventory("Cell",1,1)
		stop
		TNT1 A 0 A_TakeInventory("Cell",1)
		goto PickupAttackModLeft

	// Attack stuff (left)
	PickupAttackModLeft:
		TNT1 A 0 A_PlaySound("Painkiller/BoltGun/BoltShoot",CHAN_WEAPON)
		TNT1 A 0 A_JumpIf(CallACS("SamsaraDecorate", 48) == 1, "PickupAttackModLeftPainkiller")
	// PainkillerStake damage type (left)
	PickupAttackModLeftPainkillerStake:
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackModLeftPainkillerStakeCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackModLeftPainkillerStakeCoop")
	PickupAttackModLeftPainkillerStakeDM:
		TNT1 A 0 A_FireCustomMissile("Painkiller_BoltProjectileMod",0,false,-8,1)
		stop
	PickupAttackModLeftPainkillerStakeCoop:
		TNT1 A 0 A_FireCustomMissile("Painkiller_BoltProjectileModCoop",0,false,-8,1)
		stop

	// Painkiller (regular) damage type (left)
	PickupAttackModLeftPainkiller:
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackModLeftPainkillerCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackModLeftPainkillerCoop")
	PickupAttackModLeftPainkillerDM:
		TNT1 A 0 A_FireCustomMissile("Painkiller_BoltProjectileMod_RegularDamageType",0,false,-8,1)
		stop
	PickupAttackModLeftPainkillerCoop:
		TNT1 A 0 A_FireCustomMissile("Painkiller_BoltProjectileModCoop_RegularDamageType",0,false,-8,1)
		stop
	}
}

actor SamsaraPainkillerBoltgunPrimaryAttackHandler3 : Trigger
{
	States
	{
	Pickup:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"PickupMod")
		TNT1 A 0 A_JumpIfInventory("Painkiller_WeaponModifier_Token",1,"PickupMod")
	// Normal
	PickupNormal:
	// Ammo checking/taking stuff (right)
	PickupAmmoCheckNormalRight:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttackNormalRight")
		TNT1 A 0 A_JumpIfInventory("Cell",1,1)
		goto PickupAmmoCheckNormalLeft
		TNT1 A 0 A_TakeInventory("Cell",1)
		goto PickupAttackNormalRight

	// Attack stuff (right)
	PickupAttackNormalRight:
		TNT1 A 0 A_PlaySound("Painkiller/BoltGun/BoltShoot",CHAN_WEAPON)
		TNT1 A 0 A_JumpIf(CallACS("SamsaraDecorate", 48) == 1, "PickupAttackNormalRightPainkiller")
	// PainkillerStake damage type (right)
	PickupAttackNormalRightPainkillerStake:
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackNormalRightPainkillerStakeCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackNormalRightPainkillerStakeCoop")
	PickupAttackNormalRightPainkillerStakeDM:
		TNT1 A 0 A_FireCustomMissile("Painkiller_BoltProjectile",0,false,16)
		goto PickupAmmoCheckNormalLeft
	PickupAttackNormalRightPainkillerStakeCoop:
		TNT1 A 0 A_FireCustomMissile("Painkiller_BoltProjectileCoop",0,false,16)
		goto PickupAmmoCheckNormalLeft

	// Painkiller (regular) damage type (right)
	PickupAttackNormalRightPainkiller:
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackNormalRightPainkillerCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackNormalRightPainkillerCoop")
	PickupAttackNormalRightPainkillerDM:
		TNT1 A 0 A_FireCustomMissile("Painkiller_BoltProjectile_RegularDamageType",0,false,16)
		goto PickupAmmoCheckNormalLeft
	PickupAttackNormalRightPainkillerCoop:
		TNT1 A 0 A_FireCustomMissile("Painkiller_BoltProjectileCoop_RegularDamageType",0,false,16)
		goto PickupAmmoCheckNormalLeft

	// Ammo checking/taking stuff (left)
	PickupAmmoCheckNormalLeft:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttackNormalLeft")
		TNT1 A 0 A_JumpIfInventory("Cell",1,1)
		stop
		TNT1 A 0 A_TakeInventory("Cell",1)
		goto PickupAttackNormalLeft

	// Attack stuff (left)
	PickupAttackNormalLeft:
		TNT1 A 0 A_PlaySound("Painkiller/BoltGun/BoltShoot",CHAN_WEAPON)
		TNT1 A 0 A_JumpIf(CallACS("SamsaraDecorate", 48) == 1, "PickupAttackNormalLeftPainkiller")
	// PainkillerStake damage type (left)
	PickupAttackNormalLeftPainkillerStake:
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackNormalLeftPainkillerStakeCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackNormalLeftPainkillerStakeCoop")
	PickupAttackNormalLeftPainkillerStakeDM:
		TNT1 A 0 A_FireCustomMissile("Painkiller_BoltProjectile",0,false,-16)
		stop
	PickupAttackNormalLeftPainkillerStakeCoop:
		TNT1 A 0 A_FireCustomMissile("Painkiller_BoltProjectileCoop",0,false,-16)
		stop

	// Painkiller (regular) damage type (left)
	PickupAttackNormalLeftPainkiller:
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackNormalLeftPainkillerCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackNormalLeftPainkillerCoop")
	PickupAttackNormalLeftPainkillerDM:
		TNT1 A 0 A_FireCustomMissile("Painkiller_BoltProjectile_RegularDamageType",0,false,-16)
		stop
	PickupAttackNormalLeftPainkillerCoop:
		TNT1 A 0 A_FireCustomMissile("Painkiller_BoltProjectileCoop_RegularDamageType",0,false,-16)
		stop

	// Weapon Modifier
	PickupMod:
	// Ammo checking/taking stuff (right)
	PickupAmmoCheckModRight:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttackModRight")
		TNT1 A 0 A_JumpIfInventory("Cell",1,1)
		goto PickupAmmoCheckModLeft
		TNT1 A 0 A_TakeInventory("Cell",1)
		goto PickupAttackModRight

	// Attack stuff (right)
	PickupAttackModRight:
		TNT1 A 0 A_PlaySound("Painkiller/BoltGun/BoltShoot",CHAN_WEAPON)
		TNT1 A 0 A_JumpIf(CallACS("SamsaraDecorate", 48) == 1, "PickupAttackModRightPainkiller")
	// PainkillerStake damage type (right)
	PickupAttackModRightPainkillerStake:
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackModRightPainkillerStakeCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackModRightPainkillerStakeCoop")
	PickupAttackModRightPainkillerStakeDM:
		TNT1 A 0 A_FireCustomMissile("Painkiller_BoltProjectileMod",0,false,16)
		goto PickupAmmoCheckModLeft
	PickupAttackModRightPainkillerStakeCoop:
		TNT1 A 0 A_FireCustomMissile("Painkiller_BoltProjectileModCoop",0,false,16)
		goto PickupAmmoCheckModLeft

	// Painkiller (regular) damage type (right)
	PickupAttackModRightPainkiller:
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackModRightPainkillerCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackModRightPainkillerCoop")
	PickupAttackModRightPainkillerDM:
		TNT1 A 0 A_FireCustomMissile("Painkiller_BoltProjectileMod_RegularDamageType",0,false,16)
		goto PickupAmmoCheckModLeft
	PickupAttackModRightPainkillerCoop:
		TNT1 A 0 A_FireCustomMissile("Painkiller_BoltProjectileModCoop_RegularDamageType",0,false,16)
		goto PickupAmmoCheckModLeft

	// Ammo checking/taking stuff (left)
	PickupAmmoCheckModLeft:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttackModLeft")
		TNT1 A 0 A_JumpIfInventory("Cell",1,1)
		stop
		TNT1 A 0 A_TakeInventory("Cell",1)
		goto PickupAttackModLeft

	// Attack stuff (left)
	PickupAttackModLeft:
		TNT1 A 0 A_PlaySound("Painkiller/BoltGun/BoltShoot",CHAN_WEAPON)
		TNT1 A 0 A_JumpIf(CallACS("SamsaraDecorate", 48) == 1, "PickupAttackModLeftPainkiller")
	// PainkillerStake damage type (left)
	PickupAttackModLeftPainkillerStake:
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackModLeftPainkillerStakeCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackModLeftPainkillerStakeCoop")
	PickupAttackModLeftPainkillerStakeDM:
		TNT1 A 0 A_FireCustomMissile("Painkiller_BoltProjectileMod",0,false,-16)
		stop
	PickupAttackModLeftPainkillerStakeCoop:
		TNT1 A 0 A_FireCustomMissile("Painkiller_BoltProjectileModCoop",0,false,-16)
		stop

	// Painkiller (regular) damage type (left)
	PickupAttackModLeftPainkiller:
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackModLeftPainkillerCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackModLeftPainkillerCoop")
	PickupAttackModLeftPainkillerDM:
		TNT1 A 0 A_FireCustomMissile("Painkiller_BoltProjectileMod_RegularDamageType",0,false,-16)
		stop
	PickupAttackModLeftPainkillerCoop:
		TNT1 A 0 A_FireCustomMissile("Painkiller_BoltProjectileModCoop_RegularDamageType",0,false,-16)
		stop
	}
}

actor SamsaraPainkillerBoltgunSecondaryAttackHandler : Trigger
{
	States
	{
	// Ammo checking/taking stuff
	Pickup:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
		TNT1 A 0 A_JumpIfInventory("Painkiller_HeaterAmmo",10,1)
		stop
		TNT1 A 0 A_TakeInventory("Painkiller_HeaterAmmo",10)
		goto PickupAttack

	// Attack stuff
	PickupAttack:
		TNT1 A 0 A_PlaySound("Painkiller/BoltGun/HeaterFire",CHAN_WEAPON)
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
	PickupAttackDM:
		TNT1 AAAAAAAAAA 0 A_FireCustomMissile("Painkiller_HeaterBomb",0,false,random(-32,32),random(-12,12),0,10)
		stop
	PickupAttackCoop:
		TNT1 AAAAAAAAAA 0 A_FireCustomMissile("Painkiller_HeaterBombCoop",0,false,random(-32,32),random(-12,12),0,10)
		stop
	}
}

actor Painkiller_Boltgun_Icon : Painkiller_Weapon_Icon
{
	States
	{
		Spawn:
			PK07 "[" 2
			stop
	}
}

actor Painkiller_Boltgun_Icon2 : Painkiller_Weapon_Icon
{
	States
	{
		Spawn:
			PK07 "]" 2
			stop
	}
}

Actor Painkiller_BoltProjectile : FastProjectile
{
	+HITTRACER
	+NODAMAGETHRUST
	+NOEXTREMEDEATH
	+BLOODLESSIMPACT
	+FORCEXYBILLBOARD
	Decal "PainkillerStakeHole"
	DamageType "PainkillerStake"
	Speed 135
	Scale 0.5
	Damage (45)
	Height 4
	Radius 2
	States
	{
		Spawn:
			PK00 A 0
			//PK00 A 1
			//TNT1 A 0 A_ChangeFlag("THRUACTORS",false)
		SpawnLoop:
			TNT1 A 0 A_GiveInventory("Painkiller_StakeFlew")
			TNT1 A 0 ACS_NamedExecuteWithResult("Painkiller_ProjectilePitch")
			PK00 A 2 A_SpawnItemEx("Painkiller_StakeTrail",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)
			loop

		Death:
			TNT1 A 0 A_PlaySound("Painkiller/Stake/Default",CHAN_VOICE,0.5)
			TNT1 A 0 A_ChangeFlag("NOGRAVITY",true)
			TNT1 A 0 A_JumpIfInventory("Painkiller_StakeFlew",1,2)
			TNT1 A 0 A_SetPitch(-CallACS("Painkiller_ShooterPitch")/182)
			TNT1 A 0 A_SpawnItemEx("Painkiller_StakeHitWallEffect1",0,0,0,0,0,-0.15,0,SXF_NOCHECKPOSITION)
			TNT1 A 0 A_SpawnItemEx("Painkiller_StakeHitWallEffect2",0,0,0,0,0,frandom(0.15,0.5),0,SXF_NOCHECKPOSITION)
			PK00 B 105 A_GiveInventory("Painkiller_DamageCredit",1,AAPTR_TRACER)
			PK00 BBBBBBBBB 2 A_FadeOut(0.1)
			stop

		XDeath:
			TNT1 A 0 A_JumpIf(CallACS("Painkiller_StakeShatter") == 2, "Remove")
			TNT1 A 0 A_PlaySound("Painkiller/Stake/Body",CHAN_VOICE,0.5)
			TNT1 A 0 A_JumpIf(CallACS("Painkiller_StakeShatter") == 1, "SpawnAnother")
			TNT1 A 0 A_JumpIfInventory("Painkiller_StakeFlew",1,2)
			TNT1 A 0 A_SetPitch(-CallACS("Painkiller_ShooterPitch")/182)
			TNT1 A 0 A_SpawnItemEx("Painkiller_BoltDebris1",0,0,0,frandom(-2,2),frandom(-2,2),frandom(-2,2),0,SXF_NOCHECKPOSITION)
			TNT1 A 0 A_SpawnItemEx("Painkiller_BoltDebris2",0,0,0,frandom(-2,2),frandom(-2,2),frandom(-2,2),0,SXF_NOCHECKPOSITION)
			TNT1 A 0 A_SpawnItemEx("Painkiller_BoltDebris3",0,0,0,frandom(-2,2),frandom(-2,2),frandom(-2,2),0,SXF_NOCHECKPOSITION)
			TNT1 A 0 A_SpawnItemEx("Painkiller_BoltDebris4",0,0,0,frandom(-2,2),frandom(-2,2),frandom(-2,2),0,SXF_NOCHECKPOSITION)
			TNT1 A 0 A_SpawnItemEx("Painkiller_BoltDebris5",0,0,0,frandom(-2,2),frandom(-2,2),frandom(-2,2),0,SXF_NOCHECKPOSITION)
			TNT1 A 2 A_GiveInventory("Painkiller_DamageCredit",1,AAPTR_TRACER)
			stop

		SpawnAnother:
			TNT1 A 0 A_SetPitch(-CallACS("Painkiller_ShooterPitch")/182)
			TNT1 A 1 ACS_NamedExecuteWithResult("Painkiller_ForceStakeBodyRemoval")
			TNT1 A 5 A_CustomMissile("Painkiller_BoltIntermediate",0,0,0,CMF_AIMDIRECTION|CMF_SAVEPITCH,-pitch)
			stop

		Remove:
			TNT1 A 5
			stop
	}
}

Actor Painkiller_BoltProjectileCoop : Painkiller_BoltProjectile { +THRUSPECIES Species "Player" }

Actor Painkiller_BoltProjectileDM : Painkiller_BoltProjectile { Damage (30) } // actually 20, but that's way too weak

Actor Painkiller_BoltProjectileMod : Painkiller_BoltProjectile
{
	Damage (70) // 1.5x would be 67. Rounded up to 70
	Speed 162
	States
	{
		Spawn:
			PK00 A 0
			//PK00 A 1
			//TNT1 A 0 A_ChangeFlag("THRUACTORS",false)
		SpawnLoop:
			TNT1 A 0 A_GiveInventory("Painkiller_StakeFlew")
			TNT1 A 0 ACS_NamedExecuteWithResult("Painkiller_ProjectilePitch")
			TNT1 A 0 A_SpawnItemEx("Painkiller_BoltFire",0,0,8,frandom(-0.25,0.25),frandom(-0.25,0.25),frandom(0.25,0.5),0,SXF_NOCHECKPOSITION)
			PK00 A 2 A_SpawnItemEx("Painkiller_StakeTrail",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)
			loop
	}
}

Actor Painkiller_BoltProjectileModCoop : Painkiller_BoltProjectileMod { +THRUSPECIES Species "Player" }

Actor Painkiller_BoltFire : Painkiller_StakeFire { Scale 0.25 }

Actor Painkiller_BoltProjectileModDM : Painkiller_BoltProjectileMod { Damage (45) }

Actor Painkiller_BoltIntermediate
{
	-COUNTKILL
	-SOLID
	+THRUACTORS
	-SHOOTABLE
	Speed 0
	Height 0
	Radius 0
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_Stop
			TNT1 A 0 A_SetPitch(-CallACS("Painkiller_ShooterPitch")/182)
			//TNT1 A 0 A_SpawnItemEx("Painkiller_BoltProjectilePenetrate",0,0,0,cos(pitch)*135,0,-sin(pitch)*135,0,SXF_TRANSFERPOINTERS)
			TNT1 A 3 A_CustomMissile("Painkiller_BoltProjectilePenetrate",0,0,0,CMF_AIMDIRECTION|CMF_TRACKOWNER,-pitch)
			stop
	}
}

Actor Painkiller_BoltProjectilePenetrate : Painkiller_BoltProjectile
{
	+THRUACTORS
	States
	{
		Spawn:
			PK00 A 0
			TNT1 A 0 A_TransferPointer(AAPTR_TARGET,AAPTR_DEFAULT,AAPTR_TARGET,AAPTR_TARGET)
			TNT1 A 0 A_TransferPointer(AAPTR_TARGET,AAPTR_DEFAULT,AAPTR_TRACER,AAPTR_TRACER)
			TNT1 A 0 A_TransferPointer(AAPTR_TARGET,AAPTR_DEFAULT,AAPTR_TARGET,AAPTR_TARGET)
			TNT1 A 0 ACS_NamedExecuteWithResult("Painkiller_StakeRetrieveTracer")
			TNT1 A 0 A_GiveInventory("Painkiller_StakeFlew")
		SpawnLoop:
			TNT1 A 0 A_GiveInventory("Painkiller_StakeFlew")
			TNT1 A 0 ACS_NamedExecuteWithResult("Painkiller_ProjectilePitch")
			PK00 A 2 A_SpawnItemEx("Painkiller_StakeTrail",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)
			loop

		Death:
			TNT1 A 0 A_PlaySound("Painkiller/Stake/Default",CHAN_VOICE,0.5)
			TNT1 A 0 A_ChangeFlag("NOGRAVITY",true)
			TNT1 A 0 A_JumpIfInventory("Painkiller_StakeFlew",1,6)
			TNT1 A 0 A_TransferPointer(AAPTR_TARGET,AAPTR_DEFAULT,AAPTR_TARGET,AAPTR_TARGET)
			TNT1 A 0 A_SetPitch(CallACS("Painkiller_ShooterPitch")/182)
			TNT1 A 0 A_TransferPointer(AAPTR_TARGET,AAPTR_DEFAULT,AAPTR_TRACER,AAPTR_TRACER)
			TNT1 A 0 A_TransferPointer(AAPTR_TARGET,AAPTR_DEFAULT,AAPTR_TARGET,AAPTR_TARGET)
			TNT1 A 0 ACS_NamedExecuteWithResult("Painkiller_StakeRetrieveTracer")
			TNT1 A 0 A_SpawnItemEx("Painkiller_StakeHitWallEffect1",0,0,0,0,0,-0.15,0,SXF_NOCHECKPOSITION)
			TNT1 A 0 A_SpawnItemEx("Painkiller_StakeHitWallEffect2",0,0,0,0,0,frandom(0.15,0.5),0,SXF_NOCHECKPOSITION)
			PK00 B 105 A_GiveInventory("Painkiller_DamageCredit",1,AAPTR_TRACER)
			PK00 BBBBBBBBB 2 A_FadeOut(0.1)
			stop

		XDeath:
			TNT1 A 0 A_JumpIfInventory("Painkiller_StakeFlew",1,2)
			TNT1 A 0 A_SetPitch(-CallACS("Painkiller_ShooterPitch")/182)
			PK00 B 105 A_GiveInventory("Painkiller_DamageCredit",1,AAPTR_TRACER)
			PK00 BBBBBBBBB 2 A_FadeOut(0.1)
			stop

		SpawnAnother:
			PK00 B 105
			PK00 BBBBBBBBB 2 A_FadeOut(0.1)
			stop
	}
}

Actor Painkiller_BoltProjectile_RegularDamageType : Painkiller_BoltProjectile { DamageType "Painkiller" }
Actor Painkiller_BoltProjectileCoop_RegularDamageType : Painkiller_BoltProjectileCoop { DamageType "Painkiller" }
Actor Painkiller_BoltProjectileMod_RegularDamageType : Painkiller_BoltProjectileMod { DamageType "Painkiller" }
Actor Painkiller_BoltProjectileModCoop_RegularDamageType : Painkiller_BoltProjectileModCoop { DamageType "Painkiller" }

Actor Painkiller_HeaterBomb : PainkillerProjectile
{
	-NOGRAVITY
	+NODAMAGETHRUST
	+THRUSPECIES
	+BOUNCEONACTORS
	+ALLOWBOUNCEONACTORS
	+USEBOUNCESTATE
	+FORCERADIUSDMG
	-SKYEXPLODE
	DamageType "PainkillerExplosive"
	BounceType "Doom"
	BounceCount 30
	BounceFactor 0.6
	WallBounceFactor 0.5
	BounceSound "Painkiller/BoltGun/HeaterBounce"
	Species "PainkillerGrenade"
	//+THRUACTORS
	Scale 0.5
	Gravity 0.15
	Speed 20
	Damage (0)
	Height 4
	Radius 2
	ReactionTime 36
	var int user_pitch;
	var int user_timing;
	States
	{
		Spawn:
			PK00 B 0 NoDelay A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,2,AAPTR_TARGET)
			PK00 B 0 A_JumpIfInventory("Painkiller_WeaponModifier_Token",1,1,AAPTR_TARGET)
			goto Spawn2
			PK00 B 0 A_ChangeFlag(ALLOWBOUNCEONACTORS,false)
			PK00 B 0 A_ChangeFlag(BOUNCEONACTORS,false)
		Spawn2:
			TNT1 A 0 A_SetGravity(frandom(0.14,0.16))
			TNT1 A 0 A_ChangeVelocity(random(-2,2),0,0,CVF_RELATIVE)
			TNT1 A 0 A_SetUserVar("user_pitch",-CallACS("Painkiller_ShooterPitch")/182)
			TNT1 A 0 A_JumpIf(CallACS("Painkiller_SpawnGrenadeHitBox",2) == 1, 1)
			//TNT1 A 0 A_JumpIf(CallACS("Painkiller_GrenadeTidToken") == 1, 1)
			PK00 B 1
			//TNT1 A 0 A_SpawnItemEx("Painkiller_GrenadeHitBox",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
			PK00 B 2
			//PK00 A 1
			//TNT1 A 0 A_ChangeFlag("THRUACTORS",false)
		SpawnLoop:
			TNT1 A 0 A_Jump(32,2)
			TNT1 A 0 A_Countdown
			TNT1 A 0 A_SetUserVar("user_timing",user_timing+1)
			//TNT1 A 0 A_SetPitch(pitch+5)
			TNT1 A 0 ACS_NamedExecuteWithResult("Painkiller_ProjectilePitch")
			//TNT1 A 0 A_SpawnItemEx("Painkiller_Smoke",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
			TNT1 A 0 A_JumpIf(user_timing > 90,"SpawnGlow3")
			TNT1 A 0 A_JumpIf(user_timing > 30,"SpawnGlow2")
		SpawnGlow1:
			TNT1 A 0 A_SpawnItemEx("Painkiller_HeaterBombGlowEffect1",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)
			PK00 B 2 A_SpawnItemEx("Painkiller_GrenadeTrail",8,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)
			goto SpawnLoop

		SpawnGlow2:
			TNT1 A 0 A_ChangeFlag(ALLOWBOUNCEONACTORS,false)
			TNT1 A 0 A_ChangeFlag(BOUNCEONACTORS,false)
			TNT1 A 0 A_SpawnItemEx("Painkiller_HeaterBombGlowEffect2",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)
			PK00 B 2 A_SpawnItemEx("Painkiller_GrenadeTrail",8,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)
			goto SpawnLoop

		SpawnGlow3:
			TNT1 A 0 A_SpawnItemEx("Painkiller_HeaterBombGlowEffect3",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)
			PK00 B 2 A_SpawnItemEx("Painkiller_GrenadeTrail",8,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)
			goto SpawnLoop

		Launch:
			TNT1 A 0 A_SetUserVar("user_pitch",-CallACS("Painkiller_ShooterPitch")/182)
			TNT1 A 2 A_CustomMissile("Painkiller_HeaterBombLaunch",0,0,CallACS("Painkiller_ShooterAngle")/182,CMF_AIMDIRECTION|CMF_ABSOLUTEANGLE,user_pitch)
			stop

		Bounce.Actor.Creature:
			TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Countdown
		Death:
			TNT1 A 0 A_PlaySound("Painkiller/BoltGun/HeaterExplosion",CHAN_VOICE)
			TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,2,AAPTR_TARGET)
			TNT1 A 0 A_JumpIfInventory("Painkiller_WeaponModifier_Token",1,1,AAPTR_TARGET)
			goto DeathStart
			TNT1 A 0 A_Explode(7,45) // weapon mod = double damage AND explode on impact
		DeathStart:
			TNT1 A 0 A_Explode(14,45)
			TNT1 A 0 A_RadiusGive("Painkiller_DamageCredit",45,RGF_MONSTERS|RGF_NOSIGHT,1)
			TNT1 A 1 A_SpawnItemEx("Painkiller_HeaterBombExplodeEffectSpawner",0,0,0,0,0,0,0,SXF_CLIENTSIDE|SXF_NOCHECKPOSITION)
			stop
	}
}

Actor Painkiller_HeaterBombCoop : Painkiller_HeaterBomb
{
	+MTHRUSPECIES
	States
	{
		Launch:
			TNT1 A 0 A_SetUserVar("user_pitch",-CallACS("Painkiller_ShooterPitch")/182)
			TNT1 A 2 A_CustomMissile("Painkiller_HeaterBombLaunchCoop",0,0,CallACS("Painkiller_ShooterAngle")/182,CMF_AIMDIRECTION|CMF_ABSOLUTEANGLE,user_pitch)
			stop
	}
}

Actor Painkiller_HeaterBombDM : Painkiller_HeaterBomb
{
	States
	{
		Death:
			TNT1 A 0 A_PlaySound("Painkiller/BoltGun/HeaterExplosion",CHAN_VOICE)
			TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,2,AAPTR_TARGET)
			TNT1 A 0 A_JumpIfInventory("Painkiller_WeaponModifier_Token",1,1,AAPTR_TARGET)
			goto DeathStart
			TNT1 A 0 A_Explode(3,45) // weapon mod = double damage AND explode on impact
		DeathStart:
			TNT1 A 0 A_Explode(6,45) // was 20
			TNT1 A 0 A_RadiusGive("Painkiller_DamageCredit",45,RGF_MONSTERS|RGF_NOSIGHT,1)
			TNT1 A 1 A_SpawnItemEx("Painkiller_HeaterBombExplodeEffectSpawner",0,0,0,0,0,0,0,SXF_CLIENTSIDE|SXF_NOCHECKPOSITION)
			stop
	}
}

Actor Painkiller_HeaterBombGlowEffect1 : Painkiller_Smoke
{
	Scale 0.1
	RenderStyle Add
	Alpha 1
	States
	{
		Spawn:
			PK07 L 0
			PK07 LLLL 1 A_FadeOut(0.2)
			stop
	}
}

Actor Painkiller_HeaterBombGlowEffect2 : Painkiller_Smoke
{
	Scale 0.1
	RenderStyle Add
	Alpha 1
	States
	{
		Spawn:
			PK07 M 0
			PK07 MMMM 1 A_FadeOut(0.2)
			stop
	}
}

Actor Painkiller_HeaterBombGlowEffect3 : Painkiller_Smoke
{
	Scale 0.1
	RenderStyle Add
	Alpha 1
	States
	{
		Spawn:
			PK07 N 0
			PK07 NNNN 1 A_FadeOut(0.2)
			stop
	}
}

Actor Painkiller_HeaterBombExplodeEffectSpawner : Painkiller_VisualEffect
{
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_SpawnItemEx("Painkiller_HeaterBombExplodeEffect1",0,0,0,0,0,frandom(0.1,0.3),0,SXF_CLIENTSIDE|SXF_NOCHECKPOSITION)
			TNT1 A 0 A_SpawnItemEx("Painkiller_HeaterBombExplodeEffect2",0,0,0,0,0,frandom(0.25,0.5),0,SXF_CLIENTSIDE|SXF_NOCHECKPOSITION)
			TNT1 A 0 A_SpawnItemEx("Painkiller_HeaterBombExplodeEffect3",0,0,0,frandom(-0.25,0.25),frandom(-0.25,0.25),frandom(0.1,0.3),0,SXF_CLIENTSIDE|SXF_NOCHECKPOSITION)
			TNT1 A 2 A_SpawnItemEx("Painkiller_HeaterBombExplodeEffect2",0,0,0,frandom(-0.33,0.33),frandom(-0.33,0.33),frandom(0.25,0.5),0,SXF_CLIENTSIDE|SXF_NOCHECKPOSITION)
			TNT1 BCD 2
			TNT1 E 1
			stop
	}
}

Actor Painkiller_HeaterBombExplodeEffect1 : Painkiller_Smoke
{
	RenderStyle Translucent
	Alpha 0.5
	States
	{
		Spawn:
			PK07 O 0
			TNT1 A 0 A_SetScale(frandom(0.4,0.45))
			TNT1 A 0 A_SetScale(scalex+0.025)	PK07 O 1 A_FadeOut(0.05)
			TNT1 A 0 A_SetScale(scalex+0.025)	PK07 O 1 A_FadeOut(0.05)
			TNT1 A 0 A_SetScale(scalex+0.025)	PK07 O 1 A_FadeOut(0.05)
			TNT1 A 0 A_SetScale(scalex+0.025)	PK07 O 1 A_FadeOut(0.05)
			TNT1 A 0 A_SetScale(scalex+0.025)	PK07 O 1 A_FadeOut(0.05)
			TNT1 A 0 A_SetScale(scalex+0.025)	PK07 O 1 A_FadeOut(0.05)
			TNT1 A 0 A_SetScale(scalex+0.025)	PK07 O 1 A_FadeOut(0.05)
			TNT1 A 0 A_SetScale(scalex+0.025)	PK07 O 1 A_FadeOut(0.05)
			TNT1 A 0 A_SetScale(scalex+0.025)	PK07 O 1 A_FadeOut(0.05)
			stop
	}
}

Actor Painkiller_HeaterBombExplodeEffect2 : Painkiller_Smoke
{
	RenderStyle Add
	Alpha 1
	+BRIGHT
	States
	{
		Spawn:
			PK07 A 0
			TNT1 A 0 A_SetScale(frandom(0.4,0.45))
			TNT1 A 0 A_SetScale(scalex+0.025)	PK07 A 1 A_FadeOut(0.1)
			TNT1 A 0 A_SetScale(scalex+0.025)	PK07 A 1 A_FadeOut(0.1)
			TNT1 A 0 A_SetScale(scalex+0.025)	PK07 A 1 A_FadeOut(0.1)
			TNT1 A 0 A_SetScale(scalex+0.025)	PK07 A 1 A_FadeOut(0.1)
			TNT1 A 0 A_SetScale(scalex+0.025)	PK07 A 1 A_FadeOut(0.1)
			TNT1 A 0 A_SetScale(scalex+0.025)	PK07 A 1 A_FadeOut(0.1)
			TNT1 A 0 A_SetScale(scalex+0.025)	PK07 A 1 A_FadeOut(0.1)
			TNT1 A 0 A_SetScale(scalex+0.025)	PK07 A 1 A_FadeOut(0.1)
			TNT1 A 0 A_SetScale(scalex+0.025)	PK07 A 1 A_FadeOut(0.1)
			Stop
	}
}

Actor Painkiller_HeaterBombExplodeEffect3 : Painkiller_Smoke
{
	RenderStyle Translucent
	Alpha 0.6
	States
	{
		Spawn:
			PK07 O 0
			TNT1 A 0 A_SetScale(frandom(0.19,0.26))
			TNT1 A 0 A_SetScale(scalex+0.025)	PK07 O 1 A_FadeOut(0.06)
			TNT1 A 0 A_SetScale(scalex+0.025)	PK07 O 1 A_FadeOut(0.06)
			TNT1 A 0 A_SetScale(scalex+0.025)	PK07 O 1 A_FadeOut(0.06)
			TNT1 A 0 A_SetScale(scalex+0.025)	PK07 O 1 A_FadeOut(0.06)
			TNT1 A 0 A_SetScale(scalex+0.025)	PK07 O 1 A_FadeOut(0.06)
			TNT1 A 0 A_SetScale(scalex+0.025)	PK07 O 1 A_FadeOut(0.06)
			TNT1 A 0 A_SetScale(scalex+0.025)	PK07 O 1 A_FadeOut(0.06)
			TNT1 A 0 A_SetScale(scalex+0.025)	PK07 O 1 A_FadeOut(0.06)
			TNT1 A 0 A_SetScale(scalex+0.025)	PK07 O 1 A_FadeOut(0.06)
			Stop
	}
}

Actor Painkiller_BoltDebris1 : Painkiller_StakeDebris1 { Scale 2.0 }
Actor Painkiller_BoltDebris2 : Painkiller_BoltDebris1 {}
Actor Painkiller_BoltDebris3 : Painkiller_BoltDebris1 {}
Actor Painkiller_BoltDebris4 : Painkiller_BoltDebris1 {}
Actor Painkiller_BoltDebris5 : Painkiller_BoltDebris1 {}

Actor Painkiller_HeaterHitBox
{
	Height 8
	Radius 4
	Health 1
	Mass 50000
	-COUNTKILL
	//+THRUACTORS
	-SOLID
	+SHOOTABLE
	+NOBLOOD
	+NOGRAVITY
	DamageFactor "PainkillerStake", 1
	DamageFactor "Normal", 0
	Species "PainkillerGrenade"
	States
	{
		Spawn:
			TNT1 A 0
		SpawnLoop:
			TNT1 A 0 ACS_NamedExecuteWithResult("Painkiller_HitBoxRemover")
			TNT1 A 2 A_Warp(AAPTR_TRACER,0,0,0,0,WARPF_NOCHECKPOSITION)
			loop

		Death:
			TNT1 A 2
			stop

		Death.PainkillerStake:
			TNT1 A 2 ACS_NamedExecuteWithResult("Painkiller_StakeGrenadeComboLaunch")
			stop
	}
}

Actor Painkiller_HeaterBombLaunch : FastProjectile
{
	+HITTRACER
	+NODAMAGETHRUST
	+NOEXTREMEDEATH
	//+THRUACTORS
	DamageType "PainkillerExplosive"
	Speed 135
	Scale 0.5
	Damage (0)
	Height 4
	Radius 2
	States
	{
		Spawn:
			PK00 A 0
			//PK00 A 1
			//TNT1 A 0 A_ChangeFlag("THRUACTORS",false)
		SpawnLoop:
			TNT1 A 0 A_GiveInventory("Painkiller_StakeFlew")
			TNT1 A 0 ACS_NamedExecuteWithResult("Painkiller_ProjectilePitch")
			PK00 A 2 A_SpawnItemEx("Painkiller_StakeTrail",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)
			loop

		Death:
			TNT1 A 0 A_PlaySound("Painkiller/BoltGun/HeaterExplosion",CHAN_VOICE)
			TNT1 A 0 A_Explode(7,45)
			TNT1 A 0 A_RadiusGive("Painkiller_DamageCredit",45,RGF_MONSTERS|RGF_NOSIGHT,1)
			TNT1 A 1 A_SpawnItemEx("Painkiller_HeaterBombExplodeEffectSpawner",0,0,0,0,0,0,0,SXF_CLIENTSIDE|SXF_NOCHECKPOSITION)
			stop
	}
}

Actor Painkiller_HeaterBombLaunchCoop : Painkiller_HeaterBombLaunch { +THRUSPECIES Species "Player" }
