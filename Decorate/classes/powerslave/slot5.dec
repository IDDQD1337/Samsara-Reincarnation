actor PSCobraStaff : SamsaraPowerSlaveWeapon
{
    Inventory.PickupMessage "The Cobra Staff!"
    Weapon.SlotNumber 5
    Weapon.SelectionOrder 600
    Weapon.AmmoType "Rocketammo"
    Weapon.AmmoGive 20
    Weapon.AmmoUse 1
    Obituary "%o was devoured by %k's Cobra."
    Tag "Cobra Staff"
    +EXPLOSIVE
    States
    {
      Spawn:
        WCST A 15
        WCST BCDEF 5
        loop

      Ready:
        C_H_ Z 3 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
        C_H_ L 1 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
        TNT1 A 0 A_PlaySound("Jon/CobraHI",CHAN_WEAPON)
        C_H_ L 2 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
        C_H_ MN 3 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
        C_H_ OYXW 4 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      ReadyLoop:
        C_H_ V 1 A_WeaponReady
        loop

      Deselect:
        C_H_ U 1 A_Lower
        loop

      Select:
        C_H_ Z 1 A_Raise
        loop

      Fire:
        C_H_ U 1
      Hold:
        C_H_ A 1 Bright A_PlaySound("Jon/CobraStart")
        C_H_ BC 4 Bright
        C_H_ D 3 Bright
        C_H_ E 2 Bright
        C_H_ F 1 Bright A_GiveInventory("SamsaraPowerSlaveCobraStaffAttackHandler")
        C_H_ F 1 Bright Offset(0,38)
        C_H_ F 1 Bright Offset(0,39)
        C_H_ G 3 Bright Offset(0,37)
        C_H_ G 2 Bright Offset(0,40)
        C_H_ G 1 Bright Offset(0,38)
        C_H_ G 1 Bright Offset(0,36)
        C_H_ G 1 Bright Offset(0,30)
        C_H_ G 1 Bright Offset(0,26) A_ReFire
        goto ReadyLoop
    }
}

actor SamsaraPowerSlaveCobraStaffAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,1)
        stop
        TNT1 A 0 A_TakeInventory("RocketAmmo",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("Jon/CobraFire",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("CobraStaffSnake",0,false)
        goto PickupRecoil

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("CobraStaffSnake_2",0,false)
        goto PickupRecoil

      // Recoil stuff
      PickupRecoil:
        TNT1 A 0 A_Recoil(5)
        TNT1 A 0 A_Quake(5,2,0,16,"nosoundforyoubitch")
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 A_SpawnItemEx("CobraFakeFlash",cos(pitch)*55,0,40-(sin(pitch)*40),cos(pitch)*0,0,-sin(pitch)*0,0,SXF_CLIENTSIDE|SXF_TRANSFERPITCH)
        stop
    }
}

actor CobraFakeFlash
{
    Radius 1
    Height 1
    Scale 0.45
    +CLIENTSIDEONLY
    +FORCEXYBILLBOARD
    +NOINTERACTION
    States
    {
      Spawn:
        C_H2 G 2 Bright
        C_H2 H 1 Bright
        C_H2 H 1 A_FadeOut(0.05)
        stop
    }
}

actor CobraStaffSnake
{
    Projectile
    Radius 5
    Height 10
    Speed 23
    Damage (100)
    DamageType "PSExplosive"
    Scale 0.5
    DeathSound "Jon/CobraDeath"
    +SEEKERMISSILE
    +THRUGHOST
    States
    {
      Spawn:
        C_H2 A 0 Bright
        TNT1 A 0 A_PlaySound("Jon/CobraTrail",CHAN_VOICE,1,true)
        C_H2 AAAAA 1 Bright A_SpawnItemEx("CobraStaffTail",-12,0,0,0,0,0,0,SXF_CLIENTSIDE)
      SpawnLoop:
        C_H2 A 0 A_SpawnItemEx("CobraStaffTail",-12,0,0,0,0,0,0,SXF_CLIENTSIDE)
        C_H2 A 0 A_SeekerMissile(2,3)
        C_H2 A 1 Bright A_Weave(4,0,2,0)
        loop

      Death:
        TNT1 A 0 A_Jump(150,"Death2")
      Death1:
        C_H2 A 1 Bright
        C_H2 C 4 Bright A_Explode(200,90)
        C_H2 DEF 4 Bright
        stop

      Death2:
        C_H2 I 4 Bright A_Explode(200,90)
        C_H2 JKL 4 Bright
        C_H2 M 4 Bright A_FadeOut(0.05)
        stop
    }
}

actor CobraStaffSnake_2 : CobraStaffSnake
{
    Species "Player"
    -THRUGHOST
    +THRUSPECIES
}

actor CobraStaffTail
{
    Projectile
    Scale 0.5
    +CLIENTSIDEONLY
    +FORCEXYBILLBOARD
    States
    {
      Spawn:
        C_H2 B 2 Bright
        TNT1 A 0 A_SpawnItem("CobraStaffTail2")
        stop
    }
}

actor CobraStaffTail2 : CobraStaffTail
{
    Scale 0.4
    States
    {
      Spawn:
        C_H2 B 2 Bright
        TNT1 A 0 A_SpawnItem("CobraStaffTail3")
        stop
    }
}

actor CobraStaffTail3 : CobraStaffTail
{
    Scale 0.3
    States
    {
      Spawn:
        C_H2 B 2 Bright
        TNT1 A 0 A_SpawnItem("CobraStaffTail4")
        stop
    }
}

actor CobraStaffTail4 : CobraStaffTail
{
    Scale 0.2
    States
    {
      Spawn:
        C_H2 B 2 Bright
        TNT1 A 0 A_SpawnItem("CobraStaffTail5")
        stop
    }
}

actor CobraStaffTail5 : CobraStaffTail
{
    Scale 0.1
    States
    {
      Spawn:
        C_H2 B 2 Bright
        stop
    }
}
