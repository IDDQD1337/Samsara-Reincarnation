Actor "PSMagnum" : Weapon
{
	Inventory.PickupSound "PS/Ipickup2"
	+WEAPON.WIMPY_WEAPON
	+INVENTORY.UNDROPPABLE
	Weapon.SelectionOrder 3700
	Weapon.BobStyle Smooth
	Weapon.BobRangeX 0.75
	Weapon.BobRangeY 0.28
	Weapon.BobSpeed 1.4	
	Weapon.SlotNumber 1
	Weapon.SlotPriority 1
	Weapon.AmmoType "PSMagnumReload"
	Weapon.AmmoUse 1
	Obituary "%o killed %k with an ancient magnum."
	Inventory.RestrictedTo "PSPlayer"	
	Tag ".357 Magnum"
    +AMMO_OPTIONAL
    +NOALERT
	States
	{
		Spawn:
			WMAG A -1
			stop

		Select:
			MGNM A 1 A_Raise
			loop

		Deselect:
			MGNM A 1 A_Lower
			loop

		Ready:
            TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",0,"ReadyModernReloadable")
        ReadyNonReloadable:
			MGNM A 1 A_WeaponReady
			goto Ready

        ReadyModernReloadable:
            TNT1 A 0 A_JumpIfInventory("PSMagnumReload",0,"ReadyNonReloadable")
            TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,"ReadyModernReloadableReserve")
        ReadyModernReloadableCanReload:
            MGNM A 1 A_WeaponReady(WRF_ALLOWRELOAD)
            goto Ready

        ReadyModernReloadableReserve:
            TNT1 A 0 A_JumpIfInventory("SamsaraReloadOffPlus",1,2)
            TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReadyModernReloadableCanReload")
            TNT1 A 0 A_JumpIfInventory("Clip",1,"ReadyModernReloadableCanReload")
            goto ReadyNonReloadable

		Fire:
            TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"FireReloadable")
        FireNonReloadable:
            TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
            goto FireActual
            TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireActual")
            TNT1 A 0 A_JumpIfInventory("Clip",1,"FireActual")
            goto FireDry

        FireReloadable:
            TNT1 A 0 A_JumpIfInventory("SamsaraReloadOffPlus",1,"FireActual")
            TNT1 A 0 A_JumpIfInventory("PSMagnumReload",1,"FireActual")
        FireReloadableDryCheck:
            TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
            goto Reload
            TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"Reload")
            TNT1 A 0 A_JumpIfInventory("Clip",1,"Reload")
            goto FireDry

        FireDry:
            MGNM A 5 A_PlaySound("PS/MagnumE")
            goto Ready

        FireActual:
            TNT1 A 0 A_AlertMonsters
            TNT1 A 0 A_GiveInventory("SamsaraPowerSlaveMagnumAttackHandler")
			MGNM B 1 Bright A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
			MGNM C 1 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
			MGNM D 1 Offset(2,41) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
			MGNM D 1 Offset(3,47) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
			MGNM D 1 Offset(5,55) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
			MGNM D 1 Offset(4,56) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
			MGNM D 1 Offset(3,57) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
			MGNM C 1 Offset(-4,54) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
			MGNM C 1 Offset(-3,53) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
			MGNM C 1 Offset(-2,52) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
			MGNM A 1 Offset(-1,51) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
			MGNM A 1 Offset(0,50) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
			MGNM A 1 Offset(0,47) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
			MGNM A 1 Offset(0,43) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
			MGNM A 1 Offset(0,37) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
			TNT1 A 0 A_ReFire
			MGNM A 1 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
			goto Ready

		Reload:
			TNT1 A 0 A_PlaySound("PS/MagnumR")
			MGNM A 1 Offset(-6,39) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
			MGNM A 1 Offset(-10,74) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
			MGNM A 1 Offset(-12,87) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
			MGNM A 1 Offset(-16,93) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
			MGNM A 1 Offset(-22,102) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
			MGNM A 1 Offset(-34,120) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
			MGNM A 4 Offset(-36,143) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
			TNT1 A 0 A_PlaySound("PS/MagnumR")
			MGNM A 1 Offset(-32,137) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
			MGNM A 1 Offset(-30,124) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
			MGNM A 1 Offset(-26,118) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
			MGNM A 1 Offset(-20,102) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
			MGNM A 1 Offset(-18,96) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
			MGNM A 1 Offset(-14,89) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
			MGNM A 1 Offset(-8,72) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
			MGNM A 1 Offset(-6,38) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
            TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReloadAmmo")
			goto Ready

        ReloadAmmo:
            TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,"ReloadAmmoReserve")
            TNT1 A 0 A_JumpIfInventory("PSMagnumReload",0,"Ready")
            TNT1 A 0 A_GiveInventory("PSMagnumReload")
            loop

        ReloadAmmoReserve:
            TNT1 A 0 A_JumpIfInventory("PSMagnumReload",0,"Ready")
            TNT1 A 0 A_JumpIfInventory("SamsaraReloadOffPlus",1,2)
            TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
            TNT1 A 0 A_JumpIfInventory("Clip",1,1)
            goto Ready
            TNT1 A 0 A_JumpIfInventory("SamsaraReloadOffPlus",1,2)
            TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
            TNT1 A 0 A_TakeInventory("Clip",1)
            TNT1 A 0 A_GiveInventory("PSMagnumReload")
            goto ReloadAmmo
	}
}

actor SamsaraPowerSlaveMagnumAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"PickupReloadable")
      PickupNonReloadable:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto PickupAttack
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",1)
        goto PickupAttack

      PickupReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadOffPlus",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("PSMagnumReload",1,1)
        stop
        TNT1 A 0 A_TakeInventory("PSMagnumReload",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("PS/Magnum",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireBullets(2,2,-1,25,"PSBulletPuff",FBF_NORANDOM|FBF_NOFLASH)
        goto PickupFinish

      PickupAttackCoop:
        TNT1 A 0 A_FireBullets(2,2,-1,25,"PSBulletPuffCoop",FBF_NORANDOM|FBF_NOFLASH)
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 A_Quake(1,2,0,16,"")
        stop
    }
}

actor PSMagnumReload : Ammo
{
    Inventory.MaxAmount 6
    Tag ".357 Magnum Cylinder"
    +IGNORESKILL
}

Actor PSBullet1 : FastProjectile
{
  Speed 300
  Damage (25)
  Radius 1
  Height 1
  Species "Player"  
  Projectile
  +BLOODSPLATTER
  +NOTIMEFREEZE
  +NOEXTREMEDEATH
  +THRUSPECIES
  +DONTHARMCLASS
  +DONTHARMSPECIES
  +MTHRUSPECIES
  States
  {
  Spawn:
    TNT1 A 1
    Loop
  Death:
  Crash:  
	TNT1 A 0 A_PlaySound("PS/Ricoch")
    TNT1 A 1 A_SpawnItemEx("PSBulletPuff",0,0,0,0)        
    stop
  XDeath:
    TNT1 A 1
    stop
    }
}
