actor ExShotgun : SamsaraPowerSlaveWeapon
{
    Inventory.PickupMessage "An Ancient Shotgun!"
    Weapon.SlotNumber 2
    Weapon.SelectionOrder 1000
    Weapon.AmmoType1 "PSShotgunReload"
    Weapon.AmmoUse1 1
    Weapon.AmmoType2 "Shell"
    Weapon.AmmoGive2 8
    Obituary "%o was killed by %k."
    Tag "Shotgun"
    +AMMO_OPTIONAL
    +NOALERT
    States
    {
      Spawn:
        EX_T A -1
        stop

      Ready:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",0,"ReadyAllowReload")
      ReadyDisallowReload:
        EXSH A 1 A_WeaponReady
        goto Ready

      ReadyAllowReload:
        TNT1 A 0 A_JumpIfInventory("PSShotgunReload",0,"ReadyDisallowReload")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Shell",1,1)
        goto ReadyDisallowReload
        EXSH A 1 A_WeaponReady(WRF_ALLOWRELOAD)
        goto Ready

      Deselect:
        EXSH A 1 A_Lower
        loop

      Select:
        EXSH A 1 A_Raise
        loop

      Fire:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"FireReloadable")
      FireNonReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart")
        TNT1 A 0 A_JumpIfInventory("Shell",1,"FireStart")
        goto NoAmmoSwitch

      FireReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireStart")
        TNT1 A 0 A_JumpIfInventory("PSShotgunReload",1,"FireStart")
      FireReloadableDryCheck:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"Reload")
        TNT1 A 0 A_JumpIfInventory("Shell",1,"Reload")
        goto NoAmmoSwitch

      FireStart:
        EXSH A 1 A_AlertMonsters
        EXSH B 2 Bright Offset(-1,36) A_GiveInventory("SamsaraPowerSlaveShotgunAttackHandler")
        EXSH B 1 Bright Offset(0,37)
        EXSH C 1 Bright Offset(-1,36)
        EXSH C 1 Bright Offset(0,37)
        EXSH C 1 Bright Offset(1,38)
        EXSH C 1 Bright Offset(2,39)
        EXSH N 1 Offset(3,40)
        EXSH N 1 Offset(2,39)
        EXSH N 1 Offset(1,38)
        EXSH O 1 Offset(1,37)
        EXSH O 2 Offset(0,37)
        EXSH O 3 Offset(0,32)
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"FireContinueReloadable")
      FireContinueNonReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireContinue")
        TNT1 A 0 A_JumpIfInventory("Shell",1,"FireContinue")
        goto NoAmmoSwitch

      FireContinueReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireContinue")
        TNT1 A 0 A_JumpIfInventory("PSShotgunReload",1,"FireContinue")
      FireContinueReloadableSwitchCheck:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"Reload")
        TNT1 A 0 A_JumpIfInventory("Shell",1,"Reload")
        goto NoAmmoSwitch

      FireContinue:
        EXSH F 4
        EXSH G 5 A_PlaySound("Jon/ShotgunCock")
        EXSH D 4
        EXSH G 5
        EXSH F 3
      FireFinish:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"FireFinishReloadable")
      FireFinishNonReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireFinishReFire")
        TNT1 A 0 A_JumpIfInventory("Shell",1,"FireFinishReFire")
        goto NoAmmoSwitch

      FireFinishReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireFinishReFire")
        TNT1 A 0 A_JumpIfInventory("PSShotgunReload",1,"FireFinishReFire")
      FireFinishReloadableSwitchCheck:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireFinishReFire")
        TNT1 A 0 A_JumpIfInventory("Shell",1,"FireFinishReFire")
        goto NoAmmoSwitch

      FireFinishReFire:
        EXSH A 1 A_ReFire
        goto Ready

      Reload:
        TNT1 A 0 A_PlaySound("Jon/SReload1")
        EXSH HI 3
      ReloadCasings:
        TNT1 A 0 A_JumpIfInventory("PSShotgunShellsUsed",1,1)
        goto ReloadStart
        TNT1 A 0 A_FireCustomMissile("ExShellCasing",-45+frandom(-10.00,10.00),false,1,8+frandom(-1.10,1.10))
        TNT1 A 0 A_TakeInventory("PSShotgunShellsUsed",1)
        loop

      ReloadStart:
        EXSH J 3
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"ReloadAmmo")
        goto ReloadLoopAnim

      ReloadLoopStart:
        EXSH J 2
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"ReloadAmmo")
      ReloadLoopAnim:
        TNT1 A 0 A_PlaySound("Jon/SReload2")
        EXSH K 1
        EXSH L 2
        EXSH M 1
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",0,"ReloadLoopFireCheck")
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"ReloadLoopCheck")
      ReloadFinish:
        EXSH I 2
        EXSH H 3
        EXSH A 8
        goto Ready

      ReloadLoopFireCheck:
        //TNT1 A 0 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE) // works in gzdoom, but not zan. blah.
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        TNT1 A 0 A_JumpIfInventory("PSShotgunReload",1,1)
        goto ReloadLoopCheck
        TNT1 A 0 A_ReFire
      ReloadLoopCheck:
        TNT1 A 0 A_JumpIfInventory("PSShotgunReload",0,"ReloadFinish")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReloadLoopStart")
        TNT1 A 0 A_JumpIfInventory("Shell",1,"ReloadLoopStart")
        goto ReloadFinish

      ReloadAmmo:
        TNT1 A 0 A_TakeInventory("SamsaraReloadCalcs") // reset
      ReloadAmmoCalc:
        TNT1 A 0 A_JumpIfInventory("PSShotgunReload",0,"ReloadLoopAnim")
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadCalcs",1,"ReloadLoopAnim")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Shell",1,1)
        goto ReloadLoopAnim
        TNT1 A 0 A_GiveInventory("SamsaraReloadCalcs")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_TakeInventory("Shell",1)
        TNT1 A 0 A_GiveInventory("PSShotgunReload")
        loop

      // "No ammo" switch
      NoAmmoSwitch:
        TNT1 A 0 A_ClearReFire
        TNT1 A 0 A_SelectWeapon("PSMagnum")
        goto Deselect
    }
}

actor SamsaraPowerSlaveShotgunAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"PickupReloadable")
      PickupNonReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Shell",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Shell",1)
        goto PickupAttack

      PickupReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("PSShotgunReload",1,1)
        stop
        TNT1 A 0 A_TakeInventory("PSShotgunReload",1)
        TNT1 A 0 A_GiveInventory("PSShotgunShellsUsed")
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("Jon/ShotgunFire",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireBullets(4,4,7,15,"PSBulletPuff",FBF_NORANDOM|FBF_NOFLASH)
        goto PickupFinish

      PickupAttackCoop:
        TNT1 A 0 A_FireBullets(4,4,7,15,"PSBulletPuffCoop",FBF_NORANDOM|FBF_NOFLASH)
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 A_Quake(3,2,0,16,"nosoundforyoubitch")
        stop
    }
}

actor PSShotgunReload : Ammo
{
    Inventory.MaxAmount 8
    Tag "Shotgun Magazine"
    +IGNORESKILL
}

actor PSShotgunShellsUsed : Counter {}
