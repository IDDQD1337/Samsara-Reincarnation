actor SamsaraPowerSlaveFlamethrowerFiringSoundCheck : Boolean {}

actor PSFlamethrower : SamsaraPowerSlaveWeapon
{
    Inventory.PickupMessage "The Flame-Thrower!"
    Weapon.UpSound "Jon/FTUp"
    Weapon.SlotNumber 3
    Weapon.SelectionOrder 900
    Weapon.AmmoType "Shell"
    Weapon.AmmoGive 15
    Weapon.AmmoUse 1
    Obituary "%k made a BBQ out of %o."
    Tag "Flamethrower"
    States
    {
      Spawn:
        WFTR A -1
        stop

      Ready:
        TNT1 A 0 A_TakeInventory("SamsaraPowerSlaveFlamethrowerFiringSoundCheck")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReadyAmmo")
        TNT1 A 0 A_JumpIfInventory("Shell",1,"ReadyAmmo")
        goto ReadyNoAmmo

      ReadyNoAmmo:
        PSFT A 1 A_WeaponReady
        goto Ready

      ReadyAmmo:
        EXF0 RRRSSSTTTUUU 1 A_WeaponReady
        goto Ready

      Deselect:
        TNT1 A 0 A_GiveInventory("SamsaraPowerSlaveFlamethrowerFiringSoundStopper")
        TNT1 A 0 A_PlaySound("Jon/FTDown",CHAN_WEAPON)
      DeselectLoop:
        TNT1 A 0 A_GiveInventory("SamsaraPowerSlaveFlamethrowerFiringSoundStopper")
        PSFT A 1 A_Lower
        wait

      Select:
        TNT1 A 0 A_TakeInventory("SamsaraPowerSlaveFlamethrowerFiringSoundCheck")
        PSFT A 1 A_Raise
        loop

      Fire:
        PSFT EF 1
      Hold:
        TNT1 A 0 A_TakeInventory("SamsaraTakingAmmo")
        PSFT B 2 Bright Offset(1,38) A_GiveInventory("SamsaraPowerSlaveFlamethrowerAttackHandler1")
        PSFT A 2 Bright Offset(0,54) A_GiveInventory("SamsaraPowerSlaveFlamethrowerAttackHandler2")
        PSFT B 2 Bright Offset(-1,38) A_GiveInventory("SamsaraPowerSlaveFlamethrowerAttackHandler2")
        TNT1 A 0 A_GiveInventory("SamsaraTakingAmmo")
        PSFT B 2 Bright Offset(0,37) A_GiveInventory("SamsaraPowerSlaveFlamethrowerAttackHandler2")
      HoldFinish:
        PSFT A 1 A_ReFire
        PSFT A 1 A_GiveInventory("SamsaraPowerSlaveFlamethrowerFiringSoundStopper")
        EXF0 L 2 A_PlaySound("Jon/FTSteam",CHAN_WEAPON)
        EXF0 MNOPQ 2
        PSFT A 2
        goto Ready
    }
}

actor SamsaraPowerSlaveFlamethrowerAttackHandler1 : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Shell",1,1)
        stop
        TNT1 A 0 A_JumpIfInventory("SamsaraTakingAmmo",1,1)
        goto PickupAttack
        TNT1 A 0 A_TakeInventory("Shell",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("Jon/FTFire1",CHAN_WEAPON,1,true)
        TNT1 A 0 A_GiveInventory("SamsaraPowerSlaveFlamethrowerFiringSoundCheck")
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("PSFTProjA",0,false,0,-4)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("PSFTProjA_2",0,false,0,-4)
        stop
    }
}

actor SamsaraPowerSlaveFlamethrowerAttackHandler2 : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Shell",1,1)
        stop
        TNT1 A 0 A_JumpIfInventory("SamsaraTakingAmmo",1,1)
        goto PickupAttack
        TNT1 A 0 A_TakeInventory("Shell",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("Jon/FTFire1",CHAN_WEAPON,1,true)
        TNT1 A 0 A_GiveInventory("SamsaraPowerSlaveFlamethrowerFiringSoundCheck")
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 AA 0 A_FireCustomMissile("PSFTProjA_B",0,false,0,-4)
        TNT1 A 0 A_FireCustomMissile("PSFTProjA",0,false,0,-4)
        stop

      PickupAttackCoop:
        TNT1 AA 0 A_FireCustomMissile("PSFTProjA_B_2",0,false,0,-4)
        TNT1 A 0 A_FireCustomMissile("PSFTProjA_2",0,false,0,-4)
        stop
    }
}

actor SamsaraPowerSlaveFlamethrowerFiringSoundStopper : Trigger
{
    States
    {
      // Sound stopping stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraPowerSlaveFlamethrowerFiringSoundCheck",1,1)
        stop
        TNT1 A 0 A_PlaySound("silence",CHAN_WEAPON)
        TNT1 A 0 A_StopSound(CHAN_WEAPON)
        TNT1 A 0 A_TakeInventory("SamsaraPowerSlaveFlamethrowerFiringSoundCheck")
        stop
    }
}

actor PSFTProjA
{
    Projectile
    Radius 4
    Height 5
    Speed 24
    Damage (5)
    DamageType "PSFire"
    //RenderStyle "Add"
    //Alpha 0.8
    Scale 0.4
    +THRUGHOST
    States
    {
      Spawn:
        EXF0 A 0 Bright
        TNT1 A 0 A_Jump(224,"SpawnStart")
        TNT1 A 0 A_ChangeFlag("PAINLESS",true)
      SpawnStart:
        EXF0 A 20 Bright
        TNT1 A 0 A_SpawnItemEx("PSFireDeath",0,0,0,0,0,0,SXF_CLIENTSIDE)
        TNT1 A 1 A_SpawnItemEx("PSFTProjB",0,0,0,0,0,0,SXF_CLIENTSIDE)
        stop

      Death:
        TNT1 A 0 A_Explode(10,10,0)
      DeathFinish:
        TNT1 A 1 A_SpawnItemEx("PSFireDeath",0,0,0,0,0,0,SXF_CLIENTSIDE)
        stop
    }
}

actor PSFTProjA_B : PSFTProjA
{
    States
    {
      Death:
        goto DeathFinish
    }
}

actor PSFTProjA_2 : PSFTProjA
{
    Species "Player"
    +THRUSPECIES
}

actor PSFTProjA_B_2 : PSFTProjA_B
{
    Species "Player"
    +THRUSPECIES
}

actor PSFireDeath
{
    +CLIENTSIDEONLY
    +NOINTERACTION
    -SOLID
    Scale 0.6
    States
    {
      Spawn:
        EXF0 BCDEFG 2 Bright
        stop
    }
}

actor PSFTProjB
{
    Projectile
    Radius 3
    Height 4
    Speed 20
    //RenderStyle "Add"
    //Alpha 0.9
    Scale 0.5
    +CLIENTSIDEONLY
    States
    {
      Spawn:
        EXF0 HIJK 2 Bright
        stop
    }
}
