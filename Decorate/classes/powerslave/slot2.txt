Actor ExShotgun : Weapon
{
  Inventory.PickupSound "PS/Ipickup2"
  inventory.pickupmessage "You got the shotgun!"
  Obituary "%o was killed by %k."
  Weapon.selectionorder 1000
  Weapon.BobStyle Smooth
  Weapon.BobRangeX 0.75
  Weapon.BobRangeY 0.28
  Weapon.BobSpeed 1.4
  Weapon.AmmoType1 "PSShotgunReload"
  Weapon.AmmoUse1 1
  Weapon.AmmoType2 "Shell"
  Weapon.AmmoGive2 8
  Weapon.SlotNumber 2
  Inventory.RestrictedTo "PSPlayer"
  Tag "Shotgun"
  +AMMO_OPTIONAL
  +NOALERT
  States
  {
  Spawn:
    EX_T A -1
    stop

  Ready:
    TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",0,"ReadyModernReloadable")
  ReadyNonReloadable:
    EXSH A 1 A_WeaponReady
    goto Ready

  ReadyModernReloadable:
    TNT1 A 0 A_JumpIfInventory("PSShotgunReload",0,"ReadyNonReloadable")
    TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
    TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
    TNT1 A 0 A_JumpIfInventory("Shell",1,1)
    goto ReadyNonReloadable
    EXSH A 1 A_WeaponReady(WRF_ALLOWRELOAD)
    goto Ready

  Deselect: 
    EXSH A 1 A_Lower
    loop

  Select:
    EXSH A 1 A_Raise
    loop

  Fire:
    TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"FireReloadable")
  FireNonReloadable:
    TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireActual")
    TNT1 A 0 A_JumpIfInventory("Shell",1,"FireActual")
    goto NoAmmoSwitch

  FireReloadable:
    TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireActual")
    TNT1 A 0 A_JumpIfInventory("PSShotgunReload",1,"FireActual")
  FireReloadableDryCheck:
    TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"Reload")
    TNT1 A 0 A_JumpIfInventory("Shell",1,"Reload")
    goto NoAmmoSwitch

  FireActual:
    EXSH A 1 A_AlertMonsters
	EXSH B 2 Offset(-1,36) Bright A_GiveInventory("SamsaraPowerSlaveShotgunAttackHandler")
	EXSH B 1 Offset(0,37) Bright
    EXSH C 1 Offset(-1,36) Bright
    EXSH C 1 Offset(0,37) Bright
    EXSH C 1 Offset(1,38) Bright
    EXSH C 1 Offset(2,39) Bright
    EXSH N 1 Offset(3,40)
    EXSH N 1 Offset(2,39)
    EXSH N 1 Offset(1,38)
    EXSH O 1 Offset(1,37)
    EXSH O 2 Offset(0,37)
	EXSH O 3 Offset(0,32)
	TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"FireContinueReloadable")
  FireContinueNonReloadable:
    TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireContinue")
    TNT1 A 0 A_JumpIfInventory("Shell",1,"FireContinue")
    goto NoAmmoSwitch

  FireContinueReloadable:
    TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireContinue")
    TNT1 A 0 A_JumpIfInventory("PSShotgunReload",1,"FireContinue")
  FireContinueReloadableSwitchCheck:
    TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"Reload")
    TNT1 A 0 A_JumpIfInventory("Shell",1,"Reload")
    goto NoAmmoSwitch

  FireContinue:
	EXSH A 10
	TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"FireFinishReloadable")
  FireFinishNonReloadable:
    TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireFinish")
    TNT1 A 0 A_JumpIfInventory("Shell",1,"FireFinish")
    goto NoAmmoSwitch

  FireFinishReloadable:
    TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireFinish")
    TNT1 A 0 A_JumpIfInventory("PSShotgunReload",1,"FireFinish")
  FireFinishReloadableSwitchCheck:
    TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireFinish")
    TNT1 A 0 A_JumpIfInventory("Shell",1,"FireFinish")
    goto NoAmmoSwitch

  FireFinish:
    EXSH A 1 A_ReFire
    goto Ready

  Reload:
	TNT1 A 0 A_PlaySound("Jon/SReload1")
	EXSH HI 3 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
  ReloadCasings:
    TNT1 A 0 A_JumpIfInventory("PSShotgunShellsUsed",1,1)
    goto ReloadStart
	TNT1 A 0 A_FireCustomMissile("ExShellCasing",-45+frandom(-10.00,10.00),false,1,8+frandom(-1.10,1.10))
    TNT1 A 0 A_TakeInventory("PSShotgunShellsUsed",1)
    loop

  ReloadStart:
	EXSH J 3 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
    TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"ReloadAmmo")
    goto ReloadLoopAnim

  ReloadLoopStart:
	EXSH J 2 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
    TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"ReloadAmmo")
  ReloadLoopAnim:
	TNT1 A 0 A_PlaySound("Jon/SReload2")
	EXSH K 1 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
	EXSH L 2 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
	EXSH M 1 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
    TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",0,"ReloadLoopFireCheck")
    TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"ReloadLoopCheck")
  ReloadFinish:
	EXSH I 2 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
	EXSH H 3 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
	EXSH A 8 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
	goto Ready

  ReloadLoopFireCheck:
    //TNT1 A 0 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE) // works in gzdoom, but not zan. blah.
    TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
    TNT1 A 0 A_JumpIfInventory("PSShotgunReload",1,1)
    goto ReloadLoopCheck
    TNT1 A 0 A_ReFire
  ReloadLoopCheck:
    TNT1 A 0 A_JumpIfInventory("PSShotgunReload",0,"ReloadFinish")
    TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
    TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReloadLoopStart")
    TNT1 A 0 A_JumpIfInventory("Shell",1,"ReloadLoopStart")
    goto ReloadFinish

  ReloadAmmo:
    TNT1 A 0 A_TakeInventory("SamsaraReloadCalcs") // reset
  ReloadAmmoCalc:
    TNT1 A 0 A_JumpIfInventory("PSShotgunReload",0,"ReloadLoopAnim")
    TNT1 A 0 A_JumpIfInventory("SamsaraReloadCalcs",1,"ReloadLoopAnim")
    TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
    TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
    TNT1 A 0 A_JumpIfInventory("Shell",1,1)
    goto ReloadLoopAnim
    TNT1 A 0 A_GiveInventory("SamsaraReloadCalcs")
    TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
    TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
    TNT1 A 0 A_TakeInventory("Shell",1)
    TNT1 A 0 A_GiveInventory("PSShotgunReload")
    loop

  // "No ammo" switch
  NoAmmoSwitch:
    TNT1 A 0 A_ClearReFire
    TNT1 A 0 A_SelectWeapon("PSMagnum")
    goto Deselect
  }
}

actor SamsaraPowerSlaveShotgunAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"PickupReloadable")
      PickupNonReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Shell",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Shell",1)
        goto PickupAttack

      PickupReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("PSShotgunReload",1,1)
        stop
        TNT1 A 0 A_TakeInventory("PSShotgunReload",1)
        TNT1 A 0 A_GiveInventory("PSShotgunShellsUsed")
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("Jon/ShotgunFire",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireBullets(4,4,7,15,"PSBulletPuff",FBF_NORANDOM|FBF_NOFLASH)
        goto PickupFinish

      PickupAttackCoop:
        TNT1 A 0 A_FireBullets(4,4,7,15,"PSBulletPuffCoop",FBF_NORANDOM|FBF_NOFLASH)
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 A_Quake(3,2,0,16,"")
        stop
    }
}

actor PSShotgunReload : Ammo
{
    Inventory.MaxAmount 8
    Tag "Shotgun Magazine"
    +IGNORESKILL
}

actor PSShotgunShellsUsed : Counter {}

Actor PSBullet2 : PSBullet1 { Damage (15) }
