actor Goldeneye_PP7Silenced : SamsaraGoldenEyeWeapon
{
    Inventory.PickupMessage "Picked up a silenced PP7."
    Weapon.SlotNumber 1
    Weapon.SlotPriority 1
    Weapon.SelectionOrder 16
    Weapon.AmmoType "Goldeneye_PP7Magazine"
    Weapon.AmmoUse 1
    Obituary "%o was one of the many victims of %k's Walther PP7."
    Tag "PP7 Special Issue (Silenced)"
    +WIMPY_WEAPON
    States
    {
      Spawn:
        PIST A -1
        stop

      // Single
      Ready:
        TNT1 A 0 A_JumpIfInventory("BondWatchQueue",1,"WatchRaise")
        TNT1 A 0 A_JumpIfInventory("BondWatchToken",1,"WatchReady")
        TNT1 A 0 A_JumpIfInventory("BondAltToggle",1,"DualWield")
      ReadyStart:
        GE00 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"ReadyReloadable")
      ReadyNonReloadable:
        GE00 A 1 A_WeaponReady
        goto ReadyAmmoCheck

      ReadyReloadable:
        GE00 A 1 A_WeaponReady(WRF_ALLOWRELOAD)
        goto ReadyAmmoCheck

      ReadyAmmoCheck:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",2,1)
        goto Ready
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"Ready")
        TNT1 A 0 A_JumpIfInventory("Goldeneye_PP7Magazine",1,"Ready")
        TNT1 A 0 A_JumpIfInventory("BondPrimaryToken",1,"Ready")
        goto ReloadCheck

      Deselect:
        GE00 A 0 A_JumpIfInventory("BondDualWieldToken",1,1)
        goto DeselectLoop
        GE00 A 0 A_GunFlash("DeselectDual",GFF_NOEXTCHANGE)
      DeselectLoop:
        GE00 A 0 A_TakeInventory("BondFireDelay")
        GE00 A 0 A_TakeInventory("BondAlternateFire")
        GE00 A 0 A_TakeInventory("BondDualWieldToken")
        GE00 A 0 A_TakeInventory("BondAltToggle")
        GE00 A 0 A_TakeInventory("BondZoomToken")
        GE00 A 0 A_TakeInventory("BondZoomAmountModifyActive")
        GE00 A 0 A_TakeInventory("BondDualWieldSpamProtector")
        GE00 A 0 A_TakeInventory("BondDryFireSpamProtector1")
        GE00 A 0 A_TakeInventory("BondDryFireSpamProtector2")
        GE00 A 0 A_TakeInventory("BondAltActivationToken")
        GE00 A 1 A_Lower
        loop

      Select:
        GE00 A 0 A_PlaySound("Goldeneye/Weaponup",CHAN_7)
      SelectDualCheck:
        GE00 A 0 A_JumpIfInventory("BondDualWieldToken",1,1)
        goto SelectLoop
        GE00 A 0 A_GunFlash("SelectDual",GFF_NOEXTCHANGE)
      SelectLoop:
        GE00 A 0 A_TakeInventory("Goldeneye_UnarmedToken")
        GE00 A 0 A_GiveInventory("Goldeneye_PP7Token")
        GE00 A 0 A_TakeInventory("Goldeneye_KnifeToken")
        GE00 A 0 A_TakeInventory("Goldeneye_KF7Token")
        GE00 A 0 A_TakeInventory("Goldeneye_AutoShotgunToken")
        GE00 A 0 A_TakeInventory("Goldeneye_AR33Token")
        GE00 A 0 A_TakeInventory("Goldeneye_RocketLauncherToken")
        GE00 A 0 A_TakeInventory("Goldeneye_RCP90Token")
        GE00 A 0 A_TakeInventory("Goldeneye_LaserToken")
        GE00 A 0 A_TakeInventory("Goldeneye_GoldenGunToken")
        GE00 A 0 A_TakeInventory("Goldeneye_SniperToken")
        GE00 A 0 A_TakeInventory("Goldeneye_D5KToken")
        GE00 A 1 A_Raise
        goto SelectDualCheck

      Fire:
        GE01 B 0 A_JumpIfInventory("BondSecondaryToken",1,1)
        goto FireAltDualCheck
        GE01 B 0 A_JumpIfInventory("BondAltActivationToken",1,"FireAltDualCheck")
        GE01 B 0 A_TakeInventory("BondAltToggle")
        GE01 B 0 A_GiveInventory("BondAltActivationToken")
        GE01 B 0 ACS_NamedExecuteAlways("BondAltFire")
        loop

      FireAltDualCheck:
        GE01 B 0 A_JumpIfInventory("BondAltToggle",1,"DualWieldFire")
      FireCheckDualOrder:
        GE01 B 0 A_JumpIfInventory("BondDualWieldToken",1,1)
        goto FireAmmoCheck
        GE01 B 0 A_JumpIfInventory("BondAlternateFire",1,"Ready")
        goto FireAmmoCheck

      FireAmmoCheck:
        GE01 B 0 A_JumpIfInventory("SamsaraReloadMode",2,"FireAmmoCheckReloadable")
      FireAmmoCheckNonReloadable:
        GE01 B 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto FireStart
        GE01 B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart")
        GE01 B 0 A_JumpIfInventory("Clip",1,"FireStart")
        goto FireDry

      FireAmmoCheckReloadable:
        GE01 B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireStart")
        GE01 B 0 A_JumpIfInventory("Goldeneye_PP7Magazine",1,"FireStart")
        goto FireDry

      FireDry:
        GE00 A 0 A_JumpIfInventory("BondDryFireSpamProtector1",1,"FireSetDualOrder")
        GE00 A 0 A_GiveInventory("SamsaraGoldenEyeWeaponDrySoundPlayer")
        GE00 A 0 ACS_NamedExecuteAlways("BondDryFireSpamBlocker_Primary")
        GE00 A 0 A_GiveInventory("BondDryFireSpamProtector1")
        goto FireSetDualOrder

      FireStart:
        GE01 B 0 A_GiveInventory("BondFiringToken")
        GE01 B 1 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        GE01 C 0 A_JumpIfInventory("SamsaraReloadMode",2,"FireStartReloadable")
      FireStartNonReloadable:
        GE01 C 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto FireAttack
        GE01 C 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireAttack")
        GE01 C 0 A_JumpIfInventory("Clip",1,"FireAttack")
        goto FireContinue

      FireStartReloadable:
        GE01 C 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireAttack")
        GE01 C 0 A_JumpIfInventory("Goldeneye_PP7Magazine",1,"FireAttack")
        goto FireContinue

      FireAttack:
        GE01 C 0 A_SpawnItemEx("Goldeneye_Cartridge_Standard",cos(pitch)*22,6,42-(sin(pitch)*22),(2*cos(pitch))+(2*sin(pitch)),frandom(2,3),abs(4*cos(pitch)),0,SXF_TRANSFERPITCH)
      FireContinue:
        GE01 C 0 A_GiveInventory("SamsaraGoldenEyePP7SilencedSingleAttackHandler")
        GE01 C 0 A_TakeInventory("BondFiringToken")
        GE01 C 0 ACS_NamedExecuteWithResult("BondSemiAuto")
        GE01 CDED 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        GE01 C 0 A_JumpIfInventory("BondGunFiringFullAuto",1,2)
        GE01 C 0 A_ReFire("FireSetDualOrder")
        GE01 C 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        GE00 A 0 A_JumpIfInventory("BondGunFiringFullAuto",1,2)
        GE00 A 0 A_ReFire("FireSetDualOrder")
        GE00 A 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        GE00 A 0 A_JumpIfInventory("BondGunFiringFullAuto",1,2)
        GE00 A 0 A_ReFire("FireSetDualOrder")
        GE00 A 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        GE00 A 0 A_JumpIfInventory("BondGunFiringFullAuto",1,2)
        GE00 A 0 A_ReFire("FireSetDualOrder")
        GE00 A 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        GE00 A 0 A_JumpIfInventory("BondGunFiringFullAuto",1,2)
        GE00 A 0 A_ReFire("FireSetDualOrder")
        GE00 A 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        GE00 A 0 A_JumpIfInventory("BondGunFiringFullAuto",1,2)
        GE00 A 0 A_ReFire("FireSetDualOrder")
        GE00 A 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        GE00 A 0 A_JumpIfInventory("BondGunFiringFullAuto",1,2)
        GE00 A 0 A_ReFire("FireSetDualOrder")
        GE00 A 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto FireSetDualOrder

      FireSetDualOrder:
        TNT1 A 0 A_JumpIfInventory("BondDualWieldToken",1,1)
        goto FireFinish
        TNT1 A 0 A_GiveInventory("BondAlternateFire")
        goto FireFinish

      FireFinish:
        TNT1 A 0 A_TakeInventory("BondFiringToken")
        goto Ready

      Reload:
        TNT1 A 0 A_JumpIfInventory("BondPrimaryToken",1,"ReloadCheck")
        TNT1 A 0 A_JumpIfInventory("BondDualWieldToken",1,1)
        goto ReloadCheck
        TNT1 A 0 A_JumpIfInventory("Goldeneye_PP7DualMagazine",0,"ReloadCheck")
        TNT1 A 0 A_JumpIfInventory("BondDualReloadSpamProtector",1,"ReloadCheck")
        TNT1 A 0 A_GunFlash("ReloadCheckDual",GFF_NOEXTCHANGE)
        goto ReloadCheck

      ReloadCheck:
        TNT1 A 0 A_JumpIfInventory("Goldeneye_PP7Magazine",0,"Ready")
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto ReloadStart
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReloadStart")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"ReloadStart")
        goto Ready

      ReloadStart:
        GE02 A 0 A_GiveInventory("SamsaraGoldenEyeWeaponReloadSoundPlayer")
        GE02 ABCDEFGHI 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        GE02 I 0 A_JumpIfInventory("SamsaraReloadMode",2,"ReloadAmmo")
      ReloadFinish:
        GE02 IHGFEDCBA 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto Ready

      ReloadAmmo:
        GE02 I 0 A_JumpIfInventory("PistolModeOn",1,"ReloadAmmoReserve")
        GE02 I 0 A_JumpIfInventory("Goldeneye_PP7Magazine",0,"ReloadFinish")
        GE02 I 0 A_GiveInventory("Goldeneye_PP7Magazine")
        loop

      ReloadAmmoReserve:
        GE02 I 0 A_JumpIfInventory("Goldeneye_PP7Magazine",0,"ReloadFinish")
        GE02 I 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        GE02 I 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        GE02 I 0 A_JumpIfInventory("Clip",1,1)
        goto ReloadFinish
        GE02 I 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        GE02 I 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        GE02 I 0 A_TakeInventory("Clip",1)
        GE02 I 0 A_GiveInventory("Goldeneye_PP7Magazine")
        goto ReloadAmmo

      // Dual Init
      AltFire:
        TNT1 A 0 A_JumpIfInventory("BondAltActivationToken",1,"Ready")
        TNT1 A 0 A_TakeInventory("BondAltToggle")
        TNT1 A 0 A_GiveInventory("BondAltActivationToken")
        TNT1 A 0 ACS_NamedExecuteAlways("BondAltFire")
        goto Ready

      DualUnwield:
        TNT1 A 0 A_TakeInventory("BondAlternateFire")
        TNT1 A 0 A_JumpIfInventory("BondDualWieldSpamProtector",1,5)
        TNT1 A 0 A_TakeInventory("BondDualWieldToken")
        TNT1 A 0 A_TakeInventory("BondDryFireSpamProtector1")
        TNT1 A 0 A_TakeInventory("BondDryFireSpamProtector2")
        TNT1 A 0 A_GunFlash("DualLower",GFF_NOEXTCHANGE)
        TNT1 A 0 A_ZoomFactor(1)
        goto ReadyStart

      DualUnwieldFire:
        GE01 B 0 A_TakeInventory("BondAlternateFire")
        GE01 B 0 A_JumpIfInventory("BondDualWieldSpamProtector",1,5)
        GE01 B 0 A_TakeInventory("BondDualWieldToken")
        GE01 B 0 A_TakeInventory("BondDryFireSpamProtector1")
        GE01 B 0 A_TakeInventory("BondDryFireSpamProtector2")
        GE01 B 0 A_GunFlash("DualLower",GFF_NOEXTCHANGE)
        GE01 B 0 A_ZoomFactor(1)
        goto FireCheckDualOrder

      DualLower:
        GE12 A 0 A_TakeInventory("BondDualReloadSpamProtector")
        GE12 A 1 A_GiveInventory("BondDualWieldSpamProtector")
        GE12 BCDEFGHI 1
        TNT1 A 0 A_TakeInventory("BondDualWieldSpamProtector")
        stop

      DualWield:
        TNT1 A 0 A_TakeInventory("BondAltActivationToken")
        TNT1 A 0 A_TakeInventory("BondAltToggle")
        TNT1 A 0 A_TakeInventory("BondZoomToken")
        TNT1 A 0 A_JumpIfInventory("Goldeneye_PP7DualToken",1,1)
        goto ReadyStart
        TNT1 A 0 A_JumpIfInventory("BondDualWieldToken",1,"DualUnwield")
        TNT1 A 0 A_JumpIfInventory("BondDualWieldSpamProtector",1,3)
        TNT1 A 0 A_GiveInventory("BondDualWieldToken")
        TNT1 A 0 A_GunFlash("DualRaise",GFF_NOEXTCHANGE)
        TNT1 A 0 A_ZoomFactor(1)
        goto ReadyStart

      DualWieldFire:
        GE01 B 0 A_TakeInventory("BondAltActivationToken")
        GE01 B 0 A_TakeInventory("BondAltToggle")
        GE01 B 0 A_TakeInventory("BondZoomToken")
        GE01 B 0 A_JumpIfInventory("Goldeneye_PP7DualToken",1,1)
        goto FireCheckDualOrder
        GE01 B 0 A_JumpIfInventory("BondDualWieldToken",1,"DualUnwieldFire")
        GE01 B 0 A_JumpIfInventory("BondDualWieldSpamProtector",1,3)
        GE01 B 0 A_GiveInventory("BondDualWieldToken")
        GE01 B 0 A_GunFlash("DualRaise",GFF_NOEXTCHANGE)
        GE01 B 0 A_ZoomFactor(1)
        goto FireCheckDualOrder

      DualRaise:
        GE12 I 0 A_TakeInventory("BondDualReloadSpamProtector")
        GE12 I 0 A_GiveInventory("BondDualWieldSpamProtector")
        GE12 I 1 A_PlaySound("Goldeneye/Weaponup",CHAN_7)
        GE12 HGFEDCBA 1
        TNT1 A 0 A_TakeInventory("BondDualWieldSpamProtector")
        goto ReadyStartDual

      // Dual
      ReadyDual:
        TNT1 A 0 A_JumpIfInventory("BondAlternateFire",1,1)
        goto ReadyStartDual
        TNT1 A 0 A_JumpIfInventory("BondPrimaryToken",1,"FireDual")
      ReadyStartDual:
        GE10 A 1
        goto ReadyAmmoCheckDual

      ReadyAmmoCheckDual:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",2,1)
        goto ReadyDual
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"ReadyDual")
        TNT1 A 0 A_JumpIfInventory("Goldeneye_PP7DualMagazine",1,"ReadyDual")
        TNT1 A 0 A_JumpIfInventory("BondPrimaryToken",1,"ReadyDual")
        goto ReloadCheckDual

      DeselectDual:
        GE10 A 0 A_TakeInventory("BondDualReloadSpamProtector")
        GE10 A 1 A_TakeInventory("BondDualWieldSpamProtector")
        GE10 AAAAAAAAAAAAAAA 1
        stop

      SelectDual:
        GE10 A 0 A_TakeInventory("BondDualReloadSpamProtector")
        GE10 A 1 A_TakeInventory("BondDualWieldSpamProtector")
        goto ReadyStartDual

      FireDual:
        GE11 B 0 A_JumpIfInventory("BondAlternateFire",1,"FireAmmoCheckDual")
        goto ReadyDual

      FireAmmoCheckDual:
        GE11 B 0 A_JumpIfInventory("SamsaraReloadMode",2,"FireAmmoCheckDualReloadable")
      FireAmmoCheckDualNonReloadable:
        GE11 B 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto FireStartDual
        GE11 B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStartDual")
        GE11 B 0 A_JumpIfInventory("Clip",1,"FireStartDual")
        goto FireDryDual

      FireAmmoCheckDualReloadable:
        GE11 B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireStartDual")
        GE11 B 0 A_JumpIfInventory("Goldeneye_PP7DualMagazine",1,"FireStartDual")
        goto FireDryDual

      FireDryDual:
        GE10 A 0 A_JumpIfInventory("BondDryFireSpamProtector2",1,"FireSetDualOrderDual")
        GE10 A 0 A_GiveInventory("SamsaraGoldenEyeWeaponDrySoundPlayer")
        GE10 A 0 ACS_NamedExecuteAlways("BondDryFireSpamBlocker_Secondary")
        GE10 A 0 A_GiveInventory("BondDryFireSpamProtector2")
        goto FireSetDualOrderDual

      FireStartDual:
        GE11 B 1 Bright A_GiveInventory("BondFiringToken")
        GE11 C 0 A_JumpIfInventory("SamsaraReloadMode",2,"FireStartDualReloadable")
      FireStartDualNonReloadable:
        GE11 C 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto FireDualAttack
        GE11 C 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireDualAttack")
        GE11 C 0 A_JumpIfInventory("Clip",1,"FireDualAttack")
        goto FireDualContinue

      FireStartDualReloadable:
        GE11 C 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireDualAttack")
        GE11 C 0 A_JumpIfInventory("Goldeneye_PP7DualMagazine",1,"FireDualAttack")
        goto FireDualContinue

      FireDualAttack:
        GE11 C 0 A_SpawnItemEx("Goldeneye_Cartridge_Standard",cos(pitch)*22,-6,42-(sin(pitch)*22),(2*cos(pitch))+(2*sin(pitch)),frandom(2,3),abs(4*cos(pitch)),0,SXF_TRANSFERPITCH)
      FireDualContinue:
        GE11 C 0 A_GiveInventory("SamsaraGoldenEyePP7SilencedDualAttackHandler")
        GE11 C 0 A_TakeInventory("BondFiringToken")
        GE11 C 1 ACS_NamedExecuteWithResult("BondSemiAuto")
        GE11 DED 1
        GE11 C 0 A_JumpIfInventory("BondGunFiringFullAuto",1,2)
        GE11 C 0 A_JumpIfInventory("SamsaraSynthFireLeft",1,"FireSetDualOrderDual")
        GE11 C 1
        GE10 A 0 A_JumpIfInventory("BondGunFiringFullAuto",1,2)
        GE10 A 0 A_JumpIfInventory("SamsaraSynthFireLeft",1,"FireSetDualOrderDual")
        GE10 A 1
        GE10 A 0 A_JumpIfInventory("BondGunFiringFullAuto",1,2)
        GE10 A 0 A_JumpIfInventory("SamsaraSynthFireLeft",1,"FireSetDualOrderDual")
        GE10 A 1
        GE10 A 0 A_JumpIfInventory("BondGunFiringFullAuto",1,2)
        GE10 A 0 A_JumpIfInventory("SamsaraSynthFireLeft",1,"FireSetDualOrderDual")
        GE10 A 1
        GE10 A 0 A_JumpIfInventory("BondGunFiringFullAuto",1,2)
        GE10 A 0 A_JumpIfInventory("SamsaraSynthFireLeft",1,"FireSetDualOrderDual")
        GE10 A 1
        GE10 A 0 A_JumpIfInventory("BondGunFiringFullAuto",1,2)
        GE10 A 0 A_JumpIfInventory("SamsaraSynthFireLeft",1,"FireSetDualOrderDual")
        GE10 A 1
        GE10 A 0 A_JumpIfInventory("BondGunFiringFullAuto",1,2)
        GE10 A 0 A_JumpIfInventory("SamsaraSynthFireLeft",1,"FireSetDualOrderDual")
        GE10 A 1
        goto FireSetDualOrderDual

      FireSetDualOrderDual:
        TNT1 A 0 A_TakeInventory("BondAlternateFire")
        goto FireFinishDual

      FireFinishDual:
        TNT1 A 0 A_TakeInventory("BondFiringToken")
        goto ReadyDual

      ReloadCheckDual:
        TNT1 A 0 A_TakeInventory("BondDualWieldSpamProtector")
        TNT1 A 0 A_JumpIfInventory("Goldeneye_PP7DualMagazine",0,"ReadyDual")
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto ReloadStartDual
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReloadStartDual")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"ReloadStartDual")
        goto ReadyDual

      ReloadStartDual:
        GE12 A 0 A_GiveInventory("BondDualReloadSpamProtector")
        GE12 A 1 A_GiveInventory("SamsaraGoldenEyeWeaponReloadSoundPlayer")
        GE12 BCDEFGHI 1
        GE12 I 0 A_JumpIfInventory("SamsaraReloadMode",2,"ReloadAmmoDual")
      ReloadFinishDual:
        GE12 IHGFEDCBA 1
        TNT1 A 0 A_TakeInventory("BondDualReloadSpamProtector")
        goto ReadyDual

      ReloadAmmoDual:
        GE12 I 0 A_JumpIfInventory("PistolModeOn",1,"ReloadAmmoReserveDual")
        GE12 I 0 A_JumpIfInventory("Goldeneye_PP7DualMagazine",0,"ReloadFinishDual")
        GE12 I 0 A_GiveInventory("Goldeneye_PP7DualMagazine")
        loop

      ReloadAmmoReserveDual:
        GE12 I 0 A_JumpIfInventory("Goldeneye_PP7DualMagazine",0,"ReloadFinishDual")
        GE12 I 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        GE12 I 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        GE12 I 0 A_JumpIfInventory("Clip",1,1)
        goto ReloadFinishDual
        GE12 I 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        GE12 I 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        GE12 I 0 A_TakeInventory("Clip",1)
        GE12 I 0 A_GiveInventory("Goldeneye_PP7DualMagazine")
        goto ReloadAmmoDual
    }
}

// Attack handlers

actor SamsaraGoldenEyePP7SilencedSingleAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"PickupReloadable")
      PickupNonReloadable:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto PickupAttack
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",1)
        goto PickupAttack

      PickupReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Goldeneye_PP7Magazine",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Goldeneye_PP7Magazine",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupAttackSource")
      // GoldenEye 007 sounds
      PickupAttackN64:
        TNT1 A 0 A_PlaySound("Goldeneye/SilencerShot",CHAN_WEAPON)
        TNT1 A 0 A_FireBullets(0,0,1,0,"SamsaraBlankPuff",FBF_NORANDOM|FBF_NOFLASH)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackN64Coop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackN64Coop")
      PickupAttackN64DM:
        TNT1 A 0 A_FireCustomMissile("Goldeneye_PP7Tracer",frandom(-1,1),false,6)
        stop

      PickupAttackN64Coop:
        TNT1 A 0 A_FireCustomMissile("Goldeneye_PP7TracerCoop",frandom(-1,1),false,6)
        stop

      // GoldenEye: Source sounds
      PickupAttackSource:
        TNT1 A 0 A_PlaySound("GoldeneyeSource/SilencerShot",CHAN_WEAPON)
        TNT1 A 0 A_FireBullets(0,0,1,0,"SamsaraBlankPuff",FBF_NORANDOM|FBF_NOFLASH)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackSourceCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackSourceCoop")
      PickupAttackSourceDM:
        TNT1 A 0 A_FireCustomMissile("Goldeneye_PP7TracerSoundSource",frandom(-1,1),false,6)
        stop

      PickupAttackSourceCoop:
        TNT1 A 0 A_FireCustomMissile("Goldeneye_PP7TracerCoopSoundSource",frandom(-1,1),false,6)
        stop
    }
}

actor SamsaraGoldenEyePP7SilencedDualAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"PickupReloadable")
      PickupNonReloadable:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto PickupAttack
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",1)
        goto PickupAttack

      PickupReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Goldeneye_PP7DualMagazine",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Goldeneye_PP7DualMagazine",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupAttackSource")
      // GoldenEye 007 sounds
      PickupAttackN64:
        TNT1 A 0 A_PlaySound("Goldeneye/SilencerShot",CHAN_WEAPON)
        TNT1 A 0 A_FireBullets(0,0,1,0,"SamsaraBlankPuff",FBF_NORANDOM|FBF_NOFLASH)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackN64Coop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackN64Coop")
      PickupAttackN64DM:
        TNT1 A 0 A_FireCustomMissile("Goldeneye_PP7Tracer",frandom(-1,1),false,-6)
        stop

      PickupAttackN64Coop:
        TNT1 A 0 A_FireCustomMissile("Goldeneye_PP7TracerCoop",frandom(-1,1),false,-6)
        stop

      // GoldenEye: Source sounds
      PickupAttackSource:
        TNT1 A 0 A_PlaySound("GoldeneyeSource/SilencerShot",CHAN_WEAPON)
        TNT1 A 0 A_FireBullets(0,0,1,0,"SamsaraBlankPuff",FBF_NORANDOM|FBF_NOFLASH)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackSourceCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackSourceCoop")
      PickupAttackSourceDM:
        TNT1 A 0 A_FireCustomMissile("Goldeneye_PP7TracerSoundSource",frandom(-1,1),false,-6)
        stop

      PickupAttackSourceCoop:
        TNT1 A 0 A_FireCustomMissile("Goldeneye_PP7TracerCoopSoundSource",frandom(-1,1),false,-6)
        stop
    }
}

actor Goldeneye_PP7Magazine : Ammo
{
    Inventory.MaxAmount 7
    Tag "Silenced PP7 Magazine (Right)"
    +IGNORESKILL
}

actor Goldeneye_PP7DualMagazine : Ammo
{
    Inventory.MaxAmount 7
    Tag "Silenced PP7 Magazine (Left)"
    +IGNORESKILL
}

actor Goldeneye_PP7Tracer : Goldeneye_BulletTracer
{
    Damage (10)
    States
    {
      XDeath:
        TNT1 A 0 A_SetUserVar("user_damage",10)
        goto Super::XDeath
    }
}

actor Goldeneye_PP7TracerCoop : Goldeneye_PP7Tracer
{
    Species "Player"
    +THRUSPECIES
}

actor Goldeneye_PP7TracerSoundSource : Goldeneye_BulletTracerSoundSource
{
    Damage (10)
    States
    {
      XDeath:
        TNT1 A 0 A_SetUserVar("user_damage",10)
        goto Super::XDeath
    }
}

actor Goldeneye_PP7TracerCoopSoundSource : Goldeneye_PP7TracerSoundSource
{
    Species "Player"
    +THRUSPECIES
}
