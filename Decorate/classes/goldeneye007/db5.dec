//Goldfinger Tank

actor Goldfinger_DB5 : Goldfinger_Tank
{
	Radius 20
    Height 20
	States
	{
		Spawn:
			GE00 A 0 NoDelay ACS_NamedExecuteWithResult("Goldfinger_BuildDB5")
		SpawnLoop:
			GE00 A 0 A_JumpIfInventory("Goldeneye_UsingTank",1,2)
			GE00 A 1 A_ChangeFlag("NODAMAGE",false)
			loop
			GE00 A 1 A_ChangeFlag("NODAMAGE",true)
			loop
			
		
	}
}

actor Goldfinger_DB5_Activator : SwitchableDecoration
{
	Activation THINGSPEC_ThingTargets|THINGSPEC_Activate|THINGSPEC_Switch
	+USESPECIAL
	Radius 20
    Height 20
	States
	{
		Spawn:
			TNT1 A 0 NoDelay A_JumpIfInventory("Goldeneye_TankRemoval",1,"Remove",AAPTR_TRACER)
			TNT1 A 0 A_ChangeFlag("SOLID", true)
			TNT1 A 0 A_ChangeFlag("USESPECIAL",true)
			TNT1 A 1 A_Warp(AAPTR_TRACER,0,0,0,0,WARPF_NOCHECKPOSITION|WARPF_USECALLERANGLE|WARPF_COPYINTERPOLATION)
			loop
		Active:
		Inactive:
			TNT1 A 0 A_GiveInventory("Goldeneye_UsingTank",1,AAPTR_TRACER)
			TNT1 A 0 A_GiveInventory("Goldeneye_UsingTank",1,AAPTR_MASTER)
			TNT1 A 0 A_GiveInventory("Goldeneye_UsingTank",1,AAPTR_TARGET)
			TNT1 A 0 A_PlaySound("Goldeneye/Tank/Ignition",CHAN_VOICE,1.0,false,0.5)
			TNT1 A 0 A_ChangeFlag("SOLID", false)
			TNT1 A 0 A_ChangeFlag("USESPECIAL",false)
			TNT1 A 0 ACS_NamedExecuteWithResult("Goldfinger_DB5Activator")
		Using:
			TNT1 A 0 A_JumpIfInventory("Goldeneye_UsingTank",1,1,AAPTR_TRACER)
			goto BackToSpawn
			TNT1 A 1 A_JumpIfInventory("Goldeneye_TankRemoval",1,"Remove",AAPTR_TRACER)
			loop

		BackToSpawn:
			TNT1 A 0 A_ClearTarget
			goto Spawn
			
		Remove:
			TNT1 A 0 A_GiveInventory("Goldeneye_TankRemoval")
			TNT1 A 3 A_FadeOut(0.02)
			wait
	}
}

actor Goldfinger_DB5_Glass
{
	Height 4
	Radius 2
	Renderstyle Translucent
	Alpha 0.25
	+NOINTERACTION
	+NOGRAVITY
	+THRUACTORS
	+NODAMAGE
	States
	{
		Spawn:
			TNT1 A 0 NoDelay A_JumpIfInventory("Goldeneye_TankRemoval",1,"Remove",AAPTR_TRACER)
		SpawnLoop:
			//Copy velocity
			GE00 A 1 A_Warp(AAPTR_TRACER,0,0,0,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE|16384)
			Loop
			
		Remove:
			TNT1 A 1
			stop
	}
}

actor Goldfinger_DB5_Collision_Front
{
    Radius 28
    Height 20
    Tag "Aston Martin DB5"
	+THRUSPECIES
    +NOBLOOD
    +NODAMAGE
    +NOTIMEFREEZE
    +SHOOTABLE
    +SOLID
    States
    {
      Spawn:
        TNT1 A 0 NoDelay A_JumpIfInventory("Goldeneye_TankRemoval",1,"Remove",AAPTR_TRACER)
        TNT1 A 1 A_Warp(AAPTR_TRACER,42,0,0,0,WARPF_NOCHECKPOSITION)
        loop

      Remove:
        TNT1 A 1
        stop
    }
}

actor Goldfinger_DB5_SpeciesToken : TemporaryBoolean {}

actor Goldfinger_DB5_Collision_Roof : Goldfinger_DB5_Collision_Front
{
	Height 2
    States
    {
      Spawn:
        TNT1 A 0 NoDelay A_JumpIfInventory("Goldeneye_TankRemoval",1,"Remove",AAPTR_TRACER)
		TNT1 A 0 A_JumpIfInventory("Goldeneye_UsingTank",1,"SetSpecies",AAPTR_TRACER)
		Goto ClearSpecies
	  Warp:
        TNT1 A 1 A_Warp(AAPTR_TRACER,0,0,36,0,WARPF_NOCHECKPOSITION)
        Goto Spawn
	  SetSpecies:
		TNT1 A 0 A_JumpIfInventory("Goldfinger_DB5_SpeciesToken",1,"Warp")
		TNT1 A 0 A_GiveInventory("Goldfinger_DB5_SpeciesToken")
		TNT1 A 0 ACS_NamedExecuteWithResult("DB5UpdateSpecies")
		Goto Warp
	  ClearSpecies:
		TNT1 A 0 A_TakeInventory("Goldfinger_DB5_SpeciesToken")
		Goto Warp
    }
}

actor Goldfinger_DB5_Collision_Center : Goldfinger_DB5_Collision_Front
{
    States
    {
      Spawn:
        TNT1 A 0 NoDelay A_JumpIfInventory("Goldeneye_TankRemoval",1,"Remove",AAPTR_TRACER)
		TNT1 A 0 A_JumpIfInventory("Goldeneye_UsingTank",1,"SetSpecies",AAPTR_TRACER)
		Goto ClearSpecies
	  Warp:
        TNT1 A 1 A_Warp(AAPTR_TRACER,0,0,0,0,WARPF_NOCHECKPOSITION)
        Goto Spawn
	  SetSpecies:
		TNT1 A 0 A_JumpIfInventory("Goldfinger_DB5_SpeciesToken",1,"Warp")
		TNT1 A 0 A_GiveInventory("Goldfinger_DB5_SpeciesToken")
		TNT1 A 0 ACS_NamedExecuteWithResult("DB5UpdateSpecies")
		Goto Warp
	  ClearSpecies:
		TNT1 A 0 A_TakeInventory("Goldfinger_DB5_SpeciesToken")
		Goto Warp
    }
}

actor Goldfinger_DB5_Collision_BackL : Goldfinger_DB5_Collision_Front
{
    States
    {
      Spawn:
        TNT1 A 0 NoDelay A_JumpIfInventory("Goldeneye_TankRemoval",1,"Remove",AAPTR_TRACER)
        TNT1 A 1 A_Warp(AAPTR_TRACER,-72,12,0,0,WARPF_NOCHECKPOSITION)
        loop
    }
}

actor Goldfinger_DB5_Collision_BackR : Goldfinger_DB5_Collision_Front
{
    States
    {
      Spawn:
        TNT1 A 0 NoDelay A_JumpIfInventory("Goldeneye_TankRemoval",1,"Remove",AAPTR_TRACER)
        TNT1 A 1 A_Warp(AAPTR_TRACER,-72,-12,0,0,WARPF_NOCHECKPOSITION)
        loop
    }
}

actor Goldfinger_DB5_CollisionBoundary
{
    Radius 8
    Height 40
    +NOTIMEFREEZE
    States
    {
      Spawn:
        TNT1 A 0
        TNT1 A 0 A_CheckLOF("Stay",CLOFF_JUMPOBJECT|CLOFF_JUMP_ON_MISS|CLOFF_MUSTBESOLID|CLOFF_ALLOWNULL,16,0,-90)
        TNT1 A 0 A_CheckLOF("Stay",CLOFF_JUMPOBJECT|CLOFF_JUMP_ON_MISS|CLOFF_MUSTBESOLID|CLOFF_ALLOWNULL,16,0,90)
        TNT1 A 2 A_CheckLOF("Stay",CLOFF_JUMPOBJECT|CLOFF_JUMP_ON_MISS|CLOFF_MUSTBESOLID|CLOFF_ALLOWNULL,16)
        stop

      Stay:
        TNT1 A 0 A_GiveInventory("Goldeneye_Tank_Blocked",1,AAPTR_MASTER)
        stop
    }
}