Actor "Broad Sword" : WTWeapon
{
	Weapon.Slotnumber 5
	Weapon.SelectionOrder 1000
	Weapon.BobStyle Alpha
	Weapon.BobRangeX 0.2
	Weapon.BobRangeY 0.6
	Weapon.BobSpeed 0.8	
	+WEAPON.MELEEWEAPON
	+WEAPON.AMMO_OPTIONAL
	+THRUGHOST
	+WEAPON.NOALERT	
	Weapon.Ammotype "Clip"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 20
    Inventory.PickupSound "gpickup/gen"
    Inventory.PickupMessage "You got the broad sword!"
	Inventory.RestrictedTo "GrondovalPlayer"
    Tag "Broad Sword"
	States
	{
	Spawn:
		PWHJ ABCD 5
		Loop
	Select:
		TNT1 A 0 A_GiveInventory("WTWeaponActive",1)
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
		TNT1 A 1 A_Raise
		loop
	Deselect:
		TNT1 A 0 A_TakeInventory("WTWeaponActive",1)
		WW52 A 1 A_Lower
		loop
	Ready:
		WW52 A 0 A_JumpIfInventory("Clip",3,"MegaDraw")
	Draw:
		WW53 ABCDEFGH 3 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE) //Draw sword animation (normal)
		WW53 A 0 A_GunFlash //Call shield statemachine
		goto WIdle
	MegaDraw:
		WW53 ABCDEFGH 3 bright // "    "     "        (fire)
		WW53 A 0 A_GunFlash //Call shield statemachine
		goto MegaWIdle
	WIdle:
		WW52 A 0 A_JumpIfInventory("WTSlotIClip", 1, 2)
		WW52 A 0 A_JumpIfInventory("Clip",3,"MegaWIdle")	
		WW52 A 2 A_WeaponReady
		loop
	MegaWIdle:
		WW52 A 0 A_PlayWeaponSound("ggen/buzzloop")
		WW52 AAA 3 bright A_WeaponReady
		WW52 AAA 3 bright A_WeaponReady
		WW52 AAA 3 bright A_WeaponReady
		WW50 A 0 A_PlayWeaponSound("DSNONE")
		goto WIdle
	Fire:
		WW54 A 0 A_JumpIfInventory("WTSlotIClip", 1, 2)
		WW54 A 0 A_JumpIfInventory("Clip",3,"MegaFire")
		WW54 A 0 A_JumpIfInventory("WTAttackClip",1,2)
		WW54 A 0 A_GiveInventory("WTAttackClip",1)
		goto Attack1
		WW54 A 0 A_TakeInventory("WTAttackClip",1)
		goto Attack2
	MegaFire:
		WW54 A 0 A_JumpIfInventory("WTAttackClip",1,2)
		WW54 A 0 A_GiveInventory("WTAttackClip",1)
		goto MegaAttack1
		WW54 A 0 A_TakeInventory("WTAttackClip",1)
		goto MegaAttack2
	Attack1: 
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopAttack1")
		WW54 ABC 3
		WW54 D 0 A_PlayWeaponSound("ggen/swing4")
		WW54 D 3 A_GiveInventory("WTShieldDisable",1) //Disable shield, else the projectile gets blocked
		WW54 E 3 A_FireCustomMissile("WTBroadswordProjectile",0,0,4,6)
		WW54 F 3 A_TakeInventory("WTShieldDisable",999)
		TNT1 A 6
		WW54 G 1 /*offset(0,32)*/ A_Refire
		goto WIdle
	CoopAttack1: 
		WW54 ABC 3
		WW54 D 0 A_PlayWeaponSound("ggen/swing4")
		WW54 D 3 A_GiveInventory("WTShieldDisable",1) //Disable shield, else the projectile gets blocked
		WW54 E 3 A_FireCustomMissile("WTBroadswordProjectile_2",0,0,4,6)
		WW54 F 3 A_TakeInventory("WTShieldDisable",999)
		TNT1 A 6
		WW54 G 1 /*offset(0,32)*/ A_Refire
		goto WIdle
	Attack2:
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopAttack2")	
		WW55 ABC 3
		WW55 D 0 A_PlayWeaponSound("ggen/swing4")
		WW55 D 3 A_GiveInventory("WTShieldDisable",1) //Disable shield, else the projectile gets blocked
		WW55 E 3 A_FireCustomMissile("WTBroadswordProjectile_Weak",0,0,4,6)
		WW55 F 3 A_TakeInventory("WTShieldDisable",999)
		WW55 G 3
		TNT1 A 6
		WW55 G 1 /*offset(0,32)*/ A_Refire(1)
		goto WIdle
		TNT1 A 0 A_JumpIfInventory("WTShieldCounter",1,"WIdle")
		TNT1 A 0 A_JumpIfInventory("Fire Mace",1,"Attack2Mace")
		goto WIdle
	CoopAttack2: 
		WW55 ABC 3
		WW55 D 0 A_PlayWeaponSound("ggen/swing4")
		WW55 D 3 A_GiveInventory("WTShieldDisable",1) //Disable shield, else the projectile gets blocked
		WW55 E 3 A_FireCustomMissile("WTBroadswordProjectile_Weak_2",0,0,4,6)
		WW55 F 3 A_TakeInventory("WTShieldDisable",999)
		WW55 G 3
		TNT1 A 6
		WW10 G 1 /*offset(0,32)*/ A_Refire
		goto WIdle	
	Attack2Mace:
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopAttack2Mace")	
		WW56 A 3
		WW56 B 3 A_PlayWeaponSound("ggen/swing3")
		WW56 C 3 A_GiveInventory("WTShieldDisable",1) //Disable shield, else the projectile gets blocked
		WW56 D 3 A_FireCustomMissile("WTFireMaceProjectile_Weak",0,0,4,8)
		WW56 E 3 A_TakeInventory("WTShieldDisable",999)
		TNT1 A 6
		goto WIdle
	CoopAttack2Mace:
		WW56 A 3
		WW56 B 3 A_PlayWeaponSound("ggen/swing3")
		WW56 C 3 A_GiveInventory("WTShieldDisable",1) //Disable shield, else the projectile gets blocked
		WW56 D 3 A_FireCustomMissile("WTFireMaceProjectile_Weak_2",0,0,4,8)
		WW56 E 3 A_TakeInventory("WTShieldDisable",999)
		TNT1 A 6
		goto WIdle
	MegaAttack1:
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopMegaAttack1")		
		WW54 ABC 3 bright
		WW54 D 0 bright A_PlayWeaponSound("ggen/insect")
		WW54 D 3 bright A_GiveInventory("WTShieldDisable",1) //Disable shield, else the projectile gets blocked
		WW54 E 3 bright A_FireCustomMissile("WTMegaBroadswordProjectile",0,0,4,6)
		WW54 F 3 bright A_TakeInventory("WTShieldDisable",1)
		TNT1 A 6 bright
		WW54 G 1 bright /*offset(0,32)*/ A_Refire
		goto MegaWIdle
	CoopMegaAttack1:
		WW54 ABC 3 bright
		WW54 D 0 bright A_PlayWeaponSound("ggen/insect")
		WW54 D 3 bright A_GiveInventory("WTShieldDisable",1) //Disable shield, else the projectile gets blocked
		WW54 E 3 bright A_FireCustomMissile("WTMegaBroadswordProjectile_2",0,0,4,6)
		WW54 F 3 bright A_TakeInventory("WTShieldDisable",1)
		TNT1 A 6 bright
		WW54 G 1 bright /*offset(0,32)*/ A_Refire
		goto MegaWIdle	
	MegaAttack2:
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopMegaAttack2")			
		WW55 ABC 3 bright
		WW55 D 0 bright A_PlayWeaponSound("ggen/insect")
		WW55 D 3 bright A_GiveInventory("WTShieldDisable",1) //Disable shield, else the projectile gets blocked
		WW55 E 3 bright A_FireCustomMissile("WTMegaBroadswordProjectile_Weak",0,0,4,6)
		WW55 F 3 bright A_TakeInventory("WTShieldDisable",1)
		WW55 G 3 bright
		TNT1 A 6 bright
		WW55 G 1 bright /*offset(0,32)*/ A_Refire(1)
		Goto WIdle
		TNT1 A 0 A_JumpIfInventory("WTShieldCounter",1,"MegaWIdle")
		TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,1)	
		goto MegaWIdle
		TNT1 A 0 A_JumpIfInventory("Fire Mace",1,"MegaAttack2Mace")	
		goto MegaWIdle
	CoopMegaAttack2:
		WW55 ABC 3 bright
		WW55 D 0 bright A_PlayWeaponSound("ggen/insect")
		WW55 D 3 bright A_GiveInventory("WTShieldDisable",1) //Disable shield, else the projectile gets blocked
		WW55 E 3 bright A_FireCustomMissile("WTMegaBroadswordProjectile_Weak_2",0,0,4,6)
		WW55 F 3 bright A_TakeInventory("WTShieldDisable",1)
		WW55 G 3 bright
		TNT1 A 6 bright
		WW55 G 1 bright /*offset(0,32)*/ A_Refire(1)
		Goto WIdle
		TNT1 A 0 A_JumpIfInventory("WTShieldCounter",1,"MegaWIdle")
		TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,1)	
		goto MegaWIdle
		TNT1 A 0 A_JumpIfInventory("Fire Mace",1,"MegaAttack2Mace")	
		goto MegaWIdle
	MegaAttack2Mace:
		TNT1 A 0 A_JumpIfInventory("DMModeOn",1,"DMMegaAttack2Mace")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopMegaAttack2Mace")	
		TNT1 A 0 A_AlertMonsters	
		WW57 A 3
		WW57 B 0 A_PlayWeaponSound("gspell/fire")
		WW57 B 3 A_GiveInventory("WTShieldDisable",1) //Disable shield, else the projectile gets blocked
		WW57 C 0 A_TakeInventory("RocketAmmo",1,TIF_NOTAKEINFINITE)
		WW57 C 3 bright A_FireCustomMissile("WTFireMaceBall",0,0,4,6)
		WW57 D 3 bright A_FireCustomMissile("WTFireMaceBall",0,0,4,6)
		WW57 E 3 bright A_FireCustomMissile("WTFireMaceBall",0,0,4,6)
        TNT1 A 0 A_TakeInventory("WTShieldDisable",1)
		goto MegaWIdle
	CoopMegaAttack2Mace:
		TNT1 A 0 A_AlertMonsters	
		WW57 A 3
		WW57 B 0 A_PlayWeaponSound("gspell/fire")
		WW57 B 3 A_GiveInventory("WTShieldDisable",1) //Disable shield, else the projectile gets blocked
		WW57 C 0 A_TakeInventory("RocketAmmo",1,TIF_NOTAKEINFINITE)
		WW57 C 3 bright A_FireCustomMissile("WTFireMaceBall_2",0,0,4,6)
		WW57 D 3 bright A_FireCustomMissile("WTFireMaceBall_2",0,0,4,6)
		WW57 E 3 bright A_FireCustomMissile("WTFireMaceBall_2",0,0,4,6)
        TNT1 A 0 A_TakeInventory("WTShieldDisable",1)
		goto MegaWIdle
	DMMegaAttack2Mace:
		TNT1 A 0 A_AlertMonsters	
		WW57 A 3
		WW57 B 0 A_PlayWeaponSound("gspell/fire")
		WW57 B 3 A_GiveInventory("WTShieldDisable",1) //Disable shield, else the projectile gets blocked
		WW57 C 0 A_TakeInventory("RocketAmmo",1,TIF_NOTAKEINFINITE)
		WW57 C 3 bright A_FireCustomMissile("WTFireMaceBallDM",0,0,4,6)
		WW57 D 3 bright A_FireCustomMissile("WTFireMaceBallDM",0,0,4,6)
		WW57 E 3 bright A_FireCustomMissile("WTFireMaceBallDM",0,0,4,6)
        TNT1 A 0 A_TakeInventory("WTShieldDisable",1)
		goto MegaWIdle
	Altfire:
		WW52 A 0 A_JumpIfInventory("WTSlotIClip",1,"TakeClip")
	GiveClip:
		WW52 A 0 A_PlayWeaponSound("DSNONE")
		WW52 A 12 A_GiveInventory("WTSlotIClip",1)
		goto WIdle
	TakeClip:
		WW52 A 0 A_TakeInventory("WTSlotIClip",999)
		WW52 A 12 A_JumpIfInventory("Clip",3,1)
		goto WIdle
		WW52 A 0 A_PlayWeaponSound("ggen/fireloop")
		WW52 AAA 3 bright
		goto MegaWIdle	
	}
}

Actor WTBroadswordProjectile : WTMeleeProjectile
{
	Speed 128
	+FORCEPAIN
	Damage (random(60,75))
	Obituary "$SAMSARA_WITCHAVEN_OB_FISTS"
}

Actor WTBroadswordProjectile_Weak : WTMeleeProjectile
{
	Speed 128
	+FORCEPAIN	
	Damage (random(45,60))
	Obituary "$SAMSARA_WITCHAVEN_OB_FISTS"
}

Actor WTMegaBroadswordProjectile : WTGigaMeleeProjectile
{
	Speed 128
	+FORCEPAIN
	Damage (random(120,150)) //Too much damage?
	Obituary "$SAMSARA_WITCHAVEN_OB_FISTS_MEGA"
	States
	{
	Crash:
		TNT1 A 0 A_GiveToTarget("WTBroadswordAmmoDrain",1)
		goto Super::Crash
	XDeath:
		TNT1 A 0 A_GiveToTarget("WTBroadswordAmmoDrain",1)
		goto Super::XDeath	
	}	
}

Actor WTMegaBroadswordProjectile_Weak : WTGigaMeleeProjectile
{
	Speed 128
	+FORCEPAIN
	Damage (random(90,120))
	Obituary "$SAMSARA_WITCHAVEN_OB_FISTS_MEGA"
	States
	{
	Crash:
		TNT1 A 0 A_GiveToTarget("WTBroadswordAmmoDrain",1)
		goto Super::Crash
	XDeath:
		TNT1 A 0 A_GiveToTarget("WTBroadswordAmmoDrain",1)
		goto Super::XDeath	
	}	
}

Actor WTBroadswordAmmoDrain : WTACSTrigger
{
	states
	{
	//Pickup:
		//stop
	Use:
		TNT1 A 1 A_TakeInventory("Clip",4,TIF_NOTAKEINFINITE)
		stop
	}
}

Actor WTBroadswordProjectile_2 : WTBroadswordProjectile { Species "Player" +THRUSPECIES +DONTHARMCLASS +DONTHARMSPECIES +MTHRUSPECIES  }
Actor WTBroadswordProjectile_Weak_2 : WTBroadswordProjectile_Weak { Species "Player" +THRUSPECIES +DONTHARMCLASS +DONTHARMSPECIES +MTHRUSPECIES  }
Actor WTMegaBroadswordProjectile_2 : WTMegaBroadswordProjectile { Species "Player" +THRUSPECIES +DONTHARMCLASS +DONTHARMSPECIES +MTHRUSPECIES  }
Actor WTMegaBroadswordProjectile_Weak_2 : WTMegaBroadswordProjectile_Weak { Species "Player" +THRUSPECIES +DONTHARMCLASS +DONTHARMSPECIES +MTHRUSPECIES  }
