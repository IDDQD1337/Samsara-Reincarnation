// Effect base

actor Q2EffectBase // This actor was made by TheCamaleonMaligno to be used as base for effects (actor taken from  lo-mio)
{
    Radius 0.5
    Height 1
    Gravity 0.66
    RenderStyle "Add"
    Species "VFX"
    +CLIENTSIDEONLY
    +DONTSPLASH
    +FORCEXYBILLBOARD
    +NOBLOCKMAP
    +NOBLOCKMONST
    +NOINTERACTION
    //+NOTONAUTOMAP
    +NOTRIGGER
    +THRUACTORS
}

// Bullet puff

actor Q2BulletPuff : BulletPuff
{
    VSpeed 0
    DamageType "BittermanDamage"
    -ALLOWPARTICLES
    States
    {
      Spawn:
        TNT1 AAAAAAAAAAAAA 0 NoDelay A_SpawnItemEx("Q2BulletPuffParticle",frandom(0,12),0,frandom(-8,15),frandom(0,0.5),0,frandom(-1,-0.1),random(0,359))
        TNT1 A 0 A_SpawnItemEx("Q2BulletPuffFlash")
        TNT1 A 0 A_Jump(128,1)
        goto SpawnFinish
        TNT1 A 0 A_PlaySound("Q2World/Ricochet",CHAN_BODY,0.5)
        goto SpawnFinish

      SpawnFinish:
        TNT1 A 1
        stop
    }
}

actor Q2BulletPuffCoop : Q2BulletPuff { +MTHRUSPECIES }

actor Q2BulletPuffParticle : Q2EffectBase
{
    RenderStyle "Normal"
    Scale 1.5
    States
    {
      Spawn:
        P001 ABCDEFGH 0 Bright NoDelay A_Jump(64,"Fade")
        loop

      Fade:
        "----" A 5 Bright
      FadeContinue:
        "----" A 1 Bright A_FadeOut
        loop
    }
}

actor Q2BulletPuffFlash : Q2EffectBase
{
    States
    {
      Spawn:
        DPFF A 5 Bright NoDelay A_SpawnItemEx("Q2BulletPuffSmoke",0,0,0,0,0,frandom(0,0.5))
        DPFF B 1 Bright
        stop
    }
}

actor Q2BulletPuffSmoke : Q2EffectBase
{
    RenderStyle "Translucent"
    States
    {
      Spawn:
        DPFF ABC 4 NoDelay A_FadeOut(0.25)
        DPFF D 2
        stop
    }
}

// Blood

actor Q2BloodParticle : Q2BulletPuffParticle
{
    States
    {
      Spawn:
        P002 DEFGH 0 Bright NoDelay A_Jump(64,"Fade")
        loop

      Fade:
        "----" D 5 Bright
      FadeContinue:
        "----" D 1 Bright A_FadeOut
        loop
    }
}

actor Q2Blood : Blood
{
    -ALLOWPARTICLES
    +PUFFGETSOWNER
    States
    {
      Spawn:
        TNT1 AAA 0
        TNT1 A 0 A_SpawnItemEx("Switchable_FlyingBlood",0,0,0,random(-350,350)/100.00,random(-350,350)/100.00,random(500,1000)/100.00,random(-180,180),SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
        TNT1 AAAAAAAAAAAAA 0 A_SpawnItemEx("Q2BloodParticle",frandom(0,12),0,frandom(-8,15),frandom(0,0.5),0,frandom(-1,-0.1),random(0,359),SXF_TRANSFERTRANSLATION)
        TNT1 A 1
        stop
    }
}

actor Q2BloodSplatter : Q2Blood {}

// Gibs

actor Q2GibSpawner : Q2EffectBase
{
    States
    {
      Spawn:
        TNT1 A 0 NoDelay A_PlaySound("Bitterman/Xdeath",CHAN_VOICE)
        TNT1 A 0 A_SpawnItemEx("UniversalBloodyHellGoreXDeathEffect",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
        TNT1 AAAAAAAA 0 A_SpawnItemEx("Q2Gib",random2(20),0,random(0,50),random(0,5),0,random(0,10),random(0,359))
        TNT1 A 1
        stop
    }
}

actor Q2MediumGibSpawner : Q2EffectBase
{
    States
    {
      Spawn:
        TNT1 A 0 NoDelay A_PlaySound("Bitterman/Xdeath",CHAN_VOICE)
        TNT1 A 0 A_SpawnItemEx("UniversalBloodyHellGoreXDeathEffect",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
        TNT1 AAAAAAAAAAAAAAAA 0 A_SpawnItemEx("Q2Gib",random2(20),0,random(0,50),random(0,5),0,random(0,10),random(0,359))
        TNT1 A 1
        stop
    }
}

actor Q2LargeGibSpawner : Q2EffectBase
{
    States
    {
      Spawn:
        TNT1 A 0 NoDelay A_PlaySound("Bitterman/Xdeath",CHAN_VOICE)
        TNT1 A 0 A_SpawnItemEx("UniversalBloodyHellGoreXDeathEffect",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
        TNT1 AAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("Q2Gib",random2(20),0,random(0,50),random(0,5),0,random(0,10),random(0,359))
        TNT1 A 1
        stop
    }
}

actor Q2HugeGibSpawner : Q2EffectBase
{
    States
    {
      Spawn:
        TNT1 A 0 NoDelay A_PlaySound("Bitterman/Xdeath",CHAN_VOICE)
        TNT1 A 0 A_SpawnItemEx("UniversalBloodyHellGoreXDeathEffect",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
        TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("Q2Gib",random2(20),0,random(0,50),random(0,5),0,random(0,10),random(0,359))
        TNT1 A 1
        stop
    }
}

actor Q2ColorGibSpawner : Q2EffectBase
{
    States
    {
      Spawn:
        TNT1 A 0 NoDelay A_PlaySound("Bitterman/Xdeath",CHAN_VOICE)
        TNT1 A 0 A_SpawnItemEx("UniversalBloodyHellGoreXDeathEffect",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION|SXF_TRANSFERPOINTERS)
        TNT1 AAAAAAAA 0 A_SpawnItemEx("Q2GibColor",random2(20),0,random(0,50),random(0,5),0,random(0,10),random(0,359),SXF_TRANSFERTRANSLATION)
        TNT1 A 1
        stop
    }
}

actor Q2MediumColorGibSpawner : Q2EffectBase
{
    States
    {
      Spawn:
        TNT1 A 0 NoDelay A_PlaySound("Bitterman/Xdeath",CHAN_VOICE)
        TNT1 A 0 A_SpawnItemEx("UniversalBloodyHellGoreXDeathEffect",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION|SXF_TRANSFERPOINTERS)
        TNT1 AAAAAAAAAAAAAAAA 0 A_SpawnItemEx("Q2GibColor",random2(20),0,random(0,50),random(0,5),0,random(0,10),random(0,359),SXF_TRANSFERTRANSLATION)
        TNT1 A 1
        stop
    }
}

actor Q2LargeColorGibSpawner : Q2EffectBase
{
    States
    {
      Spawn:
        TNT1 A 0 NoDelay A_PlaySound("Bitterman/Xdeath",CHAN_VOICE)
        TNT1 A 0 A_SpawnItemEx("UniversalBloodyHellGoreXDeathEffect",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION|SXF_TRANSFERPOINTERS)
        TNT1 AAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("Q2GibColor",random2(20),0,random(0,50),random(0,5),0,random(0,10),random(0,359),SXF_TRANSFERTRANSLATION)
        TNT1 A 1
        stop
    }
}

actor Q2HugeColorGibSpawner : Q2EffectBase
{
    States
    {
      Spawn:
        TNT1 A 0 NoDelay A_PlaySound("Bitterman/Xdeath",CHAN_VOICE)
        TNT1 A 0 A_SpawnItemEx("UniversalBloodyHellGoreXDeathEffect",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION|SXF_TRANSFERPOINTERS)
        TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("Q2GibColor",random2(20),0,random(0,50),random(0,5),0,random(0,10),random(0,359),SXF_TRANSFERTRANSLATION)
        TNT1 A 1
        stop
    }
}

actor Q2Gib : Q2EffectBase
{
    var int user_angle;
    Projectile
    Speed 10
    BounceType "Doom"
    BounceCount 2
    BounceFactor 0.5
    RenderStyle "Normal"
    Scale 1.5
    +BLOODLESSIMPACT
    +DONTBLAST
    +DONTREFLECT
    -DONTSPLASH
    +MOVEWITHSECTOR
    -NOINTERACTION
    -NOGRAVITY
    +NOTRIGGER
    States
    {
      Spawn:
        TNT1 A 0 NoDelay A_Jump(128,2)
        TNT1 A 0 A_SetUserVar(user_angle,random(6,20))
        goto Decide
        TNT1 A 0 A_SetUserVar(user_angle,random(-20,-6))
        goto Decide

      Decide:
        NULL ABC 0 A_Jump(64,"Flying")
        loop

      Flying:
        "----" AAA 0 A_SpawnItemEx("Q2GibTrail",frandom(0,8),0,frandom(-8,8),frandom(0,0.25),0,-0.33,random(0,359))
        "----" A 0 A_SetPitch(pitch+user_angle)
        "----" A 1 A_SetAngle(angle+user_angle)
        loop

      Death:
        "----" A 700
        "----" A 0 A_JumpIf(ACS_NamedExecuteWithResult("SamsaraDecorate", 31) == 1, "Death")
        "----" A 35 A_CheckSight(1)
        wait
        TNT1 A 0
        stop
    }
}

actor Q2GibColor : Q2Gib
{
    States
    {
      Flying:
        "----" AAA 0 A_SpawnItemEx("Q2GibTrail",frandom(0,8),0,frandom(-8,8),frandom(0,0.25),0,-0.33,random(0,359),SXF_TRANSFERTRANSLATION)
        "----" A 0 A_SetPitch(pitch+user_angle)
        "----" A 1 A_SetAngle(angle+user_angle)
        loop
    }
}

actor Q2GibTrail : Q2BloodParticle
{
    //Translation "80:100=[255,128,0]:[255,0,0]", "101:111=[255,0,0]:[98,0,0]"
    States
    {
      Fade:
        "----" D 5 Bright
      FadeContinue:
        "----" D 1 Bright A_FadeOut
        loop
    }
}

// Explosion

actor SamsaraQuake2ExplosionParticleCount : Counter { Inventory.MaxAmount 204 }

actor Q2Explosion : Q2EffectBase
{
    RenderStyle "Translucent"
    States
    {
      Spawn:
        TNT1 A 0 NoDelay A_SetAngle(random(0,359))
      SpawnParticleLoop:
        TNT1 A 0 A_GiveInventory("SamsaraQuake2ExplosionParticleCount")
        TNT1 A 0 A_SpawnItemEx("Q2ExplosionParticle",0,0,random(0,15),frandom(-5,5),random(-5,5),random(-5,5))
        TNT1 A 0 A_JumpIfInventory("SamsaraQuake2ExplosionParticleCount",0,"Decide")
        loop

      Decide:
        TNT1 A 0 A_Jump(256,"Spawn1","Spawn2","Spawn3")
        stop

      Spawn1:
        XPL0 AB 3 Bright Light("Explosion_X1")
        XPL0 CD 3 Bright Light("Explosion_X2")
        XPL0 EF 3 Bright Light("Explosion_X3")
        XPL0 GH 3 Bright Light("Explosion_X4")
        XPL0 IJ 3 Bright Light("Explosion_X5") A_FadeOut(0.16)
        XPL0 KL 3 Bright Light("Explosion_X6") A_FadeOut(0.16)
        XPL0 MN 3 Bright Light("Explosion_X7") A_FadeOut(0.16)
        stop

      Spawn2:
        XPL1 AB 3 Bright Light("Explosion_X1")
        XPL1 CD 3 Bright Light("Explosion_X2")
        XPL1 EF 3 Bright Light("Explosion_X3")
        XPL1 GH 3 Bright Light("Explosion_X4")
        XPL1 IJ 3 Bright Light("Explosion_X5") A_FadeOut(0.16)
        XPL1 KL 3 Bright Light("Explosion_X6") A_FadeOut(0.16)
        XPL1 MNO 3 Bright Light("Explosion_X7") A_FadeOut(0.16)
        stop

      Spawn3:
        TNT1 A 0 A_CheckFloor(1)
        goto Decide
        XPL2 ABC 2 Bright Light("Explosion_X1")
        XPL2 DEF 3 Bright Light("Explosion_X2")
        XPL2 GHI 2 Bright Light("Explosion_X3")
        XPL2 JKL 3 Bright Light("Explosion_X4")
        XPL2 MNO 2 Bright Light("Explosion_X5") A_FadeOut(0.16)
        XPL2 OP 3 Bright Light("Explosion_X6") A_FadeOut(0.16)
        XPL2 QRS 2 Bright Light("Explosion_X7") A_FadeOut(0.16)
        stop
    }
}

actor Q2ExplosionParticle : Q2BulletPuffParticle
{
    Scale 1
    //Translation "80:111=208:223"
    States
    {
      Spawn:
        P006 ABCDEFGH 0 Bright NoDelay A_Jump(64,"Fade")
        loop

      Fade:
        "----" A 0 A_Jump(128,3,4,5)
        "----" AAAAA 4 Bright
        stop
    }
}

// Green puff

actor GreenPuff : Q2EffectBase
{
    Scale 1
    Translation "80:95=[255,255,0]:[0,255,0]", "96:111=[0,255,0]:[0,32,0]"
    States
    {
      Spawn:
        TNT1 A 0 NoDelay A_PlaySound("Q2Weapons/LaserHit")
        TNT1 AAAAAAAAAAAAA 0 A_SpawnItemEx("Q2BulletPuffParticleAdd",frandom(0,5),0,frandom(-5,5),frandom(0,0.5),0,frandom(-1,-0.1),random(0,359),SXF_TRANSFERTRANSLATION|SXF_TRANSFERSCALE)
        stop
    }
}

actor Q2BulletPuffParticleAdd : Q2BulletPuffParticle { RenderStyle "Add" } // cuz SXF_TRANSFERRENDERSTYLE flag does not work in zandronum 3.0

// HUD elements

actor HUDLowHP : Boolean {}
actor HUDHPDown : Boolean {}
actor HUDArmorDown : Boolean {}

// Weapon

actor Q2WeaponIndex : Counter
{
    Inventory.MaxAmount 15
    Inventory.InterHubAmount 0
}

actor SamsaraQuake2AmmoCheckFailed : Boolean {}
actor SamsaraQuake2WeaponEmptySwitchPending : Boolean {}
actor SamsaraQuake2SilentFired : Boolean {}
actor SamsaraQuake2SilentLoopSound : Boolean {} // allow restarting sounds like chaingun and hyperblaster loops if silencer status changes

actor Q2Weapon : Weapon
{
    Inventory.PickupSound "Q2Misc/W_Pkup"
    Inventory.RestrictedTo "Bitterman"
    Weapon.BobStyle "InverseSmooth"
    Weapon.BobSpeed 2
    Weapon.BobRangeX 0.66
    Weapon.BobRangeY 0.66
    +AMMO_OPTIONAL
    +NOALERT
    +UNDROPPABLE
    States
    {
      Ready:
        TNT1 A 0 A_TakeInventory("Q2Weapon")
        TNT1 A 1 A_WeaponReady
        loop

      Deselect:
      DeselectInstant:
        TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_Lower
        TNT1 A 1 A_Lower
        loop

      Select:
        TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_Raise
        TNT1 A 1 A_Raise
        loop

      Fire:
        TNT1 A 1
        goto Ready
    }
}

actor Q2WeaponFlash : Q2EffectBase
{
    +NOTIMEFREEZE
    States
    {
      Spawn:
        TNT1 A 2
        stop
    }
}

// Powerups

actor PowerSilencer : Powerup { Powerup.Duration -60 }

actor Q2Silencer : PowerupGiver
{
    Inventory.PickupMessage "Silencer"
    Inventory.PickupSound "Q2Misc/Pickup"
    Inventory.UseSound ""
    Inventory.Icon "Q2IHD0"
    Inventory.MaxAmount 2
    Inventory.InterHubAmount 2
    Inventory.RestrictedTo "Bitterman"
    Powerup.Type "Silencer"
    Tag "Silencer"
    +INVBAR
    +UNDROPPABLE
    States
    {
      Spawn:
        POWR A -1
        stop
    }
}

actor Q2EnvironmentSuit : PowerupGiver
{
    Inventory.PickupMessage "Environment Suit"
    Inventory.PickupSound "Q2Misc/Pickup"
    Inventory.UseSound ""
    Inventory.Icon "Q2IHD4"
    Inventory.MaxAmount 2
    Inventory.InterHubAmount 2
    Inventory.RestrictedTo "Bitterman"
    Powerup.Type "IronFeet"
    Powerup.Color "Green", 0.1
    Tag "Environment Suit"
    +INVBAR
    +UNDROPPABLE
    States
    {
      Spawn:
        POWR A -1
        stop
    }
}

actor Q2Invulnerability : PowerupGiver
{
    Inventory.PickupMessage "Invulnerability"
    Inventory.PickupSound "Q2Misc/Pickup"
    Inventory.UseSound "Q2Misc/Invulnerability/Use"
    Inventory.Icon "Q2IHD5"
    Inventory.MaxAmount 2
    Inventory.InterHubAmount 2
    Inventory.RestrictedTo "Bitterman"
    Powerup.Type "SamsaraPowerInvulnerable"
    Powerup.Color "Yellow", 0.1
    Tag "Invulnerability"
    +INVBAR
    +UNDROPPABLE
    States
    {
      Spawn:
        POWR A -1
        stop
    }
}

actor Q2QuadDamage : PowerupGiver
{
    Inventory.PickupMessage "Quad Damage"
    Inventory.PickupSound "Q2Misc/Pickup"
    Inventory.UseSound "Q2Misc/QuadDamage/Use"
    Inventory.Icon "Q2IHD1"
    Inventory.MaxAmount 1
    Inventory.InterHubAmount 1
    Inventory.RestrictedTo "Bitterman"
    Powerup.Type "Q2QuadDamage"
    Powerup.Duration -30
    Powerup.Color "Blue", 0.1
    Tag "Quad Damage"
    +INVBAR
    +UNDROPPABLE
    States
    {
      Spawn:
        POWR A -1
        stop
    }
}

actor PowerQ2QuadDamage : PowerDamage { Damagefactor "Normal", 4 }

actor Q2DualFireDamage : PowerupGiver
{
    Inventory.PickupMessage "Dual Fire Damage"
    Inventory.PickupSound "Q2Misc/Pickup"
    Inventory.UseSound "Q2Misc/DoubleFire/Use"
    Inventory.Icon "Q2IHD2"
    Inventory.MaxAmount 2
    Inventory.InterHubAmount 2
    Inventory.RestrictedTo "Bitterman"
    Powerup.Type "DoubleFiringSpeed"
    Powerup.Duration -30
    Powerup.Color "Red", 0.1
    Tag "Dual Fire Damage"
    +INVBAR
    +UNDROPPABLE
    States
    {
      Spawn:
        POWR A -1
        stop
    }
}

actor Q2DoubleDamage : PowerupGiver
{
    Inventory.PickupMessage "Double Damage"
    Inventory.PickupSound "Q2Misc/Pickup"
    Inventory.UseSound "Q2Misc/DoubleDamage/Use"
    Inventory.Icon "Q2IHD3"
    Inventory.MaxAmount 2
    Inventory.InterHubAmount 2
    Inventory.RestrictedTo "Bitterman"
    Powerup.Type "Q2DoubleDamage"
    Powerup.Duration -30
    Powerup.Color "Orange", 0.1
    Tag "Double Damage"
    +INVBAR
    +UNDROPPABLE
    States
    {
      Spawn:
        POWR A -1
        stop
    }
}

actor PowerQ2DoubleDamage : PowerDamage { DamageFactor "Normal", 2 }

// Unused/debug

/*actor wea : Q2BulletPuffParticle
{
    var int user_offset[3];
    Scale 2
    +NOTIMEFREEZE
    States
    {
      Spawn:
        TNT1 A 0 NoDelay A_Warp(AAPTR_TARGET,user_offset[0],user_offset[1],user_offset[2],0,WARPF_NOCHECKPOSITION)
        goto Super::Spawn
    }
}*/
