actor SamsaraQuake2TrapLoopSoundCheck : Boolean {}
actor SamsaraQuake2TrapFuseActive : Boolean {}
actor SamsaraQuake2TrapFuseTimer : Counter { Inventory.MaxAmount 32 }

actor Q2Trap : Q2Weapon
{
    Inventory.PickupMessage "Trap"
    Weapon.SlotNumber 7
    Weapon.SlotPriority 1
    Weapon.SelectionOrder 500
    Weapon.AmmoType "Cell"
    Weapon.AmmoGive 50
    Weapon.AmmoUse 50
    Obituary "%o was sucked up by %k's trap."
    Tag "Trap"
    +BFG
    States
    {
      Spawn:
        Q2WP A -1
        stop

      ReadyActual:
        TRP3 A 0 A_TakeInventory("SamsaraQuake2TrapLoopSoundCheck")
        TRP3 A 0 A_TakeInventory("SamsaraQuake2TrapFuseActive")
        TRP3 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReadyActualAmmo")
        TRP3 A 0 A_JumpIfInventory("Cell",50,"ReadyActualAmmo")
        goto ReadyActualNoAmmo

      ReadyActualNoAmmo:
        TNT1 A 1 A_WeaponReady
        goto ReadyActual

      ReadyActualAmmo:
        TRP3 A 1 A_WeaponReady
        goto ReadyActualJump

      ReadyActualJump:
        TRP3 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReadyActualJumpStart")
        TRP3 A 0 A_JumpIfInventory("Cell",50,"ReadyActualJumpStart")
        goto ReadyActual

      ReadyActualJumpStart:
        TRP3 A 0 A_Jump(10,"ReadyActualIdle")
        goto ReadyActual

      ReadyActualIdle:
        TRP3 "ABCDEFGHIJKLMNOPQRSTUVWXYZ[\]" 4 A_WeaponReady
        TRP4 ABCD 4 A_WeaponReady
        goto ReadyActual

      Deselect:
        TNT1 A 0 A_GiveInventory("SamsaraQuake2TrapLoopSoundStopper")
        TNT1 A 0 A_GiveInventory("SamsaraQuake2TrapDeathAttackHandler")
        goto DeselectInstant

      Ready: // our "Select" state
        TNT1 A 0 A_TakeInventory("Q2WeaponIndex")
        TNT1 A 0 A_GiveInventory("Q2WeaponIndex",15)
        goto ReadyActual

      Fire:
        TRP0 A 0 A_TakeInventory("SamsaraQuake2TrapFuseTimer")
        TRP0 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart")
        TRP0 A 0 A_JumpIfInventory("Cell",50,"FireStart")
        goto FireDry

      FireDry:
        TNT1 A 0 A_ClearReFire
        TNT1 A 0 A_PlaySound("Q2Weapons/NoAmmo",CHAN_5)
        TNT1 A 0 A_SelectWeapon("Q2Blaster")
        goto Deselect

      FireStart:
        TRP0 ABCDE 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        TRP0 F 0 A_PlaySound("Q2Weapons/TrapCock",CHAN_WEAPON)
        TRP0 FGHIJK 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        TRP1 A 0 A_GiveInventory("SamsaraQuake2TrapFuseActive")
      FireCharge:
        TRP1 A 0 A_JumpIfInventory("SamsaraQuake2TrapFuseTimer",0,"FireReleaseOvercookLastShotCalc")
        TRP1 A 0 A_JumpIfInventory("SamsaraQuake2TrapFuseTimer",10,1)
        goto FireChargeAddTime
        TRP1 A 0 A_GiveInventory("SamsaraQuake2TrapLoopSoundPlayer")
        goto FireChargeAddTime

      FireChargeAddTime:
        TRP1 A 0 A_GiveInventory("SamsaraQuake2TrapFuseTimer")
        TRP1 A 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        TRP1 A 0 A_ReFire("FireCharge")
      FireReleaseLastShotCalc: // finish interpolation hack
        TRP2 A 0 A_GiveInventory("SamsaraQuake2TrapLoopSoundStopper")
        TRP2 A 0 A_TakeInventory("SamsaraQuake2TrapFuseActive")
        TRP2 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireRelease")
        TRP2 A 0 A_JumpIfInventory("Cell",100,"FireRelease")
        goto FireReleaseLast

      FireRelease:
        TRP2 A 0 A_GiveInventory("SamsaraQuake2TrapAttackHandler")
        TRP2 ABCD 4 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto ReadyActual

      FireReleaseLast:
        TRP2 A 0 A_GiveInventory("SamsaraQuake2TrapAttackHandler")
        TRP2 ABCD 4 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0
        goto ReadyActual

      FireReleaseOvercookLastShotCalc: // finish interpolation hack
        TNT1 A 0 A_GiveInventory("SamsaraQuake2TrapLoopSoundStopper")
        TNT1 A 0 A_TakeInventory("SamsaraQuake2TrapFuseActive")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireReleaseOvercook")
        TNT1 A 0 A_JumpIfInventory("Cell",100,"FireReleaseOvercook")
        goto FireReleaseOvercookLast

      FireReleaseOvercook:
        TNT1 A 0 A_GiveInventory("SamsaraQuake2TrapOvercookAttackHandler")
        TNT1 AAAA 4 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto ReadyActual

      FireReleaseOvercookLast:
        TNT1 A 0 A_GiveInventory("SamsaraQuake2TrapOvercookAttackHandler")
        TNT1 AAAA 4 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0
        goto ReadyActual
    }
}

actor SamsaraQuake2TrapAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Cell",50,1)
        stop
        TNT1 A 0 A_TakeInventory("Cell",50)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("Q2TrapProjectile",0,false)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("Q2TrapProjectileCoop",0,false)
        stop
    }
}

actor SamsaraQuake2TrapDeathAttackHandler : Trigger
{
    States
    {
      // Health/charge checking stuff
      Pickup:
        TNT1 A 0 A_JumpIfHealthLower(1,1)
        goto PickupFinish
        TNT1 A 0 A_JumpIfInventory("SamsaraQuake2TrapFuseActive",1,"PickupAmmoCheck")
        goto PickupFinish

      // Ammo checking/taking stuff
      PickupAmmoCheck:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupSilentCheck")
        TNT1 A 0 A_JumpIfInventory("Cell",50,1)
        goto PickupFinish
        TNT1 A 0 A_TakeInventory("Cell",50)
        goto PickupSilentCheck

      // Silencer checking stuff
      PickupSilentCheck:
        TNT1 A 0 A_JumpIfInventory("PowerSilencer",1,"PickupAttack")
        TNT1 A 0 A_AlertMonsters // whoops
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("Q2TrapBoom",0,false)
        goto PickupFinish

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("Q2TrapBoomCoop",0,false)
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 A_TakeInventory("SamsaraQuake2TrapFuseActive")
        stop
    }
}

actor SamsaraQuake2TrapOvercookAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupSilentCheck")
        TNT1 A 0 A_JumpIfInventory("Cell",50,1)
        stop
        TNT1 A 0 A_TakeInventory("Cell",50)
        goto PickupSilentCheck

      // Silencer checking stuff
      PickupSilentCheck:
        TNT1 A 0 A_JumpIfInventory("PowerSilencer",1,"PickupAttack")
        TNT1 A 0 A_AlertMonsters // whoops
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("Q2TrapBoom",0,false)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("Q2TrapBoomCoop",0,false)
        stop
    }
}

actor SamsaraQuake2TrapLoopSoundPlayer : Trigger
{
    States
    {
      // Sound starting stuff
      Pickup:
        TNT1 A 0 A_PlaySound("Q2Weapons/TrapLoop",CHAN_WEAPON,1.0,true)
        TNT1 A 0 A_GiveInventory("SamsaraQuake2TrapLoopSoundCheck")
        stop
    }
}

actor SamsaraQuake2TrapLoopSoundStopper : Trigger
{
    States
    {
      // Sound stopping stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraQuake2TrapLoopSoundCheck",1,1)
        stop
        TNT1 A 0 A_PlaySound("silence",CHAN_WEAPON)
        TNT1 A 0 A_StopSound(CHAN_WEAPON)
        TNT1 A 0 A_TakeInventory("SamsaraQuake2TrapLoopSoundCheck")
        stop
    }
}

actor SamsaraQuake2TrapFade : Boolean {}

actor Q2TrapProjectile
{
    var int user_lifetime;
    Projectile
    Radius 8
    Speed 20
    DamageType "BittermanTrap"
    Gravity 0.5
    BounceType "Doom"
    BounceFactor 0.4
    WallBounceFactor 0.3
	//SelfObituary "%o was sucked into %p own trap."
    +BLOODLESSIMPACT
    +DONTREFLECT
    -NOGRAVITY
    States
    {
      Spawn:
        TRP0 A 0 NoDelay A_PlaySound("Q2Weapons/TrapLoop",CHAN_VOICE,1.0,true)
        TRP0 A 0 ThrustThing(angle*256/360,CallACS("Q2TrapCharge"),0,0)
        TRP0 A 0 ThrustThingZ(0,CallACS("Q2TrapCharge")/8,0,1)
      SpawnLoop:
        TRP0 A 2
        loop

      Death:
        TRP0 BCDE 2
      DeathLoop:
        TRP0 E 0 A_JumpIfInventory("SamsaraQuake2TrapFade",1,2)
        TRP0 E 0 A_PlaySound("Q2Weapons/TrapSuck",CHAN_VOICE,1.0,true)
        TRP0 E 0 A_JumpIfInventory("SamsaraQuake2TrapFade",1,2)
        TRP0 E 0 A_JumpIf(user_lifetime > 140, "DeathFade")
        TRP0 E 0 A_JumpIf(user_lifetime > 175, "DeathFinish")
        TRP0 E 0 A_SetUserVar("user_lifetime",user_lifetime+1)
        TRP0 E 0 A_Explode(20,24,0,false,24)
        TRP0 EEEEEEEE 0 A_SpawnItemEx("Q2RocketTrail",frandom(0,4),0,0,0,0,frandom(2,4),random(0,359))
        TRP0 EEEE 0 A_SpawnItemEx("Q2RocketTrail",frandom(0,4),0,frandom(46,50),frandom(0.25,1),0,frandom(-0.25,0.25),random(0,359))
        TRP0 E 2 A_RadiusThrust(-2000,768,RTF_NOIMPACTDAMAGE)
        loop

      DeathFade:
        TRP0 E 0 A_PlaySound("Q2Weapons/TrapDown",CHAN_VOICE)
        TRP0 E 0 A_GiveInventory("SamsaraQuake2TrapFade")
        goto DeathLoop

      DeathFinish:
        TRP1 ABCD 2
        TRP1 D 2 A_FadeOut
        wait
    }
}

actor Q2TrapProjectileCoop : Q2TrapProjectile
{
    Species "Player"
    +THRUSPECIES
}

actor Q2TrapBoom
{
    Projectile
    Radius 2
    Height 4
    DamageType "BittermanDamage"
    ReactionTime 1
    //SelfObituary "%o tried to put the pin back in."
    +DONTBLAST
    +DONTREFLECT
    +DONTSPLASH
    +NOTIMEFREEZE
    +NOTONAUTOMAP
    +NOTRIGGER
    +SKYEXPLODE
    States
    {
      Spawn:
        TNT1 A 1 NoDelay A_Countdown
        loop

      Death:
        TNT1 A 0 A_PlaySound("Q2Weapons/GrenadeExplosion",CHAN_VOICE)
        TNT1 A 0 A_SpawnItemEx("Q2Explosion",16,16)
        TNT1 A 1 A_Explode(200,165)
        stop
    }
}

actor Q2TrapBoomCoop : Q2TrapBoom
{
    Species "Player"
    +THRUSPECIES
}
