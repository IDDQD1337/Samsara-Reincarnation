actor SamsaraQuake2IonRipperLoopSoundCheck : Boolean {}

actor Q2IonRipper : Q2Weapon
{
    Inventory.PickupMessage "Ion Ripper"
    Weapon.SlotNumber 9
    Weapon.AmmoType "Cell"
    Weapon.AmmoGive 50
    Weapon.AmmoUse 2
    Tag "Ion Ripper"
    States
    {
      Spawn:
        Q2WP A -1
        stop

      ReadyActual:
        ION0 H 0 A_GiveInventory("SamsaraQuake2IonRipperLoopSoundStarter")
        ION0 H 1 A_WeaponReady
        ION0 H 0 A_Jump(24,1)
        loop
        ION0 H 0 A_GiveInventory("SamsaraQuake2IonRipperLoopSoundStarter")
        ION0 "HIJKLMNOPQRSTUVWXYZ[\]" 5 A_WeaponReady
        ION1 ABCDEFGH 5 A_WeaponReady
        loop

      Deselect:
        ION0 U 0 A_GiveInventory("SamsaraQuake2IonRipperLoopSoundStopper")
        ION0 U 0 A_JumpIfHealthLower(1,"DeselectInstant")
        ION0 U 2 A_PlaySound("Q2Weapons/IonAway")
        ION1 IJKL 3
        goto DeselectInstant

      Ready: // our "Select" state
        TNT1 A 0 A_TakeInventory("Q2WeaponIndex")
        TNT1 A 0 A_GiveInventory("Q2WeaponIndex",11)
        ION0 A 0 A_PlaySound("Q2Weapons/IonActive")
        ION0 A 0 A_GiveInventory("SamsaraQuake2IonRipperLoopSoundStarter")
        ION0 A 1 A_JumpIfInventory("SamsaraHasDoubleFiringSpeed",1,1)
        ION0 A 3 A_GiveInventory("SamsaraQuake2IonRipperLoopSoundStarter") // map change hack
        ION0 BCDE 4
        goto ReadyActual

      Fire:
        ION0 F 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart")
        ION0 F 0 A_JumpIfInventory("Cell",2,"FireStart")
        goto FireDry

      FireDry:
        ION0 H 0 A_ClearReFire
        ION0 H 0 A_PlaySound("Q2Weapons/NoAmmo",CHAN_5)
        ION0 H 0 A_SelectWeapon("Q2Blaster")
        goto Deselect

      FireStart:
        ION0 F 0 A_GiveInventory("SamsaraQuake2IonRipperLoopSoundStarter")
        ION0 F 0 A_GiveInventory("SamsaraQuake2IonRipperAttackHandler")
        ION0 F 0 A_GunFlash // have to place it here for recoil
        ION0 FGF 4 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto ReadyActual

      Flash:
        TNT1 A 1 A_Light1
        TNT1 A 0 A_JumpIfInventory("SamsaraRecoilIsDisabled",1,"FlashNoRecoil")
        goto FlashRecoil

      FlashNoRecoil:
        TNT1 A 1
        goto LightDone

      FlashRecoil:
        TNT1 A 1 A_SetPitch(pitch-0.005)
        TNT1 A 0 A_SetPitch(pitch+0.005)
        goto LightDone
    }
}

actor SamsaraQuake2IonRipperAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupPlaySound")
        TNT1 A 0 A_JumpIfInventory("Cell",2,1)
        stop
        TNT1 A 0 A_TakeInventory("Cell",2)
        goto PickupPlaySound

      // Sound stuff
      PickupPlaySound:
        TNT1 A 0 A_JumpIfInventory("PowerSilencer",1,4)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",2,3)
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_PlaySound("Q2Weapons/IonFire",CHAN_WEAPON)
        goto PickupAttack
        TNT1 A 0 A_PlaySound("Q2Weapons/IonFire",CHAN_WEAPON,0.25,false,ATTN_STATIC)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_FireCustomMissile("Q2WeaponFlash",0,false)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("Q2IonBoomerang",0,false,8,4)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("Q2IonBoomerang_Coop",0,false,8,4)
        stop
    }
}

actor SamsaraQuake2IonRipperLoopSoundStarter : Trigger
{
    States
    {
      // Sound starting stuff
      Pickup:
        TNT1 A 0 A_PlaySound("Q2Weapons/IonIdle",CHAN_6,0.5,true)
        TNT1 A 0 A_GiveInventory("SamsaraQuake2IonRipperLoopSoundCheck")
        stop
    }
}

actor SamsaraQuake2IonRipperLoopSoundStopper : Trigger
{
    States
    {
      // Sound stopping stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraQuake2IonRipperLoopSoundCheck",1,1)
        stop
        TNT1 A 0 A_PlaySound("silence",CHAN_6)
        TNT1 A 0 A_StopSound(CHAN_6)
        TNT1 A 0 A_TakeInventory("SamsaraQuake2IonRipperLoopSoundCheck")
        stop
    }
}

actor Q2IonBoomerang
{
    Projectile
    Radius 4
    Height 2
    Speed 16
    Damage (50)
    DamageType "BittermanDamage"
    BounceType "Hexen"
    BounceCount 5
    BounceSound "Q2Weapons/IonHit"
    DeathSound "Q2Weapons/IonExplode"
    Decal "BlasterScorch"
    +BLOODSPLATTER
    +NOGRAVITY
    States
    {
      Spawn:
        NULL A 0 Bright NoDelay A_SpawnItem("BlasterTrailSpawner",-7,2)
        NULL A 0 A_SpawnItem("BlasterTrailSpawner",-6,2)
        NULL A 0 A_SpawnItem("BlasterTrailSpawner",-5,2)
        NULL A 0 A_SpawnItem("BlasterTrailSpawner",-4,2)
        NULL A 0 A_SpawnItem("BlasterTrailSpawner",-3,2)
        NULL A 0 A_SpawnItem("BlasterTrailSpawner",-2,2)
        NULL A 0 A_SpawnItem("BlasterTrailSpawner",-1,2)
        NULL A 0 A_SpawnItem("BlasterTrailSpawner",0,2)
        NULL A 1 Bright A_PlaySound("Q2Weapons/LaserFly",CHAN_VOICE,0.5,true)
        loop

      Death:
        TNT1 A 1
        stop
    }
}

actor Q2IonBoomerang_Coop : Q2IonBoomerang
{
    Species "Player"
    +THRUSPECIES
}
