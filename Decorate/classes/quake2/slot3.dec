actor Q2SuperShotgun : Q2Weapon
{
    Inventory.PickupMessage "Super Shotgun"
    Inventory.Icon "Q2CMPW04"
    Weapon.SlotNumber 3
    Weapon.AmmoType "Shell"
    Weapon.AmmoGive 12
    Weapon.AmmoUse 2
    Tag "Super Shotgun"
    States
    {
      Spawn:
        Q2WP A -1
        stop

      ReadyActual:
        SSG1 A 1 A_WeaponReady
        SSG1 A 0 A_Jump(6,1)
        loop
        SSG1 "ABCDEFGHIJKLMNOPQRSTUVWXYZ[\]" 5 A_WeaponReady
        SSG2 ABCDEFGHIJK 5 A_WeaponReady
        loop

      Deselect:
        SSG1 A 2 A_JumpIfHealthLower(1,"DeselectInstant")
        SSG0 STUV 3
        goto DeselectInstant

      Ready: // our "Select" state
        TNT1 A 0 A_TakeInventory("Q2WeaponIndex")
        SSG0 A 4 A_GiveInventory("Q2WeaponIndex",3)
        SSG0 BCDEFG 4
        goto ReadyActual

      Fire:
        SSG0 H 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart")
        SSG0 H 0 A_JumpIfInventory("Shell",2,"FireStart")
        goto FireDry

      FireDry:
        SSG1 A 0 A_ClearReFire
        SSG1 A 0 A_PlaySound("Q2Weapons/NoAmmo",CHAN_5)
        SSG1 A 0 A_SelectWeapon("Q2Blaster")
        goto Deselect

      FireStart:
        SSG0 H 0 A_GiveInventory("SamsaraQuake2SuperShotgunAttackHandler")
        SSG0 H 0 A_GunFlash // have to place it here for recoil
        SSG0 HIJKLMNO 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        SSG0 PQR 4 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto ReadyActual

      Flash:
        TNT1 A 1 A_Light2
        TNT1 A 0 A_JumpIfInventory("SamsaraRecoilIsDisabled",1,"FlashNoRecoil")
        goto FlashRecoil

      FlashNoRecoil:
        TNT1 AAAAA 2
        TNT1 A 2 A_Light1
        TNT1 AAAA 2
        goto LightDone

      FlashRecoil:
        TNT1 A 0 A_SetPitch(pitch-5)
        TNT1 AAAAA 2 A_SetPitch(pitch+0.5)
        TNT1 A 0 A_Light1
        TNT1 AAAAA 2 A_SetPitch(pitch+0.5)
        goto LightDone
    }
}

actor SamsaraQuake2SuperShotgunAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupPlaySound")
        TNT1 A 0 A_JumpIfInventory("Shell",2,1)
        stop
        TNT1 A 0 A_TakeInventory("Shell",2)
        goto PickupPlaySound

      // Sound stuff
      PickupPlaySound:
        TNT1 A 0 A_JumpIfInventory("PowerSilencer",1,3)
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_PlaySound("Q2Weapons/SuperShotgunFire",CHAN_WEAPON)
        goto PickupAttack
        TNT1 A 0 A_PlaySound("Q2Weapons/SuperShotgunFire",CHAN_WEAPON,0.25,false,ATTN_STATIC)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_FireCustomMissile("Q2WeaponFlash",0,false)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireBullets(18,10,20,8,"SamsaraQuake2ShotgunPuff",FBF_NORANDOM|FBF_NOFLASH)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireBullets(18,10,20,8,"SamsaraQuake2ShotgunPuffCoop",FBF_NORANDOM|FBF_NOFLASH)
        stop
    }
}
