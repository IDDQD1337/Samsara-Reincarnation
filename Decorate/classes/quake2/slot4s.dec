actor SamsaraQuake2ETFRifleSide : Boolean {}

actor Q2ETFRifle : Q2Weapon
{
    Inventory.PickupMessage "ETF Rifle"
    Weapon.SlotNumber 4
    Weapon.SlotPriority 1
    Weapon.SelectionOrder 500
    Weapon.AmmoType "Q2Flechettes"
    Weapon.AmmoGive 50
    Weapon.AmmoUse 1
    Tag "ETF Rifle"
    States
    {
      Spawn:
        Q2WP A -1
        stop

      ReadyActual:
        ETF0 H 1 A_WeaponReady
        ETF0 H 0 A_Jump(24,1)
        loop
        ETF0 "HIJKLMNOPQRSTUVWXYZ[\]" 5 A_WeaponReady
        ETF1 ABCDEFGH 5 A_WeaponReady
        loop

      Deselect:
        //ETF0 U 2
        ETF1 I 3 A_JumpIfHealthLower(1,"DeselectInstant")
        ETF1 JKL 3
        goto DeselectInstant

      Ready: // our "Select" state
        TNT1 A 0 A_TakeInventory("Q2WeaponIndex")
        ETF0 A 4 A_GiveInventory("Q2WeaponIndex",10)
        ETF0 BCDE 4
        goto ReadyActual

      Fire:
        ETF0 F 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart")
        ETF0 F 0 A_JumpIfInventory("Q2Flechettes",1,"FireStart")
        goto FireDry

      FireDry:
        ETF0 H 0 A_ClearReFire
        ETF0 H 0 A_PlaySound("Q2Weapons/NoAmmo",CHAN_5)
        ETF0 H 0 A_SelectWeapon("Q2Blaster")
        goto Deselect

      FireStart:
        ETF0 F 0 A_TakeInventory("SamsaraQuake2ETFRifleSide")
        ETF0 F 0 A_GunFlash
        ETF0 F 0 A_GiveInventory("SamsaraQuake2ETFRifleAttackHandler")
        ETF0 F 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        ETF0 G 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        ETF0 G 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        ETF0 G 0 A_JumpIfInventory("Q2Flechettes",1,1)
        goto FireStart2
        ETF0 G 0 A_GunFlash
        goto FireStart2

      FireStart2:
        ETF0 G 0 A_GiveInventory("SamsaraQuake2ETFRifleAttackHandler")
        ETF0 G 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        ETF0 F 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0
        goto ReadyActual

      Flash:
        TNT1 A 1 A_Light2
        goto LightDone
    }
}

actor SamsaraQuake2ETFRifleAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupPlaySound")
        TNT1 A 0 A_JumpIfInventory("Q2Flechettes",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Q2Flechettes",1)
        goto PickupPlaySound

      // Sound stuff
      PickupPlaySound:
        TNT1 A 0 A_JumpIfInventory("PowerSilencer",1,3)
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_PlaySound("Q2Weapons/ETFFire",CHAN_WEAPON)
        goto PickupAttack
        TNT1 A 0 A_PlaySound("Q2Weapons/ETFFire",CHAN_WEAPON,0.25,false,ATTN_STATIC)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_FireCustomMissile("Q2WeaponFlash",0,false)
        TNT1 A 0 A_JumpIfInventory("SamsaraQuake2ETFRifleSide",1,"PickupAttackLeft")
      // Right
      PickupAttackRight:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackRightCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackRightCoop")
      PickupAttackRightDM:
        TNT1 A 0 A_FireCustomMissile("Q2ETFFlechette",0,false,8,4)
        goto PickupFinishRight

      PickupAttackRightCoop:
        TNT1 A 0 A_FireCustomMissile("Q2ETFFlechette_Coop",0,false,8,4)
        goto PickupFinishRight

      // Left
      PickupAttackLeft:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackLeftCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackLeftCoop")
      PickupAttackLeftDM:
        TNT1 A 0 A_FireCustomMissile("Q2ETFFlechette",0,false,4,4)
        goto PickupFinishLeft

      PickupAttackLeftCoop:
        TNT1 A 0 A_FireCustomMissile("Q2ETFFlechette_Coop",0,false,4,4)
        goto PickupFinishLeft

      // Finishing stuff
      // Right
      PickupFinishRight:
        TNT1 A 0 A_GiveInventory("SamsaraQuake2ETFRifleSide")
        TNT1 A 0 ACS_NamedExecuteWithResult("Q2ETFRifleShake", 1)
        stop

      // Left
      PickupFinishLeft:
        TNT1 A 0 A_TakeInventory("SamsaraQuake2ETFRifleSide")
        TNT1 A 0 ACS_NamedExecuteWithResult("Q2ETFRifleShake", 2)
        stop
    }
}

actor Q2Flechettes : Ammo
{
    Inventory.MaxAmount 200
    Ammo.BackpackAmount 20
    Ammo.BackpackMaxAmount 400
    Inventory.Icon "AMSMN0"
    Tag "Flechettes"
    +IGNORESKILL
}

actor Q2ETFFlechette : FastProjectile
{
    Radius 3
    Height 6
    Speed 32
    Damage (14)
    DamageType "BittermanDamage"
    Scale 0.5
    DeathSound "Q2Weapons/LaserHit"
    Decal "BlasterScorch"
    +BLOODSPLATTER
    +HITTRACER
    +PIERCEARMOR
    States
    {
      Spawn:
        NULL A -1 Bright
        stop

      Death:
        TNT1 A 3 A_SpawnItemEx("FlechetteProjectileImpact")
        TNT1 A 3
        TNT1 A 3
        TNT1 A 3
        stop

      XDeath:
        TNT1 A 0 A_JumpIf(CallACS("Q2FlechetteShieldPierce"), 1)
        goto XDeathFinish
        TNT1 A 0 A_CustomMissile("Q2ETFFlechettePierce",0,0,0,CMF_AIMDIRECTION,pitch)
        goto XDeathFinish

      XDeathFinish:
        TNT1 A 1
        stop
    }
}

actor Q2ETFFlechette_Coop : Q2ETFFlechette
{
    Species "Player"
    +THRUSPECIES
}

actor Q2ETFFlechettePierce : Q2ETFFlechette
{
    +THRUSPECIES
    States
    {
      Spawn:
        NULL A 0 Bright NoDelay A_JumpIf(CallACS("Q2FlechetteRetrieveSpecies"), 1)
        NULL A 0 A_TransferPointer(AAPTR_TARGET,AAPTR_DEFAULT,AAPTR_TARGET,AAPTR_TARGET)
        NULL A 1 Bright A_PlaySound("Q2Weapons/LaserFly",CHAN_VOICE,0.5,true)
        loop
    }
}

actor FlechetteProjectileImpact : Q2BulletPuff
{
    DamageType "BittermanDamage"
    States
    {
      Spawn:
        TNT1 AAAAAAAAAAAAA 0 NoDelay A_SpawnItemEx("FlechetteParticle",frandom(0,12),0,frandom(-8,15),frandom(0,0.5),0,frandom(-1,-0.1),random(0,359))
        stop
    }
}

actor FlechetteParticle : Q2BulletPuffParticle
{
    //Translation "80:111=192:196"
    States
    {
      Spawn:
        P007 ABCDEFGH 0 NoDelay A_Jump(64,"Fade")
        loop
    }
}
