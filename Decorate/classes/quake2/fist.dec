actor SamsaraQuake2ChainfistLoopSoundCheck : Boolean {}

actor Q2Chainfist : Q2Weapon
{
    Inventory.PickupMessage "Chainfist"
    Weapon.SlotNumber 0
    Weapon.SelectionOrder 1000
    Tag "Chainsaw"
    +MELEEWEAPON
    +WIMPY_WEAPON
    States
    {
      Spawn:
        Q2WP A -1
        stop

      ReadyActual:
        CHF2 A 0 A_GiveInventory("SamsaraQuake2ChainfistIdleSoundStarter")
      ReadyActual1:
        CHF2 ABCDEFGHIJ 4 A_WeaponReady
        CHF2 K 0 A_Jump(154,"ReadyActual2")
        CHF2 KKKKKKKKKKKKK 0 A_SpawnItemEx("Q2BulletPuffParticle",8,8,36,frandom(0,0.5),frandom(0,0.5),frandom(0.5,1))
      ReadyActual2:
        CHF2 K 4 A_WeaponReady
        CHF2 LMNOPQRS 4 A_WeaponReady
        CHF2 T 0 A_Jump(154,"ReadyActual3")
        CHF2 TTTTTTTTTTTTT 0 A_SpawnItemEx("Q2BulletPuffParticle",8,8,36,frandom(0,0.5),frandom(0,0.5),frandom(0.5,1))
      ReadyActual3:
        CHF2 TUVWXYZ 4 A_WeaponReady
        goto ReadyActual

      Deselect:
        CHF3 A 0 A_GiveInventory("SamsaraQuake2ChainfistLoopSoundStopper")
        CHF3 A 4 A_JumpIfHealthLower(1,"DeselectInstant")
        CHF3 BC 4
        goto DeselectInstant

      Ready: // our "Select" state
        TNT1 A 0 A_TakeInventory("Q2WeaponIndex")
        TNT1 A 0 A_GiveInventory("Q2WeaponIndex",12)
        CHF0 A 0 A_GiveInventory("SamsaraQuake2ChainfistIdleSoundStarter")
        CHF0 A 1 A_JumpIfInventory("SamsaraHasDoubleFiringSpeed",1,1)
        CHF0 A 3 A_GiveInventory("SamsaraQuake2ChainfistIdleSoundStarter") // map change hack
        CHF0 BCDE 4
        goto ReadyActual

      Fire:
        CHF2 A 0 A_GiveInventory("SamsaraQuake2ChainfistFiringSoundStarter")
        CHF2 A 0 A_GunFlash
        CHF2 A 0 A_Jump(256,"Fire1","Fire2","Fire3")
      Fire1:
        CHF1 BC 4 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        CHF1 D 0 A_GiveInventory("SamsaraQuake2ChainfistAttackHandler")
        CHF1 D 4 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        CHF1 E 0 A_GiveInventory("SamsaraQuake2ChainfistAttackHandler")
        CHF1 EFGHI 4 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        CHF2 A 0 A_ReFire(1)
        goto ReadyActual
        CHF2 A 0 A_GunFlash
        CHF2 A 0 A_Jump(256,"Fire2","Fire3")
        goto Fire2

      Fire2:
        CHF1 JK 4 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        CHF1 L 0 A_GiveInventory("SamsaraQuake2ChainfistAttackHandler")
        CHF1 L 4 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        CHF1 M 0 A_GiveInventory("SamsaraQuake2ChainfistAttackHandler")
        CHF1 M 4 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        CHF1 N 0 A_GiveInventory("SamsaraQuake2ChainfistAttackHandler")
        CHF1 NOPQRS 4 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        CHF2 A 0 A_ReFire(1)
        goto ReadyActual
        CHF2 A 0 A_GunFlash
        CHF2 A 0 A_Jump(256,"Fire1","Fire3")
        goto Fire3

      Fire3:
        CHF1 TU 4 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        CHF1 V 0 A_GiveInventory("SamsaraQuake2ChainfistAttackHandler")
        CHF1 VWX 4 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        CHF1 Y 0 A_GiveInventory("SamsaraQuake2ChainfistAttackHandler")
        CHF1 "YZ[" 4 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        CHF2 A 0 A_ReFire(1)
        goto ReadyActual
        CHF2 A 0 A_GunFlash
        CHF2 A 0 A_Jump(256,"Fire1","Fire2")
        goto Fire1

      Flash:
        TNT1 A 1
        TNT1 A 0 A_JumpIfInventory("SamsaraRecoilIsDisabled",1,"FlashFinish")
        TNT1 A 0 A_SetPitch(pitch-3)
        TNT1 AAAAAAAAAA 1 A_SetPitch(pitch+0.3)
      FlashFinish:
        TNT1 A 0
        stop
    }
}

actor SamsaraQuake2ChainfistAttackHandler : Trigger
{
    States
    {
      // Attack stuff
      Pickup:
        TNT1 A 0 A_PlaySound("Q2Weapons/SawHit",CHAN_WEAPON,1.0,true)
        TNT1 A 0 A_GiveInventory("SamsaraQuake2ChainfistLoopSoundCheck")
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupCoop")
      PickupDM:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupDMSpread")
        TNT1 A 0 A_CustomPunch(35,true,CPF_NORANDOMPUFFZ,"Q2ChainfistPuff")
        stop

      PickupDMSpread:
        TNT1 A 0 A_CustomPunch(35*3,true,CPF_NORANDOMPUFFZ,"Q2ChainfistPuff")
        stop

      PickupCoop:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupCoopSpread")
        TNT1 A 0 A_CustomPunch(35,true,CPF_NORANDOMPUFFZ,"Q2ChainfistPuffCoop")
        stop

      PickupCoopSpread:
        TNT1 A 0 A_CustomPunch(35*3,true,CPF_NORANDOMPUFFZ,"Q2ChainfistPuffCoop")
        stop
    }
}

actor SamsaraQuake2ChainfistIdleSoundStarter : Trigger
{
    States
    {
      // Sound starting stuff
      Pickup:
        TNT1 A 0 A_PlaySound("Q2Weapons/SawIdle",CHAN_WEAPON,1.0,true)
        TNT1 A 0 A_GiveInventory("SamsaraQuake2ChainfistLoopSoundCheck")
        stop
    }
}

actor SamsaraQuake2ChainfistFiringSoundStarter : Trigger
{
    States
    {
      // Sound starting stuff
      Pickup:
        TNT1 A 0 A_PlaySound("Q2Weapons/SawHit",CHAN_WEAPON,1.0,true)
        TNT1 A 0 A_GiveInventory("SamsaraQuake2ChainfistLoopSoundCheck")
        stop
    }
}

actor SamsaraQuake2ChainfistLoopSoundStopper : Trigger
{
    States
    {
      // Sound stopping stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraQuake2ChainfistLoopSoundCheck",1,1)
        stop
        TNT1 A 0 A_PlaySound("silence",CHAN_WEAPON)
        TNT1 A 0 A_StopSound(CHAN_WEAPON)
        TNT1 A 0 A_TakeInventory("SamsaraQuake2ChainfistLoopSoundCheck")
        stop
    }
}

actor Q2ChainfistPuff : Q2BulletPuff
{
    +PUFFONACTORS
    States
    {
      Melee:
      Crash:
        goto Spawn

      XDeath:
        TNT1 A 1
        stop
    }
}

actor Q2ChainfistPuffCoop : Q2ChainfistPuff { +MTHRUSPECIES }
