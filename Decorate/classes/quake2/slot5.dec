actor Q2RocketLauncher : Q2Weapon
{
    Inventory.PickupMessage "Rocket Launcher"
    Weapon.SlotNumber 5
    Weapon.SlotPriority 0
    Weapon.SelectionOrder 750
    Weapon.AmmoType "RocketAmmo"
    Weapon.AmmoGive 5
    Weapon.AmmoUse 1
    Tag "Rocket Launcher"
    +EXPLOSIVE
    States
    {
      Spawn:
        Q2WP A -1
        stop

      ReadyActual:
        RCK0 N 1 A_WeaponReady
        RCK0 N 0 A_Jump(32,1)
        loop
        RCK0 "NOPQRSTUVWXYZ[\]" 5 A_WeaponReady
        RCK1 ABCDEFGHIJKLMNOPQRSTUV 5 A_WeaponReady
        loop

      Deselect:
        RCK0 N 2 A_JumpIfHealthLower(1,"DeselectInstant")
        RCK1 WXYZ 3
        goto DeselectInstant

      Ready: // our "Select" state
        TNT1 A 0 A_TakeInventory("Q2WeaponIndex")
        RCK0 A 4 A_GiveInventory("Q2WeaponIndex",5)
        RCK0 BCDE 4
        goto ReadyActual

      Fire:
        RCK0 F 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart")
        RCK0 F 0 A_JumpIfInventory("RocketAmmo",1,"FireStart")
        goto FireDry

      FireDry:
        RCK0 N 0 A_ClearReFire
        RCK0 N 0 A_PlaySound("Q2Weapons/NoAmmo",CHAN_5)
        RCK0 N 0 A_SelectWeapon("Q2Blaster")
        goto Deselect

      FireStart:
        RCK0 F 0 A_GiveInventory("SamsaraQuake2RocketLauncherAttackHandler")
        RCK0 F 0 A_GunFlash // have to place it here for recoil
        RCK0 FG 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        RCK0 H 0 A_GiveInventory("SamsaraQuake2RocketLauncherLoadSoundPlayer")
        RCK0 HIJ 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        RCK0 KLM 5 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        RCK0 N 7 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto ReadyActual

      Flash:
        TNT1 A 0 A_Light2
        TNT1 A 0 A_JumpIfInventory("SamsaraRecoilIsDisabled",1,"FlashNoRecoil")
        goto FlashRecoil

      FlashNoRecoil:
        TNT1 AAAAAA 1
        TNT1 A 1 A_Light1
        TNT1 AAAA 1
        goto LightDone

      FlashRecoil:
        TNT1 A 1 A_SetPitch(pitch-5)
        TNT1 AAAAA 1 A_SetPitch(pitch+0.5)
        TNT1 A 0 A_Light1
        TNT1 AAAAA 1 A_SetPitch(pitch+0.5)
        goto LightDone
    }
}

actor SamsaraQuake2RocketLauncherAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupPlaySound")
        TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,1)
        stop
        TNT1 A 0 A_TakeInventory("RocketAmmo",1)
        goto PickupPlaySound

      // Sound stuff
      PickupPlaySound:
        TNT1 A 0 A_JumpIfInventory("PowerSilencer",1,4)
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_PlaySound("Q2Weapons/RocketLauncherFire",CHAN_WEAPON)
        TNT1 A 0 A_TakeInventory("SamsaraQuake2SilentFired")
        goto PickupAttack
        TNT1 A 0 A_PlaySound("Q2Weapons/RocketLauncherFire",CHAN_WEAPON,0.25,false,ATTN_STATIC)
        TNT1 A 0 A_GiveInventory("SamsaraQuake2SilentFired")
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("Q2Rocket_DM",0,false,4,3)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("Q2Rocket_Coop",0,false,4,3)
        stop
    }
}

actor SamsaraQuake2RocketLauncherLoadSoundPlayer : Trigger
{
    States
    {
      // Sound playing stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraQuake2SilentFired",1,2)
        TNT1 A 0 A_PlaySound("Q2Weapons/RocketLauncherLoad")
        stop
        TNT1 A 0 A_PlaySound("Q2Weapons/RocketLauncherLoad",CHAN_BODY,0.25,false,ATTN_STATIC)
        stop
    }
}

actor Q2Rocket : Rocket
{
    Speed 20
    Damage (150)
    DamageType "BittermanDamage"
    DeathSound "Q2Weapons/RocketExplosion"
    Decal "Scorch"
	//SelfObituary "%o blew %hself up."
    -ROCKETTRAIL
    States
    {
      Spawn:
        MIS1 AAAAA 0 Bright Light("Q2ROCKET") NoDelay A_SpawnItemEx("Q2GrenadeSmoke",random2(5),0,random2(5),0,0,frandom(0.1,0.5),random(0,359),0,128)
        MIS1 A 0 A_SpawnItemEx("Q2RocketTrail",0,0,0,frandom(0,0.5),0,frandom(-0.5,0),random(0,359),SXF_CLIENTSIDE)
        MIS1 A 1 Light("Q2ROCKET") A_PlaySound("Q2Weapons/RocketFly",CHAN_VOICE,0.66,true)
        loop

      Death:
        TNT1 A 0 A_Explode(150,120)
        TNT1 A 1 A_SpawnItemEx("Q2Explosion")
        stop
    }
}

actor Q2Rocket_Coop : Q2Rocket
{
    Species "Player"
    +THRUSPECIES
}

actor Q2Rocket_DM : Q2Rocket
{
    Speed 24
    Damage (0)
}

actor Q2RocketTrail : Q2GrenadeSmoke
{
    //Translation "80:111=160:167"
    States
    {
      Spawn:
        P006 ABCDEFGH 0 NoDelay A_Jump(64,"Fade")
        loop
    }
}
