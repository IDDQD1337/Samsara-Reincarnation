actor SamsaraQuake2PhalanxLoopSoundCheck : Boolean {}
actor SamsaraQuake2PhalanxAttackSilentSound : Boolean {}

actor Q2Phalanx : Q2Weapon
{
    Inventory.PickupMessage "Phalanx"
    Inventory.Icon "Q2CMPW06" // placeholder
    Weapon.SlotNumber 5
    Weapon.SlotPriority 1
    Weapon.SelectionOrder 500
    Weapon.AmmoType "RocketAmmo"
    Weapon.AmmoGive 5
    Weapon.AmmoUse 1
    Tag "Phalanx Particle Cannon"
    +EXPLOSIVE
    States
    {
      Spawn:
        Q2WP A -1
        stop

      ReadyActual:
        PHX2 A 0 A_GiveInventory("SamsaraQuake2PhalanxLoopSoundStarter")
        PHX2 A 1 A_WeaponReady
        PHX2 A 0 A_Jump(32,1)
        loop
        PHX2 A 0 A_GiveInventory("SamsaraQuake2PhalanxLoopSoundStarter")
        PHX2 "ABCDEFGHIJKLMNOPQRSTUVWXYZ[\]" 4 A_WeaponReady
        PHX3 ABCDEFGHIJ 5 A_WeaponReady
        loop

      Deselect:
        PHX2 A 0 A_JumpIfHealthLower(1,"DeselectInstant")
        PHX2 A 3 A_GiveInventory("SamsaraQuake2PhalanxLoopSoundStarter")
        PHX4 ABCDE 3
      DeselectInstant:
        TNT1 A 0 A_GiveInventory("SamsaraQuake2PhalanxLoopSoundStopper")
        goto Super::DeselectInstant

      Ready: // our "Select" state
        TNT1 A 0 A_TakeInventory("Q2WeaponIndex")
        TNT1 A 0 A_GiveInventory("Q2WeaponIndex",13)
        PHX0 A 1 A_GiveInventory("SamsaraQuake2PhalanxLoopSoundStarter")
        PHX0 A 2 A_GiveInventory("SamsaraQuake2PhalanxLoopSoundStarter") // map change hack
        PHX0 BCDEF 3
        goto ReadyActual

      Fire:
        PHX2 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart")
        PHX2 A 0 A_JumpIfInventory("RocketAmmo",1,"FireStart")
        goto FireDry

      FireDry:
        PHX2 A 0 A_ClearReFire
        PHX2 A 0 A_PlaySound("Q2Weapons/NoAmmo",CHAN_5)
        PHX2 A 0 A_SelectWeapon("Q2Blaster")
        goto Deselect

      FireStart:
        PHX2 A 0 A_GiveInventory("SamsaraQuake2PhalanxLoopSoundStarter")
        PHX2 A 4 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        PHX1 A 0 A_TakeInventory("SamsaraQuake2PhalanxAttackSilentSound")
        PHX1 A 0 A_GiveInventory("SamsaraQuake2PhalanxAttackHandler1")
        PHX1 A 0 A_JumpIfInventory("SamsaraQuake2AmmoCheckFailed",1,2)
        PHX1 A 0 A_GunFlash // have to place it here for recoil
        PHX1 A 4 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        PHX1 B 0 A_GiveInventory("SamsaraQuake2PhalanxAttackSilentSound")
        PHX1 B 0 A_GiveInventory("SamsaraQuake2PhalanxAttackHandler2")
        PHX1 BCDEFGHIJKLMN 4 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto ReadyActual

      Flash:
        TNT1 A 1 A_Light2
        TNT1 A 0 A_JumpIfInventory("SamsaraRecoilIsDisabled",1,"FlashNoRecoil")
        goto FlashRecoil

      FlashNoRecoil:
        TNT1 AAAAA 1
        TNT1 A 1 A_Light1
        TNT1 AAAA 1
        goto LightDone

      FlashRecoil:
        TNT1 A 0 A_SetPitch(pitch-3)
        TNT1 AAAAA 1 A_SetPitch(pitch+0.3)
        TNT1 A 0 A_Light1
        TNT1 AAAAA 1 A_SetPitch(pitch+0.3)
        goto LightDone
    }
}

actor SamsaraQuake2PhalanxAttackHandler1 : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_TakeInventory("SamsaraQuake2AmmoCheckFailed")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupPlaySound")
        TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,1)
        goto PickupAmmoCheckFailed
        TNT1 A 0 A_TakeInventory("RocketAmmo",1)
        goto PickupPlaySound

      // Ammo checking failure stuff
      PickupAmmoCheckFailed:
        TNT1 A 0 A_GiveInventory("SamsaraQuake2AmmoCheckFailed")
        stop

      // Sound stuff
      PickupPlaySound:
        TNT1 A 0 A_JumpIfInventory("PowerSilencer",1,2)
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_JumpIfInventory("SamsaraQuake2PhalanxAttackSilentSound",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("PowerSilencer",1,2)
        TNT1 A 0 A_PlaySound("Q2Weapons/PhalanxFire",CHAN_WEAPON)
        goto PickupAttack
        TNT1 A 0 A_PlaySound("Q2Weapons/PhalanxFire",CHAN_WEAPON,0.25,false,ATTN_STATIC)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("Q2PhalanxMissile",1.5,false,8,0,0,-2.1)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("Q2PhalanxMissileCoop",1.5,false,8,0,0,-2.1)
        stop
    }
}

actor SamsaraQuake2PhalanxAttackHandler2 : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_TakeInventory("SamsaraQuake2AmmoCheckFailed")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupPlaySound")
        TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,1)
        goto PickupAmmoCheckFailed
        TNT1 A 0 A_TakeInventory("RocketAmmo",1)
        goto PickupPlaySound

      // Ammo checking failure stuff
      PickupAmmoCheckFailed:
        TNT1 A 0 A_GiveInventory("SamsaraQuake2AmmoCheckFailed")
        stop

      // Sound stuff
      PickupPlaySound:
        TNT1 A 0 A_JumpIfInventory("PowerSilencer",1,2)
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_JumpIfInventory("SamsaraQuake2PhalanxAttackSilentSound",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("PowerSilencer",1,2)
        TNT1 A 0 A_PlaySound("Q2Weapons/PhalanxFire",CHAN_WEAPON)
        goto PickupAttack
        TNT1 A 0 A_PlaySound("Q2Weapons/PhalanxFire",CHAN_WEAPON,0.25,false,ATTN_STATIC)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("Q2PhalanxMissile",-1.5,false,8,0,0,-0.9)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("Q2PhalanxMissileCoop",-1.5,false,8,0,0,-0.9)
        stop
    }
}

actor SamsaraQuake2PhalanxLoopSoundStarter : Trigger
{
    States
    {
      // Sound starting stuff
      Pickup:
        TNT1 A 0 A_PlaySound("Q2Weapons/PhalanxLoop",CHAN_6,1.0,true)
        TNT1 A 0 A_GiveInventory("SamsaraQuake2PhalanxLoopSoundCheck")
        stop
    }
}

actor SamsaraQuake2PhalanxLoopSoundStopper : Trigger
{
    States
    {
      // Sound stopping stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraQuake2PhalanxLoopSoundCheck",1,1)
        stop
        TNT1 A 0 A_PlaySound("silence",CHAN_6)
        TNT1 A 0 A_StopSound(CHAN_6)
        TNT1 A 0 A_TakeInventory("SamsaraQuake2PhalanxLoopSoundCheck")
        stop
    }
}

actor Q2PhalanxMissile : Q2Rocket
{
    Damage (75)
    DeathSound "Q2Weapons/PhalanxExplode"
    +FORCEXYBILLBOARD
    States
    {
      Spawn:
        Q2PX A 0 Bright NoDelay Light("Q2ROCKET") A_PlaySound("Q2Weapons/RocketFly",CHAN_VOICE,0.66,true)
        TNT1 AAAAA 0 A_SpawnItemEx("Q2RocketTrail",0,0,0,-frandom(0,0.5),0,frandom(-0.5,0),random(0,359),SXF_CLIENTSIDE)
        Q2PX A 1 Bright Light("Q2ROCKET")
        TNT1 AAAAA 0 A_SpawnItemEx("Q2RocketTrail",0,0,0,-frandom(0,0.5),0,frandom(-0.5,0),random(0,359),SXF_CLIENTSIDE)
        Q2PX B 1 Bright Light("Q2ROCKET")
        TNT1 AAAAA 0 A_SpawnItemEx("Q2RocketTrail",0,0,0,-frandom(0,0.5),0,frandom(-0.5,0),random(0,359),SXF_CLIENTSIDE)
        Q2PX C 1 Bright Light("Q2ROCKET")
        loop

      Death:
        TNT1 A 0 A_Explode(225,120)
        TNT1 A 1 A_SpawnItemEx("Q2Explosion")
        stop
    }
}

actor Q2PhalanxMissileCoop : Q2PhalanxMissile
{
    Species "Player"
    +THRUSPECIES
}
