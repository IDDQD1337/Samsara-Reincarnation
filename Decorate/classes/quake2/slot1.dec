actor SamsaraQuake2MachinegunRecoilClimb : Counter { Inventory.MaxAmount 10 }

actor Q2Machinegun : Q2Weapon
{
    Inventory.PickupMessage "Machinegun"
    Weapon.SlotNumber 1
    Weapon.AmmoType "Clip"
    Weapon.AmmoGive 50
    Weapon.AmmoUse 1
    Tag "Machinegun"
    States
    {
      Spawn:
        Q2WP A -1
        stop

      ReadyActual:
        MGN0 H 1 A_WeaponReady
        MGN0 H 0 A_Jump(6,1)
        loop
        MGN0 "HIJKLMNOPQRSTUVWXYZ[\]" 5 A_WeaponReady
        MGN1 ABCDEFGHIJKLMNOPQR 5 A_WeaponReady
        loop

      Deselect:
        MGN0 H 3 A_JumpIfHealthLower(1,"DeselectInstant")
        MGN1 STUV 3
        goto Super::DeselectInstant

      DeselectInstant:
        TNT1 A 0 A_TakeInventory("SamsaraQuake2MachinegunRecoilClimb")
        goto Super::DeselectInstant

      Ready: // our "Select" state
        TNT1 A 0 A_TakeInventory("Q2WeaponIndex")
        TNT1 A 0 A_GiveInventory("Q2WeaponIndex",1)
        MGN0 A 1 A_TakeInventory("SamsaraQuake2MachinegunRecoilClimb")
        MGN0 A 4 A_TakeInventory("SamsaraQuake2MachinegunRecoilClimb") // map change hack
        MGN0 BCD 5
        goto ReadyActual

      Fire:
        MGN0 F 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart")
        MGN0 F 0 A_JumpIfInventory("Clip",1,"FireStart")
        goto FireDry

      FireDry:
        MGN0 H 0 A_ClearReFire
        MGN0 H 0 A_GunFlash("FlashClimbDown",GFF_NOEXTCHANGE)
        MGN0 H 0 A_PlaySound("Q2Weapons/NoAmmo",CHAN_5)
        MGN0 H 0 A_SelectWeapon("Q2Blaster")
        goto Deselect

      FireStart:
        MGN0 F 0 A_GiveInventory("SamsaraQuake2MachinegunAttackHandler")
        MGN0 F 0 A_GunFlash // have to place it here for recoil
        MGN0 F 4 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        MGN0 E 0 A_ReFire("Fire2")
        goto FireFinish

      Fire2:
        MGN0 E 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"Fire2Start")
        MGN0 E 0 A_JumpIfInventory("Clip",1,"Fire2Start")
        goto FireDry

      Fire2Start:
        MGN0 E 0 A_GiveInventory("SamsaraQuake2MachinegunAttackHandler")
        MGN0 E 0 A_GunFlash
        MGN0 E 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        MGN0 F 0 A_ReFire
        goto FireFinish

      FireFinish:
        MGN0 H 0 A_GunFlash("FlashClimbDown",GFF_NOEXTCHANGE)
        goto ReadyActual

      Flash:
        TNT1 A 0 A_JumpIfInventory("SamsaraQuake2MachinegunRecoilClimb",0,"FlashFinish")
        TNT1 A 0 A_GiveInventory("SamsaraQuake2MachinegunRecoilClimb")
        TNT1 A 0 A_JumpIfInventory("SamsaraRecoilIsDisabled",1,"FlashFinish")
        TNT1 AAA 1 A_SetPitch(pitch-0.33)
      FlashFinish:
        TNT1 A 0
        stop

      FlashClimbDown:
        TNT1 A 0 A_JumpIfInventory("SamsaraQuake2MachinegunRecoilClimb",1,1)
        stop
        TNT1 A 0 A_TakeInventory("SamsaraQuake2MachinegunRecoilClimb",1)
        TNT1 A 0 A_JumpIfInventory("SamsaraRecoilIsDisabled",1,2)
        TNT1 A 0 A_SetPitch(pitch+0.99)
        TNT1 A 0 A_JumpIfInventory("SamsaraQuake2MachinegunRecoilClimb",1,1)
        stop
        TNT1 A 0 A_TakeInventory("SamsaraQuake2MachinegunRecoilClimb",1)
        TNT1 A 0 A_JumpIfInventory("SamsaraRecoilIsDisabled",1,2)
        TNT1 A 0 A_SetPitch(pitch+0.99)
        TNT1 A 1
        loop
    }
}

actor SamsaraQuake2MachinegunAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupPlaySound")
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",1)
        goto PickupPlaySound

      // Sound stuff
      PickupPlaySound:
        TNT1 A 0 A_JumpIfInventory("PowerSilencer",1,3)
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_PlaySound("Q2Weapons/MachineGunFire",CHAN_WEAPON)
        goto PickupAttack
        TNT1 A 0 A_PlaySound("Q2Weapons/MachineGunFire",CHAN_WEAPON,0.25,false,ATTN_STATIC)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_FireCustomMissile("Q2WeaponFlash",0,false)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireBullets(5,2,-1,16,"Q2BulletPuff",FBF_NORANDOM|FBF_NOFLASH)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireBullets(5,2,-1,16,"Q2BulletPuffCoop",FBF_NORANDOM|FBF_NOFLASH)
        stop
    }
}
