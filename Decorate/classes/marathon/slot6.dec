actor UsingToasty: Boolean {}
actor ToztCounter: Counter { Inventory.MaxAmount 6 }

actor "TOZT-7 Napalm Unit" : SamsaraMarathonWeapon
{
    Inventory.PickupMessage "You picked up a TOZT-7 Backpack Napalm Unit!"
    Weapon.SlotNumber 6
    Weapon.SelectionOrder 700
    Weapon.AmmoType1 "NapalmInTank"
    Weapon.AmmoUse1 1
    Weapon.AmmoType2 "Cell"
    Weapon.AmmoGive2 110
    Tag "TOZT-7 Napalm Unit"
    States
    {
      Spawn:
        THRW A -1
        stop

      Ready:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",0,"ReadyAllowReload")
      ReadyDisallowReload:
        FLMT A 1 A_WeaponReady
        goto ReadyAmmoCheck

      ReadyAllowReload:
        TNT1 A 0 A_JumpIfInventory("NapalmInTank",0,"ReadyDisallowReload")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Cell",1,1)
        goto ReadyDisallowReload
        FLMT A 1 A_WeaponReady(WRF_ALLOWRELOAD)
        goto ReadyAmmoCheck

      ReadyAmmoCheck:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",2,1)
        goto Ready
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"Ready")
        TNT1 A 0 A_JumpIfInventory("NapalmInTank",1,"Ready")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"Reload")
        TNT1 A 0 A_JumpIfInventory("Cell",1,"Reload")
        goto Ready

      Deselect:
        TNT1 A 0 A_JumpIfHealthLower(1,"DeselectInstant")
        FLMT A 1 A_Lower
        loop

      Select: // 30 tics in Marathon to ready
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"SelectReloadAmmoCheck")
      SelectStart:
        TNT1 A 0 A_TakeInventory("UsingFists")
        TNT1 A 0 A_TakeInventory("UsingPistols")
        TNT1 A 0 A_TakeInventory("UsingDualPistols")
        TNT1 A 0 A_TakeInventory("UsingSMG")
        TNT1 A 0 A_TakeInventory("UsingPacifier")
        TNT1 A 0 A_TakeInventory("UsingShotguns")
        TNT1 A 0 A_TakeInventory("UsingDualShotguns")
        TNT1 A 0 A_TakeInventory("UsingFusionPistol")
        TNT1 A 0 A_TakeInventory("UsingAssaultRifle")
        TNT1 A 0 A_TakeInventory("UsingSpanker")
        //TNT1 A 0 A_TakeInventory("UsingToasty")
        TNT1 A 0 A_TakeInventory("UsingWMC")
        TNT1 A 0 A_TakeInventory("UsingAlienWeapon")
        TNT1 A 0 A_TakeInventory("UsingAlienWeapon2")
        TNT1 A 0 A_TakeInventory("UsingMarathonInstagib")

        TNT1 A 0 A_GiveInventory("UsingToasty")
        FLMT A 1 A_Raise
        loop

      SelectReloadAmmoCheck:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"SelectStart")
        TNT1 A 0 A_JumpIfInventory("NapalmInTank",1,"SelectStart")
      SelectReloadAmmo:
        TNT1 A 0 A_JumpIfInventory("NapalmInTank",0,"SelectStart")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Cell",1,1)
        goto SelectStart
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_TakeInventory("Cell",1)
        TNT1 A 0 A_GiveInventory("NapalmInTank")
        loop

      Fire:
      AltFire:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"FireReloadable")
      FireNonReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart")
        TNT1 A 0 A_JumpIfInventory("Cell",1,"FireStart")
        goto Ready

      FireReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireStart")
        TNT1 A 0 A_JumpIfInventory("NapalmInTank",1,"FireStart")
      FireReloadableDryCheck:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"Reload")
        TNT1 A 0 A_JumpIfInventory("Cell",1,"Reload")
        goto Ready

      FireStart:
        TNT1 A 0 A_JumpIfInventory("SamsaraCurrentWaterLevel",0,"Ready")
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_GiveInventory("SamsaraMarathonFlamethrowerAttackHandler")
        FLMT B 1 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_ReFire
        goto Ready

      Reload:
        FLMT A 1 Offset(0,38)
        FLMT A 1 Offset(0,44)
        FLMT A 1 Offset(0,50)
        FLMT A 1 Offset(0,56)
        FLMT A 1 Offset(0,62)
        FLMT A 1 Offset(0,68)
        FLMT A 1 Offset(0,74)
        FLMT A 1 Offset(0,80)
        FLMT A 1 Offset(0,86)
        FLMT A 1 Offset(0,92)
        FLMT A 1 Offset(0,98)
        FLMT A 1 Offset(0,104)
        FLMT A 1 Offset(0,110)
        FLMT A 1 Offset(0,116)
        FLMT A 1 Offset(0,122)
        FLMT A 18 Offset(0,128)
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"SelectReloadAmmo")
        goto SelectStart
    }
}

actor SamsaraMarathonFlamethrowerAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",2,"PickupReloadable")
      PickupNonReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Cell",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Cell",1)
        goto PickupAttack

      PickupReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("NapalmInTank",1,1)
        stop
        TNT1 A 0 A_TakeInventory("NapalmInTank",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("marathon/flamethrower/fire",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("MaraFlameShot",0,false,0,3)
        goto PickupFinish

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("MaraFlameShot2",0,false,0,3)
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 A_Recoil(0.1)
        stop
    }
}

actor NapalmInTank : Ammo
{
    Inventory.MaxAmount 210
    Tag "Napalm Canister"
    +IGNORESKILL
}

actor MaraFlameShot
{
    Projectile
    Radius 6
    Height 8
    Speed 50
    Damage (random(6,10))
    DamageType "MarathonFire"
    Scale 0.85
    Obituary "$SAMSARA_MARATHON_OB_SLOT6"
    +BLOODLESSIMPACT
    +NOBOSSRIP
    +RANDOMIZE
    +RIPPER
    States
    {
      Spawn:
        MFLS A 1 Bright
        MFLS BCDEFG 1 Bright A_GiveInventory("ToztCounter")
        stop

      Death:
        TNT1 A 0 A_JumpIfInventory("ToztCounter",0,12)
        TNT1 A 0 A_JumpIfInventory("ToztCounter",5,10)
        TNT1 A 0 A_JumpIfInventory("ToztCounter",4,8)
        TNT1 A 0 A_JumpIfInventory("ToztCounter",3,6)
        TNT1 A 0 A_JumpIfInventory("ToztCounter",2,4)
        TNT1 A 0 A_JumpIfInventory("ToztCounter",1,2)
        MFLS ABCDEFG 1 Bright
        stop
    }
}

actor MaraFlameShot2 : MaraFlameshot
{
    Species "Player"
    Damage (random(8,12))
    +THRUSPECIES
}
