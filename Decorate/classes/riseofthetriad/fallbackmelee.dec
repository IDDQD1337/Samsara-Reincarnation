actor RottKnife : SamsaraROTTWeapon
{
    Weapon.UpSound "gen/wepswitch"
    Weapon.SlotNumber 0
    Weapon.SelectionOrder 3700
    Weapon.Kickback 100
    Obituary "%k stabbed %o to death."
    Tag "Knife"
    +MELEEWEAPON
    +NOALERT
    +WIMPY_WEAPON
    States
    {
      Ready:
        TNT1 A 0 A_TakeInventory("UsingFirearm")
        TNT1 A 1 A_WeaponReady
        loop

      Deselect:
        TNT1 A 0 A_JumpIfHealthLower(1,"DeselectInstant")
        TNT1 A 1 A_Lower
        loop

      Select:
        TNT1 A 1 A_Raise
        loop

      Fire:
        TNT1 A 0 A_JumpIfInventory("RottMode",4,"FireBlack")
      FireWhite:
        RTKF ABCDE 2
        RTKF F 2 A_GiveInventory("SamsaraROTTKnifeAttackHandler")
      FireFinish:
        RTKF GH 2
        TNT1 A 2
        TNT1 AA 2 A_ReFire
        goto Ready

      FireBlack:
        RTKF KLMNO 2
        RTKF P 2 A_GiveInventory("SamsaraROTTKnifeAttackHandler")
        goto FireFinish
    }
}

actor SamsaraROTTKnifeAttackHandler : Trigger
{
    States
    {
      // Attack stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupCoop")
      PickupDM:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupDMSpread")
        TNT1 A 0 A_CustomPunch(13,true,0,"Nothing2")
        stop

      PickupDMSpread:
        TNT1 A 0 A_CustomPunch(13*3,true,0,"Nothing2")
        stop

      PickupCoop:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupCoopSpread")
        TNT1 A 0 A_CustomPunch(13,true,0,"Nothing2Coop")
        stop

      PickupCoopSpread:
        TNT1 A 0 A_CustomPunch(13*3,true,0,"Nothing2Coop")
        stop
    }
}

actor Nothing2
{
    ActiveSound "rottknife/swing"
    Obituary "%k stabbed %o to death."
    States
    {
      Spawn:
      Crash:
        TNT1 A 1 A_PlaySound("rottknife/fail")
        stop
    }
}

actor Nothing2Coop : Nothing2 { +MTHRUSPECIES }
