actor MP40 : SamsaraROTTWeapon
{
    Inventory.PickupMessage "You picked up an MP40."
    Weapon.UpSound "gen/wepswitch"
    Weapon.SlotNumber 4
    Weapon.SlotPriority 0
    Weapon.SelectionOrder 1100
    Obituary "%k bullet-whored %o with an MP40."
    Tag "MP40"
    States
    {
      Spawn:
        PM40 A -1
        stop

      Ready:
        TNT1 A 0 A_GiveInventory("UsingFirearm")
        MP40 A 1 A_WeaponReady
        loop

      Deselect:
        TNT1 A 0 A_JumpIfHealthLower(1,"DeselectInstant")
        MP40 A 1 A_Lower
        loop

      Select:
        MP40 A 1 A_Raise
        loop

      Fire:
        TNT1 A 0 A_GunFlash
        TNT1 A 0 A_GiveInventory("SamsaraROTTMP40AttackHandler")
        MP40 B 2 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        MP40 CDA 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_JumpIfInventory("PowerGodProtection",1,2)
        TNT1 A 0 A_ReFire
        TNT1 A 0 A_ClearReFire
        MP40 A 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto Ready
    }
}

actor SamsaraROTTMP40AttackHandler : Trigger
{
    States
    {
      // Sound stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraAlternateSoundToken",1,"PickupPlaySoundAlternate")
      // Rise of the Triad (Original) sounds
      PickupPlaySoundOriginal:
        TNT1 A 0 A_PlaySound("mp40/shoot",CHAN_WEAPON)
        goto PickupAttack

      // Rise of the Triad (Alternate) sounds
      PickupPlaySoundAlternate:
        TNT1 A 0 A_PlaySound("mp40/shoot/alt",CHAN_WEAPON)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireBullets(3.6,3,-1,10,"RottPuff",FBF_NORANDOM|FBF_NOFLASH)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireBullets(3.6,3,-1,10,"RottPuffCoop",FBF_NORANDOM|FBF_NOFLASH)
        stop
    }
}

actor RottMP40Bullet : RottPistolBullet // currently used by allies
{
    Damage (10)
    Obituary "%k bullet-whored %o with an MP40."
}
