actor RPistol : SamsaraROTTWeapon
{
    Inventory.PickupMessage "You got a pistol."
    Weapon.UpSound "gen/wepswitch"
    Weapon.SlotNumber 1
    Weapon.SelectionOrder 3105
    Obituary "%k sniped %o with a pistol."
    Tag "Pistol"
    +WIMPY_WEAPON
    States
    {
      Spawn:
        PPST A -1
        stop

      Ready:
        TNT1 A 0 A_GiveInventory("UsingFirearm")
        TNT1 A 0 A_JumpIfInventory("RottMode",4,"ReadyBlack")
        TNT1 A 0 A_JumpIfInventory("RottMode",2,"ReadyFemale")
      ReadyWhite:
        RISG A 1 A_WeaponReady
        goto Ready

      ReadyFemale:
        RISG C 1 A_WeaponReady
        goto Ready

      ReadyBlack:
        RISG E 1 A_WeaponReady
        goto Ready

      Deselect:
        TNT1 A 0 A_JumpIfHealthLower(1,"DeselectInstant")
        TNT1 A 0 A_JumpIfInventory("RottMode",4,"DeselectBlack")
        TNT1 A 0 A_JumpIfInventory("RottMode",2,"DeselectFemale")
      DeselectWhite:
        RISG A 1 A_Lower
        goto Deselect

      DeselectFemale:
        RISG C 1 A_Lower
        goto Deselect

      DeselectBlack:
        RISG E 1 A_Lower
        goto Deselect

      Select:
        TNT1 A 0 A_JumpIfInventory("RottMode",4,"SelectBlack")
        TNT1 A 0 A_JumpIfInventory("RottMode",2,"SelectFemale")
      SelectWhite:
        RISG A 1 A_Raise
        goto Select

      SelectFemale:
        RISG C 1 A_Raise
        goto Select

      SelectBlack:
        RISG E 1 A_Raise
        goto Select

      Fire:
        TNT1 A 0 A_JumpIfInventory("RottMode",4,"FireBlack")
        TNT1 A 0 A_JumpIfInventory("RottMode",2,"FireFemale")
      FireWhite:
        RISG A 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_GunFlash
        TNT1 A 0 A_GiveInventory("SamsaraROTTPistolAttackHandler")
        RISF A 2 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        RISG B 7 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        RISG A 5 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_JumpIfInventory("PowerGodProtection",1,2)
        TNT1 A 0 A_ReFire
        TNT1 A 0 A_ClearReFire
        RISG A 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto Ready

      FireFemale:
        RISG C 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_GunFlash
        TNT1 A 0 A_GiveInventory("SamsaraROTTPistolAttackHandler")
        RISF B 2 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        RISG D 7 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        RISG C 5 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_JumpIfInventory("PowerGodProtection",1,2)
        TNT1 A 0 A_ReFire
        TNT1 A 0 A_ClearReFire
        RISG C 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto Ready

      FireBlack:
        RISG E 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_GunFlash
        TNT1 A 0 A_GiveInventory("SamsaraROTTPistolAttackHandler")
        RISF C 2 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        RISG F 7 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        RISG E 5 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_JumpIfInventory("PowerGodProtection",1,2)
        TNT1 A 0 A_ReFire
        TNT1 A 0 A_ClearReFire
        RISG E 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto Ready
    }
}

actor SamsaraROTTPistolAttackHandler : Trigger
{
    States
    {
      // Attack stuff
      Pickup:
        TNT1 A 0 A_PlaySound("rott/pistolf",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupCoop")
      PickupDM:
        TNT1 A 0 A_FireBullets(1.3,1.3,-1,13,"RottPuff",FBF_NORANDOM|FBF_NOFLASH)
        stop

      PickupCoop:
        TNT1 A 0 A_FireBullets(1.3,1.3,-1,13,"RottPuffCoop",FBF_NORANDOM|FBF_NOFLASH)
        stop
    }
}

actor RottPistolBullet : FastProjectile // currently used by allies
{
    Projectile
    Radius 1
    Height 1
    Speed 300
    Damage (13) //(random(1,3)*7)
    DamageType "RottBullet"
    Decal "BulletChip"
    Obituary "%k sniped %o with a pistol."
    Species "Player"
    +BLOODSPLATTER
    +NOTIMEFREEZE
    +THRUSPECIES
    States
    {
      Spawn:
        TNT1 A -1
        stop

      Death:
      Crash:
        TNT1 A 0 A_Jump(85,"DeathPing1","DeathPing2")
        TNT1 A 1 A_SpawnItem("RottPuff0")
        stop

      DeathPing1:
        TNT1 A 1 A_SpawnItem("RottPuff1")
        stop

      DeathPing2:
        TNT1 A 1 A_SpawnItem("RottPuff2")
        stop

      XDeath:
        TNT1 A 1
        stop
    }
}
