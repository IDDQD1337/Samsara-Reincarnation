actor SamsaraROTTExcalibatChargeAmount : Counter {}
actor SamsaraROTTExcalibatPrimaryAttackSilentSound : Boolean {}
actor SamsaraROTTExcalibatPrimaryAttackOffset : Counter { Inventory.MaxAmount 10 }
actor SamsaraROTTExcalibatInvulning : Boolean {}

actor Excalibat : SamsaraROTTWeapon
{
    Inventory.PickupMessage "You picked up the Excalibat."
    Inventory.PickupSound "excalibat/pu"
    Weapon.UpSound "gen/wepswitch"
    Weapon.SlotNumber 0
    Weapon.SelectionOrder 1000
    Weapon.Kickback 175
    Weapon.AmmoType "BMissiles"
    Weapon.AmmoGive 10
    Weapon.AmmoUse 1
    Obituary "%k killed %o. STRIKE YOU'RE OUT!!!"
    HitObituary "%k hit a home run, %o took it in the face."
    Tag "Excalibat"
    +EXPLOSIVE
    +FLOATBOB
    +NOALERT
    States
    {
      Spawn:
        PBAT A -1 Bright
        stop

      Ready:
        TNT1 A 0 A_TakeInventory("UsingFirearm")
        TNT1 A 0 A_TakeInventory("SamsaraROTTExcalibatChargeAmount")
        TNT1 A 0 A_GiveInventory("SamsaraROTTExcalibatInvulnTaker")
        EXBT A 1 A_WeaponReady
        loop

      Deselect:
        TNT1 A 0 A_TakeInventory("SamsaraROTTExcalibatChargeAmount")
        TNT1 A 0 A_GiveInventory("SamsaraROTTExcalibatInvulnTaker")
        TNT1 A 0 A_JumpIfHealthLower(1,"DeselectInstant")
        EXBT A 1 A_Lower
        loop

      Select:
        TNT1 A 0 A_TakeInventory("SamsaraROTTExcalibatChargeAmount")
        TNT1 A 0 A_GiveInventory("SamsaraROTTExcalibatInvulnTaker")
        EXBT A 1 A_Raise
        loop

      //This was a real pain in the ass to code....
      Fire:
        EXBT A 6 A_PlaySound("excalibat/charge",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("PowerGodProtection",1,2)
        TNT1 A 0 A_ReFire("FireCharge1")
        TNT1 A 0 A_ClearReFire
        goto FireChargeStop

      FireCharge1:
        TNT1 A 0 A_JumpIfInventory("PowerGodProtection",1,2)
        TNT1 A 0 A_ReFire(2)
        TNT1 A 0 A_ClearReFire
        goto FireChargeStop
        EXBT A 2 A_GiveInventory("SamsaraROTTExcalibatChargeAmount")
        TNT1 A 0 A_JumpIfInventory("SamsaraROTTExcalibatChargeAmount",10,1)
        loop
        TNT1 A 0 A_TakeInventory("SamsaraROTTExcalibatChargeAmount")
        TNT1 A 0 A_JumpIfInventory("PowerGodProtection",1,2)
        TNT1 A 0 A_ReFire("FireCharge2")
        TNT1 A 0 A_ClearReFire
        goto FireChargeStop

      FireCharge2:
        TNT1 A 0 A_JumpIfInventory("PowerGodProtection",1,2)
        TNT1 A 0 A_ReFire(2)
        TNT1 A 0 A_ClearReFire
        goto FireChargeStop
        EXBT A 2
        TNT1 A 0 A_JumpIfInventory("PowerGodProtection",1,2)
        TNT1 A 0 A_ReFire(2)
        TNT1 A 0 A_ClearReFire
        goto FireChargeStopRaise1
        EXBT H 2 Offset(0,32)
        TNT1 A 0 A_JumpIfInventory("PowerGodProtection",1,2)
        TNT1 A 0 A_ReFire(2)
        TNT1 A 0 A_ClearReFire
        goto FireChargeStopRaise2
        EXBT I 2 Offset(0,36)
        TNT1 A 0 A_JumpIfInventory("PowerGodProtection",1,2)
        TNT1 A 0 A_ReFire(2)
        TNT1 A 0 A_ClearReFire
        goto FireChargeStopRaise3
        EXBT J 2 Offset(0,42)
        TNT1 A 0 A_JumpIfInventory("PowerGodProtection",1,2)
        TNT1 A 0 A_ReFire(2)
        TNT1 A 0 A_ClearReFire
        goto FireChargeStopRaise4
        EXBT K 2 Offset(0,46)
        TNT1 A 0 A_JumpIfInventory("PowerGodProtection",1,2)
        TNT1 A 0 A_ReFire(2)
        TNT1 A 0 A_ClearReFire
        goto FireChargeStopRaise5
        EXBT L 2 Offset(0,50)
        TNT1 A 0 A_JumpIfInventory("PowerGodProtection",1,2)
        TNT1 A 0 A_ReFire("FireCharge3")
        TNT1 A 0 A_ClearReFire
        goto FireChargeStopRaise5

      FireCharge3:
        TNT1 A 0 A_JumpIfInventory("PowerGodProtection",1,2)
        TNT1 A 0 A_ReFire(2)
        TNT1 A 0 A_ClearReFire
        goto FireChargeStopRaise5
        EXBT L 2 Offset(0,52) A_GiveInventory("SamsaraROTTExcalibatChargeAmount")
        TNT1 A 0 A_JumpIfInventory("SamsaraROTTExcalibatChargeAmount",30,1)
        loop
        TNT1 A 0 A_TakeInventory("SamsaraROTTExcalibatChargeAmount")
        goto FireStart

      FireStart:
        EXBT L 1 Offset(0,42)
        EXBT L 1 Offset(0,32)
        EXBT C 1 Offset(40,32) Bright
        EXBT C 1 Offset(30,32) Bright
        EXBT C 1 Offset(20,32) Bright
        EXBT C 1 Offset(10,32) Bright
        EXBT C 1 Offset(0,32) Bright
        EXBT C 1 Offset(-10,32) Bright
        TNT1 A 0 A_TakeInventory("SamsaraTakingAmmo")
        TNT1 A 0 A_TakeInventory("SamsaraROTTExcalibatPrimaryAttackSilentSound")
        TNT1 A 0 A_TakeInventory("SamsaraROTTExcalibatPrimaryAttackOffset")
        EXBT C 1 Offset(-20,32) Bright A_GiveInventory("SamsaraROTTExcalibatPrimaryAttackHandler")
        EXBT D 1 Offset(20,32) Bright A_GiveInventory("SamsaraROTTExcalibatPrimaryAttackHandler")
        EXBT D 1 Offset(10,32) Bright A_GiveInventory("SamsaraROTTExcalibatPrimaryAttackHandler")
        EXBT D 1 Offset(0,32) Bright A_GiveInventory("SamsaraROTTExcalibatPrimaryAttackHandler")
        EXBT E 1 Offset(30,32) Bright A_GiveInventory("SamsaraROTTExcalibatPrimaryAttackHandler")
        EXBT E 1 Offset(10,32) Bright A_GiveInventory("SamsaraROTTExcalibatPrimaryAttackHandler")
        EXBT E 1 Offset(0,32) Bright A_GiveInventory("SamsaraROTTExcalibatPrimaryAttackHandler")
        EXBT E 1 Offset(-30,32) Bright A_GiveInventory("SamsaraROTTExcalibatPrimaryAttackHandler")
        EXBT E 1 Offset(-50,32) Bright A_GiveInventory("SamsaraROTTExcalibatPrimaryAttackHandler")
        EXBT E 1 Offset(-70,32) Bright A_GiveInventory("SamsaraROTTExcalibatPrimaryAttackHandler")
        TNT1 A 0 A_GiveInventory("SamsaraTakingAmmo")
        EXBT E 1 Offset(-90,32) Bright A_GiveInventory("SamsaraROTTExcalibatPrimaryAttackHandler")
        EXBT E 1 Offset(-110,32) Bright
        EXBT F 1 Offset(0,32) Bright
        EXBT F 1 Offset(-20,32) Bright
        EXBT F 1 Offset(-40,32) Bright
        EXBT F 1 Offset(-60,32) Bright
        EXBT F 1 Offset(-80,32) Bright
        goto Ready

      FireChargeStopRaise5:
        TNT1 A 0 A_TakeInventory("SamsaraROTTExcalibatChargeAmount")
        TNT1 A 0 A_StopSound(CHAN_WEAPON)
        goto FireChargeStopRaise5Continue

      FireChargeStopRaise4:
        TNT1 A 0 A_TakeInventory("SamsaraROTTExcalibatChargeAmount")
        TNT1 A 0 A_StopSound(CHAN_WEAPON)
        goto FireChargeStopRaise4Continue

      FireChargeStopRaise3:
        TNT1 A 0 A_TakeInventory("SamsaraROTTExcalibatChargeAmount")
        TNT1 A 0 A_StopSound(CHAN_WEAPON)
        goto FireChargeStopRaise3Continue

      FireChargeStopRaise2:
        TNT1 A 0 A_TakeInventory("SamsaraROTTExcalibatChargeAmount")
        TNT1 A 0 A_StopSound(CHAN_WEAPON)
        goto FireChargeStopRaise2Continue

      FireChargeStopRaise1:
        TNT1 A 0 A_TakeInventory("SamsaraROTTExcalibatChargeAmount")
        TNT1 A 0 A_StopSound(CHAN_WEAPON)
        goto FireChargeStopRaise1Continue

      FireChargeStopRaise5Continue:
        EXBT L 2 Offset(0,50)
      FireChargeStopRaise4Continue:
        EXBT K 2 Offset(0,46)
      FireChargeStopRaise3Continue:
        EXBT J 2 Offset(0,42)
      FireChargeStopRaise2Continue:
        EXBT I 2 Offset(0,36)
      FireChargeStopRaise1Continue:
        EXBT H 2 Offset(0,32)
        goto Ready

      FireChargeStop:
        TNT1 A 0 A_TakeInventory("SamsaraROTTExcalibatChargeAmount")
        TNT1 A 0 A_StopSound(CHAN_WEAPON)
        goto Ready

      AltFire: // to make it more user friendly
        EXBT C 1 Offset(40,32) Bright
        EXBT C 1 Offset(30,34) Bright A_PlaySound("excalibat/swing",CHAN_WEAPON)
        EXBT C 1 Offset(20,36) Bright
        EXBT C 1 Offset(10,38) Bright
        EXBT C 1 Offset(0,40) Bright
        EXBT C 1 Offset(-10,42) Bright A_GiveInventory("SamsaraROTTExcalibatInvulnGiver")
        EXBT C 1 Offset(-20,44) Bright
        EXBT D 1 Offset(20,46) Bright
        EXBT D 1 Offset(10,48) Bright
        EXBT D 1 Offset(0,50) Bright A_GiveInventory("SamsaraROTTExcalibatSecondaryAttackHandler")
        EXBT E 1 Offset(30,52) Bright
        EXBT E 1 Offset(10,54) Bright
        EXBT E 1 Offset(0,56) Bright
        EXBT E 1 Offset(-30,58) Bright
        EXBT E 1 Offset(-50,60) Bright A_GiveInventory("SamsaraROTTExcalibatInvulnTaker")
        EXBT E 1 Offset(-70,62) Bright
        EXBT E 1 Offset(-90,64) Bright
        EXBT E 1 Offset(-110,64) Bright
        EXBT F 1 Offset(0,64) Bright
        EXBT F 1 Offset(-20,64) Bright
        EXBT F 1 Offset(-40,64) Bright
        EXBT F 1 Offset(-60,64) Bright
        EXBT F 1 Offset(-80,64) Bright
        TNT1 A 0 A_JumpIfInventory("PowerGodProtection",1,2)
        TNT1 A 0 A_ReFire
        TNT1 A 0 A_ClearReFire
        goto Ready
    }
}

actor SamsaraROTTExcalibatPrimaryAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("BMissiles",1,1)
        goto PickupFinish
        TNT1 A 0 A_JumpIfInventory("SamsaraTakingAmmo",1,1)
        goto PickupAttack
        TNT1 A 0 A_TakeInventory("BMissiles",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SamsaraROTTExcalibatPrimaryAttackSilentSound",1,2)
        TNT1 A 0 A_PlaySound("excalibat/blast",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SamsaraROTTExcalibatPrimaryAttackOffset",0,"PickupAttackOffset11")
        TNT1 A 0 A_JumpIfInventory("SamsaraROTTExcalibatPrimaryAttackOffset",9,"PickupAttackOffset10")
        TNT1 A 0 A_JumpIfInventory("SamsaraROTTExcalibatPrimaryAttackOffset",8,"PickupAttackOffset9")
        TNT1 A 0 A_JumpIfInventory("SamsaraROTTExcalibatPrimaryAttackOffset",7,"PickupAttackOffset8")
        TNT1 A 0 A_JumpIfInventory("SamsaraROTTExcalibatPrimaryAttackOffset",6,"PickupAttackOffset7")
        TNT1 A 0 A_JumpIfInventory("SamsaraROTTExcalibatPrimaryAttackOffset",5,"PickupAttackOffset6")
        TNT1 A 0 A_JumpIfInventory("SamsaraROTTExcalibatPrimaryAttackOffset",4,"PickupAttackOffset5")
        TNT1 A 0 A_JumpIfInventory("SamsaraROTTExcalibatPrimaryAttackOffset",3,"PickupAttackOffset4")
        TNT1 A 0 A_JumpIfInventory("SamsaraROTTExcalibatPrimaryAttackOffset",2,"PickupAttackOffset3")
        TNT1 A 0 A_JumpIfInventory("SamsaraROTTExcalibatPrimaryAttackOffset",1,"PickupAttackOffset2")
        goto PickupAttackOffset1

      PickupAttackOffset1:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackOffset1Coop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackOffset1Coop")
      PickupAttackOffset1DM:
        TNT1 A 0 A_FireCustomMissile("Baseball",-35,false)
        goto PickupFinish

      PickupAttackOffset1Coop:
        TNT1 A 0 A_FireCustomMissile("BaseballCoop",-35,false)
        goto PickupFinish

      PickupAttackOffset2:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackOffset2Coop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackOffset2Coop")
      PickupAttackOffset2DM:
        TNT1 A 0 A_FireCustomMissile("Baseball",-28,false)
        goto PickupFinish

      PickupAttackOffset2Coop:
        TNT1 A 0 A_FireCustomMissile("BaseballCoop",-28,false)
        goto PickupFinish

      PickupAttackOffset3:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackOffset3Coop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackOffset3Coop")
      PickupAttackOffset3DM:
        TNT1 A 0 A_FireCustomMissile("Baseball",-21,false)
        goto PickupFinish

      PickupAttackOffset3Coop:
        TNT1 A 0 A_FireCustomMissile("BaseballCoop",-21,false)
        goto PickupFinish

      PickupAttackOffset4:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackOffset4Coop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackOffset4Coop")
      PickupAttackOffset4DM:
        TNT1 A 0 A_FireCustomMissile("Baseball",-14,false)
        goto PickupFinish

      PickupAttackOffset4Coop:
        TNT1 A 0 A_FireCustomMissile("BaseballCoop",-14,false)
        goto PickupFinish

      PickupAttackOffset5:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackOffset5Coop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackOffset5Coop")
      PickupAttackOffset5DM:
        TNT1 A 0 A_FireCustomMissile("Baseball",-7,false)
        goto PickupFinish

      PickupAttackOffset5Coop:
        TNT1 A 0 A_FireCustomMissile("BaseballCoop",-7,false)
        goto PickupFinish

      PickupAttackOffset6:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackOffset6Coop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackOffset6Coop")
      PickupAttackOffset6DM:
        TNT1 A 0 A_FireCustomMissile("Baseball",0,false)
        goto PickupFinish

      PickupAttackOffset6Coop:
        TNT1 A 0 A_FireCustomMissile("BaseballCoop",0,false)
        goto PickupFinish

      PickupAttackOffset7:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackOffset7Coop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackOffset7Coop")
      PickupAttackOffset7DM:
        TNT1 A 0 A_FireCustomMissile("Baseball",7,false)
        goto PickupFinish

      PickupAttackOffset7Coop:
        TNT1 A 0 A_FireCustomMissile("BaseballCoop",7,false)
        goto PickupFinish

      PickupAttackOffset8:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackOffset8Coop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackOffset8Coop")
      PickupAttackOffset8DM:
        TNT1 A 0 A_FireCustomMissile("Baseball",14,false)
        goto PickupFinish

      PickupAttackOffset8Coop:
        TNT1 A 0 A_FireCustomMissile("BaseballCoop",14,false)
        goto PickupFinish

      PickupAttackOffset9:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackOffset9Coop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackOffset9Coop")
      PickupAttackOffset9DM:
        TNT1 A 0 A_FireCustomMissile("Baseball",21,false)
        goto PickupFinish

      PickupAttackOffset9Coop:
        TNT1 A 0 A_FireCustomMissile("BaseballCoop",21,false)
        goto PickupFinish

      PickupAttackOffset10:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackOffset10Coop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackOffset10Coop")
      PickupAttackOffset10DM:
        TNT1 A 0 A_FireCustomMissile("Baseball",28,false)
        goto PickupFinish

      PickupAttackOffset10Coop:
        TNT1 A 0 A_FireCustomMissile("BaseballCoop",28,false)
        goto PickupFinish

      PickupAttackOffset11:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackOffset11Coop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackOffset11Coop")
      PickupAttackOffset11DM:
        TNT1 A 0 A_FireCustomMissile("Baseball",35,false)
        goto PickupFinish

      PickupAttackOffset11Coop:
        TNT1 A 0 A_FireCustomMissile("BaseballCoop",35,false)
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 A_GiveInventory("SamsaraROTTExcalibatPrimaryAttackSilentSound")
        TNT1 A 0 A_GiveInventory("SamsaraROTTExcalibatPrimaryAttackOffset")
        stop
    }
}

actor SamsaraROTTExcalibatSecondaryAttackHandler : Trigger
{
    States
    {
      // Attack stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupCoop")
      PickupDM:
        TNT1 A 0 A_FireCustomMissile("ExcalibatMeleeProjectile",0,false)
        stop

      PickupCoop:
        TNT1 A 0 A_FireCustomMissile("ExcalibatMeleeProjectileCoop",0,false)
        stop
    }
}

actor BMissiles : Ammo
{
    Inventory.MaxAmount 10
    Tag "Excalibat Missiles"
    +IGNORESKILL
}

actor Baseball
{
    Projectile
    Radius 11
    Height 8
    Speed 50
    Damage (0)
    DamageType "RottExplosive"
    Scale 0.5
    DeathSound "god/bang"
    Obituary "%k killed %o. STRIKE YOU'RE OUT!!!"
    //+BLOODLESSIMPACT
    +EXTREMEDEATH
    +FOILINVUL
    +FORCERADIUSDMG
    +WINDTHRUST
    States
    {
      Spawn:
        BSA7 AABBCCDD 1 Bright Light("ROTTROCKET") A_CustomMissile("MissileTrail",0)
        loop

      Death:
        TNT1 A 0 A_PlaySound("gen/expl2")
        TNT1 A 0 A_Explode(75,128,XF_HURTSOURCE,true,32)
      DeathFinish:
        BOMB ABCDEFGHIJKLMNOPQRST 2 Bright Light("FIREBOMBEXP")
        stop

      Crash:
        TNT1 A 0 A_Jump(250,"Death")
        TNT1 A 0 A_PlaySound("rmissile/cook",CHAN_5)
        goto Death
    }
}

actor BaseballCoop : BaseBall
{
    Damage (50)
    Species "Player"
    +THRUSPECIES
}

actor ExcalibatMeleeProjectile : FastProjectile
{
    Radius 8
    Height 16
    Speed 80
    Damage (150)
    DeathSound "excalibat/hit"
    +DONTBLAST
    +HITTRACER
    +NODAMAGETHRUST
    +NOEXTREMEDEATH
    States
    {
      Spawn:
        TNT1 A 1
        stop

      Death:
        TNT1 A 1 ACS_NamedExecuteWithResult("RottExcalibatThrow")
        stop
    }
}

actor ExcalibatMeleeProjectileCoop : ExcalibatMeleeProjectile
{
    Species "Player" 
    +THRUSPECIES
}

/*actor Nothing
{
    DamageType "RottExplosive"
    Obituary "%k hit a home run, %o took it in the face."
    States
    {
      Spawn:
      Crash:
        TNT1 A 1 A_PlaySound("excalibat/miss")
        stop
    }
}*/

actor SamsaraROTTExcalibatInvulnGiver : Trigger
{
    States
    {
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraROTTExcalibatInvulning",1,"PickupStop")
        TNT1 A 0 A_SetReflectiveInvulnerable
        TNT1 A 0 A_GiveInventory("SamsaraROTTExcalibatInvulning")
        stop
    }
}

actor SamsaraROTTExcalibatInvulnTaker : Trigger
{
    States
    {
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraROTTExcalibatInvulning",1,1)
        stop
        TNT1 A 0 A_UnSetReflectiveInvulnerable
        TNT1 A 0 A_TakeInventory("SamsaraROTTExcalibatInvulning")
        stop
    }
}
