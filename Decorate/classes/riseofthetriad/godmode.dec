actor SamsaraROTTGodHandReadySoundTics : Counter { Inventory.MaxAmount 105 }

actor "God Hand" : SamsaraROTTWeapon
{
    Inventory.PickupMessage "God Mode!"
    Inventory.PickupSound "gen/godmode"
    //Weapon.UpSound "gen/wepswitch"
    Weapon.SelectionOrder 1
    Scale 0.5
    Obituary "The God %k took %o's soul."
    Tag "God Mode"
    +BFG
    +CHEATNOTWEAPON
    +NOGRAVITY
    States
    {
      Spawn:
        GODM ABCDEFG 2 Bright
        loop

      Ready:
        TNT1 A 0 A_TakeInventory("UsingFirearm")
        TNT1 A 0 A_TakeInventory("SamsaraROTTGodHandReadySoundTics") // reset
      ReadySoundCheck:
        TNT1 A 0 A_JumpIfInventory("SamsaraROTTGodHandReadySoundTics",0,"ReadySound")
      ReadyLoop:
        TNT1 A 0 A_JumpIfInventory("PowerGodProtection",1,1)
        goto PowerupExpiredSwitch
        TNT1 A 0 A_GiveInventory("SamsaraROTTGodHandReadySoundTics")
        GODH A 1 A_WeaponReady(WRF_NOSWITCH)
        goto ReadySoundCheck

      ReadySound:
        TNT1 A 0 A_JumpIfInventory("PowerGodProtection",1,1)
        goto ReadyLoop
        TNT1 A 0 A_TakeInventory("SamsaraROTTGodHandReadySoundTics")
        TNT1 A 0 A_PlaySound("godsounds")
        goto ReadyLoop

      Deselect: // can't really stop this from being called during select
        TNT1 A 0 A_JumpIfHealthLower(1,"DeselectInstant")
        GODH A 1 A_Lower
        loop

      Select:
        GODH A 1 A_Raise
        loop

      Fire:
        GODH A 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
      Hold:
        GODH BCDE 4 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        GODH F 4 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_GunFlash
        TNT1 A 0 A_GiveInventory("SamsaraROTTGodModeAttackHandler")
        GODH G 4 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        GODH H 6 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_JumpIfInventory("PowerGodProtection",1,2)
        TNT1 A 0 A_ClearReFire
        goto Ready
        TNT1 A 0 A_ReFire
        goto Ready

      // "Powerup expired" switch
      PowerupExpiredSwitch:
        TNT1 A 0 A_ClearReFire
        TNT1 A 0 A_SelectWeapon("RPistol")
        goto Deselect
    }
}

actor SamsaraROTTGodModeAttackHandler : Trigger
{
    States
    {
      // Attack stuff
      Pickup:
        TNT1 A 0 A_PlaySound("god/zap",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupCoop")
      PickupDM:
        TNT1 A 0 A_FireCustomMissile("GodMissile",0,false,0,-3)
        stop

      PickupCoop:
        TNT1 A 0 A_FireCustomMissile("GodMissileCoop",0,false,0,-3)
        stop
    }
}

actor GodProtection : SamsaraPowerupGiver { Powerup.Type "GodProtection" }

actor PowerGodProtection : PowerProtection
{
    Powerup.Duration -30
    DamageFactor "Normal", 0
}

actor GodMissile
{
    Projectile
    Radius 8
    Height 6
    Speed 20
    Damage (50000)
    DamageType "RottDisintegrate"
    ReactionTime 85
    Scale 0.5
    SeeSound "god/projectile"
    DeathSound "ksphere/death"
    Obituary "%o met %p maker."
    +BLOODLESSIMPACT
    +FOILINVUL
    +FORCERADIUSDMG
    +NODAMAGETHRUST
    +SEEKERMISSILE
    +SPECTRAL // hmm
    +THRUACTORS
    States
    {
      Spawn:
        GODP A 0 Bright Light("GODLIGHT")
        TNT1 A 0 A_RearrangePointers(AAPTR_TARGET,AAPTR_NULL,AAPTR_NULL)
      SpawnLoop:
        TNT1 A 0 A_Countdown
        TNT1 A 0 A_Explode(50000,8,0,false,8)
        GODP A 1 Bright Light("GODLIGHT") A_SeekerMissile(90,90,SMF_LOOK|SMF_PRECISE,256)
        TNT1 A 0 A_Explode(50000,8,0,false,8)
        GODP A 1 Bright Light("GODLIGHT") A_SeekerMissile(90,90,SMF_LOOK|SMF_PRECISE,256)
        TNT1 A 0 A_Explode(50000,8,0,false,8)
        GODP B 1 Bright Light("GODLIGHT") A_SeekerMissile(90,90,SMF_LOOK|SMF_PRECISE,256)
        TNT1 A 0 A_Explode(50000,8,0,false,8)
        GODP B 1 Bright Light("GODLIGHT") A_SeekerMissile(90,90,SMF_LOOK|SMF_PRECISE,256)
        TNT1 A 0 A_Explode(50000,8,0,false,8)
        GODP C 1 Bright Light("GODLIGHT") A_SeekerMissile(90,90,SMF_LOOK|SMF_PRECISE,256)
        TNT1 A 0 A_Explode(50000,8,0,false,8)
        GODP C 1 Bright Light("GODLIGHT") A_SeekerMissile(90,90,SMF_LOOK|SMF_PRECISE,256)
        TNT1 A 0 A_Explode(50000,8,0,false,8)
        GODP D 1 Bright Light("GODLIGHT") A_SeekerMissile(90,90,SMF_LOOK|SMF_PRECISE,256)
        TNT1 A 0 A_Explode(50000,8,0,false,8)
        GODP D 1 Bright Light("GODLIGHT") A_SeekerMissile(90,90,SMF_LOOK|SMF_PRECISE,256)
        TNT1 A 0 A_Explode(50000,8,0,false,8)
        GODP A 1 Bright Light("GODLIGHT") A_SeekerMissile(90,90,SMF_LOOK|SMF_PRECISE,256)
        TNT1 A 0 A_Explode(50000,8,0,false,8)
        GODP A 1 Bright Light("GODLIGHT") A_SeekerMissile(90,90,SMF_LOOK|SMF_PRECISE,256)
        TNT1 A 0 A_Explode(50000,8,0,false,8)
        GODP B 1 Bright Light("GODLIGHT") A_SeekerMissile(90,90,SMF_LOOK|SMF_PRECISE,256)
        TNT1 A 0 A_Explode(50000,8,0,false,8)
        GODP B 1 Bright Light("GODLIGHT") A_SeekerMissile(90,90,SMF_LOOK|SMF_PRECISE,256)
        TNT1 A 0 A_Explode(50000,8,0,false,8)
        GODP C 1 Bright Light("GODLIGHT") A_SeekerMissile(90,90,SMF_LOOK|SMF_PRECISE,256)
        TNT1 A 0 A_Explode(50000,8,0,false,8)
        GODP C 1 Bright Light("GODLIGHT") A_SeekerMissile(90,90,SMF_LOOK|SMF_PRECISE,256)
        TNT1 A 0 A_Explode(50000,8,0,false,8)
        GODP D 1 Bright Light("GODLIGHT") A_SeekerMissile(90,90,SMF_LOOK|SMF_PRECISE,256)
        TNT1 A 0 A_Explode(50000,8,0,false,8)
        GODP D 1 Bright Light("GODLIGHT") A_SeekerMissile(90,90,SMF_LOOK|SMF_PRECISE,256)
        TNT1 A 0 A_Explode(50000,8,0,false,8)
        loop

      Death:
        TNT1 A 0 A_PlaySound("gen/expl2")
        TNT1 A 0 A_ChangeFlag("NODAMAGETHRUST",false)
        TNT1 A 0 A_Explode(128,128,0,false,1) // was its own actor for some reason. also explains the flag change in previous line // actor apparently called this twice to deal 256 damage total, but first frame is skipped on spawn, only dealing 128 then
      DeathFinish:
        BOMB ABCDEFGHIJKLMNOPQRST 2 Bright Light("FIREBOMBEXP")
        stop

      Crash:
        TNT1 A 0 A_Jump(250,"Death")
        TNT1 A 0 A_PlaySound("rmissile/cook",CHAN_5)
        goto Death
    }
}

actor GodMissileCoop : GodMissile
{
    Species "Player"
    +THRUSPECIES
}
