Actor RottDoomStickCharge : Inventory { -INVBAR +INVENTORY.UNDROPPABLE Inventory.MaxAmount 35 }
ACTOR "Doomstick" : Weapon
{
     Weapon.SlotNumber 5
	 Weapon.SlotPriority 1
     Weapon.SelectionOrder 1200
     Weapon.UpSound "gen/wepswitch"
     Inventory.PickupSound "gen/pickup"
     Inventory.Pickupmessage "You snatched a Doomstick!"
     Weapon.AmmoGive 5
     Weapon.AmmoUse 1
     Weapon.AmmoType "DMMissiles"
     Obituary "%o was shown %k's doomstick!"
	 Inventory.RestrictedTo "IPFreeley"
     Tag "Doomstick"
     +IGNORESKILL
	 +WEAPON.EXPLOSIVE
	 +WEAPON.AMMO_OPTIONAL
	 States
     {
     Spawn: 
        PDSK A -1
        Stop
     Ready:
		TNT1 A 0 A_StopSound(CHAN_6)
		TNT1 A 0 A_TakeInventory("UsingFirearm", 1)
        RTDS A 1 A_WeaponReady
        Goto Ready+1
     Deselect:
		TNT1 A 0 A_StopSound(CHAN_6)
        RTDS A 1 A_Lower
        Loop
     Select:
        RTDS A 1 A_Raise
        Loop
     Fire:
		TNT1 A 0 A_PlayWeaponSound("dstick/charge",CHAN_6)
	 Charge:
		RTDS A 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		RTDS A 0 A_JumpIfInventory("DMMissiles",1,1)
		Goto Charge+5
		RTDS A 0 A_GiveInventory("RottDoomStickCharge",1)
		RTDS A 0 A_JumpIfInventory("RottDoomStickCharge",0,"Fireball")
		RTDS A 0 A_Refire("Charge")
		TNT1 A 0 A_StopSound(CHAN_6)
		TNT1 A 0 A_TakeInventory("RottDoomStickCharge")
		TNT1 A 0 A_PlayWeaponSound("dstick/fire")
		TNT1 A 0 A_JumpIfInventory("DMModeOn", 1, "DMFire")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn", 1, "CoopFire")
		TNT1 A 0 A_FireBullets(6.2,5.5,8,15,"RottDoomStickPuff", FBF_NORANDOM)
		RTDS A 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
	 Continue:
        RTDS B 1 Bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		RTDS C 1 Bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		RTDS D 1 Bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
        RTDS E 1 Bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		RTDS F 1 Bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		RTDS G 1 Bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
	 Finish:
		RTDS HIA 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
        RTDS A 0 A_Refire
        Goto Ready+1
	 CoopFire:
		TNT1 A 0 A_FireBullets(6.2,5.5,8,15,"RottDoomStickPuffCoop", FBF_NORANDOM)
		Goto Continue
	 DMFire:
		TNT1 A 0 A_FireBullets(6.2,5.5,8,10,"RottDoomStickPuff", FBF_NORANDOM)
		Goto Continue
	 Fireball:
		TNT1 A 0 A_TakeInventory("DMMissiles",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_TakeInventory("RottDoomStickCharge")
		TNT1 A 0 A_PlayWeaponSound("dstick/fireball")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn", 1, "FireballCoop")
		TNT1 A 0 A_FireCustomMissile("DoomStickFireBall",frandom(-7.5,7.5),0,0,-8,0,frandom(-5.0,5.0))
		RTDS B 1 Bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_FireCustomMissile("DoomStickFireBall",frandom(-7.5,7.5),0,0,-8,0,frandom(-5.0,5.0))
		RTDS C 1 Bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_FireCustomMissile("DoomStickFireBall",frandom(-7.5,7.5),0,0,-8,0,frandom(-5.0,5.0))
		RTDS D 1 Bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_FireCustomMissile("DoomStickFireBall",frandom(-7.5,7.5),0,0,-8,0,frandom(-5.0,5.0))
        RTDS E 1 Bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_FireCustomMissile("DoomStickFireBall",frandom(-7.5,7.5),0,0,-8,0,frandom(-5.0,5.0))
		RTDS F 1 Bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_FireCustomMissile("DoomStickFireBall",frandom(-7.5,7.5),0,0,-8,0,frandom(-5.0,5.0))
		RTDS G 1 Bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 AA 0 A_FireCustomMissile("DoomStickFireBall",frandom(-7.5,7.5),0,0,-8,0,frandom(-5.0,5.0))
		Goto Finish
	 FireballCoop:
		TNT1 A 0 A_FireCustomMissile("DoomStickFireBallCoop",frandom(-7.5,7.5),0,0,-8,0,frandom(-5.0,5.0))
		RTDS B 1 Bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_FireCustomMissile("DoomStickFireBallCoop",frandom(-7.5,7.5),0,0,-8,0,frandom(-5.0,5.0))
		RTDS C 1 Bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_FireCustomMissile("DoomStickFireBallCoop",frandom(-7.5,7.5),0,0,-8,0,frandom(-5.0,5.0))
		RTDS D 1 Bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_FireCustomMissile("DoomStickFireBallCoop",frandom(-7.5,7.5),0,0,-8,0,frandom(-5.0,5.0))
        RTDS E 1 Bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_FireCustomMissile("DoomStickFireBallCoop",frandom(-7.5,7.5),0,0,-8,0,frandom(-5.0,5.0))
		RTDS F 1 Bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_FireCustomMissile("DoomStickFireBallCoop",frandom(-7.5,7.5),0,0,-8,0,frandom(-5.0,5.0))
		RTDS G 1 Bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 AA 0 A_FireCustomMissile("DoomStickFireBallCoop",frandom(-7.5,7.5),0,0,-8,0,frandom(-5.0,5.0))
		Goto Finish
	 AltFire:
		TNT1 A 0 A_PlayWeaponSound("dstick/fire")
		TNT1 A 0 A_JumpIfInventory("DMModeOn", 1, "DMFire")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn", 1, "CoopFire")
		TNT1 A 0 A_FireBullets(6.2,5.5,8,15,"RottDoomStickPuff", FBF_NORANDOM)
		RTDS A 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		Goto Continue
     }
}

actor DMMissiles : Ammo
{
    Inventory.MaxAmount 25
    Ammo.BackpackAmount 1
    Ammo.BackpackMaxAmount 50
    Inventory.Icon "AMSMS0"
    Tag "Doomstick Missiles"
    +IGNORESKILL
}

ACTOR RottDoomStickPuff : RottPuff
{
	+HITTRACER
	+PUFFGETSOWNER
	+PUFFONACTORS
	States
    { 
	  XDeath:
		TNT1 A 0
		TNT1 A 2 ACS_NamedExecuteWithResult("RottDoomStickThrust")
		Stop
     }
}

Actor RottDoomStickPuffCoop : RottDoomStickPuff { +THRUSPECIES +DONTHURTSPECIES Species "Player" }

Actor DoomStickFireBall : RottMissile {
	Speed 40
	Damage (20)
	Scale 0.5
	SeeSound "NME/Fire3"
    ActiveSound ""	
	States {
		Spawn:  
			RNME JKLM 3 Bright
			Loop
		Death:
		XDeath:
			TNT1 A 0 A_Explode(15, 48, 1, true, 24)
			EXPL AAB 1 Bright
			TNT1 A 0 A_RadiusThrust(200, 96)
			EXPL BCC 1 Bright
			TNT1 A 0 A_Explode(15, 48, 1, true, 24)
			EXPL DDEEFFGGHHIIJJKKLLMMNNOOPPQQRRSSTT 1 Bright
			Stop
		Crash:
			TNT1 A 0 A_StopSound(5)	
			TNT1 A 0 A_PlaySound("gen/expl2",6,1.0,0,ATTN_NORM)
			TNT1 A 0 A_Jump(250,2)
			TNT1 A 0 A_PlaySound("rmissile/cook",5,1.0,0,ATTN_NORM)	
			TNT1 A 0
			EXPL A 2 Bright A_Explode(15, 48, 1, true, 24)
			EXPL BCDEFGHIJKLMNOPQRST 2 Bright
			Stop
	}
}

Actor DoomStickFireBallCoop : DoomStickFireBall { +THRUSPECIES +DONTHURTSPECIES Species "Player" }