actor "Imperial Repeater": SamsaraDarkForcesWeapon
{
    Inventory.PickupMessage "Auto Gun"
    Weapon.UpSound "katarn/wepslct"
    Weapon.SlotNumber 4
    Weapon.SelectionOrder 600
    Weapon.AmmoType "Cell" //"Shell" //Why?
    Weapon.AmmoGive 40
    Weapon.AmmoUse 1
    Obituary "%k drilled %o with the Repeater."
    Tag "Imperial Repeater Gun"
    +ALT_USES_BOTH // only one ammo type with same ammo amount use (this time, for the purposes of determining switching). alt fire still behaves as intended
    +NOALERT
    States
    {
      Spawn:
        IREP A -1
        stop

      Ready:
        KREP A 1 A_WeaponReady
        loop

      Deselect:
        TNT1 A 0 A_Lower
        KREP A 1 A_Lower
        loop

      Select:
        TNT1 A 0 A_Raise
        KREP A 1 A_Raise
        loop

      Fire:
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"FireFast")
        TNT1 A 0 A_JumpIfInventory("KatarnFireRateUp",1,"FireFast")
      FireNormal:
        KREP B 1
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Cell",1,1)
        goto FireNormalContinue
        TNT1 A 0 A_GunFlash
        goto FireNormalContinue

      FireNormalContinue:
        KREP C 4 Bright A_GiveInventory("SamsaraDarkForcesImperialRepeaterPrimaryAttackHandler")
        KREP C 3
        KREP C 2 A_ReFire
        goto Ready

      FireFast:
        KREP B 1
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Cell",1,1)
        goto FireFastContinue
        TNT1 A 0 A_GunFlash
        goto FireFastContinue

      FireFastContinue:
        KREP C 2 Bright A_GiveInventory("SamsaraDarkForcesImperialRepeaterPrimaryAttackHandler")
        KREP C 2
        KREP C 1 A_ReFire
        goto Ready

      AltFire:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"AltFireStart")
        TNT1 A 0 A_JumpIfInventory("Cell",3,"AltFireStart")
        goto AltFireDry

      AltFireDry:
        TNT1 A 0 A_ClearReFire
        TNT1 A 0 A_PlaySound("katarn/repempt",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"AltFireDryFast")
        TNT1 A 0 A_JumpIfInventory("KatarnFireRateUp",1,"AltFireDryFast")
      AltFireDryNormal:
        KREP A 12
        goto Ready

      AltFireDryFast:
        KREP A 6
        goto Ready

      AltFireStart:
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"AltFireFast")
        TNT1 A 0 A_JumpIfInventory("KatarnFireRateUp",1,"AltFireFast")
      AltFireNormal:
        KREP B 2
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Cell",3,1)
        goto AltFireNormalContinue
        TNT1 A 0 A_GunFlash
        goto AltFireNormalContinue

      AltFireNormalContinue:
        KREP C 3 Bright A_GiveInventory("SamsaraDarkForcesImperialRepeaterSecondaryAttackHandler")
        KREP A 1 Bright
        KREP A 6
        KREP A 2 A_ReFire
        goto Ready

      AltFireFast:
        KREP B 1
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Cell",3,1)
        goto AltFireFastContinue
        TNT1 A 0 A_GunFlash
        goto AltFireFastContinue

      AltFireFastContinue:
        KREP C 2 Bright A_GiveInventory("SamsaraDarkForcesImperialRepeaterSecondaryAttackHandler")
        KREP A 1 Bright
        KREP A 3
        KREP A 1 A_ReFire
        goto Ready

      Flash:
        TNT1 A 3 Bright A_Light(5)
        TNT1 A 2 A_Light(3)
        goto LightDone
    }
}

actor SamsaraDarkForcesImperialRepeaterPrimaryAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Cell",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Cell",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("katarn/repshot",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("repeater_shot",frandom(-0.5,0.5),false,6,0,0,frandom(-0.5,0.5))
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("repeater_shot2",frandom(-0.5,0.5),false,6,0,0,frandom(-0.5,0.5))
        stop
    }
}

actor SamsaraDarkForcesImperialRepeaterSecondaryAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Cell",3,1)
        stop
        TNT1 A 0 A_TakeInventory("Cell",3)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("katarn/repburs",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("repeater_shot",0,false,6,0,0,frandom(1.75,2.25)) //Top center
        TNT1 A 0 A_FireCustomMissile("repeater_shot",3,false,12,-4,0,frandom(-2.25,-1.75)) //Bottom right
        TNT1 A 0 A_FireCustomMissile("repeater_shot",-3,false,0,-4,0,frandom(-2.25,-1.75)) //Bottom left
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("repeater_shot2",0,false,6,0,0,frandom(1.75,2.25))
        TNT1 A 0 A_FireCustomMissile("repeater_shot2",3,false,12,-4,0,frandom(-2.25,-1.75))
        TNT1 A 0 A_FireCustomMissile("repeater_shot2",-3,false,0,-4,0,frandom(-2.25,-1.75))
        stop
    }
}

actor repeater_shot : FastProjectile
{
    Projectile
    Radius 2
    Height 2
    Speed 120
    Damage (random(9,13)*2)
    RenderStyle "Add"
    Obituary "%k drilled %o with the Repeater."
    +HITTRACER
    +NOEXTREMEDEATH
    +THRUGHOST
    States
    {
      Spawn:
        PREP A -1 Bright
        stop

      Death:
        TNT1 A 0 A_PlaySound("katarn/extiny",CHAN_VOICE)
        TNT1 A 1 A_SpawnItemEx("repeater_puff")
        stop

      XDeath:
        TNT1 A 0 A_RearrangePointers(AAPTR_TRACER,AAPTR_NULL,AAPTR_NULL)
        TNT1 A 0 A_SpawnItemEx("Switchable_FlyingBlood_NonViolent",0,0,0,random(-350,350)/100.00,random(-350,350)/100.00,random(500,1000)/100.00,random(-180,180),33|SXF_TRANSFERPOINTERS)
        goto Death
  }
}

actor repeater_shot2 : repeater_shot
{
    Species "Player"
    +THRUSPECIES
}

actor repeater_puff : spark_puff
{
    States
    {
      Spawn:
        EXBL ABC 2 Bright
        EXBL DEFG 2
        stop
    }
}
