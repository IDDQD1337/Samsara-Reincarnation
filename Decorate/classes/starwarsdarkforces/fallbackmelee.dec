actor "  Fist_2x5  " : Weapon
{
  Weapon.SlotNumber     0
    Weapon.BobStyle          Inverse
    Weapon.BobSpeed          1.05
    Weapon.BobRangeX      1.0
    Weapon.BobRangeY      0.55  
  Weapon.SelectionOrder 3700
  Weapon.Kickback       100
  +INVENTORY.UNDROPPABLE
  +WEAPON.WIMPY_WEAPON
  +WEAPON.MELEEWEAPON
  +WEAPON.NOALERT
  +PIERCEARMOR
  Tag "Fist"
  Obituary "%k beat %o senseless."
  Inventory.RestrictedTo "KatarnPlayer"
  Tag "Hand to Hand"
  States
  {
    Ready:
      KHAN A 1 A_WeaponReady
      loop

    Deselect:
      TNT1 A 0 A_Lower
      KHAN A 1 A_Lower
      loop

    Select: 
      TNT1 A 0 A_Raise
      KHAN A 1 A_Raise
      loop

    AltFire:
    Fire:
      TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"FastFire")
      KPUN A 3 A_JumpIfInventory("KatarnFireRateUp",1,"FastFire")
      KPUN B 5 A_GiveInventory("SamsaraDarkForcesFistAttackHandler")
      KPUN C 6
      KHAN A 5
      KHAN A 5 A_ReFire
      goto Ready

    FastFire:
      KPUN A 2
      KPUN B 3 A_GiveInventory("SamsaraDarkForcesFistAttackHandler")
      KPUN C 3
      KHAN A 5
      KHAN A 3 A_ReFire
      goto Ready
  }
}

actor SamsaraDarkForcesFistAttackHandler : Trigger
{
    States
    {
      // Attack stuff
      Pickup:
        TNT1 A 0 A_PlaySound("katarn/puncmis",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupCoop")
      PickupDM:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupDMSpread")
        TNT1 A 0 A_CustomPunch(35,true,0,"BlankPuffKatarn")
        stop

      PickupDMSpread:
        TNT1 A 0 A_CustomPunch(35*3,true,0,"BlankPuffKatarn")
        stop

      PickupCoop:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupCoopSpread")
        TNT1 A 0 A_CustomPunch(35,true,0,"BlankPuffKatarnCoop")
        stop

      PickupCoopSpread:
        TNT1 A 0 A_CustomPunch(35*3,true,0,"BlankPuffKatarnCoop")
        stop
    }
}
