actor "X-21 Shock Gloves" : SamsaraDarkForcesWeapon
{
    Weapon.SlotNumber 0
    Weapon.SelectionOrder 2200
    Obituary "%k fisted %o. How shocking."
    Tag "X-21 Shock Gloves"
    +MELEEWEAPON
    +NOALERT
    States
    {
      Spawn:
        IGNT A -1
        stop

      Ready:
        KGNT A 1 A_WeaponReady
        loop

      Deselect:
        TNT1 A 0 A_Lower
        KGNT A 1 A_Lower
        loop

      Select:
        TNT1 A 0 A_Raise
        KGNT A 1 A_Raise
        loop

      Fire:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"FireFast")
        TNT1 A 0 A_JumpIfInventory("KatarnFireRateUp",1,"FireFast")
      FireNormal:
        KGNT C 4
        TNT1 A 0 A_TakeInventory("SamsaraSynthFireRightPress")
        KGNT F 2 Bright A_GiveInventory("SamsaraDarkForcesShockGlovesAttackHandler")
        KGNT G 2 Bright
        KGNT B 2
        KGNT CA 3
        KGNT A 14 A_JumpIfInventory("SamsaraSynthFireRightPress",1,"AltFire")
        goto Ready

      FireFast:
        KGNT C 2
        TNT1 A 0 A_TakeInventory("SamsaraSynthFireRightPress")
        KGNT F 1 Bright A_GiveInventory("SamsaraDarkForcesShockGlovesAttackHandler")
        KGNT G 1 Bright
        KGNT B 1
        KGNT CA 2
        KGNT A 7 A_JumpIfInventory("SamsaraSynthFireRightPress",1,"AltFire")
        goto Ready

      AltFire:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"AltFireFast")
        TNT1 A 0 A_JumpIfInventory("KatarnFireRateUp",1,"AltFireFast")
      AltFireNormal:
        KGNT E 4
        TNT1 A 0 A_TakeInventory("SamsaraSynthFireLeftPress")
        KGNT H 2 Bright A_GiveInventory("SamsaraDarkForcesShockGlovesAttackHandler")
        KGNT I 2 Bright
        KGNT D 2
        KGNT EA 3
        KGNT A 14 A_JumpIfInventory("SamsaraSynthFireLeftPress",1,"Fire")
        goto Ready

      AltFireFast:
        KGNT E 2
        TNT1 A 0 A_TakeInventory("SamsaraSynthFireLeftPress")
        KGNT H 1 Bright A_GiveInventory("SamsaraDarkForcesShockGlovesAttackHandler")
        KGNT I 1 Bright
        KGNT D 1
        KGNT EA 2
        KGNT A 7 A_JumpIfInventory("SamsaraSynthFireLeftPress",1,"Fire")
        goto Ready
    }
}

actor SamsaraDarkForcesShockGlovesAttackHandler : Trigger
{
    States
    {
      // Attack stuff
      Pickup:
        TNT1 A 0 A_PlaySound("katarn/puncmis",CHAN_WEAPON)
        TNT1 A 0 A_PlaySound("katarn/stunhit")
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupCoop")
      PickupDM:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupDMSpread")
        TNT1 A 0 A_CustomPunch(50,true,0,"BlankPuffKatarn")
        stop

      PickupDMSpread:
        TNT1 A 0 A_CustomPunch(50*3,true,0,"BlankPuffKatarn")
        stop

      PickupCoop:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupCoopSpread")
        TNT1 A 0 A_CustomPunch(50,true,0,"BlankPuffKatarnCoop")
        stop

      PickupCoopSpread:
        TNT1 A 0 A_CustomPunch(50*3,true,0,"BlankPuffKatarnCoop")
        stop
    }
}
