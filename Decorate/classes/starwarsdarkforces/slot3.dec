actor SamsaraDarkForcesFusionCutterBarrel : Counter { Inventory.MaxAmount 5 }
actor SamsaraDarkForcesFusionCutterFiredOnce : Boolean {}

actor "Fusion Cutter" : SamsaraDarkForcesWeapon
{
    Inventory.PickupMessage "Fusion Cutter"
    Weapon.UpSound "katarn/wepslct"
    Weapon.SlotNumber 3
    Weapon.SelectionOrder 700
    Weapon.AmmoType "Shell"
    Weapon.AmmoGive 16
    Weapon.AmmoUse 1
    Obituary "%k cut down %o with the Jeron Fusion Cutter."
    Tag "Jeron Fusion Cutter"
    +ALT_USES_BOTH // only one ammo type with same ammo amount use (this time, for the purposes of determining switching). alt fire still behaves as intended
    +NOALERT
    States
    {
      Spawn:
        IFUS A -1
        stop

      Ready:
        KFUS A 1 A_WeaponReady
        loop

      Deselect:
        TNT1 A 0 A_Lower
        KFUS A 1 A_Lower
        loop

      Select:
        TNT1 A 0 A_Raise
        KFUS A 1 A_Raise
        loop

      Fire:
        TNT1 A 0 A_AlertMonsters
        KFUS C 0 A_JumpIfInventory("SamsaraDarkForcesFusionCutterBarrel",0,"FireStart")
        KFUS D 0 A_JumpIfInventory("SamsaraDarkForcesFusionCutterBarrel",4,"FireStart")
        KFUS E 0 A_JumpIfInventory("SamsaraDarkForcesFusionCutterBarrel",3,"FireStart")
        KFUS D 0 A_JumpIfInventory("SamsaraDarkForcesFusionCutterBarrel",2,"FireStart")
        KFUS C 0 A_JumpIfInventory("SamsaraDarkForcesFusionCutterBarrel",1,"FireStart")
        KFUS B 0
      FireStart:
        "####" "#" 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"FireFast")
        "####" "#" 0 A_JumpIfInventory("KatarnFireRateUp",1,"FireFast")
      FireNormal:
        "####" "#" 0 A_GunFlash
        "####" "#" 5 Bright A_GiveInventory("SamsaraDarkForcesFusionCutterPrimaryAttackHandler")
        KFUS A 1 Bright
        KFUS A 3
        KFUS A 3 A_ReFire
        goto Ready

      FireFast:
        "####" "#" 0 A_GunFlash
        "####" "#" 3 Bright A_GiveInventory("SamsaraDarkForcesFusionCutterPrimaryAttackHandler")
        KFUS A 1 Bright
        KFUS A 2
        KFUS A 1 A_ReFire
        goto Ready

      AltFire:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"AltFireStart")
        TNT1 A 0 A_JumpIfInventory("Shell",2,"AltFireStart")
        goto AltFireDry

      AltFireDry:
        TNT1 A 0 A_ClearReFire
        TNT1 A 0 A_PlaySound("katarn/fusempt",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"AltFireDryFast")
        TNT1 A 0 A_JumpIfInventory("KatarnFireRateUp",1,"AltFireDryFast")
      AltFireDryNormal:
        KFUS A 17
        goto Ready

      AltFireDryFast:
        KFUS A 8
        goto Ready

      AltFireStart:
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"AltFireFast")
        TNT1 A 0 A_JumpIfInventory("KatarnFireRateUp",1,"AltFireFast")
      AltFireNormal:
        TNT1 A 0 A_GunFlash
        KFUS F 7 Bright A_GiveInventory("SamsaraDarkForcesFusionCutterSecondaryAttackHandler")
        KFUS A 1 Bright
        KFUS A 9
        KFUS A 1 A_ReFire
        goto Ready

      AltFireFast:
        TNT1 A 0 A_GunFlash
        KFUS F 4 Bright A_GiveInventory("SamsaraDarkForcesFusionCutterSecondaryAttackHandler")
        KFUS A 1 Bright
        KFUS A 5
        KFUS A 1 A_ReFire
        goto Ready

      Flash:
        TNT1 A 2 A_Light1
        goto LightDone
    }
}

actor SamsaraDarkForcesFusionCutterPrimaryAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Shell",1,1)
        stop
        TNT1 A 0 A_JumpIfInventory("SamsaraDarkForcesFusionCutterFiredOnce",0,2)
        TNT1 A 0 A_GiveInventory("SamsaraDarkForcesFusionCutterFiredOnce")
        goto PickupAttack
        TNT1 A 0 A_TakeInventory("Shell",1)
        TNT1 A 0 A_TakeInventory("SamsaraDarkForcesFusionCutterFiredOnce")
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("katarn/fusshot",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SamsaraDarkForcesFusionCutterBarrel",0,"PickupAttack2")
        TNT1 A 0 A_JumpIfInventory("SamsaraDarkForcesFusionCutterBarrel",4,"PickupAttack3")
        TNT1 A 0 A_JumpIfInventory("SamsaraDarkForcesFusionCutterBarrel",3,"PickupAttack4")
        TNT1 A 0 A_JumpIfInventory("SamsaraDarkForcesFusionCutterBarrel",2,"PickupAttack3")
        TNT1 A 0 A_JumpIfInventory("SamsaraDarkForcesFusionCutterBarrel",1,"PickupAttack2")
      // First barrel
      PickupAttack1:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttack1Coop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttack1Coop")
      PickupAttack1DM:
        TNT1 A 0 A_FireCustomMissile("fusion_shot",2,false,-15,-5)
        goto PickupFinish

      PickupAttack1Coop:
        TNT1 A 0 A_FireCustomMissile("fusion_shot2",2,false,-15,-5)
        goto PickupFinish

      // Second barrel
      PickupAttack2:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttack2Coop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttack2Coop")
      PickupAttack2DM:
        TNT1 A 0 A_FireCustomMissile("fusion_shot",1,false,-5,-8)
        goto PickupFinish

      PickupAttack2Coop:
        TNT1 A 0 A_FireCustomMissile("fusion_shot2",1,false,-5,-8)
        goto PickupFinish

      // Third barrel
      PickupAttack3:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttack3Coop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttack3Coop")
      PickupAttack3DM:
        TNT1 A 0 A_FireCustomMissile("fusion_shot",0,false,3,-11)
        goto PickupFinish

      PickupAttack3Coop:
        TNT1 A 0 A_FireCustomMissile("fusion_shot2",0,false,3,-11)
        goto PickupFinish

      // Fourth barrel
      PickupAttack4:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttack4Coop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttack4Coop")
      PickupAttack4DM:
        TNT1 A 0 A_FireCustomMissile("fusion_shot",-1,false,13,-14)
        goto PickupFinish

      PickupAttack4Coop:
        TNT1 A 0 A_FireCustomMissile("fusion_shot2",-1,false,13,-14)
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 A_JumpIfInventory("SamsaraDarkForcesFusionCutterBarrel",0,2)
        TNT1 A 0 A_GiveInventory("SamsaraDarkForcesFusionCutterBarrel")
        stop
        TNT1 A 0 A_TakeInventory("SamsaraDarkForcesFusionCutterBarrel")
        stop
    }
}

actor SamsaraDarkForcesFusionCutterSecondaryAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Shell",2,1)
        stop
        TNT1 A 0 A_TakeInventory("Shell",2)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("katarn/fusshot",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("fusion_shot",4,false,-15,-5)
        TNT1 A 0 A_FireCustomMissile("fusion_shot",2,false,-5,-8)
        TNT1 A 0 A_FireCustomMissile("fusion_shot",0,false,3,-11)
        TNT1 A 0 A_FireCustomMissile("fusion_shot",-2,false,13,-14)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("fusion_shot2",4,false,-15,-5)
        TNT1 A 0 A_FireCustomMissile("fusion_shot2",2,false,-5,-8)
        TNT1 A 0 A_FireCustomMissile("fusion_shot2",0,false,3,-11)
        TNT1 A 0 A_FireCustomMissile("fusion_shot2",-2,false,13,-14)
        stop
    }
}

actor fusion_shot
{
    Projectile
    Radius 3
    Height 3
    Speed 45
    Damage (random(25,45))
    DamageType "DoomPlasma"
    RenderStyle "Add"
    Scale 1.2
    Obituary "%k cut down %o with the Jeron Fusion Cutter."
    +DONTREFLECT
    //+FOILINVUL //???
    +HITTRACER
    +NOEXTREMEDEATH
    +PIERCEARMOR
    States
    {
      Spawn:
        PFUS ABCD 10 Bright
        loop

      Death:
        TNT1 A 0 A_PlaySound("katarn/extiny",CHAN_VOICE)
        TNT1 A 1 A_SpawnItemEx("fusion_puff")
        stop

      XDeath:
        TNT1 A 0 A_RearrangePointers(AAPTR_TRACER,AAPTR_NULL,AAPTR_NULL)
        TNT1 A 0 A_SpawnItemEx("Switchable_FlyingBlood_NonViolent",0,0,0,random(-350,350)/100.00,random(-350,350)/100.00,random(500,1000)/100.00,random(-180,180),33,SXF_TRANSFERPOINTERS)
        goto Death
    }
}

actor fusion_shot2 : fusion_shot
{
    Species "Player"
    +THRUSPECIES
}

actor fusion_puff : spark_puff
{
    States
    {
      Spawn:
        EXFU ABCD 2 Bright
        EXFU EFGH 2 
        stop
    }
}
