actor KatarnPunchDrunk : Trigger
{
  States
  {
    Pickup:
      TNT1 A 0 A_GiveInventory("PermaTripleDamage")
      stop
  }
}

actor KatarnPunchUnique : Trigger
{
  States
  {
    Pickup:
      TNT1 A 0 A_SetBlend("FF0000",0.15,105)
      TNT1 A 0 A_TakeInventory("PermaTripleDamage")
      TNT1 A 0 A_GiveInventory("PowerStrength")
      TNT1 A 0 ACS_NamedExecuteWithResult("SamsaraPlayerHealer", 100)
      TNT1 A 0 A_SelectWeapon("  Fist_2x5  ")
      stop
  }
}

//Useless, swapped due to popular request and for not beign a dark forces weapon
actor "Stun Gauntlets" : Weapon
{
  Weapon.SlotNumber        1
  Weapon.SelectionOrder 2200
  +INVENTORY.UNDROPPABLE
  +WEAPON.MELEEWEAPON
  +WEAPON.NOALERT
  Obituary "%k \c[M8]fisted\c- %o \c[M8]. How shocking.\c-"
  Inventory.RestrictedTo "KatarnPlayer"
  Tag "Stun Gauntlets"
  States
  {
    Ready:
      KGNT A 1 A_WeaponReady
      loop

    Deselect:
      TNT1 A 0 A_Lower
      KGNT A 1 A_Lower
      loop

    Select: 
      TNT1 A 0 A_Raise
      KGNT A 1 A_Raise
      loop

    Fire:
      KGNT A 0 A_JumpIfInventory("KatarnFireRateUp",1,"FastFire")
      KGNT C 4
      KGNT B 0 A_TakeInventory("RightTriggerHit",1)
      KGNT F 2 BRIGHT A_GiveInventory("SamsaraDarkForcesStunGauntletsAttackHandler")
      KGNT G 2 BRIGHT
      KGNT B 2
      KGNT C 3
      KGNT A 3
      KGNT A 14 A_JumpIfInventory("RightTriggerHit",1,"AltFire")
      goto Ready
      
    AltFire:
      KGNT A 0 A_JumpIfInventory("KatarnFireRateUp",1,"AltFastFire")
      KGNT E 4
      KGNT D 0 A_TakeInventory("LeftTriggerHit",1)
      KGNT H 2 BRIGHT A_GiveInventory("SamsaraDarkForcesStunGauntletsAttackHandler")
      KGNT I 2 BRIGHT
      KGNT D 2
      KGNT E 3
      KGNT A 3
      KGNT A 14 A_JumpIfInventory("LeftTriggerHit",1,"Fire")
      goto Ready

    FastFire:
      KGNT C 2
      KGNT B 0 A_TakeInventory("RightTriggerHit",1)
      KGNT F 1 BRIGHT A_GiveInventory("SamsaraDarkForcesStunGauntletsAttackHandler")
      KGNT G 1 BRIGHT
      KGNT B 1
      KGNT C 2
      KGNT A 2
      KGNT A 7 A_JumpIfInventory("RightTriggerHit",1,"AltFastFire")
      goto Ready

    AltFastFire:
      KGNT E 2
      KGNT E 0 A_TakeInventory("LeftTriggerHit",1)
      KGNT H 1 BRIGHT A_GiveInventory("SamsaraDarkForcesStunGauntletsAttackHandler")
      KGNT I 1 BRIGHT
      KGNT E 1
      KGNT E 2
      KGNT A 2
      KGNT A 7 A_JumpIfInventory("LeftTriggerHit",1,"FastFire")
      goto Ready
  }
}

actor SamsaraDarkForcesStunGauntletsAttackHandler : Trigger
{
    States
    {
      // Attack stuff
      Pickup:
        TNT1 A 0 A_PlaySound("katarn/puncmis",CHAN_WEAPON)
        TNT1 A 0 A_PlaySound("katarn/stunhit")
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupCoop")
      PickupDM:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupDMSpread")
        TNT1 A 0 A_CustomPunch(50,true,0,"BlankPuffKatarn")
        stop

      PickupDMSpread:
        TNT1 A 0 A_CustomPunch(50*3,true,0,"BlankPuffKatarn")
        stop

      PickupCoop:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupCoopSpread")
        TNT1 A 0 A_CustomPunch(50,true,0,"BlankPuffKatarnCoop")
        stop

      PickupCoopSpread:
        TNT1 A 0 A_CustomPunch(50*3,true,0,"BlankPuffKatarnCoop")
        stop
    }
}
