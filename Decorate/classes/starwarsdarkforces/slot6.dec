actor "Concussion Rifle" : SamsaraDarkForcesWeapon
{
    Inventory.PickupMessage "Concussion Rifle"
    Weapon.UpSound "katarn/wepslct"
    Weapon.SlotNumber 6
    Weapon.SlotPriority 0
    Weapon.SelectionOrder 500
    Weapon.AmmoType "Cell"
    Weapon.AmmoGive 48
    Weapon.AmmoUse 4
    Obituary "%k ripped apart %o with the concussion rifle."
    Tag "Stouker Concussion Rifle"
    +ALT_USES_BOTH // only one ammo type with same ammo amount use, so this makes alt fire behave as intended
    +EXPLOSIVE
    +NOAUTOFIRE
    States
    {
      Spawn:
        ICON A -1
        stop

      Ready:
        KCON A 1 A_WeaponReady
        loop

      Deselect:
        TNT1 A 0 A_Lower
        KCON A 1 A_Lower
        loop

      Select:
        TNT1 A 0 A_Raise
        KCON A 1 A_Raise
        loop

      Fire:
      AltFire:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"FireFast")
        TNT1 A 0 A_JumpIfInventory("KatarnFireRateUp",1,"FireFast")
      FireNormal:
        TNT1 A 0 A_GunFlash
        KCON B 10 Bright A_GiveInventory("SamsaraDarkForcesConcussionRifleAttackHandler")
        KCON C 1 Bright
        KCON C 3
        KCON A 14
        KCON A 2 A_ReFire
        goto Ready

      FireFast:
        TNT1 A 0 A_GunFlash
        KCON B 5 Bright A_GiveInventory("SamsaraDarkForcesConcussionRifleAttackHandler")
        KCON C 1 Bright
        KCON C 2
        KCON A 7
        KCON A 1 A_ReFire
        goto Ready

      Flash:
        TNT1 A 1 A_Light2
        TNT1 A 1 A_Light1
        goto LightDone
    }
}

actor SamsaraDarkForcesConcussionRifleAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Cell",4,1)
        stop
        TNT1 A 0 A_TakeInventory("Cell",4)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("katarn/conshot",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("concussion_projectile",frandom(-4,4),false,0,0,0,frandom(-4,4))
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("concussion_projectile2",frandom(-4,4),false,0,0,0,frandom(-4,4))
        stop
    }
}

//Weapon was changed to mimic Jedi Knight's behavior instead of Dark Forces.
//This was to avoid calling A_BFGSpray to try and mimic the original behavior.
actor concussion_projectile : FastProjectile
{
    Projectile
    Radius 3
    Height 3
    Speed 500
    Damage (0)
    DamageType "Laser"
    Obituary "%k ripped apart %o with the concussion rifle."
    +DONTBLAST
    +DONTSPLASH
    +FORCERADIUSDMG
    +NOTELEPORT
    +OLDRADIUSDMG
    States
    {
      Spawn:
        TNT1 A -1
        stop

      Death:
        TNT1 A 0 A_PlaySound("katarn/exmiss",CHAN_VOICE)
        TNT1 A 0 A_Explode(60,128,0)
      DeathFinish:
        EXCN A 1
        EXCN BC 1 Bright
        EXCN DEFGH 2 Bright
        stop
    }
}

actor concussion_projectile2 : concussion_projectile
{
    Species "Player"
    +THRUSPECIES
    States
    {
      Death:
        TNT1 A 0 A_PlaySound("katarn/exmiss",CHAN_VOICE)
        TNT1 A 0 A_Explode(random(180,240),128,0)
        goto DeathFinish
    }
}
