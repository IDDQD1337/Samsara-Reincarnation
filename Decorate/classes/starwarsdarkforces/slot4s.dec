actor "DLT-19 Heavy Blaster Rifle" : SamsaraDarkForcesWeapon
{
    Inventory.PickupMessage "Heavy Blaster Rifle"
    Weapon.UpSound "katarn/wepslct"
    Weapon.SlotNumber 4
    Weapon.SelectionOrder 400
    Weapon.AmmoType "Clip"
    Weapon.AmmoGive 60
    Weapon.AmmoUse 1
    Obituary "%k blasted %o down with the Stormtrooper Rifle."
    Tag "Stormtrooper Heavy Blaster Rifle"
    +ALT_USES_BOTH // only one ammo type with same ammo amount use, so this makes alt fire behave as intended
    States
    {
      Spawn:
        KD19 Z -1
        stop

      Ready:
        KD19 A 1 A_WeaponReady
        loop

      Deselect:
        TNT1 A 0 A_Lower
        KD19 A 1 A_Lower
        loop

      Select:
        TNT1 A 0 A_Raise
        KD19 A 1 A_Raise
        loop

      Fire:
      AltFire:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"FireFast")
        TNT1 A 0 A_JumpIfInventory("KatarnFireRateUp",1,"FireFast")
      FireNormal:
        TNT1 A 0 A_GunFlash
        KD19 B 1 Bright A_GiveInventory("SamsaraDarkForcesHeavyBlasterRifleAttackHandler")
        KD19 A 2
        KD19 AA 1 A_ReFire
        goto Ready

      FireFast:
        TNT1 A 0 A_GunFlash
        KD19 B 1 Bright A_GiveInventory("SamsaraDarkForcesHeavyBlasterRifleAttackHandler")
        KD19 A 1
        KD19 A 1 A_ReFire
        goto Ready

      Flash:
        TNT1 A 1 Bright A_Light2
        TNT1 A 1 A_Light1
        goto LightDone
    }
}

actor SamsaraDarkForcesHeavyBlasterRifleAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("katarn/dltshot",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("blaster_laser",random(-3,3),false,6,0,0,random(-3,3))
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("blaster_laser2",random(-3,3),false,6,0,0,random(-3,3))
        stop
    }
}
