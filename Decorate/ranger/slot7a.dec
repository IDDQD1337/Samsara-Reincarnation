actor CantQuad: Boolean {}
actor DoNotQuad: Boolean {}
actor QuakeQuadTimer: Counter {}
actor SamsaraQuadDamageCooldownDisplay : Counter {}

actor QuadDamagePickup: CustomInventory
{
    Inventory.MaxAmount 0
    Inventory.Icon "QUADICON"
    Inventory.RestrictedTo "QuakePlayer"
    +AUTOACTIVATE
    +FLOATBOB
    States
    {
      Spawn:
        QUDD A -1 bright
        stop

      Pickup:
        TNT1 A 0 ACS_NamedExecuteWithResult("SamsaraDecorate", 7, 1050, 1)
        stop
    }
}

actor QuadDamageItem: CustomInventory
{
    Inventory.UseSound ""
    Inventory.Icon "QUADICON"
    Inventory.Amount 1
    Inventory.MaxAmount 1
    Inventory.RestrictedTo "QuakePlayer"
    Tag "Quad Damage"
    +FLOATBOB
    +INVBAR
    +UNDROPPABLE
    States
    {
      Spawn:
        QUDD A -1 bright
        stop

      Use:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasDisabledInventoryCooldown",1,2)
        TNT1 A 0 A_JumpIfInventory("CantQuad",1,"NoU")
        TNT1 A 0 ACS_NamedExecuteWithResult("SamsaraDecorate", 7, 1050)
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"UseFail")
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"UseFail")
        TNT1 A 0 A_TakeInventory("QuadDamageItem")
        stop

      UseFail:
        TNT1 A 0
        fail

      NoU:
        TNT1 A 0 A_Print("$QUADCOOLDOWN")
        fail
    }
}

actor QuadDamagePower: PowerDamage
{
    Powerup.Duration 0x7FFFFFFF
    Powerup.Color "00 50 FF", 0.15
    DamageFactor "Normal", 4
    DamageFactor "QuakeExplosive", 4
    DamageFactor "QuakeElectric", 4
    DamageFactor "QuakeQuadded", 4
    DamageFactor "NotQuadded", 1
    DamageFactor "SpectralNoQuad", 1
    DamageFactor "EnemyInfightDamage", 0
}
