actor Ranger_AxeSelected : Boolean { Inventory.InterHubAmount 0 }

actor "Axe" : SamsaraQuakeWeapon
{
    Weapon.SelectionOrder 8
    Weapon.KickBack 100
    Obituary "$SAMSARA_RANGER_OB_FIST"
    Tag "Axe"
    +MELEEWEAPON
    +WIMPY_WEAPON
    States
    {
      Ready:
        QAXE A 1 A_WeaponReady
        loop

      Select:
        TNT1 A 0 A_GiveInventory("Ranger_AxeSelected")
        TNT1 A 0 A_TakeInventory("Ranger_GunSelected")
        TNT1 A 0 A_TakeInventory("Ranger_ExplosiveSelected")
        TNT1 A 0 A_TakeInventory("Ranger_NailSelected")
        TNT1 A 0 A_TakeInventory("Ranger_PlasmaSelected")
        goto Super::Select

      // 18 tics
      Fire:
        QAXE B 0 A_PlaySound("quakeweps/axe",CHAN_WEAPON)
        QAXE B 0 A_Jump(256,"Fire1","Fire2")
      Fire1:
        QAXE B 4 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        QAXE C 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        QAXE D 0 A_GiveInventory("SamsaraQuakeAxeAttackHandler")
        QAXE D 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        QAXE E 4 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto FireFinish

      Fire2:
        QAXE F 4 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        QAXE G 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        QAXE H 0 A_GiveInventory("SamsaraQuakeAxeAttackHandler")
        QAXE H 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        QAXE I 4 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto FireFinish

      FireFinish:
        QAXE A 6 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        QAXE A 0 A_ReFire
        goto Ready
    }
}

actor SamsaraQuakeAxeAttackHandler : Trigger
{
    States
    {
      // Attack stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupCoop")
      PickupDM:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupDMSpread")
        TNT1 A 0 A_CustomPunch(20,true,0,"QuakeAxePuff")
        stop

      PickupDMSpread:
        TNT1 A 0 A_CustomPunch(20*3,true,0,"QuakeAxePuff")
        stop

      PickupCoop:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupCoopSpread")
        TNT1 A 0 A_CustomPunch(20,true,0,"QuakeAxePuffCoop")
        stop

      PickupCoopSpread:
        TNT1 A 0 A_CustomPunch(20*3,true,0,"QuakeAxePuffCoop")
        stop
    }
}

actor QuakeAxePuff: QuakeBulletPuff
{
    +PUFFONACTORS
    DamageType "QuakeExplosive"
    AttackSound "quakeweps/axewall"
    SeeSound "quakeweps/axehit"
    MeleeSound "quakeweps/axehit"
    States
    {
      Spawn:
      Crash:
        TNT1 A 0
        TNT1 A 0 A_PlaySound("quakeweps/axewall")
        goto Super::Spawn

      Melee:
        TNT1 A 1 A_PlaySound("quakeweps/axehit")
        stop
    }
}

actor QuakeAxePuffCoop : QuakeAxePuff { +MTHRUSPECIES }

actor SpectralAxePuff: QuakeAxePuff { DamageType "Spectral" }
actor SpectralAxePuffCoop : SpectralAxePuff { +MTHRUSPECIES }
