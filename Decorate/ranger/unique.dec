actor Ranger_PlasmaSelected : Boolean { Inventory.InterHubAmount 0 }

actor "Laser Cannon" : SamsaraQuakeWeapon
{
    Inventory.PickupMessage "You got the Laser Cannon."
    Weapon.SelectionOrder 100
    Weapon.AmmoType "Cell"
    Weapon.AmmoGive 60
    Weapon.AmmoUse 1
    Scale 0.75
    Tag "Laser Cannon"
    States
    {
      Spawn:
        QLSC Z -1
        stop

	  Ready:
        QLSC A 1 A_WeaponReady
        loop

      Select:
        TNT1 A 0 A_TakeInventory("Ranger_AxeSelected")
        TNT1 A 0 A_TakeInventory("Ranger_GunSelected")
        TNT1 A 0 A_TakeInventory("Ranger_ExplosiveSelected")
        TNT1 A 0 A_TakeInventory("Ranger_NailSelected")
        TNT1 A 0 A_GiveInventory("Ranger_PlasmaSelected")
        goto Super::Select

      Fire:
        QLSC B 0 A_JumpIfInventory("QuakeInvisTimer",1,2)
        QLSC B 0 A_AlertMonsters
        QLSC B 0 A_GunFlash
        QLSC B 0 A_GiveInventory("SamsaraQuakeLaserCannonAttackHandler1")
        QLSC B 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"FireSmooth")
      FireVanilla:
        QLSC B 4 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto FireFinish

      FireSmooth:
        QLSC BC 2 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto FireFinish

      FireFinish:
        QLSC A 0 A_ReFire("Fire2")
        goto Ready

      Fire2:
        QLSC D 0 A_JumpIfInventory("QuakeInvisTimer",1,2)
        QLSC D 0 A_AlertMonsters
        QLSC D 0 A_GunFlash
        QLSC D 0 A_GiveInventory("SamsaraQuakeLaserCannonAttackHandler2")
        QLSC B 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"Fire2Smooth")
      Fire2Vanilla:
        QLSC D 0 A_JumpIfInventory("SamsaraHasDoubleFiringSpeed",1,2)
        QLSC D 3 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto Fire2Finish
        QLSC D 2 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto Fire2Finish

      Fire2Smooth:
        QLSC D 0 A_JumpIfInventory("SamsaraHasDoubleFiringSpeed",1,"Fire2SmoothRage")
        QLSC D 1 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        QLSC E 2 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto Fire2Finish

      Fire2SmoothRage:
        QLSC D 2 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto Fire2Finish

      Fire2Finish:
        QLSC A 0 A_ReFire
        goto Ready

      Flash:
        TNT1 A 2 A_Light1
        goto LightDone
    }
}

actor SamsaraQuakeLaserCannonAttackHandler1 : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Cell",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Cell",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("quakeweps/lasershot",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("LaserCannonBolt",0,false,3,-2)
        TNT1 A 0 A_FireCustomMissile("LaserCannonBolt",0,false,-3,-2)
        goto PickupFinish

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("LaserCannonBoltCoop",0,false,3,-2)
        TNT1 A 0 A_FireCustomMissile("LaserCannonBoltCoop",0,false,-3,-2)
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasDoubleFiringSpeed",1,2)
        TNT1 A 0 ACS_NamedExecuteAlways("SamsaraRecoil", 0, -50, 1, 3)
        stop
        TNT1 A 0 ACS_NamedExecuteAlways("SamsaraRecoil", 0, 1, 0, 2)
        stop
    }
}

actor SamsaraQuakeLaserCannonAttackHandler2 : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Cell",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Cell",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("quakeweps/lasershot",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("LaserCannonBoltSecond",0,false,0,5)
        goto PickupFinish

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("LaserCannonBoltSecondCoop",0,false,0,5)
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasDoubleFiringSpeed",1,2)
        TNT1 A 0 ACS_NamedExecuteAlways("SamsaraRecoil", 0, -50, 1, 2)
        stop
        TNT1 A 0 ACS_NamedExecuteAlways("SamsaraRecoil", 0, -50, 0, 1)
        stop
    }
}

actor LaserCannonBolt
{
    Radius 2
    Height 2
    Speed 50
    Projectile
    Damage (7)
    DamageType "QuakeExplosive"
    BounceType "Hexen"
    +BLOODSPLATTER
    BounceCount 3
    Scale 0.55
    BounceSound "quakeweps/laserbounce"
    DeathSound "quakeweps/laserhit"
    Obituary "$SAMSARA_RANGER_OB_BERSERK"
    //SelfObituary "%o checks if %p weapon is loaded." // had randomized obituary in SOA, use this one for differentiation
    States
    {
      Spawn:
        TNT1 A 0
        TNT1 A 0 A_Jump(16,"Spawn2")
        LSCL A 1 bright A_Explode(2,1,1)
        loop
        
      Spawn2:
        TNT1 A 0
        TNT1 A 0 A_ChangeFlag("ALLOWBOUNCEONACTORS",false)
        TNT1 A 0 A_ChangeFlag("BOUNCEONACTORS",false)
        TNT1 A 0 A_ChangeFlag("BOUNCEONCEILINGS",false)
        TNT1 A 0 A_ChangeFlag("BOUNCEONFLOORS",false)
        TNT1 A 0 A_ChangeFlag("BOUNCEONWALLS",false)
        TNT1 A 0 A_ChangeFlag("USEBOUNCESTATE",false)
      Spawn3:
        LSCL A 1 bright A_Explode(2,1,1)
        loop
        
      Death:
        LSCL A 1 bright
      Crash:
        TNT1 A 1 A_SpawnItemEx("QuakeBulletPuff")
        stop
        
      XDeath:
        TNT1 A 1
        stop
    }
}

actor LaserCannonBoltCoop : LaserCannonBolt
{
    Damage (10)
    +THRUSPECIES
    Species "Player"
}

actor SpectralLaserCannonBolt: LaserCannonBoltCoop { DamageType "Spectral" }

actor LaserCannonBoltSecond : LaserCannonBolt
{
    Damage (14)
    States
    {
      Spawn:
        TNT1 A 0
        TNT1 A 0 A_Jump(16,"Spawn2")
        LSCL A 1 bright A_Explode(4,1,1)
        loop
        
      Spawn2:
        TNT1 A 0
        TNT1 A 0 A_ChangeFlag("ALLOWBOUNCEONACTORS",false)
        TNT1 A 0 A_ChangeFlag("BOUNCEONACTORS",false)
        TNT1 A 0 A_ChangeFlag("BOUNCEONCEILINGS",false)
        TNT1 A 0 A_ChangeFlag("BOUNCEONFLOORS",false)
        TNT1 A 0 A_ChangeFlag("BOUNCEONWALLS",false)
        TNT1 A 0 A_ChangeFlag("USEBOUNCESTATE",false)
      Spawn3:
        LSCL A 1 bright A_Explode(4,1,1)
        loop
    }
}

actor LaserCannonBoltSecondCoop : LaserCannonBoltSecond
{
    Damage (20)
    +THRUSPECIES
    Species "Player"
}

actor SpectralLaserCannonBoltSecond: LaserCannonBoltSecondCoop { DamageType "Spectral" }
