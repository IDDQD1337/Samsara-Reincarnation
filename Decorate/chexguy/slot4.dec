actor "Rapid Zorcher" : Weapon
{
    Weapon.SelectionOrder 700
    Weapon.SlotNumber 4
    Inventory.PickUpSound "chex/weaponget"
    Weapon.AmmoUse 1
    Weapon.AmmoGive 20
    Weapon.AmmoType "Clip"
    +BLOODLESSIMPACT
    +PUFFONACTORS
    +THRUGHOST
    //+AMMO_OPTIONAL
    +INVENTORY.UNDROPPABLE
    DamageType "Zorch"
    Inventory.PickupMessage "You got the Rapid Zorcher!"
    Obituary "$SAMSARA_CHEX_OB_SLOT4"
	Inventory.RestrictedTo "ChexWarrior"
    Tag "Rapid Zorcher"
    States
    {
      Ready:
        CHGC A 1 A_WeaponReady
        Loop

      Deselect:
        CHGC A 1 A_Lower
        Loop

      Select:
        CHGC A 1 A_Raise
        Loop

      Fire:
        CHGC A 0 A_GiveInventory("SamsaraChexWarriorChaingunAttackHandler")
        CHGC A 4 A_GunFlash
        CHGC B 0 A_GiveInventory("SamsaraChexWarriorChaingunAttackHandler")
        CHGC B 4
        CHGC B 0 A_ReFire
        goto Ready

      Hold:
        CHGC A 0 A_GiveInventory("SamsaraChexWarriorChaingunAttackHandler")
        CHGC A 4 A_GunFlash
        CHGC B 0 A_GiveInventory("SamsaraChexWarriorChaingunAttackHandler")
        CHGC B 4
        CHGC B 0 A_ReFire
        CHGC B 0 A_ClearRefire
        goto Ready

      Flash:
        CCHG A 4 bright A_Light1
        CCHG B 4 bright A_Light2
        CCHG A 0 bright A_Light0
        stop

      Spawn:
        MGUC A -1
        Stop
    }
}

actor SamsaraChexWarriorChaingunAttackHandler : Trigger
{
    States
    {
      // Ammo check stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupTakeAmmo")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"PickupTakeAmmo")
        stop

      // Ammo taking stuff
      PickupTakeAmmo:
        TNT1 A 0 A_TakeInventory("Clip",1,TIF_NOTAKEINFINITE)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("chex/chngun",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SamsaraImprovedDoomguyBalance",1,"PickupAttackImproved")
      // Vanilla
      PickupAttackVanilla:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackVanillaCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackVanillaCoop")
      PickupAttackVanillaDM:
        TNT1 A 0 A_FireBullets(5, 0, 1, 5, "ChexPuff", FBF_NOFLASH)
        stop

      PickupAttackVanillaCoop:
        TNT1 A 0 A_FireBullets(5, 0, 1, 5, "ChexPuffCoop", FBF_NOFLASH)
        stop

      // Improved
      PickupAttackImproved:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackImprovedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackImprovedCoop")
      PickupAttackImprovedDM:
        TNT1 A 0 A_FireBullets(5, 0, 1, random(8,10)*2, "ChexPuff", FBF_NORANDOM|FBF_NOFLASH)
        stop

      PickupAttackImprovedCoop:
        TNT1 A 0 A_FireBullets(5, 0, 1, random(8,10)*2, "ChexPuffCoop", FBF_NORANDOM|FBF_NOFLASH)
        stop
    }
}