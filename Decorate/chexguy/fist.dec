actor " Bootspoon " : Weapon
{
    Weapon.SlotNumber 0
    Weapon.SelectionOrder 3700
    Weapon.Kickback 100
    +WEAPON.WIMPY_WEAPON
    +WEAPON.MELEEWEAPON
    +WEAPON.NOALERT
    +BLOODLESSIMPACT
    +INVENTORY.UNDROPPABLE
    Obituary "$SAMSARA_CHEX_OB_FIST"
	Inventory.RestrictedTo "ChexWarrior"
    Tag "Bootspoon"
    States
    {
      Ready:
        CPUN A 1 A_WeaponReady
        loop

      Deselect:
        CPUN A 1 A_Lower
        loop

      Select: 
        CPUN A 1 A_Raise
        loop

      Fire:
        CPUN B 4
        CPUN C 4 A_GiveInventory("SamsaraChexWarriorFistAttackHandler")
        CPUN D 5
        CPUN C 4
        CPUN B 5 A_ReFire
        goto Ready
    }
}

actor SamsaraChexWarriorFistAttackHandler : Trigger
{
    States
    {
      // Vanilla
	  Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraImprovedDoomguyBalance",1,"PickupNormalImproved")
      PickupNormalVanilla:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupNormalVanillaCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupNormalVanillaCoop")
      PickupNormalVanillaDM:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupNormalVanillaDMSpread") // A_CustomPunch doesn't support spread, multiply damage by 3 to compensate
        TNT1 A 0 A_CustomPunch(random(1,10)*2,true,0,"ChexSpoonPuff")
        stop

      PickupNormalVanillaDMSpread:
        TNT1 A 0 A_CustomPunch((random(1,10)*2)*3,true,0,"ChexSpoonPuff")
        stop

      PickupNormalVanillaCoop:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupNormalVanillaCoopSpread")
        TNT1 A 0 A_CustomPunch(random(1,10)*2,true,0,"ChexSpoonPuffCoop")
        stop

      PickupNormalVanillaCoopSpread:
        TNT1 A 0 A_CustomPunch((random(1,10)*2)*3,true,0,"ChexSpoonPuffCoop")
        stop

      // Improved
      PickupNormalImproved:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupNormalImprovedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupNormalImprovedCoop")
      PickupNormalImprovedDM:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupNormalImprovedDMSpread")
        TNT1 A 0 A_CustomPunch(random(2,10)*5,true,0,"ChexSpoonPuff")
        stop

      PickupNormalImprovedDMSpread:
        TNT1 A 0 A_CustomPunch((random(2,10)*5)*3,true,0,"ChexSpoonPuff")
        stop

      PickupNormalImprovedCoop:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupNormalImprovedCoopSpread")
        TNT1 A 0 A_CustomPunch(random(2,10)*5,true,0,"ChexSpoonPuffCoop")
        stop

      PickupNormalImprovedCoopSpread:
        TNT1 A 0 A_CustomPunch((random(2,10)*5)*3,true,0,"ChexSpoonPuffCoop")
        stop
    }
}

actor ChexSpoonPuff : BulletPuff
{
    DamageType "ZorchPistol"
    RenderStyle Add
    -BLOODSPLATTER
    +BLOODLESSIMPACT
    +PUFFONACTORS
    Alpha 0.4
    Scale 0.22
    VSpeed 0
    States
    {
      Spawn:
      Melee:
		TNT1 A 0 A_SpawnItemEx("Switchable_FlyingBlood_NonViolent",0,0,0,random(-350,350)/100.00,random(-350,350)/100.00,random(500,1000)/100.00,random(-180,180),33)
        TNT1 A 0 A_PlaySound("chex/spoon")
        CMIS DCB 4 bright
        stop

      Crash:
        TNT1 A 0 A_PlaySound("chex/wallspoon")
        CMIS DCB 4 bright
        Stop
    }
}

actor ChexSpoonPuffCoop : ChexSpoonPuff { Species "Player" +THRUSPECIES +MTHRUSPECIES }
