// Token

actor SamsaraChexGigazorcherFiringLastShot : Boolean {}

// Weapon

actor "Gigazorcher 2100" : Weapon
{
    Weapon.SelectionOrder 100
    Weapon.SlotNumber 6
    Inventory.PickUpSound "chex/weaponget"
    Weapon.AmmoType1 "ChexGigazorcherClip"
    Weapon.AmmoUse1 1
    Weapon.AmmoType2 "Cell"
    Weapon.AmmoGive2 40
    +AMMO_OPTIONAL
    +BLOODLESSIMPACT
    +NOALERT
    +PUFFONACTORS
    +UNDROPPABLE
    Inventory.PickupMessage "You got the Phasing Zorcher!"
	//Inventory.RestrictedTo "ChexWarrior"
	Obituary "$SAMSARA_CHEX_OB_SLOT6S"
	Inventory.RestrictedTo "ChexWarrior"
    Tag "Gigazorcher 2100"
    States
    {
      Spawn:
        PLAC B -1
        stop

      Ready:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReadyMagazineInit")
      ReadyReserveInit:
        CHIZ A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReadyContinue")
        CHIZ A 0 A_JumpIfInventory("Cell",40,"ReadyContinue")
        CHIZ D 0 A_JumpIfInventory("Cell",30,"ReadyContinue")
        CHIZ G 0 A_JumpIfInventory("Cell",20,"ReadyContinue")
        CHIZ J 0 A_JumpIfInventory("Cell",10,"ReadyContinue")
        CHIZ M 0
        goto ReadyContinue

      ReadyMagazineInit:
        CHIZ A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"ReadyContinue")
        CHIZ A 0 A_JumpIfInventory("ChexGigazorcherClip",0,"ReadyContinue")
        CHIZ D 0 A_JumpIfInventory("ChexGigazorcherClip",3,"ReadyContinue")
        CHIZ G 0 A_JumpIfInventory("ChexGigazorcherClip",2,"ReadyContinue")
        CHIZ J 0 A_JumpIfInventory("ChexGigazorcherClip",1,"ReadyContinue")
        CHIZ M 0
        goto ReadyContinue

      ReadyContinue:
        "####" "#" 0 A_JumpIfInventory("SamsaraReloadMode",0,"ReadyModernReloadable")
      ReadyNonReloadable:
        "####" "#" 1 A_WeaponReady
        goto Ready

      ReadyModernReloadable:
        "####" "#" 0 A_JumpIfInventory("ChexGigazorcherClip",0,"ReadyNonReloadable")
        "####" "#" 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        "####" "#" 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        "####" "#" 0 A_JumpIfInventory("Cell",10,1)
        goto ReadyNonReloadable
        "####" "#" 1 A_WeaponReady(WRF_ALLOWRELOAD)
        goto Ready

      Deselect:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"DeselectMagazineInit")
      DeselectReserveInit:
        CHIZ A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"DeselectContinue")
        CHIZ A 0 A_JumpIfInventory("Cell",40,"DeselectContinue")
        CHIZ D 0 A_JumpIfInventory("Cell",30,"DeselectContinue")
        CHIZ G 0 A_JumpIfInventory("Cell",20,"DeselectContinue")
        CHIZ J 0 A_JumpIfInventory("Cell",10,"DeselectContinue")
        CHIZ M 0
        goto DeselectContinue

      DeselectMagazineInit:
        CHIZ A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"DeselectContinue")
        CHIZ A 0 A_JumpIfInventory("ChexGigazorcherClip",0,"DeselectContinue")
        CHIZ D 0 A_JumpIfInventory("ChexGigazorcherClip",3,"DeselectContinue")
        CHIZ G 0 A_JumpIfInventory("ChexGigazorcherClip",2,"DeselectContinue")
        CHIZ J 0 A_JumpIfInventory("ChexGigazorcherClip",1,"DeselectContinue")
        CHIZ M 0
        goto DeselectContinue

      DeselectContinue:
        "####" "#" 1 A_Lower
        goto Deselect

      Select:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"SelectMagazineInit")
      SelectReserveInit:
        CHIZ A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"SelectContinue")
        CHIZ A 0 A_JumpIfInventory("Cell",40,"SelectContinue")
        CHIZ D 0 A_JumpIfInventory("Cell",30,"SelectContinue")
        CHIZ G 0 A_JumpIfInventory("Cell",20,"SelectContinue")
        CHIZ J 0 A_JumpIfInventory("Cell",10,"SelectContinue")
        CHIZ M 0
        goto SelectContinue

      SelectMagazineInit:
        CHIZ A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"SelectContinue")
        CHIZ A 0 A_JumpIfInventory("ChexGigazorcherClip",0,"SelectContinue")
        CHIZ D 0 A_JumpIfInventory("ChexGigazorcherClip",3,"SelectContinue")
        CHIZ G 0 A_JumpIfInventory("ChexGigazorcherClip",2,"SelectContinue")
        CHIZ J 0 A_JumpIfInventory("ChexGigazorcherClip",1,"SelectContinue")
        CHIZ M 0
        goto SelectContinue

      SelectContinue:
        "####" "#" 1 A_Raise
        goto Select

      Fire:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"FireReloadable")
      FireNonReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart1")
        TNT1 A 0 A_JumpIfInventory("Cell",40,"FireStart1")
        TNT1 A 0 A_JumpIfInventory("Cell",30,"FireStart2")
        TNT1 A 0 A_JumpIfInventory("Cell",20,"FireStart3")
        TNT1 A 0 A_JumpIfInventory("Cell",10,"FireStart4")
        goto Nothing

      FireReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireStart1")
        TNT1 A 0 A_JumpIfInventory("ChexGigazorcherClip",0,"FireStart1")
        TNT1 A 0 A_JumpIfInventory("ChexGigazorcherClip",3,"FireStart2")
        TNT1 A 0 A_JumpIfInventory("ChexGigazorcherClip",2,"FireStart3")
        TNT1 A 0 A_JumpIfInventory("ChexGigazorcherClip",1,"FireStart4")
      FireReloadableDryCheck:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"Reload")
        TNT1 A 0 A_JumpIfInventory("Cell",10,"Reload")
        goto Nothing

      FireStart1:
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_GunFlash("Flash",GFF_NOEXTCHANGE)
        CHIZ B 4 Bright A_GiveInventory("SamsaraChexGigazorcherAttackHandler")
        CHIZ C 4 Bright
        CHIZ T 4
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"FireFinish1Reloadable")
        goto FireFinish1

      FireFinish1Reloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireFinish1")
        TNT1 A 0 A_JumpIfInventory("ChexGigazorcherClip",1,"FireFinish1")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReloadStart")
        TNT1 A 0 A_JumpIfInventory("Cell",10,"ReloadStart")
        goto FireFinish1

      FireFinish1:
        CHIZ D 6
        goto FireReFireCheck

      FireStart2:
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_GunFlash("Flash",GFF_NOEXTCHANGE)
        CHIZ E 4 Bright A_GiveInventory("SamsaraChexGigazorcherAttackHandler")
        CHIZ F 4 Bright
        CHIZ U 4
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"FireFinish2Reloadable")
        goto FireFinish2

      FireFinish2Reloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireFinish2")
        TNT1 A 0 A_JumpIfInventory("ChexGigazorcherClip",1,"FireFinish2")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReloadStart")
        TNT1 A 0 A_JumpIfInventory("Cell",10,"ReloadStart")
        goto FireFinish2

      FireFinish2:
        CHIZ G 6
        goto FireReFireCheck

      FireStart3:
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_GunFlash("Flash",GFF_NOEXTCHANGE)
        CHIZ H 4 Bright A_GiveInventory("SamsaraChexGigazorcherAttackHandler")
        CHIZ I 4 Bright
        CHIZ V 4
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"FireFinish3Reloadable")
        goto FireFinish3

      FireFinish3Reloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireFinish3")
        TNT1 A 0 A_JumpIfInventory("ChexGigazorcherClip",1,"FireFinish3")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReloadStart")
        TNT1 A 0 A_JumpIfInventory("Cell",10,"ReloadStart")
        goto FireFinish3

      FireFinish3:
        CHIZ J 6
        goto FireReFireCheck

      FireStart4:
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_GunFlash("Flash",GFF_NOEXTCHANGE)
        CHIZ K 4 Bright A_GiveInventory("SamsaraChexGigazorcherAttackHandler")
        CHIZ L 4 Bright
        CHIZ M 4
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"FireFinish4Reloadable")
        goto FireFinish4

      FireFinish4Reloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireFinish4")
        TNT1 A 0 A_JumpIfInventory("ChexGigazorcherClip",1,"FireFinish4")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReloadStart")
        TNT1 A 0 A_JumpIfInventory("Cell",10,"ReloadStart")
        goto FireFinish4

      FireFinish4:
        CHIZ M 6
        goto FireReFireCheck

      FireReFireCheck:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"FireReFireReloadable")
        goto FireReFireNonReloadable

      FireReFireNonReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireReFire")
        TNT1 A 0 A_JumpIfInventory("Cell",10,"FireReFire")
        goto Nothing

      FireReFireReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireReFire")
        TNT1 A 0 A_JumpIfInventory("ChexGigazorcherClip",1,"FireReFire")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireReFire")
        TNT1 A 0 A_JumpIfInventory("Cell",10,"FireReFire")
        goto Nothing

      FireReFire:
        TNT1 A 0 A_ReFire
        goto Ready

      Flash:
        TNT1 A 5 Bright A_Light1
        TNT1 A 5 Bright A_Light2
        goto LightDone

      Reload:
        CHIZ M 6
      ReloadStart:
        CHIZ M 10
        CHIZ M 3 A_PlaySound("chex/instazorchload")
        CHIZ NOPQR 3
        CHIZ S 2
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReloadAmmo")
      ReloadFinish:
        CHIZ S 3
        CHIZ A 15 // doom railgun = 6 + 6 + FireFinish's 6
        goto FireReFireCheck

      ReloadAmmo:
        TNT1 A 0 A_JumpIfInventory("ChexGigazorcherClip",0,"ReloadFinish")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Cell",10,1)
        goto ReloadFinish
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_TakeInventory("Cell",10)
        TNT1 A 0 A_GiveInventory("ChexGigazorcherClip")
        loop

      Nothing:
        TNT1 A 0 A_ClearReFire
        TNT1 A 0 A_SelectWeapon("Mini-Zorcher")
        goto Deselect
    }
}

// Attack handler

actor SamsaraChexGigazorcherAttackHandler : Trigger
{
    States
    {
      // Ammo check stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"PickupCheckAmmoReloadable")
      PickupCheckAmmoNonReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupTakeAmmoNonReloadable")
        TNT1 A 0 A_JumpIfInventory("Cell",10,"PickupTakeAmmoNonReloadable")
        stop

      PickupCheckAmmoReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"PickupEffectAmmoCheck")
        TNT1 A 0 A_JumpIfInventory("ChexGigazorcherClip",1,"PickupTakeAmmoReloadable")
        stop

      // Ammo taking stuff
      PickupTakeAmmoNonReloadable:
        TNT1 A 0 A_TakeInventory("Cell",10,TIF_NOTAKEINFINITE)
        goto PickupEffectAmmoCheck

      PickupTakeAmmoReloadable:
        TNT1 A 0 A_TakeInventory("ChexGigazorcherClip",1)
        goto PickupEffectAmmoCheck

      // Effect ammo check stuff
      PickupEffectAmmoCheck:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"PickupEffectAmmoCheckReloadable")
      PickupEffectAmmoCheckNonReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupEffectAmmoCheckTake")
        TNT1 A 0 A_JumpIfInventory("Cell",10,"PickupEffectAmmoCheckTake")
        goto PickupEffectAmmoCheckGive

      PickupEffectAmmoCheckReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"PickupEffectAmmoCheckTake")
        TNT1 A 0 A_JumpIfInventory("ChexGigazorcherClip",1,"PickupEffectAmmoCheckTake")
        goto PickupEffectAmmoCheckGive

      PickupEffectAmmoCheckGive:
        TNT1 A 0 A_GiveInventory("SamsaraChexGigazorcherFiringLastShot")
        goto PickupAttack

      PickupEffectAmmoCheckTake:
        TNT1 A 0 A_TakeInventory("SamsaraChexGigazorcherFiringLastShot")
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("chex/instazorchfire",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_JumpIfInventory("SamsaraChexGigazorcherFiringLastShot",1,"PickupAttackDMLastShot")
        TNT1 A 0 A_RailAttack(0,3,false,None,Pink,RGF_SILENT|RGF_FULLBRIGHT,0,"GigazorcherPuffEffect")
        TNT1 A 0 A_RailAttack(0,3,false,None,White,RGF_SILENT|RGF_FULLBRIGHT,0,"GigazorcherPuffEffect")
        TNT1 A 0 A_RailAttack(0,3,false,None,Red,RGF_SILENT|RGF_FULLBRIGHT,0,"GigazorcherPuffEffect")
        goto PickupAttackDMActual

      PickupAttackDMLastShot:
        TNT1 A 0 A_RailAttack(0,3,false,None,Pink,RGF_SILENT|RGF_FULLBRIGHT,5,"GigazorcherPuffEffect")
        TNT1 A 0 A_RailAttack(0,3,false,None,White,RGF_SILENT|RGF_FULLBRIGHT,5,"GigazorcherPuffEffect")
        TNT1 A 0 A_RailAttack(0,3,false,None,Red,RGF_SILENT|RGF_FULLBRIGHT,5,"GigazorcherPuffEffect")
        goto PickupAttackDMActual

      PickupAttackDMActual:
        TNT1 A 0 A_RailAttack(75,0,false,None,None,RGF_SILENT|RGF_FULLBRIGHT,0,"GigazorcherPuff")
        stop

      PickupAttackCoop:
        TNT1 A 0 A_JumpIfInventory("SamsaraChexGigazorcherFiringLastShot",1,"PickupAttackCoopLastShot")
        TNT1 A 0 A_RailAttack(0,3,false,None,Pink,RGF_SILENT|RGF_FULLBRIGHT,0,"GigazorcherPuff2Effect")
        TNT1 A 0 A_RailAttack(0,3,false,None,White,RGF_SILENT|RGF_FULLBRIGHT,0,"GigazorcherPuff2Effect")
        TNT1 A 0 A_RailAttack(0,3,false,None,Red,RGF_SILENT|RGF_FULLBRIGHT,0,"GigazorcherPuff2Effect")
        goto PickupAttackCoopActual

      PickupAttackCoopLastShot:
        TNT1 A 0 A_RailAttack(0,3,false,None,Pink,RGF_SILENT|RGF_FULLBRIGHT,5,"GigazorcherPuff2Effect")
        TNT1 A 0 A_RailAttack(0,3,false,None,White,RGF_SILENT|RGF_FULLBRIGHT,5,"GigazorcherPuff2Effect")
        TNT1 A 0 A_RailAttack(0,3,false,None,Red,RGF_SILENT|RGF_FULLBRIGHT,5,"GigazorcherPuff2Effect")
        goto PickupAttackCoopActual

      PickupAttackCoopActual:
        TNT1 A 0 A_RailAttack(200,0,false,None,None,RGF_SILENT|RGF_FULLBRIGHT,0,"GigazorcherPuff2")
        stop
    }
}

actor SamsaraChexGigazorcherReloader : Trigger // used for (re)spawn
{
    States
    {
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,1)
        stop
        TNT1 A 0 A_JumpIfInventory("ChexGigazorcherClip",0,"PickupStop")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Cell",10,1)
        stop
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_TakeInventory("Cell",10)
        TNT1 A 0 A_GiveInventory("ChexGigazorcherClip")
        loop
    }
}

actor ChexGigazorcherClip : Ammo
{
    Inventory.MaxAmount 4
    Tag "Gigazorcher 2100 Magazine"
    +IGNORESKILL
    +UNTOSSABLE
}

actor GigazorcherPuff
{
    Radius 1
    Height 1
    Speed 0
    Damage 0
    DamageType "Zorch"
    +ALWAYSPUFF
    +SKYEXPLODE
    +PIERCEARMOR
    +PUFFONACTORS
    +PUFFGETSOWNER
    +BLOODLESSIMPACT
    +NOEXTREMEDEATH
    States
    {
      Spawn:
        TNT1 A 0
        TNT1 A 8
        stop
    }
}

actor GigazorcherPuffEffect : GigazorcherPuff
{
    +CLIENTSIDEONLY
    +DONTSPLASH
    +NOTRIGGER
}

actor GigazorcherPuff2 : GigazorcherPuff 
{
    //+ALLOWTHRUFLAGS // doesn't exist in zandronum yet
	+MTHRUSPECIES
}

actor GigazorcherPuff2Effect : GigazorcherPuff2
{
    +CLIENTSIDEONLY
    +DONTSPLASH
    +NOTRIGGER
}
