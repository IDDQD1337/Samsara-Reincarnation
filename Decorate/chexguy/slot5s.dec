actor "Zorch Launcher" : Weapon
{
    Weapon.SelectionOrder 2500
    Weapon.SlotNumber 5
    Inventory.PickUpSound "chex/weaponget"
    Weapon.AmmoUse 1
    Weapon.AmmoGive 2
    Weapon.AmmoType "RocketAmmo"
    +WEAPON.NOAUTOFIRE
    //+AMMO_OPTIONAL
    +INVENTORY.UNDROPPABLE
    Inventory.PickupMessage "You got the Zorch Propulsor!"
	Inventory.RestrictedTo "ChexWarrior"
	Obituary "$SAMSARA_CHEX_OB_SLOT5S"
    Tag "Zorch Launcher"
    States
    {
      Ready:
        MISC J 1 A_WeaponReady
        Loop

      Deselect:
        MISC J 1 A_Lower
        Loop

      Select:
        MISC J 1 A_Raise
        Loop

      Fire:
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopFire")
		MISC K 8 A_GunFlash
		MISC K 12 A_FireCustomMissile ("ZorchGrenade", 0, true, 0, 0, 0, 6.328125)
		MISC K 0 A_ReFire
        Goto Ready

      CoopFire:
		MISC K 8 A_GunFlash
		MISC K 12 A_FireCustomMissile ("ZorchGrenade2", 0, true, 0, 0, 0, 6.328125)
		MISC K 0 A_ReFire
        Goto Ready

      Flash:
		MCSF Q 3 bright A_Light1
		MCSF R 4 bright
		MCSF S 4 bright A_Light2
		MCSF T 4 bright A_Light2
        Goto LightDone

      Spawn:
        LAUC B -1
        Stop
    }
}

actor ZorchGrenade
{
	Projectile
	Radius 8
	Height 8
	Speed 25
	Damage (100)
	DamageType "Zorch"
	ReactionTime 87
	Gravity 0.25
	BounceType "Doom" // replaces +DOOMBOUNCE in zan, plays sounds as intended
	SeeSound "chex/grenade"
	BounceSound "doomguy/grbnce" // no longer uses +USESTBOUNCESOUND from zan
	DeathSound "chex/rocklx"
	Obituary "%k played zorch grenade catch with %o."
	//SelfObituary "%o tripped %p own zorch grenade."	// This shouldn't ever appear, but just in case
	+DEHEXPLOSION
	+EXPLODEONDEATH
	+GRENADETRAIL
	-NOGRAVITY
	+RANDOMIZE
	+BLOODLESSIMPACT
	+NOEXTREMEDEATH
	States
	{
	  Spawn:
		CMIS I 1 Bright A_Countdown
		loop

	  Death:
		TNT1 A 0 A_ChangeFlag("NOGRAVITY",true)
		TNT1 A 0 A_Explode(128,128,0)
	  DeathFinish:
		CMIS B 8 Bright
		CMIS C 6 Bright
		CMIS D 4 Bright
		stop
	}
}

actor ZorchGrenade2 : ZorchGrenade { +THRUSPECIES Species "Player" }