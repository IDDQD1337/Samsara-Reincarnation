actor "Chain-Lightning Zorcher" : Weapon
{
    Weapon.SelectionOrder 1600
    Weapon.SlotNumber 8
    Inventory.PickUpSound "chex/weaponget"
    Weapon.AmmoUse 2
    Weapon.AmmoGive 20
    Weapon.AmmoType "Cell"
    +BLOODLESSIMPACT
    +PUFFONACTORS
    //+AMMO_OPTIONAL
    +INVENTORY.UNDROPPABLE
    DamageType "Zorch"
    Inventory.PickupMessage "You got the Large Zorcher!"
	Inventory.RestrictedTo "ChexWarrior"
    Tag "Chain-Lightning Zorcher"
    States
    {
      Ready:
        PISC S 1 A_WeaponReady
        Loop

      Deselect:
        PISC S 1 A_Lower
        Loop

      Select:
        PISC S 1 A_Raise
        Loop

      Fire:
        TNT1 A 0 A_GunFlash
        PISC U 4 A_GiveInventory("SamsaraChexChainLightningZorcherAttackHandler")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"Fire2Attack")
        TNT1 A 0 A_JumpIfInventory("Cell",2,"Fire2Attack")
        goto Fire2

      Fire2Attack:
        TNT1 A 0 A_GunFlash("Flash2")
      Fire2:
        PISC U 4 A_GiveInventory("SamsaraChexChainLightningZorcherAttackHandler")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"Fire3Attack")
        TNT1 A 0 A_JumpIfInventory("Cell",2,"Fire3Attack")
        goto Fire3

      Fire3Attack:
        TNT1 A 0 A_GunFlash("Flash3")
      Fire3:
        PISC U 4 A_GiveInventory("SamsaraChexChainLightningZorcherAttackHandler")
      FireFinish:
        PISC T 7
        PISC T 7 A_CheckReload
        PISC T 8 A_ReFire
        goto Ready

      Flash:
        CPIS E 2 Bright A_Light1
        CPIS E 2 Bright A_Light0
        goto LightDone

      Flash2:
        CPIS F 2 Bright A_Light2
        CPIS F 2 Bright A_Light0
        goto LightDone

      Flash3:
        CPIS G 2 Bright A_Light1
        CPIS G 2 Bright A_Light0
        goto LightDone

      Spawn:
        SHOC C -1
        Stop
    }
}

actor SamsaraChexChainLightningZorcherAttackHandler : Trigger
{
    States
    {
      // Ammo check stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupTakeAmmo")
        TNT1 A 0 A_JumpIfInventory("Cell",2,"PickupTakeAmmo")
        stop

      // Ammo taking stuff
      PickupTakeAmmo:
        TNT1 A 0 A_TakeInventory("Cell",2,TIF_NOTAKEINFINITE)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("ChexPackChainLightning",-1,false,-8,1)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("ChexPackChainLightning2",-1,false,-8,1)
        stop
    }
}

actor ChexPackChainLightning
{
	Height 3
	Radius 3
	Damage 5
	Speed 60
	PROJECTILE
	-NOGRAVITY
	+RIPPER
	+HEXENBOUNCE
	+BLOODLESSIMPACT
	-BLOODSPLATTER
	DamageType "Zorch"
	Gravity 0
	Bouncecount 3
	Mass 0
	Seesound "chex/clshot"
	Deathsound "chex/icehit"
	Obituary "$SAMSARA_CHEX_OB_BERSERK"
	States
	{
	Spawn:
		CMIS J 1000 BRIGHT A_Die
		wait
	Death:
		CMIS K 3 bright 
		CMIS L 3 bright
		stop
	}
}

actor ChexPackChainLightning2 : ChexPackChainLightning { +THRUSPECIES Species "Player" }
