actor "Large Zorcher" : Weapon
{
    Weapon.SelectionOrder 1300
    Weapon.SlotNumber 2
    Inventory.PickUpSound "chex/weaponget"
    Weapon.AmmoUse 1
    Weapon.AmmoGive 8
    Weapon.AmmoType "Shell"
    +BLOODLESSIMPACT
    +PUFFONACTORS
    +THRUGHOST
    //+AMMO_OPTIONAL
    +INVENTORY.UNDROPPABLE
    DamageType "Zorch"
    Inventory.PickupMessage "You got the Large Zorcher!"
    Obituary "$SAMSARA_CHEX_OB_SLOT2"
	Inventory.RestrictedTo "ChexWarrior"
    Tag "Large Zorcher"
    States
    {
      Ready:
        SHTC A 1 A_WeaponReady
        Loop

      Deselect:
        SHTC A 1 A_Lower
        Loop

      Select:
        SHTC A 1 A_Raise
        Loop

      Fire: // slightly faster compared to doom's shotgun
        SHTC A 3
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireAttack")
        TNT1 A 0 A_JumpIfInventory("Shell",1,"FireAttack")
        goto FireContinue

      FireAttack:
        TNT1 A 0 A_GunFlash
      FireContinue:
        SHTC A 6 A_GiveInventory("SamsaraChexLargeZorcherAttackHandler")
        SHTC BCDCB 4
        SHTC A 3
        SHTC A 5 A_ReFire
        goto Ready

      Flash:
        CSHT A 3 Bright A_Light1
        CSHT B 3 Bright A_Light2
        Goto LightDone

      Spawn:
        SHOC A -1
        Stop
    }
}

actor SamsaraChexLargeZorcherAttackHandler : Trigger
{
    States
    {
      // Ammo check stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupTakeAmmo")
        TNT1 A 0 A_JumpIfInventory("Shell",1,"PickupTakeAmmo")
        stop

      // Ammo taking stuff
      PickupTakeAmmo:
        TNT1 A 0 A_TakeInventory("Shell",1,TIF_NOTAKEINFINITE)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("chex/shotgf",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SamsaraImprovedDoomguyBalance",1,"PickupAttackImproved")
      // Vanilla
      PickupAttackVanilla:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackVanillaCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackVanillaCoop")
      PickupAttackVanillaDM:
        TNT1 A 0 A_FireBullets(5,0,7,5,"ChexPuffThruGhost",FBF_NOFLASH)
        stop

      PickupAttackVanillaCoop:
        TNT1 A 0 A_FireBullets(5,0,7,5,"ChexPuffThruGhostCoop",FBF_NOFLASH)
        stop

      // Improved
      PickupAttackImproved:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackImprovedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackImprovedCoop")
      PickupAttackImprovedDM:
        TNT1 A 0 A_FireBullets(5,0,7,7,"ChexPuffThruGhost",FBF_NOFLASH)
        stop

      PickupAttackImprovedCoop:
        TNT1 A 0 A_FireBullets(5,0,7,7,"ChexPuffThruGhostCoop",FBF_NOFLASH)
        stop
    }
}
