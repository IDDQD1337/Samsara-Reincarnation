actor "Ultra Rapid Zorcher" : Weapon
{
    Weapon.SelectionOrder 700
    Weapon.SlotNumber 4
    Inventory.PickUpSound "chex/weaponget"
    Weapon.AmmoUse 1
    Weapon.AmmoGive 20
    Weapon.AmmoType "Clip"
    +BLOODLESSIMPACT
    +PUFFONACTORS
    +THRUGHOST
    //+AMMO_OPTIONAL
    +INVENTORY.UNDROPPABLE
    DamageType "Zorch"
    Inventory.PickupMessage "You got the Rapid Zorcher!"
	Inventory.RestrictedTo "ChexWarrior"
	Obituary "$SAMSARA_CHEX_OB_SLOT4S"
    Tag "Ultra Rapid Zorcher"
    States
    {
      Ready:
        CHGC C 1 A_WeaponReady
        Loop

      Deselect:
        CHGC C 1 A_Lower
        Loop

      Select:
        CHGC C 1 A_Raise
        Loop

      Fire:
		TNT1 A 0 A_GunFlash
        CHGC C 2 A_GiveInventory("SamsaraChexUltraRapidZorcherAttackHandler")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"Fire2Attack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"Fire2Attack")
        goto Fire2

      Fire2Attack:
        TNT1 A 0 A_GunFlash("Flash2")
      Fire2:
        CHGC D 2 A_GiveInventory("SamsaraChexUltraRapidZorcherAttackHandler")
      FireFinish:
		CHGC C 2 A_ReFire
		CHGC D 2
		CHGC CD 4
		CHGC CD 8
        goto Ready

      Flash:
        CCHG C 3 Bright A_Light1
        goto LightDone

      Flash2:
        CCHG D 3 Bright A_Light2
        goto LightDone

      Spawn:
        MGUC B -1
        Stop
    }
}

actor SamsaraChexUltraRapidZorcherAttackHandler : Trigger
{
    States
    {
      // Ammo check stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupTakeAmmo")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"PickupTakeAmmo")
        stop

      // Ammo taking stuff
      PickupTakeAmmo:
        TNT1 A 0 A_TakeInventory("Clip",1,TIF_NOTAKEINFINITE)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("chex/minigun",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SamsaraImprovedDoomguyBalance",1,"PickupAttackImproved")
      // Vanilla
      PickupAttackVanilla:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackVanillaCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackVanillaCoop")
      PickupAttackVanillaDM:
        TNT1 A 0 A_FireBullets(5,0,1,5,"ChexPuff",FBF_NOFLASH)
        stop

      PickupAttackVanillaCoop:
        TNT1 A 0 A_FireBullets(5,0,1,5,"ChexPuffCoop",FBF_NOFLASH)
        stop

      // Improved
      PickupAttackImproved:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackImprovedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackImprovedCoop")
      PickupAttackImprovedDM:
        TNT1 A 0 A_FireBullets(5,0,1,random(8,10)*2,"ChexPuff",FBF_NORANDOM|FBF_NOFLASH)
        stop

      PickupAttackImprovedCoop:
        TNT1 A 0 A_FireBullets(5,0,1,random(8,10)*2,"ChexPuffCoop",FBF_NORANDOM|FBF_NOFLASH)
        stop
    }
}
