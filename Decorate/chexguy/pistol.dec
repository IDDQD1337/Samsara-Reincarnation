actor "Mini-Zorcher" : Weapon
{
    Weapon.SelectionOrder 1900
    Weapon.SlotNumber 1
    Weapon.SlotPriority 1
    Inventory.PickUpSound "chex/weaponget"
    Obituary "$SAMSARA_CHEX_OB_PISTOL"
    Inventory.PickupMessage "You got the Mini-Zorcher!"
    Weapon.AmmoUse 0
    Weapon.AmmoGive 0
    +WEAPON.WIMPY_WEAPON
    +BLOODLESSIMPACT
    +PUFFONACTORS
    +INVENTORY.UNDROPPABLE
    DamageType "ZorchPistol"
	Inventory.RestrictedTo "ChexWarrior"
    Tag "Mini-Zorcher"
    States
    {
      Ready:
        PISC A 1 A_WeaponReady
        Loop

      Deselect:
        PISC A 1 A_Lower
        Loop

      Select:
        PISC A 1 A_Raise
        Loop

      Fire:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,"CheckForAmmo")
      Fire2:
        PISC A 4
        TNT1 A 0 A_GiveInventory("SamsaraChexWarriorPistolAttackHandler")
        goto FireAnim

      Hold:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,"CheckForAmmoHold")
      Hold2:
        PISC A 4
        TNT1 A 0 A_GiveInventory("SamsaraChexWarriorPistolAttackHandler")
        goto FireAnim
        
      FireAnim:
        PISC B 6 A_GunFlash
        PISC C 4
        TNT1 A 0 A_ReFire
        PISC D 5 A_ClearRefire
        Goto Ready

      Flash:
        CPIS A 7 Bright A_Light1
        Goto LightDone
        CPIS A 7 Bright A_Light0
        Goto LightDone

      Spawn:
        CIST A -1
        Stop
        
      CheckForAmmo:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"Fire2")
		TNT1 A 0 A_JumpIfInventory("Clip",1,"Fire2")
        TNT1 A 0 A_SelectWeapon(" Bootspoon ")
        goto Deselect
        
      CheckForAmmoHold:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"Fire2")
		TNT1 A 0 A_JumpIfInventory("Clip",1,"Hold2")
        TNT1 A 0 A_SelectWeapon(" Bootspoon ")
        goto Deselect
    }
}

actor SamsaraChexWarriorPistolAttackHandler : Trigger
{
    States
    {
      // Ammo check stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,"PickupCheckAmmo")
        goto PickupAttack

      PickupCheckAmmo:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupTakeAmmo")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"PickupTakeAmmo")
        stop

      // Ammo taking stuff
      PickupTakeAmmo:
        TNT1 A 0 A_TakeInventory("Clip",1,TIF_NOTAKEINFINITE)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("chex/pistol",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SamsaraImprovedDoomguyBalance",1,"PickupAttackImproved")
      // Vanilla
      PickupAttackVanilla:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackVanillaCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackVanillaCoop")
      PickupAttackVanillaDM:
        TNT1 A 0 A_FireBullets(5, 0, -1, 5, "ChexPuff2", FBF_NOFLASH)
        stop

      PickupAttackVanillaCoop:
        TNT1 A 0 A_FireBullets(5, 0, -1, 5, "ChexPuff2Coop", FBF_NOFLASH)
        stop

      // Improved
      PickupAttackImproved:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackImprovedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackImprovedCoop")
      PickupAttackImprovedDM:
        TNT1 A 0 A_FireBullets(5, 0, -1, random(6,8)*2, "ChexPuff2", FBF_NORANDOM|FBF_NOFLASH)
        stop

      PickupAttackImprovedCoop:
        TNT1 A 0 A_FireBullets(5, 0, -1, random(6,8)*2, "ChexPuff2Coop", FBF_NORANDOM|FBF_NOFLASH)
        stop
    }
}

actor ChexPuff2 : ChexPuff { -THRUGHOST DamageType "ZorchPistol" } // I never understood why you had to damagetype puffs on hitscan weapons.
actor ChexPuff2Coop : ChexPuff2 { +MTHRUSPECIES +THRUSPECIES Species "Player" }

actor ChexBullet2 : FastProjectile
{
    Damage (Random(1,3)*5)
    Radius 1
    Height 1
    Speed 320
    Projectile
    Renderstyle "Normal"
    Species "Player"
    DamageType "ZorchPistol"
    -BLOODSPLATTER
    +BLOODLESSIMPACT
    +THRUSPECIES
    +NOTIMEFREEZE
    Alpha 1.0
    States
    {
      Spawn:
        TNT1 A 1
        loop

      Death:
        TNT1 A 1 A_SpawnItem("ChexPuff")
        stop
    }
}