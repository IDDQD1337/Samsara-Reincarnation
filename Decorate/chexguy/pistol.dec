actor "Mini-Zorcher" : Weapon
{
    Weapon.SelectionOrder 1900
    Weapon.SlotNumber 1
    Weapon.SlotPriority 1
    Inventory.PickUpSound "chex/weaponget"
    Obituary "$SAMSARA_CHEX_OB_PISTOL"
    Inventory.PickupMessage "You got the Mini-Zorcher!"
    Weapon.AmmoUse 0
    Weapon.AmmoGive 0
    +WEAPON.WIMPY_WEAPON
    +BLOODLESSIMPACT
    +PUFFONACTORS
    +INVENTORY.UNDROPPABLE
    DamageType "ZorchPistol"
	Inventory.RestrictedTo "ChexWarrior"
    Tag "Mini-Zorcher"
    States
    {
      Ready:
        PISC A 1 A_WeaponReady
        Loop

      Deselect:
        PISC A 1 A_Lower
        Loop

      Select:
        PISC A 1 A_Raise
        Loop

      Fire:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,"FireAmmoCheck")
        goto FireStart

      FireAmmoCheck:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"FireStart")
        TNT1 A 0 A_SelectWeapon(" Bootspoon ")
        goto Deselect

      FireStart:
        PISC A 4 A_AlertMonsters
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,"FireStartAmmoCheck")
        goto FireAttack

      FireStartAmmoCheck:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"FireAttack")
        goto FireContinue

      FireAttack:
        TNT1 A 0 A_GunFlash
      FireContinue:
        PISC B 6 A_GiveInventory("SamsaraChexMiniZorcherAttackHandler")
        PISC C 4
        PISC D 5 A_ReFire
        goto Ready

      Flash:
        CPIS A 7 Bright A_Light1
        goto LightDone

      Spawn:
        CIST A -1
        Stop
    }
}

actor SamsaraChexMiniZorcherAttackHandler : Trigger
{
    States
    {
      // Ammo check stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,"PickupCheckAmmo")
        goto PickupAttack

      PickupCheckAmmo:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupTakeAmmo")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"PickupTakeAmmo")
        stop

      // Ammo taking stuff
      PickupTakeAmmo:
        TNT1 A 0 A_TakeInventory("Clip",1,TIF_NOTAKEINFINITE)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("chex/pistol",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SamsaraImprovedDoomguyBalance",1,"PickupAttackImproved")
      // Vanilla
      PickupAttackVanilla:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackVanillaCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackVanillaCoop")
      PickupAttackVanillaDM:
        TNT1 A 0 A_FireBullets(5,0,1,5,"ChexPuff2",FBF_NOFLASH)
        stop

      PickupAttackVanillaCoop:
        TNT1 A 0 A_FireBullets(5,0,1,5,"ChexPuff2Coop",FBF_NOFLASH)
        stop

      // Improved
      PickupAttackImproved:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackImprovedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackImprovedCoop")
      PickupAttackImprovedDM:
        TNT1 A 0 A_FireBullets(5,0,1,random(6,8)*2,"ChexPuff2",FBF_NORANDOM|FBF_NOFLASH)
        stop

      PickupAttackImprovedCoop:
        TNT1 A 0 A_FireBullets(5,0,1,random(6,8)*2,"ChexPuff2Coop",FBF_NORANDOM|FBF_NOFLASH)
        stop
    }
}

actor ChexPuff2 : ChexPuff { -THRUGHOST DamageType "ZorchPistol" }
actor ChexPuff2Coop : ChexPuff2 { +MTHRUSPECIES +THRUSPECIES Species "Player" }

actor ChexBullet2 : FastProjectile
{
    Damage (Random(1,3)*5)
    Radius 1
    Height 1
    Speed 320
    Projectile
    Renderstyle "Normal"
    Species "Player"
    DamageType "ZorchPistol"
    -BLOODSPLATTER
    +BLOODLESSIMPACT
    +THRUSPECIES
    +NOTIMEFREEZE
    Alpha 1.0
    States
    {
      Spawn:
        TNT1 A 1
        loop

      Death:
        TNT1 A 1 A_SpawnItem("ChexPuff")
        stop
    }
}
