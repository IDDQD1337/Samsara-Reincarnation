actor "Super Bootspork" : Weapon
{
    Weapon.Kickback 0
    Weapon.SelectionOrder 2200
    Weapon.SlotNumber 1
    Weapon.SlotPriority 0
    Inventory.PickUpSound "chex/weaponget"
    Weapon.UpSound "chex/sawup"
    Weapon.ReadySound "chex/sawidle"
    Inventory.PickupMessage "You got a Super Bootspork! Find some milk!"
    Obituary "$SAMSARA_CHEX_OB_SLOT1"
    +WEAPON.MELEEWEAPON
    +BLOODLESSIMPACT
    +INVENTORY.UNDROPPABLE
    DamageType "Zorch"
	Inventory.RestrictedTo "ChexWarrior"
    Tag "Super Bootspork"
    States
    {
      Ready:
        SAWC CD 4 A_WeaponReady
        loop

      Deselect:
        SAWC C 1 A_Lower
        loop

      Select:
        SAWC C 1 A_Raise
        loop

      Fire:
        SAWC AB 4 A_GiveInventory("SamsaraChexSuperBootsporkAttackHandler")
      FireFinish:
        TNT1 A 0 A_ReFire
        goto Ready

      Spawn:
        CSPR A -1
        stop
    }
}

actor SamsaraChexSuperBootsporkAttackHandler : Trigger
{
	States
	{
	  Pickup:
		TNT1 A 0 A_JumpIfInventory("SamsaraImprovedDoomguyBalance",1,"PickupImproved")
	  // Vanilla
	  PickupVanilla:
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupVanillaCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupVanillaCoop")
	  PickupVanillaDM:
		TNT1 A 0 A_Saw("chex/sawfull","chex/sawhit",2,"ChexPuff")
		stop

	  PickupVanillaCoop:
		TNT1 A 0 A_Saw("chex/sawfull","chex/sawhit",2,"ChexPuffCoop")
		stop

	  // Improved
	  PickupImproved:
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupImprovedCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupImprovedCoop")
	  PickupImprovedDM:
		TNT1 A 0 A_Saw("chex/sawfull","chex/sawhit",random(1,2)*10,"ChexPuff",SF_NORANDOM)
		stop

	  PickupImprovedCoop:
		TNT1 A 0 A_Saw("chex/sawfull","chex/sawhit",random(1,2)*10,"ChexPuffCoop",SF_NORANDOM)
		stop
	}
}
