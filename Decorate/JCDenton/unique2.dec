Actor DeusEx_SniperMagazine : Ammo
{
	+INVENTORY.IGNORESKILL
	Inventory.Amount 1
	Inventory.MaxAmount 6
}

Actor DeusEx_SniperRifle : DeusExWeapon 
{
	Weapon.SelectionOrder 1600
	Weapon.SlotNumber 2
	Weapon.SlotPriority 2
	Weapon.AmmoType1 "DeusEx_SniperMagazine"
	Weapon.AmmoUse1 1
	Weapon.AmmoType2 "Shell"
	Weapon.AmmoUse2 0
	Weapon.AmmoGive1 0
	Weapon.AmmoGive2 8
	Tag "Sniper Rifle"
	Weapon.UpSound "DeusEx/Sniper/Select"
	Inventory.PickupMessage "You found a Sniper Rifle."
	Obituary "%o was sniped by %k."
	States
	{
		Spawn:
			DX99 A -1
			Stop
		Select:
			TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_Raise
			TNT1 A 1 A_Raise
			loop
	    Ready:
			DX00 B 0 A_GiveInventory("DeusEx_SniperEquipped", 1)
			DX00 BCDEFGHIJK 2
			DX00 A 0 A_TakeInventory("DeusEx_Reloading", 1)
			Goto ReadyLoop
		ReadyLoop:
			DX00 A 0 A_JumpIfInventory("DeusEx_Zoomed", 1, "ReadyZoomed")
			DX00 A 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM) DX00 A 0 A_Jump(3, "Idle1", "Idle2", "Idle3")
			Loop
		ReadyZoomed:
			TNT1 A 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
			Loop
		Idle1:
			DX01 HIJKLMNOPQ 6 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
			Goto ReadyLoop
		Idle2:
			DX01 "RSTUVWXYZ[" 6 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
			Goto ReadyLoop
		Idle3:
			DX01 "\]" 6 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
			DX02 ABCDEFGH 6 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
			Goto ReadyLoop
		Fire:
			DX00 P 0 A_JumpIfInventory("DeusEx_SniperMagazine", 1, "Firing")
			Goto FireReload
		Firing:
			DX00 L 0 A_GiveInventory("DeusEx_Firing", 1)
			DX00 L 0 ACS_NamedExecuteWithResult("DeusExRecoil", 18, 6, 1)
			DX00 L 0 A_PlaySound("DeusEx/Sniper/Fire", CHAN_WEAPON, 1.1)
			DX00 L 0 A_AlertMonsters(3500)
			DX00 L 0 A_JumpIfInventory("DeusEx_Zoomed", 1, "FireZoomed")
			DX00 L 0 A_JumpIfInventory("CoopModeOn",1,2)
			DX00 L 5 A_FireCustomMissile("DeusEx_SniperBullet", FRandom(-2.2, 2.2), 1, 5, 16)
			Goto FireEnd
			DX00 L 5 A_FireCustomMissile("DeusEx_SniperBulletCoop", FRandom(-2.2, 2.2), 1, 5, 16)
			Goto FireEnd
		FireEnd:
			DX00 M 0 A_SpawnItemEx("DeusEx_ShellCasing2", 8, -16, 39, Random(1,3), 0, 3, -90, SXF_NOCHECKPOSITION|SXF_MULTIPLYSPEED|SXF_TRANSFERPITCH)
			DX00 MN 5	
			DX00 OPQA 5
			Goto ReadyLoop
		FireZoomed:
			TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,2)
			TNT1 A 5 A_FireCustomMissile("DeusEx_SniperBullet", 0, 1, 0, 16)
			Goto FireZoomedEnd
			TNT1 A 5 A_FireCustomMissile("DeusEx_SniperBulletCoop", 0, 1, 0, 16)
			Goto FireZoomedEnd
		FireZoomedEnd:
			TNT1 A 0 A_SpawnItemEx("DeusEx_ShellCasing2", 8, -16, 39, Random(1,3), 0, 3, -90, SXF_NOCHECKPOSITION|SXF_MULTIPLYSPEED|SXF_TRANSFERPITCH)
			TNT1 AAAAAA 5	
			Goto ReadyLoop
		FireReload:
			DX00 A 3 A_PlaySound("DeusEx/DryFire", CHAN_WEAPON)
			Goto Reload
		Reload:
			DX00 T 0 A_JumpIfInventory("DeusEx_SniperMagazine", 0, "ReadyLoop")
			DX00 T 0 A_JumpIfInventory("Shell", 1, "ReloadSuccess")
			Goto ReadyLoop
		ReloadSuccess:
			DX00 T 0 A_GiveInventory("DeusEx_Reloading", 1)
			DX00 T 0 A_PlaySound("DeusEx/Sniper/Reload1", CHAN_WEAPON)
			DX00 T 0 A_ZoomFactor(1.0, ZOOM_INSTANT)
			DX00 T 3 A_TakeInventory("DeusEx_Zoomed", 999)
			DX00 "TUVWXYZ[" 3
			DX00 "\" 0 A_JumpIfInventory("DMModeOn", 1, 20)
			DX00 "\" 0 A_JumpIfInventory("DeusEx_RifleSkill", 4, "ReloadAmmo")
			DX00 "\" 0 A_JumpIfInventory("DeusEx_RifleSkill", 3, 18)
			DX00 "\" 0 A_JumpIfInventory("DeusEx_RifleSkill", 2, 12)
			DX00 "\" 0 A_JumpIfInventory("DeusEx_RifleSkill", 1, 6)
			DX00 "\]" 3
			DX01 ABC 3
			DX00 "\]" 3
			DX01 ABC 3
			DX00 "\]" 3
			DX01 ABC 3
			Goto ReloadAmmo
		ReloadAmmo:
			DX01 D 0 A_JumpIfInventory("DeusEx_SniperMagazine",0,"ReloadFinish")
			DX01 D 0 A_JumpIfInventory("Shell",1,1)
			Goto ReloadFinish
			DX01 D 0 A_GiveInventory("DeusEx_SniperMagazine",1)
			DX01 D 0 A_TakeInventory("Shell",1,TIF_NOTAKEINFINITE)
			Loop
		ReloadFinish:
			DX01 D 0 A_TakeInventory("DeusEx_Reloading", 1)
			DX01 D 0 A_PlaySound("DeuxEx/Sniper/Reload2", CHAN_WEAPON)
			DX01 DEFG 3
			Goto ReadyLoop
		Deselect:
			DX00 R 0 A_ZoomFactor(1.0, ZOOM_INSTANT)
			DX00 R 0 A_TakeInventory("DeusEx_Zoomed", 999)
			DX00 R 0 A_GiveInventory("DeusEx_Reloading", 1)
			DX00 RS 4
			TNT1 A 0 A_TakeInventory("DeusEx_SniperEquipped", 1)
			Goto Super::InstantDeselect
		Altfire:
		Zoom:
			DX02 A 0 A_JumpIfInventory("DeusEx_Zoomed", 1, "ZoomOut")
		ZoomIn:
			DX02 IJ 3
			DX02 K 0 A_GiveInventory("DeusEx_Zoomed", 1)
			DX02 K 0 ACS_NamedExecuteWithResult("DeusEx_ScopeZoom")
			TNT1 A 3 A_ZoomFactor(5.0, ZOOM_INSTANT)
			Goto ReadyLoop
		ZoomOut:
			TNT1 A 0 A_TakeInventory("DeusEx_Zoomed", 999)
			DX02 K 3 A_ZoomFactor(1.0, ZOOM_INSTANT)
			DX02 JI 3
			Goto ReadyLoop
	}
}

Actor DeusEx_SniperBullet : DeusExBullet
{
	Damage (CallACS("DeusEx_WeaponSkillDamage", 3, 30)) // was 25. buffed for rarity and to keep it viable alongside other sniper weapons
	DamageType "DeusExRifleShot"
	States
	{
		Crash:
			TNT1 A 0 A_PlaySound("DeusEx/Bullet/HitRobot", CHAN_BODY)
			TNT1 A 1 A_Explode(CallACS("DeusEx_DamageModifiers",CallACS("DeusEx_WeaponSkillDamage", 3, 30)),8,!XF_HURTSOURCE,0,8)
			stop
		XDeath:
			//TNT1 A 0 A_PlaySound("DeusEx/Bullet/HitFlesh", CHAN_BODY)
			TNT1 A 0 A_Explode(CallACS("DeusEx_DamageModifiers",CallACS("DeusEx_WeaponSkillDamage", 3, 30)),8,!XF_HURTSOURCE,0,8)
			TNT1 A 0 ACS_NamedExecuteWithResult("DeusEx_BloodGenerator_Offsets")
			TNT1 A 1 A_GiveInventory("DeusEx_BloodGenerator",1,AAPTR_TRACER)
			stop
	}
}

Actor DeusEx_SniperBulletCoop : DeusEx_SniperBullet { +THRUSPECIES Species "Player" }

Actor DeusEx_SniperPuff : DeusEx_BulletPuff
{
	DamageType "DeusExRifleShot"
	+ALWAYSPUFF
	+PUFFGETSOWNER
	+PUFFONACTORS
	+FORCERADIUSDMG
	+HITTRACER
	+NODAMAGETHRUST
	States
	{
		Crash:
		Melee:
		XDeath:
			TNT1 A 1 A_Explode(CallACS("DeusEx_DamageModifiers",CallACS("DeusEx_WeaponSkillDamage", 3, 30)),8,!XF_HURTSOURCE,0,8)
			Goto Super::Spawn
	}
}

Actor DeusEx_SniperPuffCoop : DeusEx_SniperPuff { +THRUSPECIES +MTHRUSPECIES Species "Player" }
