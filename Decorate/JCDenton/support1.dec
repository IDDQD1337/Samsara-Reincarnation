Actor DeusEx_StealthMagazine : Ammo
{
	+INVENTORY.IGNORESKILL
	Inventory.Amount 1
	Inventory.MaxAmount 10
}

Actor DeusEx_StealthPistol : DeusEx_10mmPistol
{
	Weapon.AmmoType1 "DeusEx_StealthMagazine"
	Weapon.SlotNumber 1
	Weapon.SlotPriority 2
	Tag "Stealth Pistol"
	Weapon.UpSound "DeusEx/Stealth/Select"
	Inventory.PickupMessage "You found a Stealth Pistol."
	Obituary "%o was wetworked with %k's Stealth Pistol."
	States
	{
		Spawn:
			DX99 A -1
			Stop
		Ready:
			DX00 B 0 A_GiveInventory("DeusEx_StealthEquipped", 1)
			DX00 BCDEFG 2 
			DX00 A 0 A_TakeInventory("DeusEx_Reloading", 1)
			Goto ReadyLoop
		ReadyLoop:
			DX00 A 1 A_WeaponReady(WRF_ALLOWRELOAD) DX00 A 0 A_Jump(4, "Idle1", "Idle2", "Idle3")
			Loop
		Idle1:
			DX01 CDEFGHIJ 5 A_WeaponReady(WRF_ALLOWRELOAD)
			Goto ReadyLoop
		Idle2:
			DX01 KLMNOPQR 5 A_WeaponReady(WRF_ALLOWRELOAD)
			Goto ReadyLoop
		Idle3:
			DX01 STUVWXYZ 5 A_WeaponReady(WRF_ALLOWRELOAD)
			Goto ReadyLoop
		Fire:
			DX00 H 0 BRIGHT A_JumpIfInventory("DeusEx_StealthMagazine", 1, "Fire2")
			Goto FireReload
		Fire2:
			DX00 H 0 A_GiveInventory("DeusEx_Firing",1)
			DX00 H 0 ACS_NamedExecuteWithResult("DeusExRecoil", 6, 3, 1)
			DX00 H 0 A_GiveInventory("DeusEx_StealthSpread", 5)
			DX00 H 0 A_FireBullets(0,0,1,0,"SamsaraBlankPuff",FBF_NORANDOM|FBF_NOFLASH)
			DX00 H 0 A_JumpIfInventory("CoopModeOn",1,2)
			DX00 H 3 A_FireCustomMissile("DeusEx_StealthPistolBullet",CallACS("DeusEx_Decorate", 5, 5, 1)*frandom(-1.0,1.0), 1, 12, 16, 1, CallACS("DeusEx_Decorate", 5, 5, 1)*FRandom(-0.5,0.5))
			Goto FireEnd
			DX00 H 3 A_FireCustomMissile("DeusEx_StealthPistolBulletCoop",CallACS("DeusEx_Decorate", 5, 5, 1)*frandom(-1.0,1.0), 1, 12, 16, 1, CallACS("DeusEx_Decorate", 5, 5, 1)*FRandom(-0.5,0.5))
			Goto FireEnd
		FireEnd:
			DX00 I 0 A_SpawnItemEx("DeusEx_ShellCasing1", 8, -16, 39, Random(1,3), 0, 3, -90, SXF_NOCHECKPOSITION|SXF_MULTIPLYSPEED|SXF_TRANSFERPITCH)
			DX00 I 0 A_PlaySound("DeusEx/Stealth/Fire", CHAN_WEAPON)
			DX00 JK 3
			DX00 L 3 A_Refire("Fire")
			Goto ReadyLoop
		FireReload:
			DX00 A 3 A_PlaySound("DeusEx/DryFire", CHAN_WEAPON)
			DX00 A 1 A_ClearRefire
			Goto Reload
		Reload:
			DX00 M 0 A_JumpIfInventory("DeusEx_StealthMagazine", 0, "ReadyLoop")
			//DX00 M 0 A_JumpIfInventory("Clip", 1, "ReloadSuccess")
			//Goto ReadyLoop
			Goto ReloadSuccess
		ReloadSuccess:
			DX00 M 0 A_PlaySound("DeusEx/Stealth/Reload1", CHAN_WEAPON)
			DX00 MNOPQRS 4 A_WeaponReady(WRF_NOFIRE)
			DX00 T 0 A_JumpIfInventory("DMModeOn", 1, 20)
			DX00 T 0 A_JumpIfInventory("DeusEx_PistolSkill", 4, "ReloadAmmo")
			DX00 T 0 A_JumpIfInventory("DeusEx_PistolSkill", 3, 18)
			DX00 T 0 A_JumpIfInventory("DeusEx_PistolSkill", 2, 12)
			DX00 T 0 A_JumpIfInventory("DeusEx_PistolSkill", 1, 6)
			DX00 TUVWXTUVWXTUVWXTUVWX 4 A_WeaponReady(WRF_NOFIRE)
			Goto ReloadAmmo
		ReloadAmmo:
			DX00 Y 0 A_JumpIfInventory("DeusEx_StealthMagazine",0,"ReloadFinish")
			//TNT1 A 0 A_JumpIfInventory("Clip",1,1)
			//goto ReloadFinish
			DX00 Y 0 A_GiveInventory("DeusEx_StealthMagazine",1)
			//TNT1 A 0 A_TakeInventory("Clip",1,TIF_NOTAKEINFINITE)
			loop
		ReloadFinish:
			DX00 Y 0 A_PlaySound("DeusEx/Stealth/Reload2", CHAN_WEAPON)
			DX00 YZ 4
			Goto ReadyLoop
		Deselect:
			DX00 "[" 0 A_TakeInventory("DeusEx_StealthSpread", 900)
			DX00 "[" 0 A_GiveInventory("DeusEx_Reloading", 1)
			DX00 "[\]" 3
			DX01 AB 3
			TNT1 A 0 A_TakeInventory("DeusEx_StealthEquipped", 1)
			Goto Super::InstantDeselect
	}
}

Actor DeusEx_StealthPistolBullet : DeusExBullet
{
	Damage(CallACS("DeusEx_WeaponSkillDamage", 1, 8))
	DamageType "DeusExPistol"
	States
	{
		Spawn:
			TNT1 A 0 NODELAY //A_SetUserVar(user_damage, CallACS("DeusEx_WeaponSkillDamage", 1, 14))
			TNT1 A 1 //A_SetUserVar(user_damage, CallACS("DeusEx_WeaponSkillDamage", 1, 14))
			Goto Spawn2
		Crash:
			TNT1 A 0 A_PlaySound("DeusEx/Bullet/HitRobot", CHAN_BODY)
			TNT1 A 1 A_Explode(CallACS("DeusEx_DamageModifiers",CallACS("DeusEx_WeaponSkillDamage", 1, 8)),8,!XF_HURTSOURCE,0,8)
			stop	
		XDeath:
			//TNT1 A 0 A_PlaySound("DeusEx/Bullet/HitFlesh", CHAN_BODY)
			TNT1 A 0 A_Explode(CallACS("DeusEx_DamageModifiers",CallACS("DeusEx_WeaponSkillDamage", 1, 8)),8,!XF_HURTSOURCE,0,8)
			TNT1 A 0 ACS_NamedExecuteWithResult("DeusEx_BloodGenerator_Offsets")
			TNT1 A 1 A_GiveInventory("DeusEx_BloodGenerator",1,AAPTR_TRACER)
			stop
	}
}
Actor DeusEx_StealthPistolBulletCoop : DeusEx_StealthPistolBullet { +THRUSPECIES Species "Player" }

Actor DeusEx_StealthSpread : Ammo { +INVENTORY.UNDROPPABLE Inventory.MaxAmount 115 Inventory.InterHubAmount 0 Ammo.BackPackAmount 0 Ammo.BackPackMaxAmount 115 }