Actor DeusEx_NanoSword : DeusEx_CombatKnife
{
	Weapon.SlotNumber 9
	Weapon.SlotPriority 2
	-WEAPON.WIMPY_WEAPON
	Tag "Dragon's Tooth Sword"
	Inventory.PickupMessage "You found the Dragon Tooth Sword."
	Obituary "%o was shown %k's large Dragon's Tooth Sword."		
	States
	{
		Spawn:
			DX99 A -1 Bright
			stop

		ReadyActual:
			DX00 A 0 A_GunFlash("ReadyActualGlow",GFF_NOEXTCHANGE)
			DX00 AAAAAAAA 2 A_WeaponReady 
			goto ReadyActualJump

		ReadyActualGlow:
			DX04 ABCDEFGH 2 Bright
			loop

        ReadyActualJump:
            DX00 A 0 A_Jump(64,"ReadyActualIdle1","ReadyActualIdle2","ReadyActualIdle3")
			goto ReadyActual

		ReadyActualIdle1:
			DX00 A 0 A_GunFlash("ReadyActualIdle1Glow",GFF_NOEXTCHANGE)
			DX00 A 6 A_WeaponReady
			DX01 "VWXYZ[\]" 6 A_WeaponReady
			goto ReadyActual

		ReadyActualIdle1Glow:
			DX04 H 6 Bright
			DX05 ABCDEFGH 6 Bright
			stop

		ReadyActualIdle2:
			DX00 A 0 A_GunFlash("ReadyActualIdle2Glow",GFF_NOEXTCHANGE)
			DX00 A 6 A_WeaponReady
			DX02 ABCDEFGH 6 A_WeaponReady
			goto ReadyActual

		ReadyActualIdle2Glow:
			DX04 H 6 Bright
			DX06 ABCDEFGH 6 Bright
			stop

		ReadyActualIdle3:
			DX00 A 0 A_GunFlash("ReadyActualIdle3Glow",GFF_NOEXTCHANGE)
			DX00 A 6 A_WeaponReady
			DX02 IJKLMNOP 6 A_WeaponReady
			goto ReadyActual

		ReadyActualIdle3Glow:
			DX04 H 6 Bright
			DX07 ABCDEFGH 6 Bright
			stop

		Deselect:
			DX01 O 0 A_GiveInventory("DeusEx_Reloading")
            DX01 O 0 A_JumpIfHealthLower(1,"DeathDeselect")
			DX01 O 3 A_GunFlash("DeselectGlow",GFF_NOEXTCHANGE)
			DX01 PQRSTU 3
            goto DeselectFinish

		DeselectGlow:
			DX08 ABCDEFG 3 Bright
			stop

        DeathDeselect:
            TNT1 A 3 A_GunFlash("DeathDeselectGlow",GFF_NOEXTCHANGE)
            TNT1 AAAAAA 3
            goto DeselectFinish

        DeathDeselectGlow:
            TNT1 A 0
            stop

        DeselectFinish:
			TNT1 A 0 A_TakeInventory("DeusEx_NanoSwordEquipped")
			goto InstantDeselect

		Ready: // our "Select" state
            DX00 B 0 A_PlaySound("DeusEx/NanoSword/Select")
			DX00 B 0 A_GiveInventory("DeusEx_NanoSwordEquipped")
			DX00 B 3 A_GunFlash("ReadyGlow",GFF_NOEXTCHANGE)
			DX00 CDEFGHIJKLM 3
			DX00 A 0 A_TakeInventory("DeusEx_Reloading")
			goto ReadyActual

		ReadyGlow:
			DX03 ABCDEFGHIJKL 3 Bright
			stop

		Fire:
			DX00 M 0 A_GiveInventory("DeusEx_Firing")
			DX00 M 0 A_Jump(256,"Fire1","Fire2")
		Fire1:
			DX00 N 0 A_JumpIfInventory("DeusEx_LowTechSkill",0,"Fire1Master")
			DX00 N 0 A_JumpIfInventory("DeusEx_LowTechSkill",3,"Fire1Advanced")
			DX00 N 0 A_JumpIfInventory("DeusEx_LowTechSkill",2,"Fire1Proficient")
			DX00 N 0 A_JumpIfInventory("DeusEx_LowTechSkill",1,"Fire1Trained")
        Fire1Untrained:
			DX00 N 0 A_GunFlash("Fire1UntrainedGlow",GFF_NOEXTCHANGE)
			DX00 NOPQRS 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX00 T 0 A_PlaySound("DeusEx/NanoSword/Fire",CHAN_WEAPON)
            DX00 T 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX00 U 0 A_GiveInventory("SamsaraDeusExNanoSwordAttackHandler1")
			DX00 "UVWXYZ[\" 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
            DX00 A 0 A_ReFire
			DX00 A 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto ReadyActual

		Fire1UntrainedGlow:
			DX09 ABCDEFGHIJKLMNO 2 Bright
			DX04 H 2 Bright
			stop

		Fire1Trained:
			DX00 N 0 A_GunFlash("Fire1TrainedGlow",GFF_NOEXTCHANGE)
			DX00 N 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX00 OPQ 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX00 R 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX00 S 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX00 T 0 A_PlaySound("DeusEx/NanoSword/Fire",CHAN_WEAPON)
            DX00 T 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX00 U 0 A_GiveInventory("SamsaraDeusExNanoSwordAttackHandler1")
			DX00 U 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX00 VWXYZ 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX00 "[" 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX00 "\" 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
            DX00 A 0 A_ReFire
			DX00 A 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto ReadyActual

		Fire1TrainedGlow:
			DX09 A 1 Bright
			DX09 BCD 2 Bright
			DX09 E 1 Bright
			DX09 FG 2 Bright
			DX09 H 1 Bright
			DX09 IJKLM 2 Bright
			DX09 N 1 Bright
			DX09 O 2 Bright
			DX04 H 2 Bright
			stop

		Fire1Proficient:
			DX00 N 0 A_GunFlash("Fire1ProficientGlow",GFF_NOEXTCHANGE)
			DX00 NO 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX00 PQ 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX00 R 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX00 S 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX00 T 0 A_PlaySound("DeusEx/NanoSword/Fire",CHAN_WEAPON)
            DX00 T 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX00 U 0 A_GiveInventory("SamsaraDeusExNanoSwordAttackHandler1")
			DX00 UV 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX00 W 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX00 XY 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX00 Z 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX00 "[" 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX00 "\" 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
            DX00 A 0 A_ReFire
			DX00 A 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto ReadyActual

		Fire1ProficientGlow:
			DX09 AB 1 Bright
			DX09 CD 2 Bright
			DX09 E 1 Bright
			DX09 FGHI 2 Bright
			DX09 J 1 Bright
			DX09 KL 2 Bright
			DX09 M 1 Bright
			DX09 N 2 Bright
			DX09 O 1 Bright
			DX04 H 2 Bright
			stop

		Fire1Advanced:
			DX00 N 0 A_GunFlash("Fire1AdvancedGlow",GFF_NOEXTCHANGE)
			DX00 NOP 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX00 Q 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX00 RS 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX00 T 0 A_PlaySound("DeusEx/NanoSword/Fire",CHAN_WEAPON)
            DX00 T 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX00 U 0 A_GiveInventory("SamsaraDeusExNanoSwordAttackHandler1")
			DX00 UVW 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX00 X 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX00 "YZ[" 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX00 "\" 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
            DX00 A 0 A_ReFire
			DX00 A 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto ReadyActual

		Fire1AdvancedGlow:
			DX09 ABC 1 Bright
			DX09 D 2 Bright
			DX09 EFGHIJ 1 Bright
			DX09 K 2 Bright
			DX09 LMN 1 Bright
			DX09 O 2 Bright
			DX04 H 2 Bright
			stop

		Fire1Master:
			DX00 N 0 A_GunFlash("Fire1MasterGlow",GFF_NOEXTCHANGE)
			DX00 NOPQRS 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX00 T 0 A_PlaySound("DeusEx/NanoSword/Fire",CHAN_WEAPON)
            DX00 T 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX00 U 0 A_GiveInventory("SamsaraDeusExNanoSwordAttackHandler1")
			DX00 "UVWXYZ[\" 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
            DX00 A 0 A_ReFire
			DX00 A 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto ReadyActual

		Fire1MasterGlow:
			DX09 ABCDEFGHIJKLMNO 1 Bright
			DX04 H 1 Bright
			stop

		Fire2:
			DX00 "]" 0 A_JumpIfInventory("DeusEx_LowTechSkill",0,"Fire2Master")
			DX00 "]" 0 A_JumpIfInventory("DeusEx_LowTechSkill",3,"Fire2Advanced")
			DX00 "]" 0 A_JumpIfInventory("DeusEx_LowTechSkill",2,"Fire2Proficient")
			DX00 "]" 0 A_JumpIfInventory("DeusEx_LowTechSkill",1,"Fire2Trained")
		Fire2Untrained:
			DX00 "]" 0 A_GunFlash("Fire2UntrainedGlow",GFF_NOEXTCHANGE)
			DX00 "]" 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX01 ABCDE 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
            DX01 F 0 A_PlaySound("DeusEx/NanoSword/Fire",CHAN_WEAPON)
			DX01 F 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX01 G 0 A_GiveInventory("SamsaraDeusExNanoSwordAttackHandler2")
			DX01 GHIJKLMN 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
            DX00 A 0 A_ReFire
			DX00 A 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto ReadyActual

		Fire2UntrainedGlow:
			DX10 ABCDEFGHIJKLMNO 2 Bright
			DX04 H 2 Bright
			stop

		Fire2Trained:
			DX00 "]" 0 A_GunFlash("Fire2TrainedGlow",GFF_NOEXTCHANGE)
			DX00 "]" 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX01 A 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX01 BC 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX01 D 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX01 E 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
            DX01 F 0 A_PlaySound("DeusEx/NanoSword/Fire",CHAN_WEAPON)
			DX01 F 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX01 G 0 A_GiveInventory("SamsaraDeusExNanoSwordAttackHandler2")
			DX01 G 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX01 H 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX01 IJ 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX01 K 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX01 LM 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX01 N 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
            DX00 A 0 A_ReFire
			DX00 A 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto ReadyActual

		Fire2TrainedGlow:
			DX10 A 2 Bright
			DX10 B 1 Bright
			DX10 CD 2 Bright
			DX10 E 1 Bright
			DX10 FGH 2 Bright
			DX10 I 1 Bright
			DX10 JK 2 Bright
			DX10 L 1 Bright
			DX10 MN 2 Bright
			DX10 O 1 Bright
			DX04 H 2 Bright
			stop

		Fire2Proficient:
			DX00 "]" 0 A_GunFlash("Fire2ProficientGlow",GFF_NOEXTCHANGE)
			DX00 "]" 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX01 AB 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX01 C 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX01 DE 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
            DX01 F 0 A_PlaySound("DeusEx/NanoSword/Fire",CHAN_WEAPON)
			DX01 F 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX01 G 0 A_GiveInventory("SamsaraDeusExNanoSwordAttackHandler2")
			DX01 G 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX01 HI 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX01 J 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX01 KL 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX01 M 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX01 N 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
            DX00 A 0 A_ReFire
			DX00 A 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto ReadyActual

		Fire2ProficientGlow:
			DX10 A 2 Bright
			DX10 BC 1 Bright
			DX10 D 2 Bright
			DX10 EF 1 Bright
			DX10 GH 2 Bright
			DX10 IJ 1 Bright
			DX10 K 2 Bright
			DX10 LM 1 Bright
			DX10 N 2 Bright
			DX10 O 1 Bright
			DX04 H 2 Bright
			stop

		Fire2Advanced:
			DX00 "]" 0 A_GunFlash("Fire2AdvancedGlow",GFF_NOEXTCHANGE)
			DX00 "]" 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX01 ABCD 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX01 E 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
            DX01 F 0 A_PlaySound("DeusEx/NanoSword/Fire",CHAN_WEAPON)
			DX01 F 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX01 G 0 A_GiveInventory("SamsaraDeusExNanoSwordAttackHandler2")
			DX01 G 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX01 HIJKLM 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX01 N 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
            DX00 A 0 A_ReFire
			DX00 A 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto ReadyActual

		Fire2AdvancedGlow:
			DX10 A 1 Bright
			DX10 BCDE 1 Bright
			DX10 F 2 Bright
			DX10 G 1 Bright
			DX10 H 2 Bright
			DX10 IJKLMN 1 Bright
			DX10 O 2 Bright
			DX04 H 2 Bright
			stop

		Fire2Master:
			DX00 "]" 0 A_GunFlash("Fire2MasterGlow",GFF_NOEXTCHANGE)
			DX01 ABCDE 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
            DX01 F 0 A_PlaySound("DeusEx/NanoSword/Fire",CHAN_WEAPON)
			DX01 F 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			DX01 G 0 A_GiveInventory("SamsaraDeusExNanoSwordAttackHandler2")
			DX01 GHIJKLMN 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
            DX00 A 0 A_ReFire
			DX00 A 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto ReadyActual

		Fire2MasterGlow:
			DX10 ABCDEFGHIJKLMNO 1 Bright
			DX04 H 1 Bright
			stop
	}
}

actor SamsaraDeusExNanoSwordAttackHandler1 : Trigger
{
    States
    {
      // Attack stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupCoop")
      PickupDM:
        TNT1 A 0 A_FireCustomMissile("DeusEx_NanoSwordMissile",8,false,0,-4)
        TNT1 A 0 A_FireCustomMissile("DeusEx_NanoSwordMissile",4,false,0,-2)
        TNT1 A 0 A_FireCustomMissile("DeusEx_NanoSwordMissile",0,false)
        TNT1 A 0 A_FireCustomMissile("DeusEx_NanoSwordMissile",-4,false,0,2)
        TNT1 A 0 A_FireCustomMissile("DeusEx_NanoSwordMissile",-8,false,0,4)
        stop

      PickupCoop:
        TNT1 A 0 A_FireCustomMissile("DeusEx_NanoSwordMissileCoop",8,false,0,-4)
        TNT1 A 0 A_FireCustomMissile("DeusEx_NanoSwordMissileCoop",4,false,0,-2)
        TNT1 A 0 A_FireCustomMissile("DeusEx_NanoSwordMissileCoop",0,false)
        TNT1 A 0 A_FireCustomMissile("DeusEx_NanoSwordMissileCoop",-4,false,0,2)
        TNT1 A 0 A_FireCustomMissile("DeusEx_NanoSwordMissileCoop",-8,false,0,4)
        stop
    }
}

actor SamsaraDeusExNanoSwordAttackHandler2 : Trigger
{
    States
    {
      // Attack stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupCoop")
      PickupDM:
        TNT1 A 0 A_FireCustomMissile("DeusEx_NanoSwordMissile",12,false,0,2)
        TNT1 A 0 A_FireCustomMissile("DeusEx_NanoSwordMissile",6,false,0,2)
        TNT1 A 0 A_FireCustomMissile("DeusEx_NanoSwordMissile",0,false,0,2)
        TNT1 A 0 A_FireCustomMissile("DeusEx_NanoSwordMissile",-6,false,0,2)
        TNT1 A 0 A_FireCustomMissile("DeusEx_NanoSwordMissile",-12,false,0,2)
        stop

      PickupCoop:
        TNT1 A 0 A_FireCustomMissile("DeusEx_NanoSwordMissileCoop",12,false,0,2)
        TNT1 A 0 A_FireCustomMissile("DeusEx_NanoSwordMissileCoop",6,false,0,2)
        TNT1 A 0 A_FireCustomMissile("DeusEx_NanoSwordMissileCoop",0,false,0,2)
        TNT1 A 0 A_FireCustomMissile("DeusEx_NanoSwordMissileCoop",-6,false,0,2)
        TNT1 A 0 A_FireCustomMissile("DeusEx_NanoSwordMissileCoop",-12,false,0,2)
        stop
    }
}

Actor DeusEx_NanoSwordMissile : DeusExBullet
{
	Damage(CallACS("DeusEx_WeaponSkillDamage", 2, 10))
	Speed 60
	Decal none
	DamageType "DeusExMelee"
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 1 
			Stop
		Crash:
			TNT1 A 0 A_PlaySound("DeusEx/NanoSword/HitSoft", CHAN_BODY)
			TNT1 A 1 A_Explode(CallACS("DeusEx_BackDamage",CallACS("DeusEx_WeaponSkillDamage", 2, 10)),8,!XF_HURTSOURCE,0,8)
			Stop
		Death:
			TNT1 A 1 A_PlaySound("DeusEx/NanoSword/HitHard", CHAN_BODY)
			Stop
		XDeath:
			TNT1 A 0 A_PlaySound("DeusEx/NanoSword/HitFlesh", CHAN_BODY)
			TNT1 A 0 A_Explode(CallACS("DeusEx_BackDamage",CallACS("DeusEx_WeaponSkillDamage", 2, 10)),8,!XF_HURTSOURCE,0,8)
			TNT1 A 0 ACS_NamedExecuteWithResult("DeusEx_BloodGenerator_Offsets")
			TNT1 A 1 A_GiveInventory("DeusEx_BloodGenerator",1,AAPTR_TRACER)
			stop
	}
}

actor DeusEx_NanoSwordMissileCoop : DeusEx_NanoSwordMissile
{
    Species "Player"
    +THRUSPECIES
}
