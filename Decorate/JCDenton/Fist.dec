Actor DeusEx_CombatKnife : DeusExWeapon
{
	Weapon.SelectionOrder 2000
	Weapon.SlotNumber 0
	Weapon.SlotPriority 1
	+WEAPON.MELEEWEAPON
	+WEAPON.WIMPY_WEAPON
//	+WEAPON.NOAUTOFIRE
	Tag "Combat Knife"
	Weapon.UpSound "DeusEx/Knife/Select"
	Inventory.PickupMessage "You found a Combat Knife"
	Obituary "%o was shanked with %o's Combat Knife."
	States
	{
		Spawn:
			DX02 A -1
			Stop
		Select:
			TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_Raise
			TNT1 A 1 A_Raise
			loop
		Ready:
			DX00 "]" 0 A_GiveInventory("DeusEx_KnifeEquipped", 1)
			DX00 "]" 2
			DX01 AB 2
			DX01 B 0 A_TakeInventory("DeusEx_Reloading", 1)
		ReadyLoop:
			DX00 Z 1 A_WeaponReady 	DX00 Z 0 A_Jump(5, "Idle1")
			Loop
		Idle1:
			DX01 CDEFGHIJ 5 A_WeaponReady
			Goto ReadyLoop
		Fire:
			DX00 A 0 A_Jump(256, "Atk1", "Atk2", "Atk3")
			Goto Atk1
		Atk1:
			DX00 D 0 A_PlaySound("DeusEx/Knife/Fire", CHAN_WEAPON)
			DX00 D 0 A_JumpIfInventory("DeusEx_LowTechSkill", 4, "Atk1_Master")
			DX00 D 0 A_JumpIfInventory("DeusEx_LowTechSkill", 3, "Atk1_Adv")
			DX00 D 0 A_JumpIfInventory("DeusEx_LowTechSkill", 2, "Atk1_Pro")
			DX00 D 0 A_JumpIfInventory("DeusEx_LowTechSkill", 1, "Atk1_Trained")
			DX00 DEFG 2
			DX00 H 2 A_GiveInventory("SamsaraDeusExKnifeAttackHandler")
			DX00 I 2
			Goto ReadyLoop
		Atk1_Trained:
			DX00 D 1
			DX00 EFG 2
			DX00 H 2 A_GiveInventory("SamsaraDeusExKnifeAttackHandler")
			DX00 I 1
			Goto ReadyLoop
		Atk1_Pro:
			DX00 D 1
			DX00 EFG 2
			DX00 H 1 A_GiveInventory("SamsaraDeusExKnifeAttackHandler")
			DX00 I 1
			Goto ReadyLoop
		Atk1_Adv:
			DX00 D 1
			DX00 EF 2
			DX00 G 1
			DX00 H 1 A_GiveInventory("SamsaraDeusExKnifeAttackHandler")
			DX00 I 1
			Goto ReadyLoop
		Atk1_Master:
			DX00 D 1
			DX00 EF 1
			DX00 G 1
			DX00 H 1 A_GiveInventory("SamsaraDeusExKnifeAttackHandler")
			DX00 I 1
			Goto ReadyLoop
		Atk2:
			DX00 J 0 A_JumpIfInventory("DeusEx_LowTechSkill", 1, "Atk2_Trained")
			DX00 J 2
			DX00 KLMNO 1
			DX00 P 1 A_GiveInventory("SamsaraDeusExKnifeAttackHandler")
			DX00 QRS 1
			DX00 T 2
			Goto ReadyLoop
		Atk2_Trained:	// can't really make this much faster
			DX00 J 1
			DX00 KLMNO 1
			DX00 P 1 A_GiveInventory("SamsaraDeusExKnifeAttackHandler")
			DX00 QRS 1
			DX00 T 1
			Goto ReadyLoop
		Atk3:
			DX00 U 0 A_PlaySound("DeusEx/Knife/Fire", CHAN_WEAPON)
			DX00 U 0 A_JumpIfInventory("DeusEx_LowTechSkill", 4, "Atk3_Master")
			DX00 U 0 A_JumpIfInventory("DeusEx_LowTechSkill", 3, "Atk3_Adv")
			DX00 U 0 A_JumpIfInventory("DeusEx_LowTechSkill", 2, "Atk3_Pro")
			DX00 U 0 A_JumpIfInventory("DeusEx_LowTechSkill", 1, "Atk3_Trained")
			DX00 UVWX 2
			DX00 Y 2 A_GiveInventory("SamsaraDeusExKnifeAttackHandler")
			DX00 Z 2
			Goto ReadyLoop
		Atk3_Trained:
			DX00 U 1
			DX00 VWX 2
			DX00 Y 2 A_GiveInventory("SamsaraDeusExKnifeAttackHandler")
			DX00 Z 1
			Goto ReadyLoop
		Atk3_Pro:
			DX00 U 1
			DX00 VWX 2
			DX00 Y 1 A_GiveInventory("SamsaraDeusExKnifeAttackHandler")
			DX00 Z 1
			Goto ReadyLoop
		Atk3_Adv:
			DX00 U 1
			DX00 VW 2
			DX00 X 1
			DX00 Y 1 A_GiveInventory("SamsaraDeusExKnifeAttackHandler")
			DX00 Z 1
			Goto ReadyLoop
		Atk3_Master:
			DX00 U 1
			DX00 VW 1
			DX00 X 1
			DX00 Y 1 A_GiveInventory("SamsaraDeusExKnifeAttackHandler")
			DX00 Z 1
			Goto ReadyLoop
		Deselect:
			DX00 "[" 0 A_TakeInventory("DeusEx_KnifeEquipped", 1)
			DX00 "[" 0 A_GiveInventory("DeusEx_Reloading", 1)
			DX00 "[\" 2
			Goto InstantDeselect
		InstantDeselect:
			TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
			TNT1 A 1 A_Lower
			Wait
		}
}

Actor DeusEx_KnifeMissile : DeusExBullet
{
	Damage(CallACS("DeusEx_WeaponSkillDamage", 2, 5))
	Speed 32
	Decal none
	DamageType "DeusExMelee"
	States
	{
		Spawn:
			TNT1 A 0 NODELAY
			TNT1 A 1 
			Stop
		Crash:
			TNT1 A 0 A_PlaySound("DeusEx/Knife/HitSoft", CHAN_BODY)
			TNT1 A 1 A_Explode(CallACS("DeusEx_BackDamage",CallACS("DeusEx_WeaponSkillDamage", 2, 5)),8,!XF_HURTSOURCE,0,8)
			Stop
		Death:
			TNT1 A 0 A_PlaySound("DeusEx/Knife/HitHard", CHAN_BODY)
			TNT1 A 1 A_SpawnItemEx("DeusEx_BulletPuff",0,0,0,0,0,1,0,0)
			Stop
		XDeath:
			TNT1 A 0 A_PlaySound("DeusEx/Knife/HitFlesh", CHAN_BODY)
			TNT1 A 1 A_Explode(CallACS("DeusEx_BackDamage",CallACS("DeusEx_WeaponSkillDamage", 2, 5)),8,!XF_HURTSOURCE,0,8)
			Stop
	}
}

Actor DeusEx_KnifeMissileCoop : DeusEx_KnifeMissile { Species "Player" +THRUSPECIES }

Actor SamsaraDeusExKnifeAttackHandler : Trigger
{
	States
	{
		Pickup:
			TNT1 A 0
			TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
			TNT1 A 0 A_FireCustomMissile("DeusEx_KnifeMissile", 0, 0, 0)
			Stop
		PickupAttackCoop:
			TNT1 A 0 A_FireCustomMissile("DeusEx_KnifeMissileCoop", 0, 0, 0)
			Stop
	}
}

Actor DeusEx_ProdMagazine : Ammo
{
	+INVENTORY.IGNORESKILL
	Inventory.Amount 1
	Inventory.MaxAmount 4
}

Actor DeusEx_ProdAmmo : Ammo
{
	+INVENTORY.IGNORESKILL
	Inventory.Amount 4
	Inventory.MaxAmount 40
	Ammo.BackPackMaxAmount 80
	Ammo.BackPackAmount 2
}

Actor DeusEx_RiotProd : DeusExWeapon
{
	Weapon.SlotNumber 0
	Weapon.SlotPriority 0
	+WEAPON.MELEEWEAPON
	+BRIGHT
	+WEAPON.CHEATNOTWEAPON
	Tag "Riot Prod"
	Weapon.AmmoType1 "DeusEx_ProdMagazine"
	Weapon.AmmoUse1 1
	Weapon.AmmoType2 "DeusEx_ProdAmmo"
	Weapon.AmmoUse2 0
	Weapon.UpSound "DeusEx/Prod/Select"
	Inventory.PickupMessage "You found a Riot Prod"
	Obituary "%o was tazed to sleep by %k."	
	States
	{
		Select:
			TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_Raise
			TNT1 A 1 A_Raise
			loop
		Ready:
			DX00 ABCDEFGHIJKLMN 2
			Goto ReadyLoop
		ReadyLoop:
			DX00 O 1 A_WeaponReady(WRF_ALLOWRELOAD) DX00 O 0 A_Jump(2, "IdleAnim")
			Loop
		IdleAnim:
			DX01 LMNOPQRSTU 4 A_WeaponReady(WRF_ALLOWRELOAD)
			DX01 "VWXYZ[\" 4 A_WeaponReady(WRF_ALLOWRELOAD)
			Goto ReadyLoop
		Fire:
			DX00 P 0 BRIGHT A_JumpIfInventory("DeusEx_ProdMagazine", 1, "Fire2")
			Goto FireReload
		Fire2:
			DX00 P 0 A_JumpIfInventory("CoopModeOn",1,2)
			DX00 P 5 A_FireCustomMissile("DeusEx_ProdMissile", 0, 1, 0, 0)
			Goto FireEnd
			DX00 P 5 A_FireCustomMissile("DeusEx_ProdMissileCoop", 0, 1, 0, 0)
			Goto FireEnd
		FireEnd:
			DX00 Q 0 A_PlaySound("DeusEx/Prod/Fire", CHAN_WEAPON)
			DX00 RSTU 5
			DX00 V 8 A_Refire
			Goto ReadyLoop
		Deselect:
			DX00 "WXYZ[\" 2
			Goto Super::InstantDeselect
		Reload:
			DX00 Z 0 A_JumpIfInventory("DeusEx_ProdMagazine", 0, "ReadyLoop")
			DX00 Z 0 A_JumpIfInventory("DeusEx_ProdAmmo", 1, "ReloadSuccess")
			Goto ReadyLoop
		ReloadSuccess:
			DX01 A 0 A_PlaySound("DeusEx/Prod/Reload1", CHAN_WEAPON)
			DX01 ABC 2
			DX01 DEFGHI 6
			DX01 DEFGHI 6
			goto ReloadAmmo
		ReloadAmmo:
			TNT1 A 0 A_JumpIfInventory("DeusEx_ProdMagazine",0,"ReloadFinish")
			TNT1 A 0 A_JumpIfInventory("DeusEx_ProdAmmo",1,1)
			goto ReloadFinish
			TNT1 A 0 A_GiveInventory("DeusEx_ProdMagazine",1)
			TNT1 A 0 A_TakeInventory("DeusEx_ProdAmmo",1,TIF_NOTAKEINFINITE)
			loop
		ReloadFinish:
			DX01 J 0 A_PlaySound("DeusEx/Prod/Reload2", CHAN_WEAPON)
			DX01 JKL 6
			Goto ReadyLoop
		FireReload:
			DX00 O 0 A_PlaySound("DeusEx/DryFire", CHAN_WEAPON)
			DX00 OOO 1
			DX00 O 1 A_ClearRefire
			Goto Reload
		}
}

Actor DeusEx_ProdMissile : DeusExBullet
{
	Speed 32
	Damage (CallACS("DeusEx_WeaponSkillDamage", 2, 15))
	+FORCEPAIN
	DamageType "DeusExStunned"
	Decal None
	States
	{
		Spawn:
			TNT1 A 0 NODELAY A_SetUserVar(user_damage, CallACS("DeusEx_WeaponSkillDamage", 2, 15))
			TNT1 A 1
			Stop
		Crash:
		XDeath:
			//TNT1 A 0 A_GiveInventory("PowerDeusEx_StunDamage", 1, AAPTR_TRACER)
			TNT1 A 1 A_Explode(CallACS("DeusEx_BackDamage",CallACS("DeusEx_WeaponSkillDamage", 2, 15)),8,!XF_HURTSOURCE,0,8)
			Stop
	}
}

Actor DeusEx_ProdMissileCoop : DeusEx_ProdMissile { Species "Player" +THRUSPECIES }

Actor PowerDeusEx_StunDamage : PowerProtection
{
	DamageFactor -4.0
	Powerup.Duration -3
}