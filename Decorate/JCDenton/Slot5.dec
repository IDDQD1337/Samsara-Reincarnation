Actor DeusEx_GEPGunLockTimer : Inventory { Inventory.MaxAmount 20 +Inventory.UNDROPPABLE -INVBAR }
Actor DeusEx_RocketLoaded : Boolean {}
Actor DeusEx_WPLoaded : Boolean {}
Actor DeusEx_RocketFired : Boolean {}

Actor DeusEx_GEPGun : DeusExWeapon
{
	Weapon.SelectionOrder 2100
	Weapon.SlotNumber 5
	Weapon.SlotPriority 1
	Weapon.AmmoType "RocketAmmo"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 4
	-WEAPON.AMMO_OPTIONAL
	+WEAPON.EXPLOSIVE
	Tag "Guided Explosive Projectile Gun"
	Weapon.UpSound "DeusEx/GEPGun/Select"
	Inventory.PickupMessage "You found a GEP Gun."
	Obituary "%o couldn't outrun %k's GEPs."
	States
	{
		Spawn:
			DX99 A -1
			Stop
		Select:
			TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_Raise
			TNT1 A 1 A_Raise
			Loop
		Ready:
			DX00 B 0 ACS_NamedExecuteWithResult("DeusExGEPTargetter")
			//DX00 C 0 A_JumpIfInventory("DeusEx_HeavyWeaponSkill", 2, 2)
			DX00 B 0 A_GiveInventory("DeusEx_GEPGunEquipped", 1) //ACS_NamedExecuteAlways("DeusEx_Decorate", 0, 3)
			DX00 BCDEFGHIJK 4
			DX00 A 0 A_TakeInventory("DeusEx_Reloading", 1)
			Goto ReadyLoop
		ReadyLoop:
			DX00 A 1 A_WeaponReady(WRF_ALLOWRELOAD) DX00 A 0 A_Jump(4,"Idle1","Idle2","Idle3")
			Loop
		Idle1:
			DX00 A 5
			DX01 MNOPQRST 5 A_WeaponReady
			Goto ReadyLoop
		Idle2:
			DX00 A 5
			DX01 "UVWXYZ[\" 5 A_WeaponReady
			Goto ReadyLoop
		Idle3:
			DX00 A 5
			DX01 "]" 5 A_WeaponReady
			DX02 ABCDEFG 5 A_WeaponReady
			Goto ReadyLoop
		Fire:
			DX00 L 0 A_GiveInventory("DeusEx_Firing", 1)
			DX00 L 0 A_JumpIfInventory("DeusEx_RocketFired", 1, "CheckRockets")
			DX00 L 0 A_JumpIfInventory("DeusEx_WPLoaded", 1, "FireWP")
			DX00 L 0 ACS_NamedExecuteWithResult("DeusExRecoil", 24, 6, 3)
			DX00 L 0 A_AlertMonsters(1200)
			DX00 L 0 A_JumpIfInventory("CoopModeOn",1,2)
			DX00 L 5 A_FireCustomMissile("DeusEx_GEPRocket", 0, 1, 12, 10)
			Goto FireContinue
			DX00 L 5 A_FireCustomMissile("DeusEx_GEPRocketCoop", 0, 1, 12, 10)
			Goto FireContinue
		FireWP:
			DX00 L 0 A_JumpIfInventory("DeusEx_RocketFired", 1, "CheckRockets")
			DX00 L 0 ACS_NamedExecuteWithResult("DeusExRecoil", 24, 6, 3)
			DX00 L 0 A_AlertMonsters(1200)
			DX00 L 0 A_JumpIfInventory("CoopModeOn",1,2)
			DX00 L 5 A_FireCustomMissile("DeusEx_WPRocket", 0, 1, 12, 10)
			Goto FireContinue
			DX00 L 5 A_FireCustomMissile("DeusEx_WPRocketCoop", 0, 1, 12, 10)
			Goto FireContinue
		FireContinue:
			DX00 M 0 A_PlaySound("DeusEx/GEPGun/Fire", CHAN_WEAPON)
			DX00 M 0 A_GiveInventory("DeusEx_RocketFired", 1)
			DX00 M 5
			DX00 NOP 5
			Goto CheckRockets
		Reload:
			DX00 "[" 0 A_JumpIfInventory("DeusEx_RocketFired", 1, "CheckRockets")
			Goto ReadyLoop
		CheckRockets:
			DX00 "[" 0 A_JumpIfInventory("RocketAmmo", 1, "FireEnd")
			Goto ReadyLoop
		FireEnd:
			DX00 "[" 0 A_PlaySound("DeusEx/GEPGun/Reload", CHAN_BODY)
			DX00 "[\]" 5 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			DX01 ABCDEFGH 5 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			DX01 I 0 A_TakeInventory("DeusEx_RocketFired", 999)
			DX01 IJKL 5 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			Goto ReadyLoop
		Deselect:
			DX00 O 0 A_TakeInventory("DeusEx_GEPGunLockTimer", 999)
			DX00 O 0 A_TakeInventory("DeusEx_RocketFired", 1)
			DX00 O 0 A_TakeInventory("Unreal_TargetLocked", 999)
			DX00 O 0 A_GiveInventory("DeusEx_Reloading", 1)
			DX00 QRSTUVWXYZ 4
			DX00 Z 0 A_TakeInventory("DeusEx_GEPGunEquipped", 999)
			Goto Super::InstantDeselect
		AltFire:
			DX00 "[" 0 A_JumpIfInventory("DeusEx_RocketFired", 1, "CheckRockets")
			DX00 "[" 0 A_JumpIfInventory("DeusEx_WPLoaded", 1, "LoadRocket")
			DX00 "[" 0 A_JumpIfInventory("RocketAmmo", 1, "AltfireSuccess")
			Goto ReadyLoop
		AltFireSuccess:
			DX00 "[" 0 A_PlaySound("DeusEx/GEPGun/Reload", CHAN_BODY)
			DX00 "[\]" 5 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			DX01 ABCDEFGH 5 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			DX01 I 0 A_TakeInventory("DeusEx_RocketFired", 999)
			DX01 IJKL 5 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			DX00 A 0 A_JumpIfInventory("DeusEx_WPLoaded", 1, "ChangeToRocket")
			Goto ChangeToWP
		ChangeToWP:
			DX00 A 0 A_TakeInventory("DeusEx_RocketLoaded", 999)
			DX00 A 0 A_GiveInventory("DeusEx_WPLoaded", 1)
			DX00 A 0 A_Print("GEP Gun now has WP Rockets loaded")
			Goto ReadyLoop
		LoadRocket:
			DX00 A 0 A_JumpIfInventory("RocketAmmo", 1, "AltfireSuccess")
			Goto ReadyLoop
		ChangeToRocket:
			DX00 A 0 A_TakeInventory("DeusEx_WPLoaded", 999)
			DX00 A 0 A_GiveInventory("DeusEx_RocketLoaded", 1)
			DX00 A 0 A_Print("GEP Gun now has Rockets loaded")
			Goto ReadyLoop
	}
}

Actor DeusEx_GEPRocket
{
	Projectile
	Radius 8
	Height 6
	Damage (0)
	Speed 20
	+FORCERADIUSDMG
	+SEEKERMISSILE
	DamageType "DeusExExploded"
	//ActiveSound "DeusEx/GEPGun/Approach"
	Decal "DeusExExplosionDecal"
	var int user_rocketpitch;
	States
	{
		Spawn:
			DX00 A 0 NoDelay A_PlaySound("DeusEx/GEPGun/Approach",CHAN_BODY,0.5)
			DX00 A 0 A_JumpIfInTargetInventory("Unreal_TargetLocked", 1, "SpawnLocked")
			Goto SpawnLoop
		SpawnLoop:
			DX00 A 0 A_SeekerMissile(90,90,SMF_PRECISE)
			DX00 A 0 A_SetUserVar("user_rocketpitch",CallACS("SamsaraProjectileMovementPitch")/182.0)
			DX00 A 0 A_SpawnItemEx("DeusEx_RocketFlameTrail",(-5*cos(user_rocketpitch))+(2*sin(user_rocketpitch)),0,(-5*sin(user_rocketpitch))+(2*cos(user_rocketpitch)),0,0,0,0,SXF_CLIENTSIDE)
			DX00 A 0 A_SpawnItemEx("DeusEx_RocketFlameTrail",(-10*cos(user_rocketpitch))+(2*sin(user_rocketpitch)),0,-10*sin(user_rocketpitch)+(2*cos(user_rocketpitch)),0,0,0,0,SXF_CLIENTSIDE)
			DX00 A 0 A_SpawnItemEx("DeusEx_RocketFlameTrail",(-15*cos(user_rocketpitch))+(2*sin(user_rocketpitch)),0,-15*sin(user_rocketpitch)+(2*cos(user_rocketpitch)),0,0,0,0,SXF_CLIENTSIDE)
			DX00 A 0 A_SpawnItemEx("DeusEx_RocketFlameTrail",(-20*cos(user_rocketpitch))+(2*sin(user_rocketpitch)),0,-20*sin(user_rocketpitch)+(2*cos(user_rocketpitch)),0,0,0,0,SXF_CLIENTSIDE)
			DX00 A 0 A_SpawnItemEx("DeusEx_RocketSmoke",(-5*cos(user_rocketpitch))-(4*sin(user_rocketpitch)),0,(-5*sin(user_rocketpitch))-(4*cos(user_rocketpitch)),(0.25*cos(user_rocketpitch))+(0.25*sin(user_rocketpitch)),0,(0.25*sin(user_rocketpitch))+(0.25*cos(user_rocketpitch)),0,SXF_CLIENTSIDE)
			DX00 A 1 A_SpawnItemEx("DeusEx_RocketSmoke",(-15*cos(user_rocketpitch))-(4*sin(user_rocketpitch)),0,(-15*sin(user_rocketpitch))-(4*cos(user_rocketpitch)),(0.25*cos(user_rocketpitch))+(0.25*sin(user_rocketpitch)),0,(0.25*sin(user_rocketpitch))+(0.25*cos(user_rocketpitch)),0,SXF_CLIENTSIDE)
			Loop
		SpawnLocked:
			DX00 A 0 A_TransferPointer(AAPTR_TARGET,AAPTR_DEFAULT,AAPTR_PLAYER_GETTARGET,AAPTR_TRACER)
			Goto SpawnLoop
		Death:
			TNT1 A 0 A_CheckCeiling(1)
			Goto Death+2
			TNT1 A 0 A_SpawnItemEx("DeusEx_ExplosionCraterCeiling")
			TNT1 A 0 A_CheckFloor(1)
			Goto Death+4
			TNT1 A 0 A_SpawnItemEx("DeusEx_ExplosionCraterFloor")
			TNT1 A 0 A_PlaySound("DeusEx/GEPGun/Explode", CHAN_BODY)
			TNT1 A 0 A_SpawnItemEX("DeusEx_ExplosionMid", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
			TNT1 A 0 A_SpawnItemEX("DeusEx_ShockWaveSpawner", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
			TNT1 AAA 0 A_SpawnItemEx("DeusEx_FireComet",0,0,0,frandom(2.0,4.0),0,frandom(4.0,8.0),random(0,360))
			TNT1 A 0 A_AlertMonsters(3000)
			TNT1 A 2 A_Explode(CallACS("DeusEx_WeaponSkillDamage", 5, 120), 38, 1, 1, 16)
			TNT1 A 2 A_Explode(CallACS("DeusEx_WeaponSkillDamage", 5, 120), 77, 1, 1, 16)
			TNT1 A 2 A_Explode(CallACS("DeusEx_WeaponSkillDamage", 5, 120), 115, 1, 1, 16)
			TNT1 A 2 A_Explode(CallACS("DeusEx_WeaponSkillDamage", 5, 120), 154, 1, 1, 16)
			TNT1 A 2 A_Explode(CallACS("DeusEx_WeaponSkillDamage", 5, 120), 192, 1, 1, 24)
			Stop
	}
}

Actor DeusEx_GEPRocketCoop : DeusEx_GEPRocket { Species "Player" +THRUSPECIES }

Actor DeusEx_WPRocket : DeusEx_GEPRocket
{
	DamageType "DeusExBurned"
	PoisonDamageType "DeusExFlamed"
	PoisonDamage 5, 30, 35
	var int user_angle;
	var int user_pitch;
	States
	{
		Death:
			TNT1 A 0 A_PlaySound("DeusEx/GEPGun/Explode", CHAN_BODY)
			TNT1 A 0 A_SpawnItemEx("DeusEx_WPExplosion",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
			TNT1 A 0 A_AlertMonsters(3000)
			TNT1 A 0 A_SetUserVar("user_angle",-10)
		AdjustAngle:
			TNT1 A 0 A_JumpIf(user_angle >= 360,"DoneTrace")
			TNT1 A 0 A_SetUserVar("user_angle",user_angle+10)
		AdjustPitch:
			TNT1 A 0 A_SetUserVar("user_pitch",-90)
		LoopPitch:
			TNT1 A 0 A_JumpIf(user_pitch >= 90,"AdjustAngle")
			TNT1 A 0 A_SetUserVar("user_pitch",user_pitch+10)
			TNT1 A 0 A_CustomMissile("DeusEx_WPProjectile", 0, 0, user_angle, CMF_AIMOFFSET|CMF_TRACKOWNER|CMF_ABSOLUTEPITCH,user_pitch)
			Loop
		DoneTrace:
			TNT1 AAAAA 0 A_SpawnItemEx("DeusEx_WPSmoke",0,0,0,frandom(0.5,2.0),0,2,random(0,360),SXF_CLIENTSIDE)
			TNT1 A 2
			TNT1 AAAAA 0 A_SpawnItemEx("DeusEx_WPSmoke",0,0,0,frandom(0.5,2.0),0,2,random(0,360),SXF_CLIENTSIDE)
			TNT1 A 2 A_Explode(CallACS("DeusEx_WeaponSkillDamage", 5, 60), 48)
			TNT1 AAAAA 0 A_SpawnItemEx("DeusEx_WPSmoke",0,0,0,frandom(0.5,2.0),0,2,random(0,360),SXF_CLIENTSIDE)
			TNT1 A 2 A_Explode(CallACS("DeusEx_WeaponSkillDamage", 5, 60), 132)
			TNT1 AAAAA 0 A_SpawnItemEx("DeusEx_WPSmoke",0,0,0,frandom(0.5,2.0),0,2,random(0,360),SXF_CLIENTSIDE)
			TNT1 A 2 A_Explode(CallACS("DeusEx_WeaponSkillDamage", 5, 60), 216)
			TNT1 AAAAA 0 A_SpawnItemEx("DeusEx_WPSmoke",0,0,0,frandom(0.5,2.0),0,2,random(0,360),SXF_CLIENTSIDE)
			TNT1 A 2 A_Explode(CallACS("DeusEx_WeaponSkillDamage", 5, 60), 300)
			TNT1 AAAAA 0 A_SpawnItemEx("DeusEx_WPSmoke",0,0,0,frandom(0.5,2.0),0,2,random(0,360),SXF_CLIENTSIDE)
			TNT1 A 2 A_Explode(CallACS("DeusEx_WeaponSkillDamage", 5, 60), 384)
			TNT1 A 1
			Stop
	}
}

Actor DeusEx_WPRocketBurner : Trigger
{
	States
	{
		Pickup:
			TNT1 A 0 ACS_NamedExecuteWithResult("DeusExIgnite",1)
			Stop
	}
}

Actor DeusEx_WPRocketCoop : DeusEx_WPRocket 
{ 
	Species "Player" 
	+THRUSPECIES 
	States
	{
		Death:
			TNT1 A 0 A_PlaySound("DeusEx/GEPGun/Explode", CHAN_BODY)
			TNT1 A 0 A_SpawnItemEx("DeusEx_WPExplosion",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
			TNT1 A 0 A_AlertMonsters(3000)
			TNT1 A 0 A_SetUserVar("user_angle",-10)
		AdjustAngle:
			TNT1 A 0 A_JumpIf(user_angle >= 360,"DoneTrace")
			TNT1 A 0 A_SetUserVar("user_angle",user_angle+10)
		AdjustPitch:
			TNT1 A 0 A_SetUserVar("user_pitch",-90)
		LoopPitch:
			TNT1 A 0 A_JumpIf(user_pitch >= 90,"AdjustAngle")
			TNT1 A 0 A_SetUserVar("user_pitch",user_pitch+10)
			TNT1 A 0 A_CustomMissile("DeusEx_WPProjectileCoop", 0, 0, user_angle, CMF_AIMOFFSET|CMF_TRACKOWNER|CMF_ABSOLUTEPITCH,user_pitch)
			Loop
	}
}

Actor DeusEx_WPProjectile : FastProjectile
{
	Projectile
	Speed 384
	Damage (0)
	Height 4
	Radius 2
	PoisonDamage 5, 30, 35
	DamageType "DeusExFlamed"
	PoisonDamageType "DeusExFlamed"
	Obituary "%k bathed %o in flames."
	+NOEXTREMEDEATH
	+NODAMAGETHRUST
	-BLOODSPLATTER
	+BLOODLESSIMPACT
	+NODAMAGETHRUST
	+HITTRACER
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 1
			Stop
		Crash:
		Death:
			TNT1 A 1
			Stop
		XDeath:
			TNT1 A 1 ACS_NamedExecuteWithResult("DeusExIgnite")
			Stop
	}
}

Actor DeusEx_WPProjectileCoop : DeusEx_WPProjectile { Species "Player" +THRUSPECIES }

Actor DeusEx_WPExplosion : DeusEx_ExplosionMid
{
	Scale 1.0
	States
	{
		Spawn:
			DX12 ABC 2 BRIGHT
			Stop
	}
}

Actor DeusEx_WPSmoke
{
	Height 4
	Radius 2
	Renderstyle Add
	Gravity 0.1
	-NOGRAVITY
	+FORCEXYBILLBOARD
	States
	{
		Spawn:
			DX13 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_SetScale(scalex+0.025)
			DX14 A 0 A_FadeOut(0.07)
			DX14 A 1 A_SetScale(scalex+0.025)
			DX14 B 0 A_FadeOut(0.07)
			DX14 B 1 A_SetScale(scalex+0.025)
			DX14 C 0 A_FadeOut(0.07)
			DX14 C 1 A_SetScale(scalex+0.025)
			DX14 D 0 A_FadeOut(0.07)
			DX14 D 1 A_SetScale(scalex+0.025)
			DX14 E 0 A_FadeOut(0.07)
			DX14 E 1 A_SetScale(scalex+0.025)
			DX14 F 0 A_FadeOut(0.07)
			DX14 F 1 A_SetScale(scalex+0.025)
			DX14 G 0 A_FadeOut(0.07)
			DX14 G 1 A_SetScale(scalex+0.025)
			DX14 H 0 A_FadeOut(0.07)
			DX14 H 1 A_SetScale(scalex+0.025)
			DX14 I 0 A_FadeOut(0.07)
			DX14 I 1 A_SetScale(scalex+0.025)
			DX14 J 0 A_FadeOut(0.07)
			DX14 J 1 A_SetScale(scalex+0.025)
			DX14 K 0 A_FadeOut(0.07)
			DX14 K 1 A_SetScale(scalex+0.025)
			DX14 L 0 A_FadeOut(0.07)
			DX14 L 1 A_SetScale(scalex+0.025)
			DX14 M 0 A_FadeOut(0.07)
			DX14 M 1 A_SetScale(scalex+0.025)
			DX14 N 0 A_FadeOut(0.07)
			DX14 N 1 A_SetScale(scalex+0.025)
			Stop
	}
}

Actor DeusEx_RocketFlameTrail
{
	Height 4
	Radius 2
	Renderstyle Add
	Scale 0.25
	+NOGRAVITY
	+NOINTERACTION
	+FORCEXYBILLBOARD
	States
	{
		Spawn:
			DX15 ABCDEFGH 1 Bright A_Fadeout(0.125)
			Stop
	}
}

Actor DeusEx_GEPTargeterPuff
{
	Renderstyle Add
	Mass 0
	Radius 1
	Height 1
	+BLOODLESSIMPACT
	+NOBLOCKMAP
	+NOGRAVITY
	-SKYEXPLODE
	+FORCEXYBILLBOARD
	+CLIENTSIDEONLY
	+ALWAYSPUFF
	+PUFFONACTORS
	+PUFFGETSOWNER
	+DONTSPLASH
	+FIXMAPTHINGPOS
	+FORCEXYBILLBOARD
	states
	{
		Spawn:
		XDeath:
		Melee:
			TNT1 A 0
			TNT1 A 0 A_GiveToTarget("DeusEx_GEPGunLockTimer", 3)
			TNT1 A 0 A_JumpIfInTargetInventory("DeusEx_GEPGunLockTimer", 0, "LockON")
			TNT1 A 1 A_GiveToTarget("DeusEx_TrackSound", 1)
			Stop
		Crash:
			TNT1 A 0
			TNT1 A 0 //A_TakeFromTarget("Unreal_TargetLocked", 999)
			TNT1 A 0 A_TakeFromTarget("DeusEx_GEPGunLockTimer", 5)
			TNT1 A 1 A_JumpIfInTargetInventory("DeusEx_GEPGunLockTimer", 1, 1)
			Goto LoseLock
			TNT1 A 1
			Stop
		LockOn:
			TNT1 A 0
			TNT1 A 0 //A_JumpIfInTargetInventory("Unreal_TargetLocked", 1, 2)
			TNT1 A 0 A_GiveToTarget("DeusEx_LockOnSound", 1)
			TNT1 A 0 A_GiveToTarget("Unreal_TargetLocked", 1)
			TNT1 A 0 A_TakeFromTarget("Unreal_TargetLost", 999)
			TNT1 A 0 A_SpawnItemEx("Unreal_SeekerActor",0,0,0,0,0,0,0, SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS) //A_GiveInventory("Unreal_MarkedForDeath", 24, AAPTR_TRACER)
			TNT1 A 1
			Stop
		LoseLock:
			TNT1 A 0
			//TNT1 A 0 A_JumpIfInTargetInventory("Unreal_TargetLost", 1, 2)
			TNT1 A 0 A_GiveToTarget("Unreal_TargetLost", 1)
			TNT1 A 0 A_TakeFromTarget("Unreal_TargetLocked", 999)
			TNT1 A 1
			Stop
	}
}

Actor DeusEx_LockOnSound : CustomInventory
{
	-COUNTITEM
	+INVENTORY.ALWAYSPICKUP
	+INVENTORY.AUTOACTIVATE
	Inventory.MaxAmount 1
	Inventory.PickupMessage ""
	Inventory.PickupSound ""
	States
	{
		Pickup:
			TNT1 A 0
			TNT1 A 0
		Use:
			TNT1 A 0
			//TNT1 A 0 ACS_NamedExecuteAlways("Unreal_EightballReticle", 0)
			TNT1 A 0 A_PlaySound("DeusEx/GEPGun/Lock", CHAN_BODY, 1.0)
			TNT1 A 1
			Stop
	}
}

Actor DeusEx_TrackSound : CustomInventory
{
	-COUNTITEM
	+INVENTORY.ALWAYSPICKUP
	+INVENTORY.AUTOACTIVATE
	Inventory.MaxAmount 1
	Inventory.PickupMessage ""
	Inventory.PickupSound ""
	States
	{
		Pickup:
			TNT1 A 0
			TNT1 A 0
		Use:
			TNT1 A 0
			TNT1 A 0 A_PlaySound("DeusEx/GEPGun/Track", CHAN_BODY, 1.0)
			TNT1 A 1
			Stop
	}
}

Actor DeusEx_GEPTargeter : Trigger
{
	States
	{
		Pickup:
			TNT1 A 0 A_FireBullets(1,1,1,0, "DeusEx_GEPTargeterPuff", FBF_NORANDOM|FBF_NOFLASH|FBF_EXPLICITANGLE)
			Stop
	}
}