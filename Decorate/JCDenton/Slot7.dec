Actor DeusEx_PlasmaRifleMagazine : Ammo
{
	+INVENTORY.IGNORESKILL
	Inventory.Amount 1
	Inventory.MaxAmount 16 //was 12
}

Actor MJ12PlasmaDirectHitToken : Counter { Inventory.MaxAmount 20 }

Actor DeusEx_PlasmaCellsLoaded : Boolean {}
Actor DeusEx_NanoPhageLoaded : Boolean {}

Actor DeusEx_MJ12PlasmaRifle : DeusExWeapon
{
	Weapon.SelectionOrder 1600
	Weapon.SlotNumber 7
	Weapon.SlotPriority 1
	Weapon.AmmoType1 "DeusEx_PlasmaRifleMagazine"
	Weapon.AmmoUse1 1
	Weapon.AmmoType2 "Cell"
	Weapon.AmmoUse2 0 
	Weapon.AmmoGive1 0
	Weapon.AmmoGive2 25
	+WEAPON.BFG
	+WEAPON.EXPLOSIVE
	Tag "Majestic 12 Plasma Rifle"
	Weapon.UpSound "DeusEx/PlasmaRifle/Select"
	Inventory.PickupMessage "You found a MJ12 Plasma Rifle."
	Obituary "%o was disintigrated with %k's Plasma Rifle."
	States
	{
		Spawn:
			DX99 A -1
			Stop
		Select:
			TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_Raise
			TNT1 A 1 A_Raise
			loop
		Ready:
			DX00 B 0 A_GunFlash("ReadyGlow",GFF_NOEXTCHANGE)
			//DX00 B 0 A_JumpIfInventory("DeusEx_HeavyWeaponSkill", 2, 2)
			DX00 B 0 A_GiveInventory("DeusEx_PREquipped", 1) //ACS_NamedExecuteAlways("DeusEx_Decorate", 0, 3)
			DX00 BCDEFGHI 3
			DX00 A 0 A_TakeInventory("DeusEx_Reloading", 1)
			Goto ReadyLoop
		ReadyGlow:
			DX07 ABCDEFGH 3 Bright
			Stop
		ReadyLoop:
			DX00 A 0 A_GunFlash("ReadyLoopGlow",GFF_NOEXTCHANGE)
			DX00 AAAAAAAAAAAAAAAA 1 A_WeaponReady(WRF_ALLOWRELOAD) DX00 A 0 A_TakeInventory("DeusEx_PlasmaRifleSpread", 16) DX00 A 0 A_Jump(64, "Idle1", "Idle2", "Idle3")
			Loop
		ReadyLoopGlow:
			DX03 ABCDEFGH 2 Bright
			Loop
		Idle1:
			DX00 A 6 A_GunFlash("Idle1Glow",GFF_NOEXTCHANGE)
			DX01 MNOPQRST 6 A_WeaponReady(WRF_ALLOWRELOAD)
			Goto ReadyLoop
		Idle1Glow:
			"####" "#" 6 Bright
			DX04 ABCDEFGH 6 Bright
			Stop
		Idle2:
			DX00 A 6 A_GunFlash("Idle2Glow",GFF_NOEXTCHANGE)
			DX01 "UVWXYZ[\" 6 A_WeaponReady(WRF_ALLOWRELOAD)
			Goto ReadyLoop
		Idle2Glow:
			"####" "#" 6 Bright
			DX05 ABCDEFGH 6 Bright
			Stop
		Idle3:
			DX00 A 6 A_GunFlash("Idle3Glow",GFF_NOEXTCHANGE)
			DX01 "]" 6 A_WeaponReady(WRF_ALLOWRELOAD)
			DX02 ABCDEFG 6 A_WeaponReady(WRF_ALLOWRELOAD)
			Goto ReadyLoop
		Idle3Glow:
			"####" "#" 6 Bright
			DX06 ABCDEFGH 6 Bright
			Stop
		Fire:
			DX00 J 0 A_JumpIfInventory("DeusEx_NanoPhageLoaded", 1, "FireNanoPhage")
			DX00 J 0 A_JumpIfInventory("DeusEx_PlasmaRifleMagazine", 1, "Firing")
			Goto FireReload
		Firing:
			DX00 J 0 A_GunFlash("FireGlow",GFF_NOEXTCHANGE)
			DX00 J 3 BRIGHT A_PlaySound("DeusEx/PlasmaRifle/Fire", CHAN_WEAPON)
			DX00 K 3 BRIGHT
			DX00 L 0 BRIGHT ACS_NamedExecuteWithResult("DeusExRecoil", 4)
			DX00 L 0 BRIGHT A_GiveInventory("DeusEx_PlasmaRifleSpread", 5)
			DX00 L 0 BRIGHT A_JumpIfInventory("CoopModeOn",1,"FiringCoop")
			DX00 L 0 BRIGHT A_FireCustomMissile("DeusEx_GreenPlasmaBolt", CallACS("DeusEx_Decorate", 5, 4, 4)*frandom(-1.0,1.0), 0, 7, 5, 0, CallACS("DeusEx_Decorate", 5, 4, 4)*FRandom(-1.0,1.0))
			DX00 L 0 BRIGHT A_FireCustomMissile("DeusEx_GreenPlasmaBolt", CallACS("DeusEx_Decorate", 5, 4, 4)*frandom(-1.0,1.0), 0, -7, 5, 0, CallACS("DeusEx_Decorate", 5, 4, 4)*FRandom(-1.0,1.0))
			DX00 L 3 BRIGHT A_FireCustomMissile("DeusEx_GreenPlasmaBolt", CallACS("DeusEx_Decorate", 5, 4, 4)*frandom(-1.0,1.0), 1, 0, 12, 0, CallACS("DeusEx_Decorate", 5, 4, 4)*FRandom(-1.0,1.0))
			Goto FireEnd
		FiringCoop:
			DX00 L 0 BRIGHT A_FireCustomMissile("DeusEx_GreenPlasmaBoltCoop", CallACS("DeusEx_Decorate", 5, 4, 4)*frandom(-1.0,1.0), 0, 7, 5, 0, CallACS("DeusEx_Decorate", 5, 4, 4)*FRandom(-1.0,1.0))
			DX00 L 0 BRIGHT A_FireCustomMissile("DeusEx_GreenPlasmaBoltCoop", CallACS("DeusEx_Decorate", 5, 4, 4)*frandom(-1.0,1.0), 0, -7, 5, 0, CallACS("DeusEx_Decorate", 5, 4, 4)*FRandom(-1.0,1.0))
			DX00 L 3 BRIGHT A_FireCustomMissile("DeusEx_GreenPlasmaBoltCoop", CallACS("DeusEx_Decorate", 5, 4, 4)*frandom(-1.0,1.0), 1, 0, 12, 0, CallACS("DeusEx_Decorate", 5, 4, 4)*FRandom(-1.0,1.0))
			Goto FireEnd
		FireEnd:
			DX00 MN 3 BRIGHT
			DX00 OP 3
			DX00 Q 3 A_Refire("Firing")
			Goto ReadyLoop	
		FireGlow:
			DX09 ABCDEFGH 3 Bright
			DX03 A 3 Bright
			Stop
		FireNanoPhage:
			TNT1 A 0 A_JumpIfInventory("DeusEx_PlasmaRifleMagazine", 0, 1)
			Goto FireReload
			DX00 J 0 A_GunFlash("FireNanoGlow",GFF_NOEXTCHANGE)
			DX00 J 4 BRIGHT A_PlaySound("DeusEx/PlasmaRifle/Fire", CHAN_WEAPON)
			DX00 K 4 BRIGHT
			DX00 L 0 BRIGHT ACS_NamedExecuteWithResult("DeusExRecoil", 4)
			DX00 L 0 BRIGHT A_PlaySound("DeusEx/PlasmaRifle/FireNano", CHAN_WEAPON)
			DX00 L 0 BRIGHT A_JumpIfInventory("CoopModeOn", 1, 2)
			DX00 L 4 BRIGHT A_FireCustomMissile("DeusEx_GreenNanoPhageBolt", 0, 0, 0, 12, 0, 4)
			Goto FireNanoPhageEnd
			DX00 L 4 BRIGHT A_FireCustomMissile("DeusEx_GreenNanoPhageBoltCoop", 0, 0, 0, 12, 0, 4)
			Goto FireNanoPhageEnd
		FireNanoPhageEnd:
			DX00 M 0 A_TakeInventory("DeusEx_PlasmaRifleMagazine", 32, TIF_NOTAKEINFINITE)
			DX00 MNO 4 BRIGHT
			DX00 P 4
			DX00 Q 4 A_Refire("Fire")
			Goto ReadyLoop
		FireNanoGlow:
			DX09 ABCDEFGH 4 Bright
			DX03 A 4 Bright
			Stop
		FireReload:
			DX00 A 3 A_PlaySound("DeusEx/DryFire", CHAN_WEAPON)
			DX00 A 1 A_ClearRefire
			Goto Reload
		Deselect:
			DX00 R 0 A_GunFlash("DeselectGlow",GFF_NOEXTCHANGE)
			DX00 R 0 A_TakeInventory("DeusEx_PlasmaRifleSpread", 120)
			DX00 RSTUVWXY 3
			TNT1 A 0 A_TakeInventory("MJ12PlasmaDirectHitToken", 999)
			TNT1 A 3 A_TakeInventory("DeusEx_PREquipped", 999) //ACS_NamedExecuteAlways("DeusEx_Decorate", 0, 4)
			Goto Super::InstantDeselect
		DeselectGlow:
			DX08 ABCDEFGH 3 Bright
			Stop
		Reload:
			DX00 Z 0 A_JumpIfInventory("DeusEx_PlasmaRifleMagazine", 0, "ReadyLoop")
			DX00 Z 0 A_JumpIfInventory("Cell", 5, "ReloadSuccess")
			Goto ReadyLoop
		ReloadSuccess:
			DX00 Z 0 A_GunFlash("ReloadGlow",GFF_NOEXTCHANGE)
			DX00 Z 0 A_TakeInventory("MJ12PlasmaDirectHitToken", 999)
			DX00 Z 0 A_PlaySound("DeusEx/PlasmaRifle/Reload1", CHAN_WEAPON)
			DX00 Z 0 A_GiveInventory("DeusEx_Reloading", 1)
			DX00 "Z[\]" 3
			DX01 A 0 A_JumpIfInventory("DeusEx_HeavyWeaponSkill", 4, "ReloadAmmo")
			DX01 A 0 A_JumpIfInventory("DeusEx_HeavyWeaponSkill", 3, 30)
			DX01 A 0 A_JumpIfInventory("DeusEx_HeavyWeaponSkill", 2, 20)
			DX01 A 0 A_JumpIfInventory("DeusEx_HeavyWeaponSkill", 1, 10)
			DX01 ABCDEFGHIABCDEFGHIABCDEFGHIABCDEFGHI 3
			Goto ReloadAmmo
		ReloadAmmo:
			DX01 J 0 A_JumpIfInventory("DeusEx_PlasmaRifleMagazine",0,"ReloadFinish")
			DX01 J 0 A_JumpIfInventory("Cell",5,1)
			goto ReloadFinish
			DX01 J 0 A_GiveInventory("DeusEx_PlasmaRifleMagazine",1)
			DX01 J 0 A_TakeInventory("Cell",5,TIF_NOTAKEINFINITE)
			Loop
		ReloadFinish:
			DX01 J 0 A_PlaySound("DeusEx/PlasmaRifle/Reload2", CHAN_WEAPON)
			DX01 J 0 A_TakeInventory("DeusEx_Reloading", 1)
			DX01 JKL 3
			Goto ReadyLoop
		AltFire:
			DX00 Z 0 A_GunFlash("ReloadGlow",GFF_NOEXTCHANGE)
			DX00 Z 0 A_PlaySound("DeusEx/PlasmaRifle/Reload1", CHAN_WEAPON)
			DX00 Z 0 A_GiveInventory("DeusEx_Reloading", 1)
			DX00 "Z[\]" 3
			DX01 A 0 A_JumpIfInventory("DeusEx_HeavyWeaponSkill", 4, 40)
			DX01 A 0 A_JumpIfInventory("DeusEx_HeavyWeaponSkill", 3, 30)
			DX01 A 0 A_JumpIfInventory("DeusEx_HeavyWeaponSkill", 2, 20)
			DX01 A 0 A_JumpIfInventory("DeusEx_HeavyWeaponSkill", 1, 10)
			DX01 ABCDEFGHIABCDEFGHIABCDEFGHIABCDEFGHI 3
			DX01 J 0 A_JumpIfInventory("DeusEx_NanoPhageLoaded", 1, "ChangeToPlasma")
			Goto ChangeToNanoPhage
		ChangeToNanoPhage:
			DX00 A 0 A_TakeInventory("DeusEx_PlasmaCellsLoaded", 999)
			DX00 A 0 A_GiveInventory("DeusEx_NanoPhageLoaded", 1)
			DX00 A 0 A_Print("MJ12 Plasma Rifle now has NanoPhage Cells loaded")
			DX00 A 0 A_JumpIfInventory("DeusEx_PlasmaRifleMagazine",0,"AltfireFinish")
			Goto ReloadAmmo
		AltFireFinish:
			DX01 J 0 A_PlaySound("DeusEx/PlasmaRifle/Reload2", CHAN_WEAPON)
			DX01 J 0 A_TakeInventory("DeusEx_Reloading", 1)
			DX01 JKL 3
			Goto ReadyLoop
		ChangeToPlasma:
			DX00 A 0 A_TakeInventory("DeusEx_NanoPhageLoaded", 999)
			DX00 A 0 A_GiveInventory("DeusEx_PlasmaCellsLoaded", 1)
			DX00 A 0 A_Print("MJ12 Plasma Rifle now has Plasma Cells loaded")
			DX00 A 0 A_JumpIfInventory("DeusEx_PlasmaRifleMagazine",1,"AltfireFinish")
			Goto ReloadAmmo
		ReloadGlow:
			DX10 ABCD 3 Bright
			DX10 E 0 A_JumpIfInventory("DeusEx_HeavyWeaponSkill", 4, 40)
			DX10 E 0 A_JumpIfInventory("DeusEx_HeavyWeaponSkill", 3, 30)
			DX10 E 0 A_JumpIfInventory("DeusEx_HeavyWeaponSkill", 2, 20)
			DX10 E 0 A_JumpIfInventory("DeusEx_HeavyWeaponSkill", 1, 10)
			DX10 EFGHIJKLMEFGHIJKLMEFGHIJKLMEFGHIJKLM 3 Bright
			DX10 NOP 3 Bright
			Stop
	}
}

Actor DeusEx_GreenPlasmaBolt
{
	Projectile
	Radius 6
	Height 5
	Speed 24
	Damage (CallACS("DeusEx_WeaponSkillDamage", 5, 40))
	RenderStyle Add
	Alpha 0.9
	+NOEXTREMEDEATH
	+FORCERADIUSDMG
	+HITTRACER
	DamageType "DeusExBurned"
	States
	{
		Spawn:
			DX00 AAAAAAAA 0 A_SpawnItemEx("DeusEx_PlasmaParticle",0,frandom(-2.0,2.0),0,frandom(-1.0,-0.5),frandom(-1.0,1.0),frandom(-0.5,0.5))
			DX00 A 1 BRIGHT
			Loop
		XDeath:
		Crash:
			TNT1 A 0 A_GiveToTarget("MJ12PlasmaDirectHitToken", 1)
			Goto Death+1
		Death:
			TNT1 A 0 A_PlaySound("DeusEx/PlasmaRifle/Hit", CHAN_BODY)
			//TNT1 A 0 A_SpawnItemEX("DeusEx_ExplosionMid", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
			TNT1 A 0 A_AlertMonsters(2100)
			TNT1 A 0 A_SpawnItemEx("DeusEx_PlasmaSmoke", 0, 0, 0, fRandom(-1.0,1.0), fRandom(-1.0,1.0), 2, 0, SXF_CLIENTSIDE)
			TNT1 A 2 A_Explode(CallACS("DeusEx_WeaponSkillDamage", 5, 16), 60)
			TNT1 B 2
			TNT1 C 0 A_SpawnItemEx("DeusEx_PlasmaSmoke", 0, 0, 0, fRandom(-1.0,1.0), fRandom(-1.0,1.0), 2, 0, SXF_CLIENTSIDE)
			TNT1 C 2 A_Explode(CallACS("DeusEx_WeaponSkillDamage", 5, 16), 120)
			TNT1 D 2
			TNT1 E 0 A_SpawnItemEx("DeusEx_PlasmaSmoke", 0, 0, 0, fRandom(-1.0,1.0), fRandom(-1.0,1.0), 2, 0, SXF_CLIENTSIDE)
			TNT1 E 2 A_Explode(CallACS("DeusEx_WeaponSkillDamage", 5, 16), 180, 0)
			TNT1 F 2
			TNT1 F 0 A_SpawnItemEx("DeusEx_PlasmaSmoke", 0, 0, 0, fRandom(-1.0,1.0), fRandom(-1.0,1.0), 2, 0, SXF_CLIENTSIDE)
			TNT1 F 4 A_Explode(CallACS("DeusEx_WeaponSkillDamage", 5, 16), 240, 0)
			TNT1 F 0 A_SpawnItemEx("DeusEx_PlasmaSmoke", 0, 0, 0, fRandom(-1.0,1.0), fRandom(-1.0,1.0), 2, 0, SXF_CLIENTSIDE)
			TNT1 F 4 A_Explode(CallACS("DeusEx_WeaponSkillDamage", 5, 16), 300, 0)
			Stop
	}
}

Actor DeusEx_GreenNanoPhageBolt
{
	Projectile
	Radius 12
	Height 15
	Speed 32
	Gravity 0.5
	Damage (0)
	RenderStyle Add
	Alpha 0.9
	-NOGRAVITY
	+HITTRACER
	+FORCERADIUSDMG
	+NOEXTREMEDEATH
	DamageType "DeusExBurned"
	States
	{
		Spawn:
			DX00 AAAAA 0 A_SpawnItemEx("DeusEx_PlasmaSmoke", Random(-4,4), 0, Random(-5,5), Random(-5,5), 0, Random(1,4), 0, SXF_CLIENTSIDE)
			DX00 A 1 BRIGHT
			Loop
		Death:
			TNT1 A 0 A_NoGravity
		Pulse:	
			TNT1 A 0 A_PlaySound("DeusEx/PlasmaRifle/Hit", CHAN_BODY)
			TNT1 A 0 A_AlertMonsters(4200)
			TNT1 A 0 A_SpawnItemEX("DeusEx_GreenShockWaveHugeSpawner", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
			TNT1 AAAAAAAA 0 A_SpawnItemEx("DeusEx_PlasmaSmoke2", 0, 0, 0, fRandom(-1.0,1.0), fRandom(-1.0,1.0), fRandom(1.0,3.0), 0, SXF_CLIENTSIDE)
			TNT1 A 2 A_Explode(CallACS("DeusEx_WeaponSkillDamage", 5, 160), 120, 0)
			TNT1 A 2 A_Explode(CallACS("DeusEx_WeaponSkillDamage", 5, 160), 240, 0)
			TNT1 A 2 A_Explode(CallACS("DeusEx_WeaponSkillDamage", 5, 160), 360, 0)
			TNT1 A 2 A_Explode(CallACS("DeusEx_WeaponSkillDamage", 5, 160), 480, 0)
			TNT1 A 2 A_Explode(CallACS("DeusEx_WeaponSkillDamage", 5, 160), 600, 0)
			Stop
	}
}

Actor DeusEx_GreenPlasmaBoltCoop : DeusEx_GreenPlasmaBolt { +THRUSPECIES Species "Player" }
Actor DeusEx_GreenNanoPhageBoltCoop : DeusEx_GreenNanoPhageBolt { +THRUSPECIES Species "Player" +MTHRUSPECIES }

Actor DeusEx_PlasmaRifleSpread : Ammo { +INVENTORY.UNDROPPABLE Inventory.MaxAmount 90 Inventory.InterHubAmount 0 Ammo.BackPackAmount 0 Ammo.BackPackMaxAmount 90 }