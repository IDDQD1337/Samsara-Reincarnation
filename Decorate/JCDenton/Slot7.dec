Actor DeusEx_PlasmaRifleMagazine : Ammo
{
	+INVENTORY.IGNORESKILL
	Inventory.Amount 1
	Inventory.MaxAmount 16 //was 12
}


Actor DeusEx_MJ12PlasmaRifle : DeusExWeapon
{
	Weapon.SelectionOrder 1600
	Weapon.SlotNumber 7
	Weapon.SlotPriority 1
	Weapon.AmmoType1 "DeusEx_PlasmaRifleMagazine"
	Weapon.AmmoUse1 1
	Weapon.AmmoType2 "Cell"
	Weapon.AmmoUse2 0 
	Weapon.AmmoGive1 0
	Weapon.AmmoGive2 25
	Tag "Majestic 12 Plasma Rifle"
	Weapon.UpSound "DeusEx/PlasmaRifle/Select"
	Inventory.PickupMessage "You found a MJ12 Plasma Rifle."
	Obituary "%o was disintigrated with %k's Plasma Rifle."		
	States
	{
		Spawn:
			DX03 A -1
			Stop
		Select:
			TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_Raise
			TNT1 A 1 A_Raise
			loop		
	    Ready:	
			//DX00 B 0 A_JumpIfInventory("DeusEx_HeavyWeaponSkill", 2, 2)
			DX00 B 0 A_GiveInventory("DeusEx_PREquipped", 1) //ACS_NamedExecuteAlways("DeusEx_Decorate", 0, 3)		
			DX00 BCDEFGH 3 
			DX00 H 0 A_TakeInventory("DeusEx_Reloading", 1)
			Goto ReadyLoop
		ReadyLoop:
			DX01 L 1 A_WeaponReady(WRF_ALLOWRELOAD) DX01 L 0 A_TakeInventory("DeusEx_PlasmaRifleSpread", 1) DX01 L 0 A_Jump(4, "IdleAnim")
			Loop
		IdleAnim:
			DX01 LMNOPQRST 6 A_WeaponReady(WRF_ALLOWRELOAD)
			DX01 "UVWXYZ[\]" 6 A_WeaponReady(WRF_ALLOWRELOAD)
			DX02 AB 6 A_WeaponReady(WRF_ALLOWRELOAD)
			Goto ReadyLoop
		Fire:
			DX01 L 0 A_JumpIfInventory("DeusEx_PlasmaRifleMagazine", 1, "Fire2")
			Goto FireReload
		Fire2:
			DX00 I 3 BRIGHT A_PlaySound("DeusEx/PlasmaRifle/Fire", CHAN_WEAPON)
			DX00 J 3 BRIGHT
			DX00 J 0 BRIGHT A_GiveInventory("DeusEx_PlasmaRifleSpread", 5)
			DX00 K 0 BRIGHT A_FireCustomMissile("DeusEx_GreenPlasmaBolt", CallACS("DeusEx_Decorate", 5, 4, 4)*frandom(-1.0,1.0), 0, 7, 5, 0, CallACS("DeusEx_Decorate", 5, 4, 4)*FRandom(-1.0,1.0))
			DX00 K 0 BRIGHT A_FireCustomMissile("DeusEx_GreenPlasmaBolt", CallACS("DeusEx_Decorate", 5, 4, 4)*frandom(-1.0,1.0), 0, -7, 5, 0, CallACS("DeusEx_Decorate", 5, 4, 4)*FRandom(-1.0,1.0))
			DX00 K 3 BRIGHT A_FireCustomMissile("DeusEx_GreenPlasmaBolt", CallACS("DeusEx_Decorate", 5, 4, 4)*frandom(-1.0,1.0), 1, 0, 12, 0, CallACS("DeusEx_Decorate", 5, 4, 4)*FRandom(-1.0,1.0))
			DX00 LMN 3 BRIGHT
			DX00 O 3
			DX00 P 1 A_Refire("Fire")
			Goto ReadyLoop
		FireReload:
			DX01 L 0 A_PlaySound("DeusEx/DryFire", CHAN_WEAPON)
			DX01 LLL 1
			DX01 L 1 A_ClearRefire
			Goto Reload			
		Deselect:
			DX00 Q 0 A_TakeInventory("DeusEx_PlasmaRifleSpread", 120)
			DX00 QRSTUVW 3
			DX00 X 3 A_TakeInventory("DeusEx_PREquipped", 999) //ACS_NamedExecuteAlways("DeusEx_Decorate", 0, 4)
			Goto Super::InstantDeselect
		Reload:
			DX00 Z 0 A_JumpIfInventory("DeusEx_PlasmaRifleMagazine", 0, "ReadyLoop")
			DX00 Z 0 A_JumpIfInventory("Cell", 5, "ReloadSuccess")
			Goto ReadyLoop		
		ReloadSuccess:	
			DX00 Z 0 A_PlaySound("DeusEx/PlasmaRifle/Reload1", CHAN_WEAPON)
			DX00 Z 0 A_GiveInventory("DeusEx_Reloading", 1)
			DX00 "Z[\]" 2
			DX01 ABCDEFABCDEF 4
			Goto ReloadAmmo
		ReloadAmmo:
			TNT1 A 0 A_JumpIfInventory("DeusEx_PlasmaRifleMagazine",0,"ReloadFinish")
			TNT1 A 0 A_JumpIfInventory("Cell",5,1)
			goto ReloadFinish
			TNT1 A 0 A_GiveInventory("DeusEx_PlasmaRifleMagazine",1)
			TNT1 A 0 A_TakeInventory("Cell",5,TIF_NOTAKEINFINITE)
			loop			
		ReloadFinish:
			DX01 G 0 A_PlaySound("DeusEx/PlasmaRifle/Reload2", CHAN_WEAPON)
			DX01 GHI 4
			DX01 J 0 A_TakeInventory("DeusEx_Reloading", 1)
			DX01 JKL 2
			Goto ReadyLoop
		}
}		

Actor DeusEx_GreenPlasmaBolt
{
	PROJECTILE
	Radius 6
	Height 5
	Speed 25
	Damage (CallACS("DeusEx_WeaponSkillDamage", 5, 40))
	RenderStyle Add
	Alpha 0.9
	+NOEXTREMEDEATH
	+FORCERADIUSDMG
	+HITTRACER
	DamageType "DeusExBurned"
	Scale 0.8
	States
	{
		Spawn:
			DX00 A 1 BRIGHT
			Loop
		Death:
			TNT1 A 0 A_PlaySound("DeusEx/PlasmaRifle/Hit", CHAN_BODY)
			TNT1 A 0 A_SpawnItemEx("DeusEx_PlasmaSmoke", 0, 0, 0, 0, 0, 1, 0, SXF_CLIENTSIDE)
			//TNT1 A 0 A_SpawnItemEX("DeusEx_ExplosionMid", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
			TNT1 A 0 A_AlertMonsters(2100)
			TNT1 A 2 A_Explode(CallACS("DeusEx_WeaponSkillDamage", 5, 16), 60)
			TNT1 A 2 A_Explode(CallACS("DeusEx_WeaponSkillDamage", 5, 16), 120)
			TNT1 A 2 A_Explode(CallACS("DeusEx_WeaponSkillDamage", 5, 16), 180, 0)
			TNT1 A 2 A_Explode(CallACS("DeusEx_WeaponSkillDamage", 5, 16), 240, 0)
			TNT1 A 2 A_Explode(CallACS("DeusEx_WeaponSkillDamage", 5, 16), 300, 0)
			Stop		
	}
}		

Actor DeusEx_GreenPlasmaBoltCoop : DeusEx_GreenPlasmaBolt { +THRUSPECIES Species "Player" }

Actor DeusEx_PlasmaRifleSpread : Ammo { +INVENTORY.UNDROPPABLE Inventory.MaxAmount 90 Inventory.InterHubAmount 0 Ammo.BackPackAmount 0 Ammo.BackPackMaxAmount 90 }