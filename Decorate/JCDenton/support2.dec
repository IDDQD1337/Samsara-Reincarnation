actor DeusEx_LAMMayPlant : Boolean {}
actor SamsaraDeusExLAMPlantAmmoTaken : Boolean {} // for spread rune

Actor DeusEx_LAM : DeusExWeapon
{
	Weapon.SelectionOrder 1900
	Weapon.SlotNumber 9
	Weapon.SlotPriority 0
	Weapon.AmmoType "RocketAmmo"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 5
	Tag "Lightweight Attack Munitions"
	+WEAPON.EXPLOSIVE
	+WEAPON.NOAUTOFIRE
	Inventory.PickupMessage "You found some Lightweight Attack Munitions (LAMs)."
	Obituary "%o was blown up with %k's LAM."
	States
	{
		Spawn:
			DX99 A -1
			stop

		ReadyActual:
			TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReadyActualAmmo")
			TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,"ReadyActualAmmo")
            goto ReadyActualNoAmmo

		ReadyActualNoAmmo:
			TNT1 A 1 A_WeaponReady
            goto ReadyActualNoAmmoCheck

		ReadyActualAmmo:
			DX00 A 1 A_WeaponReady
            goto ReadyActualJump

        ReadyActualJump:
			TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReadyActualJumpStart")
			TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,"ReadyActualJumpStart")
            goto ReadyActual

        ReadyActualJumpStart:
            TNT1 A 0 A_JumpIfInventory("DeusEx_LAMMayPlant",1,"ReadyActualSwitchingToPlant")
            TNT1 A 0 A_Jump(4,"ReadyActualIdle1","ReadyActualIdle2","ReadyActualIdle3")
			goto ReadyActual

		ReadyActualIdle1:
            DX01 OPQRSTUV 5 A_WeaponReady
			goto ReadyActual

		ReadyActualIdle2:
			DX01 "WXYZ[\]" 5 A_WeaponReady
			DX02 A 5 A_WeaponReady
			goto ReadyActual

		ReadyActualIdle3:
			DX02 BCDEFGHI 5 A_WeaponReady
			goto ReadyActual

		ReadyActualSwitchingToPlant:
			DX00 "\]" 5 A_WeaponReady
			DX01 AB 5 A_WeaponReady
		ReadyActualPlant:
			DX01 C 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
			DX01 C 0 A_JumpIfInventory("RocketAmmo",1,1)
            goto ReadyActualNoAmmo
			DX01 C 1 A_WeaponReady
            DX01 C 0 A_JumpIfInventory("DeusEx_LAMMayPlant",1,"ReadyActualPlant")
		ReadyActualSwitchingToThrow:
			DX01 D 0
			DX01 EFGH 5 A_WeaponReady
			goto ReadyActual

        ReadyActualNoAmmoCheck:
            TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReadyAmmo")
            TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,"ReadyAmmo")
            goto ReadyActual

		Deselect:
			DX00 W 0 A_GiveInventory("DeusEx_Reloading")
			DX00 W 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"DeselectAmmo")
			DX00 W 0 A_JumpIfInventory("RocketAmmo",1,"DeselectAmmo")
			goto DeselectNoAmmo

		DeselectNoAmmo:
			TNT1 A 0 A_JumpIfHealthLower(1,"DeathDeselect")
			goto DeselectFinish

        DeselectAmmo:
            DX00 W 2 A_JumpIfHealthLower(1,"DeathDeselect")
			DX00 "XYZ[" 2
			goto DeselectFinish

		DeathDeselect:
			TNT1 AAAAA 2
			goto DeselectFinish

        DeselectFinish:
			TNT1 A 0 A_TakeInventory("DeusEx_LAMEquipped")
			goto DeselectInstant

		Ready: // our "Select" state
			DX00 B 0 A_GiveInventory("DeusEx_LAMEquipped")
			DX00 B 0 ACS_NamedExecuteWithResult("DeusEx_LAMTrigger")
			DX00 B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReadyAmmo")
			DX00 B 0 A_JumpIfInventory("RocketAmmo",1,"ReadyAmmo")
			goto ReadyNoAmmo

        ReadyNoAmmo:
			TNT1 A 0 A_TakeInventory("DeusEx_Reloading")
			goto ReadyActual

        ReadyAmmo:
            DX00 B 2 A_PlaySound("DeusEx/LAM/Select")
			DX00 CDEFGHIJKLM 2
			DX00 A 0 A_TakeInventory("DeusEx_Reloading")
			goto ReadyActual

		Fire:
            DX00 N 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart")
            DX00 N 0 A_JumpIfInventory("RocketAmmo",1,"FireStart")
            goto FireDry

		FireDry:
			TNT1 A 0 A_ClearReFire
            TNT1 A 4 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
            goto ReadyActualNoAmmoCheck

		FireStart:
			DX00 N 0 A_GiveInventory("DeusEx_Firing")
			DX00 N 0 A_JumpIfInventory("DeusEx_LAMMayPlant",1,"FirePlant")
			DX00 NOPQRS 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        FireLastShotCalc: // finish interpolation hack
			DX00 U 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireContinue")
			DX00 U 0 A_JumpIfInventory("RocketAmmo",2,"FireContinue")
            goto FireContinueLast

        FireContinue:
			DX00 U 0 A_GiveInventory("SamsaraDeusExLAMThrowAttackHandler")
			DX00 UV 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
            DX00 A 0 A_ReFire
			goto ReadyActual

        FireContinueLast:
			DX00 U 0 A_GiveInventory("SamsaraDeusExLAMThrowAttackHandler")
			DX00 UV 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
            TNT1 A 0 A_ReFire
			goto ReadyActual

		FirePlant:
			DX01 IJ 4 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        FirePlantLastShotCalc: // finish interpolation hack
			DX01 K 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FirePlantContinue")
			DX01 K 0 A_JumpIfInventory("RocketAmmo",2,"FirePlantContinue")
            goto FirePlantContinueLast

        FirePlantContinue:
			DX01 K 0 A_GiveInventory("SamsaraDeusExLAMPlantAttackHandler")
			DX01 KLMN 4 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
            DX01 C 0 A_ReFire
			goto ReadyActualPlant

        FirePlantContinueLast:
			DX01 K 0 A_GiveInventory("SamsaraDeusExLAMPlantAttackHandler")
			DX01 KLMN 4 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
            TNT1 A 0 A_ReFire
			goto ReadyActualPlant
	}
}

actor SamsaraDeusExLAMThrowAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,1)
        stop
        TNT1 A 0 A_TakeInventory("RocketAmmo",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("DeusEx_LAMProjectile",random(-3,3),false,16,5,0,5)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("DeusEx_LAMProjectileCoop",random(-3,3),false,16,5,0,5)
        stop
    }
}

actor SamsaraDeusExLAMPlantAttackHandler : Trigger
{
    States
    {
      // Initializing stuff
      Pickup:
        TNT1 A 0 A_TakeInventory("SamsaraDeusExLAMPlantAmmoTaken")
        goto PickupAmmoCheck

      // Ammo checking stuff
      PickupAmmoCheck:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttackInfinite")
        TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,"PickupAttack")
        stop

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_FireCustomMissile("DeusEx_LAMPlantMissile",0,false,4,16)
        stop

      PickupAttackInfinite:
        TNT1 A 0 A_FireCustomMissile("DeusEx_LAMPlantMissileInfinite",0,false,4,16)
        stop
    }
}

actor SamsaraDeusExLAMWallChecker : Trigger
{
    States
    {
      Pickup:
        TNT1 A 0 A_FireCustomMissile("DeusEx_LAMWallMissile",0,false)
        stop
    }
}

actor DeusEx_LAMTimer : Counter { Inventory.MaxAmount 5 }

Actor DeusEx_LAMProjectile
{
	Projectile
	-NOGRAVITY
	+BOUNCEONWALLS
	+BOUNCEONCEILINGS
	+BOUNCEONFLOORS
	+FORCERADIUSDMG
	+USEBOUNCESTATE
	BounceSound "DeusEx/LAM/Bounce"
	BounceType Doom
	BounceCount 5
	Radius 5
	Height 5
	Gravity 0.66
	Speed 12
	Damage (1)
	DamageType "DeusExLAM"
	Var Int User_Angle;
	States
	{
		Spawn:
			TNT1 A 0 NoDelay A_Jump(128,2)
			TNT1 A 0 A_SetUserVar(User_Angle,Random(1,6))
			Goto SpawnLoop
			TNT1 A 0 A_SetUserVar(User_Angle,Random(-6,-1))
			Goto SpawnThrust
		SpawnThrust:
			DX00 A 0 ThrustThingZ(0,10,0,1)
			Goto SpawnLoop
		SpawnLoop:
			DX00 A 0 A_JumpIfInventory("DeusEx_LAMTimer", 0, "Detonate")
			DX00 A 0 A_SetAngle(Angle+User_Angle)
			DX00 AAABB 1 A_SetPitch(Pitch-15)
			DX00 B 1 A_GiveInventory("DeusEx_LAMTimer", 1)
			Loop
		Bounce:
			DX00 A 0 A_ChangeFlag("USEBOUNCESTATE",0)
			DX00 A 0 A_JumpIfInventory("DeusEx_LAMTimer", 0, "Detonate")
			DX00 A 0 A_SetAngle(Angle+User_Angle)
			DX00 AAABB 1 A_SetPitch(Pitch-15)
			DX00 B 1 A_GiveInventory("DeusEx_LAMTimer", 1)
			Goto OnFloor+1
		OnFloor:
			DX00 A 0 A_GiveInventory("DeusEx_LAMTimer",1)
			DX00 ABABAB 3 A_JumpIfInventory("DeusEx_LAMTimer",0,"Detonate")
			Loop
		Death:
			RW00 A 0 A_Stop
			RW00 A 0 A_JumpIfInventory("DeusEx_LAMTimer",0,"Detonate")
			Goto OnFloor
		Detonate:
			DX00 A 3 A_AlertMonsters(72)
			DX00 B 3 A_PlaySound("DeusEx/LAM/Beep", CHAN_BODY)
			DX00 A 3 A_AlertMonsters(72)
			DX00 B 3 A_PlaySound("DeusEx/LAM/Beep", CHAN_BODY)
			DX00 A 3 A_AlertMonsters(72)
			DX00 B 3 A_PlaySound("DeusEx/LAM/Beep", CHAN_BODY)
			DX00 A 3 A_AlertMonsters(72)
			DX00 B 3 A_PlaySound("DeusEx/LAM/Beep", CHAN_BODY)
			DX00 A 3 A_AlertMonsters(72)
			DX00 B 3 A_PlaySound("DeusEx/LAM/Beep", CHAN_BODY)
			DX00 A 3 A_AlertMonsters(72)
			Goto XPlode
		Crash:
		XDeath:
		XPlode:
			TNT1 A 0 A_NoGravity
			TNT1 A 0 A_Stop
			TNT1 A 0 A_CheckCeiling(1)
			Goto XPlode+4
			TNT1 A 0 A_SpawnItemEx("DeusEx_ExplosionCraterCeiling")
			TNT1 A 0 A_CheckFloor(1)
			Goto XPlode+6
			TNT1 A 0 A_SpawnItemEx("DeusEx_ExplosionCraterFloor")
			TNT1 A 0 A_PlaySound("DeusEx/LAM/Explode", CHAN_BODY)
			TNT1 A 0 A_AlertMonsters(2400)
			TNT1 AAAAAAAA 0 A_SpawnItemEx("DeusEx_FireCometLAM",0,0,0,frandom(2.0,6.0),0,frandom(4.0,8.0),random(0,360))
			TNT1 A 0 A_SpawnItemEX("DeusEx_ExplosionMid", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
			TNT1 A 0 A_SpawnItemEX("DeusEx_ShockWaveSpawner", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
			TNT1 A 2 A_Explode(CallACS("DeusEx_WeaponSkillDamage", 2, 50), 77)
			TNT1 A 0 A_SpawnItemEx("DeusEx_SmokeTrail",0,0,0,frandom(-0.25,0.25),frandom(-0.25,0.25),1.0)
			TNT1 A 2 A_Explode(CallACS("DeusEx_WeaponSkillDamage", 2, 50), 154)
			TNT1 A 0 A_SpawnItemEx("DeusEx_SmokeTrail",0,0,0,frandom(-0.25,0.25),frandom(-0.25,0.25),1.0)
			TNT1 A 2 A_Explode(CallACS("DeusEx_WeaponSkillDamage", 2, 50), 230)
			TNT1 A 0 A_SpawnItemEx("DeusEx_SmokeTrail",0,0,0,frandom(-0.25,0.25),frandom(-0.25,0.25),1.0)
			TNT1 A 2 A_Explode(CallACS("DeusEx_WeaponSkillDamage", 2, 50), 307)
			TNT1 A 0 A_SpawnItemEx("DeusEx_SmokeTrail",0,0,0,frandom(-0.25,0.25),frandom(-0.25,0.25),1.0)
			TNT1 A 2 A_Explode(CallACS("DeusEx_WeaponSkillDamage", 2, 50), 384)
			TNT1 AAAAAAAAA 2 A_SpawnItemEx("DeusEx_SmokeTrail",0,0,0,frandom(-0.25,0.25),frandom(-0.25,0.25),1.0)
			Stop
	}
}

Actor DeusEx_LAMProjectileCoop : DeusEx_LAMProjectile { Species "Player" +THRUSPECIES }

Actor DeusEx_LAMWallMissile : DeusExBullet
{
	Damage (0)
    +DONTSPLASH
    +NOTRIGGER
    +SKYEXPLODE
	+THRUACTORS
	Speed 32
	Decal ""
	States
	{
		Spawn:
			TNT1 A 0 NODELAY
			TNT1 A 1 A_TakeFromTarget("DeusEx_LAMMayPlant")
			Stop
		XDeath:
		Crash:
			TNT1 A 0 
			TNT1 A 1 A_TakeFromTarget("DeusEx_LAMMayPlant")
			Stop
		Death:
			TNT1 A 0 //A_PlaySound("DeusEx/Knife/HitHard", CHAN_BODY)
			TNT1 A 1 A_GiveToTarget("DeusEx_LAMMayPlant")
			Stop
	}
}

Actor DeusEx_LAMStatic
{
	+LOOKALLAROUND
	-COUNTKILL
	-SHOOTABLE
	+VULNERABLE
	+NOGRAVITY
	+MOVEWITHSECTOR
	+THRUACTORS
	+NOBLOOD
	+FORCERADIUSDMG
	Speed 0
	Mass 10000
	Health 10
	DamageType "DeusExLAM"
	Height 5
	Radius 5
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_TransferPointer(AAPTR_TRACER,AAPTR_DEFAULT,AAPTR_TARGET,AAPTR_MASTER)
			TNT1 A 0 A_CopyFriendliness(AAPTR_MASTER)
			TNT1 A 0 A_CheckFloor("SetFloor")
			TNT1 A 0 A_CheckCeiling("SetCeiling")
			DX00 A 0 A_SetAngle(angle)
		Set:
			DX00 A 0 A_SetPitch(0)
			Goto Prime
		SetFloor:
			DX01 A 0 A_SetPitch(270)
			Goto Prime
		SetCeiling:
			DX02 A 0 A_SetPitch(90)
			Goto Prime
		Prime:
			"####" A 0 A_SpawnItemEx("DeusEx_LAMStaticGlow",0,0,0,0,0,0,0,SXF_TRANSFERPITCH)
			"####" "#" 0 A_PlaySound("DeusEx/LAM/Beep", CHAN_BODY)
			"####" AAAA 3 A_LookEx(0, 0, 128, 128, 0, "Detonate")
			"####" A 0 A_SpawnItemEx("DeusEx_LAMStaticGlow",0,0,0,0,0,0,0,SXF_TRANSFERPITCH)
			"####" "#" 0 A_PlaySound("DeusEx/LAM/Beep", CHAN_BODY)
			"####" AAAA 3 A_LookEx(0, 0, 128, 128, 0, "Detonate")
			"####" A 0 A_SpawnItemEx("DeusEx_LAMStaticGlow",0,0,0,0,0,0,0,SXF_TRANSFERPITCH)
			"####" "#" 0 A_PlaySound("DeusEx/LAM/Beep", CHAN_BODY)
			"####" AAAA 3 A_LookEx(0, 0, 128, 128, 0, "Detonate")
			"####" A 0 A_SpawnItemEx("DeusEx_LAMStaticGlow",0,0,0,0,0,0,0,SXF_TRANSFERPITCH)
			"####" "#" 0 A_PlaySound("DeusEx/LAM/Beep", CHAN_BODY)
			"####" AAA 3 A_LookEx(0, 0, 128, 128, 0, "Detonate")
			"####" A 0 A_SpawnItemEx("DeusEx_LAMStaticGlow",0,0,0,0,0,0,0,SXF_TRANSFERPITCH)
			"####" "#" 0 A_PlaySound("DeusEx/LAM/Beep", CHAN_BODY)
			"####" AAA 3 A_LookEx(0, 0, 128, 128, 0, "Detonate")
			"####" A 0 A_SpawnItemEx("DeusEx_LAMStaticGlow",0,0,0,0,0,0,0,SXF_TRANSFERPITCH)
			"####" "#" 0 A_PlaySound("DeusEx/LAM/Beep", CHAN_BODY)
			"####" AAA 3 A_LookEx(0, 0, 128, 128, 0, "Detonate")
			"####" A 0 A_SpawnItemEx("DeusEx_LAMStaticGlow",0,0,0,0,0,0,0,SXF_TRANSFERPITCH)
			"####" "#" 0 A_PlaySound("DeusEx/LAM/Beep", CHAN_BODY)
			"####" AA 3 A_LookEx(0, 0, 128, 128, 0, "Detonate")
			"####" A 0 A_SpawnItemEx("DeusEx_LAMStaticGlow",0,0,0,0,0,0,0,SXF_TRANSFERPITCH)
			"####" "#" 0 A_PlaySound("DeusEx/LAM/Beep", CHAN_BODY)
			"####" AA 3 A_LookEx(0, 0, 128, 128, 0, "Detonate")
			"####" A 0 A_SpawnItemEx("DeusEx_LAMStaticGlow",0,0,0,0,0,0,0,SXF_TRANSFERPITCH)
			"####" "#" 0 A_PlaySound("DeusEx/LAM/Beep", CHAN_BODY)
			"####" AA 3 A_LookEx(0, 0, 128, 128, 0, "Detonate")
			"####" A 0 A_SpawnItemEx("DeusEx_LAMStaticGlow",0,0,0,0,0,0,0,SXF_TRANSFERPITCH)
			"####" "#" 0 A_PlaySound("DeusEx/LAM/Beep", CHAN_BODY)
			"####" AA 2 A_LookEx(0, 0, 128, 128, 0, "Detonate")
			"####" A 0 A_SpawnItemEx("DeusEx_LAMStaticGlow",0,0,0,0,0,0,0,SXF_TRANSFERPITCH)
			"####" "#" 0 A_PlaySound("DeusEx/LAM/Beep", CHAN_BODY)
			"####" AA 2 A_LookEx(0, 0, 128, 128, 0, "Detonate")
			"####" A 0 A_SpawnItemEx("DeusEx_LAMStaticGlow",0,0,0,0,0,0,0,SXF_TRANSFERPITCH)
			"####" "#" 0 A_PlaySound("DeusEx/LAM/Beep", CHAN_BODY)
			"####" AA 2 A_LookEx(0, 0, 128, 128, 0, "Detonate")
			Goto SpawnLoop
		SpawnLoop:
			"####" A 0 A_SpawnItemEx("DeusEx_LAMStaticGlow",0,0,0,0,0,0,0,SXF_TRANSFERPITCH)
			"####" AAAA 1 A_LookEx(0, 0, 128, 128, 0, "Detonate")
			Loop
		Detonate:
			"####" "##" 2 A_SpawnItemEx("DeusEx_LAMStaticGlow",0,0,0,0,0,0,0,SXF_TRANSFERPITCH)
			"####" "#" 0 A_PlaySound("DeusEx/LAM/Beep", CHAN_BODY)
			"####" "##" 2 A_SpawnItemEx("DeusEx_LAMStaticGlow",0,0,0,0,0,0,0,SXF_TRANSFERPITCH)
			"####" "#" 0 A_PlaySound("DeusEx/LAM/Beep", CHAN_BODY)
			"####" "##" 2 A_SpawnItemEx("DeusEx_LAMStaticGlow",0,0,0,0,0,0,0,SXF_TRANSFERPITCH)
			"####" "#" 0 A_PlaySound("DeusEx/LAM/Beep", CHAN_BODY)
			"####" "##" 2 A_SpawnItemEx("DeusEx_LAMStaticGlow",0,0,0,0,0,0,0,SXF_TRANSFERPITCH)
			"####" "#" 0 A_PlaySound("DeusEx/LAM/Beep", CHAN_BODY)
			"####" "##" 2 A_SpawnItemEx("DeusEx_LAMStaticGlow",0,0,0,0,0,0,0,SXF_TRANSFERPITCH)
			"####" "#" 0 A_PlaySound("DeusEx/LAM/Beep", CHAN_BODY)
			"####" "##" 2 A_SpawnItemEx("DeusEx_LAMStaticGlow",0,0,0,0,0,0,0,SXF_TRANSFERPITCH)
			"####" "#" 0 A_PlaySound("DeusEx/LAM/Beep", CHAN_BODY)
		Death:	
			TNT1 A 0 A_CheckCeiling(1)
			Goto Death+2
			TNT1 A 0 A_SpawnItemEx("DeusEx_ExplosionCraterCeiling")
			TNT1 A 0 A_CheckFloor(1)
			Goto Death+4
			TNT1 A 0 A_SpawnItemEx("DeusEx_ExplosionCraterFloor")
			TNT1 A 15 A_CustomMissile("DeusEx_LAMExplosion",0)
			Stop
	}
}

Actor DeusEx_LAMStaticGlow
{
	Height 2
	Radius 1
	+NOINTERACTION
	+NOGRAVITY
	States
	{
		Spawn:
			DX00 A 0 NoDelay ACS_NamedExecuteWithResult("DeusEX_LAMMineGlowPlacement")
			TNT1 A 1
			Wait
		SpawnNormal:
			DX00 A 2 Bright
			Stop
		SpawnFloor:
			DX01 A 2 Bright
			Stop
		SpawnFloor:
			DX02 A 2 Bright
			Stop
	}
}

Actor DeusEx_LAMExplosion
{
	Projectile
	Damage (0)
	Speed 0
	Height 2
	Radius 1
	Decal "DeusExExplosionDecal"
	+THRUACTORS
	+BLOODLESSIMPACT
	States
	{
		Spawn:
			TNT1 A 1 NoDelay A_Die
		Death:
			TNT1 A 0 A_TransferPointer(AAPTR_Target,AAPTR_Default,AAPTR_Master,AAPTR_Target)
			TNT1 A 0 A_PlaySound("DeusEx/LAM/Explode", CHAN_BODY)
			TNT1 A 0 A_AlertMonsters(2400)
			TNT1 AAAAAAAA 0 A_SpawnItemEx("DeusEx_FireCometLAM",0,0,0,frandom(2.0,6.0),0,frandom(4.0,8.0),random(0,360))
			TNT1 A 0 A_SpawnItemEX("DeusEx_ExplosionMid", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
			TNT1 A 0 A_SpawnItemEX("DeusEx_ShockWaveSpawner", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
			TNT1 A 2 A_Explode(CallACS("DeusEx_WeaponSkillDamage", 2, 50), 77)
			TNT1 A 0 A_SpawnItemEx("DeusEx_SmokeTrail",0,0,0,frandom(-0.25,0.25),frandom(-0.25,0.25),1.0)
			TNT1 A 2 A_Explode(CallACS("DeusEx_WeaponSkillDamage", 2, 50), 154)
			TNT1 A 0 A_SpawnItemEx("DeusEx_SmokeTrail",0,0,0,frandom(-0.25,0.25),frandom(-0.25,0.25),1.0)
			TNT1 A 2 A_Explode(CallACS("DeusEx_WeaponSkillDamage", 2, 50), 230)
			TNT1 A 0 A_SpawnItemEx("DeusEx_SmokeTrail",0,0,0,frandom(-0.25,0.25),frandom(-0.25,0.25),1.0)
			TNT1 A 2 A_Explode(CallACS("DeusEx_WeaponSkillDamage", 2, 50), 307)
			TNT1 A 0 A_SpawnItemEx("DeusEx_SmokeTrail",0,0,0,frandom(-0.25,0.25),frandom(-0.25,0.25),1.0)
			TNT1 A 2 A_Explode(CallACS("DeusEx_WeaponSkillDamage", 2, 50), 384)
			TNT1 AAAAAAAAA 2 A_SpawnItemEx("DeusEx_SmokeTrail",0,0,0,frandom(-0.25,0.25),frandom(-0.25,0.25),1.0)
			Stop
	}
}

Actor DeusEx_LAMPlantMissile : DeusExBullet
{
	Damage (0)
    +DONTSPLASH
    +NOTRIGGER
    +SKYEXPLODE
	+THRUACTORS
	Speed 32
	Decal ""
	States
	{
		Spawn:
			TNT1 A 0 NODELAY
			TNT1 A 1 //A_TakeFromTarget("DeusEx_LAMMayPlant")
			Stop
		XDeath:
		Crash:
			TNT1 A 0
			TNT1 A 1 //A_TakeFromTarget("DeusEx_LAMMayPlant")
			Stop
		Death:
            TNT1 A 0 A_JumpIfInTargetInventory("SamsaraDeusExLAMPlantAmmoTaken",1,"DeathPlant")
			TNT1 A 0 A_TakeFromTarget("RocketAmmo",1)
            TNT1 A 0 A_GiveToTarget("SamsaraDeusExLAMPlantAmmoTaken")
        DeathPlant:
			TNT1 A 0 A_PlaySound("DeusEx/LAM/Bounce", CHAN_BODY)
			TNT1 A 3 ACS_NamedExecuteWithResult("DeusEx_PlantMine")
			Stop
	}
}

actor DeusEx_LAMPlantMissileInfinite : DeusEx_LAMPlantMissile
{
	States
	{
		Death:
            goto DeathPlant
	}
}
