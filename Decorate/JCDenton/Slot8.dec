Actor DeusEx_LAWAmmo : Ammo
{
	+INVENTORY.IGNORESKILL
	Inventory.Amount 1
	Inventory.MaxAmount 3
    Ammo.BackpackAmount 0
    Ammo.BackpackMaxAmount 3
}	

Actor DeusEx_LAW : DeusExWeapon
{
	Weapon.SelectionOrder 2400
	Weapon.SlotNumber 8
	Weapon.AmmoType "DeusEx_LAWAmmo"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 1
	+WEAPON.BFG
	+WEAPON.EXPLOSIVE
	Tag "Light Anti-Tank Weapon"
	Weapon.UpSound "DeusEx/LAW/Select"
	Inventory.PickupMessage "You found a LAW."
	Obituary "%o fought the LAW and the LAW won."
	States
	{
		Spawn:
			DX99 A -1
			Stop
		Select:
			TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_Raise
			TNT1 A 1 A_Raise
			loop
		Ready:
			DX00 B 0 A_JumpIfInventory("DeusEx_LAWAmmo", 1, 1)
			Goto ReadyNoAmmo
//			DX00 B 0 A_JumpIfInventory("DeusEx_HeavyWeaponSkill", 2, 2)
			DX00 B 0 A_GiveInventory("DeusEx_LAWEquipped", 1) //ACS_NamedExecuteAlways("DeusEx_Decorate", 0, 3)
			DX00 BCDEFGHIJK 4
			DX00 A 0 A_TakeInventory("DeusEx_Reloading", 1)
			Goto ReadyLoop
		SelectNoAmmo:
			TNT1 AAAA 2
			Goto ReadyNoAmmo
		ReadyLoop:
			DX00 A 1 A_WeaponReady DX00 A 0 A_Jump(3, "Idle1", "Idle2", "Idle3")
			Loop
		Idle1:
			DX01 "YZ[\]" 6 A_WeaponReady
			DX02 ABCDE 6 A_WeaponReady
			Goto ReadyLoop
		Idle2:
			DX02 DGHIJKLMNO 6 A_WeaponReady
			Goto ReadyLoop
		Idle3:
			DX02 PQRSTUVWXY 6 A_WeaponReady
			Goto ReadyLoop
		ReadyNoAmmo:
			TNT1 A 0 A_JumpIfInventory("DeusEx_LAWAmmo", 1, "GrabAnother")
			TNT1 A 1 A_WeaponReady(WRF_NOFIRE)
			Loop
		Fire:
			DX00 L 0 A_GiveInventory("DeusEx_Firing", 1)
			DX00 L 0 A_JumpIfInventory("DeusEx_LAWAmmo", 1, 1)
			Goto DropLAW
			DX00 L 0 A_PlaySound("DeusEx/LAW/Fire", CHAN_WEAPON)
			DX00 L 0 ACS_NamedExecuteWithResult("DeusExRecoil", 24, 6, 3)
			DX00 L 0 A_JumpIfInventory("CoopModeOn",1,2)
			DX00 L 1 A_FireCustomMissile("DeusEx_LAWMissile", Random(-3,3), 1, 14, 14)
			Goto FireEnd
			DX00 L 1 A_FireCustomMissile("DeusEx_LAWMissileCoop", Random(-3,3), 1, 14, 14)
			Goto FireEnd
		FireEnd:
			DX00 "MNOPQRSTUVWXYZ[\]" 1
			Goto DropLAW
		DropLAW:
			DX01 A 0 A_GiveInventory("DeusEx_Reloading", 1)
			DX01 ABCDEFGHIJKLMN 3
			TNT1 A 4 A_SpawnItemEx("DeusEx_LAWDrop", 8, -16, 24, 4, 0, 3, -90, SXF_NOCHECKPOSITION|SXF_MULTIPLYSPEED|SXF_TRANSFERPITCH)
			Goto CheckAmmo
		CheckAmmo:
			TNT1 A 0 A_JumpIfInventory("DeusEx_LAWAmmo", 1, 1)
			Goto DeselectNoAmmo
			DX01 A 0 A_JumpIfInventory("DMModeOn", 1, 5)
			TNT1 A 0 A_JumpIfInventory("DeusEx_HeavyWeaponSkill", 4, 5)
			TNT1 A 0 A_JumpIfInventory("DeusEx_HeavyWeaponSkill", 3, 3)
			TNT1 A 0 A_JumpIfInventory("DeusEx_HeavyWeaponSkill", 2, 1)
			TNT1 AAA 17
			Goto GrabAnother
		GrabAnother:
			DX00 B 0 A_PlaySound("DeusEx/LAW/Select", CHAN_WEAPON)
			DX00 BCDEFGHIJK 3
			DX00 A 0 A_TakeInventory("DeusEx_Reloading", 1)
			Goto ReadyLoop
		Deselect:
			DX00 A 0 A_JumpIfInventory("DeusEx_LAWAmmo", 1, 1)
			Goto DeselectNoAmmo
			DX01 O 0 A_GiveInventory("DeusEx_Reloading", 1)
			DX01 OPQRSTUVWX 4
			TNT1 A 0 A_TakeInventory("DeusEx_LAWEquipped", 999) //ACS_NamedExecuteAlways("DeusEx_Decorate", 0, 4)
			Goto Super::InstantDeselect
		DeselectNoAmmo:
			TNT1 A 2 A_TakeInventory("DeusEx_LAWEquipped", 999)
			Goto Super::InstantDeselect
	}
}

Actor DeusEx_LAWMissile 
{
	Projectile
	Radius 8
	Height 6
	Damage (0)
	Speed 15
	+FORCERADIUSDMG
	DamageType "DeusExExploded"
	//ActiveSound "DeusEx/LAW/Approach"
	Decal "DeusExExplosionDecal"
	var int user_rocketpitch;
	States
	{
		Spawn:
			DX00 A 0 NoDelay A_PlaySound("DeusEx/LAW/Approach",CHAN_BODY,0.5)
			Goto SpawnLoop
		SpawnLoop:
			DX00 A 0 A_SetUserVar("user_rocketpitch",CallACS("SamsaraProjectileMovementPitch")/182.0)
			DX00 A 0 A_SpawnItemEx("DeusEx_RocketFlameTrail",(-5*cos(user_rocketpitch))+(2*sin(user_rocketpitch)),0,(-5*sin(user_rocketpitch))+(2*cos(user_rocketpitch)),0,0,0,0,SXF_CLIENTSIDE)
			DX00 A 0 A_SpawnItemEx("DeusEx_RocketFlameTrail",(-10*cos(user_rocketpitch))+(2*sin(user_rocketpitch)),0,-10*sin(user_rocketpitch)+(2*cos(user_rocketpitch)),0,0,0,0,SXF_CLIENTSIDE)
			DX00 A 0 A_SpawnItemEx("DeusEx_RocketFlameTrail",(-15*cos(user_rocketpitch))+(2*sin(user_rocketpitch)),0,-15*sin(user_rocketpitch)+(2*cos(user_rocketpitch)),0,0,0,0,SXF_CLIENTSIDE)
			DX00 A 0 A_SpawnItemEx("DeusEx_RocketSmoke",(-5*cos(user_rocketpitch))-(4*sin(user_rocketpitch)),0,(-5*sin(user_rocketpitch))-(4*cos(user_rocketpitch)),(0.25*cos(user_rocketpitch))+(0.25*sin(user_rocketpitch)),0,(0.25*sin(user_rocketpitch))+(0.25*cos(user_rocketpitch)),0,SXF_CLIENTSIDE)
			DX00 A 1 A_SpawnItemEx("DeusEx_RocketSmoke",(-15*cos(user_rocketpitch))-(4*sin(user_rocketpitch)),0,(-15*sin(user_rocketpitch))-(4*cos(user_rocketpitch)),(0.25*cos(user_rocketpitch))+(0.25*sin(user_rocketpitch)),0,(0.25*sin(user_rocketpitch))+(0.25*cos(user_rocketpitch)),0,SXF_CLIENTSIDE)
			Loop
		Death:
			TNT1 A 0 A_CheckCeiling(1)
			Goto Death+2
			TNT1 A 0 A_SpawnItemEx("DeusEx_ExplosionCraterCeiling")
			TNT1 A 0 A_CheckFloor(1)
			Goto Death+4
			TNT1 A 0 A_SpawnItemEx("DeusEx_ExplosionCraterFloor")
			TNT1 A 0 A_PlaySound("DeusEx/LAW/Explode", CHAN_BODY, 1.5, false, ATTN_NONE)
			TNT1 A 0 A_SpawnItemEX("DeusEx_ExplosionLarge", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
			TNT1 A 0 A_SpawnItemEX("DeusEx_ShockWaveHugeSpawner", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
			TNT1 A 0 A_AlertMonsters(3000)
			TNT1 A 2 A_Explode(CallACS("DeusEx_WeaponSkillDamage", 5, 300), 76, 1, 1, 76)
			TNT1 A 2 A_Explode(CallACS("DeusEx_WeaponSkillDamage", 5, 300), 154, 1, 1, 154)
			TNT1 A 2 A_Explode(CallACS("DeusEx_WeaponSkillDamage", 5, 300), 232, !XF_HURTSOURCE, 1, 232)
			TNT1 A 2 A_Explode(CallACS("DeusEx_WeaponSkillDamage", 5, 300), 308, !XF_HURTSOURCE, 1, 308)
			TNT1 A 2 A_Explode(CallACS("DeusEx_WeaponSkillDamage", 5, 300), 384, !XF_HURTSOURCE, 1, 384)
			Stop
	}
}

Actor DeusEx_LAWMissileCoop : DeusEx_LAWMissile { Species "Player" +THRUSPECIES }