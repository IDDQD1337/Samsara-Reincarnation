actor " Staff " : Weapon
{
    Weapon.SlotNumber 0
    Weapon.SelectionOrder 3800
    +WIMPY_WEAPON
    +MELEEWEAPON
    +NOALERT
    +STAFF2_KICKBACK
    +INVENTORY.UNDROPPABLE
    Obituary "$SAMSARA_CORVUS_OB_FIST"
	Inventory.RestrictedTo "CorvusPlayer"
    Tag "Staff"
    States
    {
      // Unpowered
      Ready:
		TNT1 A 0 A_TakeInventory("CorvusHoldingSlot")
		TNT1 A 0 A_JumpIfInventory("PowerHereticTome",1,"ReadyPowered")
		TNT1 A 0 A_JumpIfInventory("PowerHereticTomePerma",1,"ReadyPowered")
        STFF A 1 A_WeaponReady
        loop

      Deselect:
        TNT1 A 0 A_JumpIfInventory("PowerHereticTome",1,"DeselectPowered")
		TNT1 A 0 A_JumpIfInventory("PowerHereticTomePerma",1,"DeselectPowered")
        STFF A 1 A_Lower
        loop

      Select:
        TNT1 A 0 A_JumpIfInventory("PowerHereticTome",1,"SelectPowered")
		TNT1 A 0 A_JumpIfInventory("PowerHereticTomePerma",1,"SelectPowered")
        STFF A 1 A_Raise
        loop

      Fire:
		TNT1 A 0 A_GiveInventory("CorvusShootCloneTrigger")
        TNT1 A 0 A_JumpIfInventory("PowerHereticTome",1,"FirePowered")
        STFF B 6 A_JumpIfInventory("PowerHereticTomePerma",1,"FirePowered")
        STFF C 8 A_GiveInventory("SamsaraHereticStaffAttackHandler")
        STFF B 8 A_ReFire
        goto Ready

      // Powered
      ReadyPowered:
        TNT1 A 0 A_Jump(128,2)
        TNT1 A 0 A_PlaySound("heretic/staffcrackle",CHAN_WEAPON)
        STFF DEF 4 A_WeaponReady
        goto Ready

      DeselectPowered:
        STFF D 1 A_Lower
        goto Deselect

      SelectPowered:
        STFF D 1 A_Raise
        goto Select

      FirePowered:
        STFF G 6
        STFF H 8 A_GiveInventory("SamsaraHereticStaffPoweredAttackHandler")
        STFF G 8 A_ReFire
        goto Ready
    }
}

actor SamsaraHereticStaffAttackHandler : Trigger
{
    States
    {
      // Attack stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupCoop")
      PickupDM:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupDMSpread")
        TNT1 A 0 A_CustomPunch(random(5,20),true,0,"StaffPuff3") //A_StaffAttack(random[StaffAttack](5,20),"StaffPuff3")
        stop

      PickupDMSpread:
        TNT1 A 0 A_CustomPunch(random(5,20)*3,true,0,"StaffPuff3")
        stop

      PickupCoop:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupCoopSpread")
        TNT1 A 0 A_CustomPunch(random(5,20),true,0,"StaffPuff3Coop")
        stop

      PickupCoopSpread:
        TNT1 A 0 A_CustomPunch(random(5,20)*3,true,0,"StaffPuff3Coop")
        stop
    }
}

actor StaffPuff3 : StaffPuff
{
    DamageType "Pistol"
    SeeSound "heretic/staffhit"
    States
    {
      Spawn:
      Crash:
      Melee:
        TNT1 A 0 A_PlaySound("heretic/staffhit")
        PUF3 A 4 BRIGHT
        PUF3 BCD 4
        Stop
    }
}

actor StaffPuff3Coop : StaffPuff3 { +MTHRUSPECIES }

actor SamsaraHereticStaffPoweredAttackHandler : Trigger
{
    States
    {
      // Attack stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupCoop")
      PickupDM:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupDMSpread")
        TNT1 A 0 A_CustomPunch(random(60,120),true,0,"StaffPuff4") //A_StaffAttack(random[StaffAttack](18,81),"StaffPuff4")
        stop

      PickupDMSpread:
        TNT1 A 0 A_CustomPunch(random(60,120)*3,true,0,"StaffPuff4")
        stop

      PickupCoop:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupCoopSpread")
        TNT1 A 0 A_CustomPunch(random(60,120),true,0,"StaffPuff4Coop")
        stop

      PickupCoopSpread:
        TNT1 A 0 A_CustomPunch(random(60,120)*3,true,0,"StaffPuff4Coop")
        stop
    }
}

actor StaffPuff4 : StaffPuff2
{
    DamageType "Pistol"
    SeeSound "heretic/staffpowerhit"
    States
    {
      Spawn:
      Crash:
      Melee:
        TNT1 A 0 A_PlaySound("heretic/staffpowerhit")
        PUF4 ABCDEF 4 BRIGHT
        Stop
    }
}

actor StaffPuff4Coop : StaffPuff4 { +MTHRUSPECIES }
