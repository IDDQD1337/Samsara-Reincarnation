actor SamsaraHereticPhoenixRodPoweredActive : Boolean {}
actor SamsaraHereticPhoenixRodPoweredPlayingSound : Boolean {}
actor SamsaraHereticPhoenixRodPoweredFlameCount : Counter { Inventory.MaxAmount 280 }

actor "Phoenix Rod" : Weapon
{
	Weapon.SelectionOrder 2600
	Weapon.Kickback 150
	Weapon.YAdjust 16
	Weapon.AmmoUse 1
	Weapon.AmmoGive 2
	Weapon.SlotNumber 5
	//+AMMO_OPTIONAL
	+INVENTORY.UNDROPPABLE
	+WEAPON.EXPLOSIVE
    +NOAUTOFIRE
	Weapon.AmmoType "RocketAmmo"
	Inventory.PickUpSound "heretic/weaponget"
	Inventory.PickupMessage "You got the Phoenix Rod!"
	Obituary "$SAMSARA_CORVUS_OB_SLOT5"
	Inventory.RestrictedTo "CorvusPlayer"
	Tag "Phoenix Rod"
	States
	{
      Spawn:
        WPHX A -1
        stop

      Ready:
        TNT1 A 0 A_TakeInventory("CorvusHoldingSlot")
        TNT1 A 0 A_GiveInventory("CorvusHoldingSlot",4)
        TNT1 A 0 A_GiveInventory("SamsaraHereticPhoenixRodPoweredPowerdownHandler")
        TNT1 A 0 A_GiveInventory("SamsaraHereticPhoenixRodPoweredPowerdownStopSoundHandler")
        PHNX A 1 A_WeaponReady
        loop

      Deselect:
        TNT1 A 0 A_GiveInventory("SamsaraHereticPhoenixRodPoweredPowerdownHandler")
        TNT1 A 0 A_GiveInventory("SamsaraHereticPhoenixRodPoweredPowerdownStopSoundHandler")
        PHNX A 1 A_Lower
        loop

      Select:
        TNT1 A 0 A_GiveInventory("SamsaraHereticPhoenixRodPoweredPowerdownHandler")
        TNT1 A 0 A_GiveInventory("SamsaraHereticPhoenixRodPoweredPowerdownStopSoundHandler")
        PHNX A 1 A_Raise
        loop

      // Unpowered
      Fire:
        TNT1 A 0 A_GiveInventory("CorvusShootCloneTrigger")
        TNT1 A 0 A_JumpIfInventory("PowerHereticTome",1,"FirePowered")
        PHNX B 5 Bright A_JumpIfInventory("PowerHereticTomePerma",1,"FirePowered")
        PHNX C 7 Bright A_GiveInventory("SamsaraHereticPhoenixRodAttackHandler")
        PHNX DB 4
        TNT1 A 0 A_ReFire
        goto Ready

      Hold:
        TNT1 A 0 A_JumpIfInventory("PowerHereticTome",1,"HoldPoweredInit")
        TNT1 A 0 A_JumpIfInventory("PowerHereticTomePerma",1,"HoldPoweredInit")
        TNT1 A 0 A_JumpIfInventory("SamsaraHereticPhoenixRodPoweredActive",1,1)
        goto Fire
        TNT1 A 0 A_GiveInventory("SamsaraHereticPhoenixRodPoweredPowerdownHandler")
        TNT1 A 0 A_GiveInventory("SamsaraHereticPhoenixRodPoweredPowerdownStopSoundHandler")
        TNT1 A 0 A_CheckReload
        goto Fire

      // Powered
      FirePowered:
        PHNX B 3
      HoldPoweredInit:
        TNT1 A 0 A_JumpIfInventory("SamsaraHereticPhoenixRodPoweredActive",1,1)
        goto HoldPowered
        TNT1 A 0 A_JumpIfInventory("SamsaraHereticPhoenixRodPoweredFlameCount",0,1)
        goto HoldPowered
        TNT1 A 0 A_GiveInventory("SamsaraHereticPhoenixRodPoweredPowerdownHandler")
        TNT1 A 0 A_CheckReload
        goto HoldPowered

      HoldPowered:
        PHNX C 1 Bright A_GiveInventory("SamsaraHereticPhoenixRodPoweredAttackHandler")
        TNT1 A 0 A_JumpIfInventory("PowerHereticTome",1,3)
        TNT1 A 0 A_JumpIfInventory("PowerHereticTomePerma",1,2)
        TNT1 A 0 A_CheckReload
        goto HoldPoweredFinish
        TNT1 A 0 A_ReFire
        goto HoldPoweredFinish

      HoldPoweredFinish:
        PHNX B 4 Bright
        goto Ready // no Powerdown state this time, handle its stuff in Ready state instead
	}
}

actor SamsaraHereticPhoenixRodAttackHandler : Trigger
{
    States
    {
      // Ammo check stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,"PickupTakeAmmo")
        stop

      // Ammo taking stuff
      PickupTakeAmmo:
        TNT1 A 0 A_TakeInventory("RocketAmmo",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("PhoenixShot",0,false)
        goto PickupFinish

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("PhoenixShot2",0,false)
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 A_Recoil(4)
        stop
    }
}

actor PhoenixShot : PhoenixFX1
{
	-SPECIALFIREDAMAGE
	DamageType "FireExplosive"
	Decal Scorch
	Obituary "$SAMSARA_CORVUS_OB_SLOT5"
	//Damage 100
}

actor PhoenixShot2 : PhoenixShot { +THRUSPECIES Species "Player" }

actor SamsaraHereticPhoenixRodPoweredAttackHandler : Trigger
{
    States
    {
      // Attack stuff
      Pickup:
        TNT1 A 0 A_PlaySound("weapons/phoenixpowshoot",CHAN_WEAPON,1,true)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupCoop")
      PickupDM:
        TNT1 A 0 A_FireCustomMissile("PhoenixFX2DM",random(-2,2),false,0,0,1,2)//,26,2,2) //A_FirePhoenixPL2
        goto PickupFinish

      PickupCoop:
        TNT1 A 0 A_FireCustomMissile("PhoenixFX2A",random(-2,2),false,0,0,1,2)
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 A_GiveInventory("SamsaraHereticPhoenixRodPoweredActive")
        TNT1 A 0 A_GiveInventory("SamsaraHereticPhoenixRodPoweredPlayingSound")
        TNT1 A 0 A_GiveInventory("SamsaraHereticPhoenixRodPoweredFlameCount")
        stop
    }
}

actor SamsaraHereticPhoenixRodPoweredPowerdownHandler : Trigger
{
    States
    {
      // Ammo check stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHereticPhoenixRodPoweredActive",1,1)
        stop
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupFinish")
        TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,"PickupTakeAmmo")
        goto PickupFinish

      // Ammo taking stuff
      PickupTakeAmmo:
        TNT1 A 0 A_TakeInventory("RocketAmmo",1)
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 A_TakeInventory("SamsaraHereticPhoenixRodPoweredActive")
        TNT1 A 0 A_TakeInventory("SamsaraHereticPhoenixRodPoweredFlameCount")
        stop
    }
}

actor SamsaraHereticPhoenixRodPoweredPowerdownStopSoundHandler : Trigger
{
    States
    {
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHereticPhoenixRodPoweredPlayingSound",1,1)
        stop
        TNT1 A 0 A_PlaySound("silence",CHAN_WEAPON)
        TNT1 A 0 A_StopSound(CHAN_WEAPON)
        TNT1 A 0 A_TakeInventory("SamsaraHereticPhoenixRodPoweredPlayingSound")
        stop
    }
}

actor PhoenixFX2DM //: PhoenixFX2 // inheritance caused players to be temporarily locked when hit due to hardcoded fun
{
    Projectile
    Radius 6
    Height 8
    Speed 10
    Damage 4
    DamageType "Fire"
    RenderStyle Add
    Decal "PhoenixThrowerScorch"
    Obituary "$SAMSARA_CORVUS_OB_SLOT5"
    +BLOODSPLATTER
    States
    {
      Spawn:
        FX09 ABABA 2 Bright
        FX09 B 2 Bright A_ChangeVelocity(0,0,velz+1.5) //A_FlameEnd
        FX09 CDEF 2 Bright
        stop

      Death:
        FX09 G 3 Bright
        FX09 H 3 Bright A_ChangeVelocity(0,0,velz+1.8) //A_FloatPuff
        FX09 I 4 Bright
        FX09 JK 5 Bright
        stop
    }
}

actor PhoenixFX2A : PhoenixFX2DM
{
	Damage 2
	Species "Player"
	+THRUSPECIES
}

actor PhoenixPuff2 : PhoenixPuff replaces PhoenixPuff { +CLIENTSIDEONLY }

Actor PheonixBlastFX : VolcanoBlast
{
	Damage 12
	Gravity 0.525
	DamageType "FireExplosive"
	SeeSound "weapons/phoenixshoot"
	Speed 20
	Scale 1.2
	Radius 10
	Height 10
	States
	{
		Spawn:
			VFBL AB 4 A_SpawnItemEx("PhoenixPuff2", 0, 0, 0, 0)
			Loop
		Death:
			FBMB F 4 BRight A_Explode(50, 50, 1)
			TNT1 AAAA 0 A_SpawnItemEX("VolcanoTBlast22", 0,0,0,0.7,0.7, Random(0,2), Random(0,360), SXF_TRANSFERPOINTERS)
			FBMB GHIJK 4 Bright
			Stop
	}		
}	

Actor VolcanoTBlast22 : VolcanoTBlast { Scale 1.2 Damage 3 }
