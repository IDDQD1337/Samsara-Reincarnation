actor "Elven Wand" : Weapon
{
    +BLOODSPLATTER
    +PUFFONACTORS
    +WEAPON.WIMPY_WEAPON
    +INVENTORY.UNDROPPABLE
    +NOALERT
    Inventory.PickUpSound "heretic/weaponget"
    Weapon.SelectionOrder 2000
    Weapon.SlotNumber 1
    Weapon.SlotPriority 1
    Inventory.PickupMessage "You got the Elven Wand!"
    Weapon.AmmoUse 0
    Weapon.AmmoGive 0
    Decal RailScorchLower
    Obituary "$SAMSARA_CORVUS_OB_PISTOL"
    Weapon.YAdjust 5
    Inventory.RestrictedTo "CorvusPlayer"
    Tag "Elven Wand"
    States
    {
      Ready:
		TNT1 A 0 A_TakeInventory("CorvusHoldingSlot")
		GWND A 1 A_WeaponReady
        loop

      Deselect:
        GWND A 1 A_Lower
        loop

      Select:
        GWND A 1 A_Raise
        loop

      // Unpowered
      Fire:
        TNT1 A 0 A_JumpIfInventory("PowerHereticTome",1,"FirePowered")
		TNT1 A 0 A_JumpIfInventory("PowerHereticTomePerma",1,"FirePowered")
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto FireStart
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"FireStart")
        goto Nothing

      FireStart:
        TNT1 A 0 A_AlertMonsters
        GWND B 3 Bright A_GiveInventory("CorvusShootCloneTrigger")
        TNT1 A 0 A_GiveInventory("SamsaraHereticGoldWandAttackHandler")
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"FireContinueCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"FireContinueCoop")
	  FireContinueDM:
        GWND C 4 Bright
        GWND D 2
        goto FireFinish

	  FireContinueCoop:
        GWND C 5 Bright
        GWND D 3
        goto FireFinish

      FireFinish:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto FireFinishReFire
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireFinishReFire")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"FireFinishReFire")
        goto Nothing

      FireFinishReFire:
        TNT1 A 0 A_ReFire
        goto Ready

      // Powered
      FirePowered:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto FireStartPowered
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStartPowered")
        TNT1 A 0 A_JumpIfInventory("Clip",3,"FireStartPowered")
        goto Nothing

      FireStartPowered:
        TNT1 A 0 A_AlertMonsters
        GWND B 3 Bright A_GiveInventory("CorvusShootCloneTrigger")
        GWND C 4 Bright A_GiveInventory("SamsaraHereticGoldWandPoweredAttackHandler")
        GWND D 3
      FireFinishPowered:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto FireFinishReFire
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireFinishReFire")
        TNT1 A 0 A_JumpIfInventory("Clip",3,"FireFinishReFire")
        goto Nothing

      Nothing:
        TNT1 A 0 A_ClearReFire
        TNT1 A 0 A_SelectWeapon(" Staff ")
        goto Deselect
    }
}

actor SamsaraHereticGoldWandAttackHandler : Trigger
{
    States
    {
      // Ammo check stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto PickupAttack
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"PickupTakeAmmo")
        stop

      // Ammo taking stuff
      PickupTakeAmmo:
        TNT1 A 0 A_TakeInventory("Clip",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("heretic/wand",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireBullets(5.625,0,1,random(1,6)+12,"GoldWandPuff3",FBF_NORANDOM|FBF_NOFLASH) // Previously random(1,8)+6
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireBullets(5.625,0,1,random(1,8)+6,"GoldWandPuff3Coop",FBF_NORANDOM|FBF_NOFLASH)
        stop
    }
}

actor GoldWandPuff3 : GoldWandPuff1
{
    DamageType "Pistol"
    +CLIENTSIDEONLY
}

actor GoldWandPuff3Coop : GoldWandPuff3 { +MTHRUSPECIES }

actor SamsaraHereticGoldWandPoweredAttackHandler : Trigger
{
    States
    {
      // Ammo check stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto PickupAttack
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",3,"PickupTakeAmmo")
        stop

      // Ammo taking stuff
      PickupTakeAmmo:
        TNT1 A 0 A_TakeInventory("Clip",3)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("heretic/wand",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("GoldWandFX3",-5.625,false,0,1)
        TNT1 A 0 A_FireCustomMissile("GoldWandFX3",5.625,false,0,1)
        TNT1 A 0 A_FireBullets(-5.625,0,-1,random(2,12),"GoldWandPuff4",FBF_NORANDOM|FBF_EXPLICITANGLE|FBF_NOFLASH)
        TNT1 A 0 A_FireBullets(-2.8125,0,-1,random(2,12),"GoldWandPuff4",FBF_NORANDOM|FBF_EXPLICITANGLE|FBF_NOFLASH)
        TNT1 A 0 A_FireBullets(0,0,-1,random(2,12),"GoldWandPuff4",FBF_NORANDOM|FBF_EXPLICITANGLE|FBF_NOFLASH)
        TNT1 A 0 A_FireBullets(2.8125,0,-1,random(2,12),"GoldWandPuff4",FBF_NORANDOM|FBF_EXPLICITANGLE|FBF_NOFLASH)
        TNT1 A 0 A_FireBullets(5.625,0,-1,random(2,12),"GoldWandPuff4",FBF_NORANDOM|FBF_EXPLICITANGLE|FBF_NOFLASH)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("GoldWandFX32",-5.625,false,0,1)
        TNT1 A 0 A_FireCustomMissile("GoldWandFX32",5.625,false,0,1)
        TNT1 A 0 A_FireBullets(-5.625,0,-1,random(2,12),"GoldWandPuff4Coop",FBF_NORANDOM|FBF_EXPLICITANGLE|FBF_NOFLASH)
        TNT1 A 0 A_FireBullets(-2.8125,0,-1,random(2,12),"GoldWandPuff4Coop",FBF_NORANDOM|FBF_EXPLICITANGLE|FBF_NOFLASH)
        TNT1 A 0 A_FireBullets(0,0,-1,random(2,12),"GoldWandPuff4Coop",FBF_NORANDOM|FBF_EXPLICITANGLE|FBF_NOFLASH)
        TNT1 A 0 A_FireBullets(2.8125,0,-1,random(2,12),"GoldWandPuff4Coop",FBF_NORANDOM|FBF_EXPLICITANGLE|FBF_NOFLASH)
        TNT1 A 0 A_FireBullets(5.625,0,-1,random(2,12),"GoldWandPuff4Coop",FBF_NORANDOM|FBF_EXPLICITANGLE|FBF_NOFLASH)
        stop
    }
}

actor GoldWandFX3 : GoldWandFX2
{
    Decal HImpScorch
    DamageType "Pistol"
    Obituary "$SAMSARA_CORVUS_OB_PISTOL"
}

actor GoldWandFX32 : GoldWandFX3
{
    Species "Player"
    +THRUSPECIES
}

actor GoldWandPuff4 : GoldWandPuff2
{
    DamageType "Pistol"
    +CLIENTSIDEONLY
}

actor GoldWandPuff4Coop : GoldWandPuff4 { +MTHRUSPECIES }
