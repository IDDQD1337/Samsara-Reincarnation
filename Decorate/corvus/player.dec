actor CorvusPlayer : PlayerPawn
{
    Player.DisplayName "Corvus"
    Player.StartItem "Elven Wand", 1
    Player.StartItem " Staff ", 1
    Player.StartItem "CorvusClass", 1
    Player.StartItem "IsSamsaraClass", 1
    Player.StartItem "Clip", 50
    Player.StartItem "PermaRemoteBomb", 1
    Player.StartItem "PermaTimeBomb", 1
	Player.StartItem "TauntButton", 1
    Player.Face "CRV"
    Player.Soundclass "CorvusPlayer"
    Player.JumpZ 9//Default is 8
    BloodColor "Red"
	DamageFactor "DynamiteCoop", 0.0	
    DamageFactor "PoisonCloud", 1.75
	DamageFactor "SWFlameCoop", 0.0	
    DamageFactor "CorvusCrossbow", 1.1
    DamageFactor "DragonClaw", 1.1
	DamageFactor "SWStickyCoop", 0.0	
    DamageFactor "MightyBoot", 1.5
    DamageFactor "WMCBeam", 0.75
    DamageFactor "WMCNade", 0.75
    DamageFactor "EnemyInfightDamage", 0.0
	DamageFactor "PhosFireCoop", 0.0
	DamageFactor "ElectricBolt", 1.0	
	DamageFactor "EradCoopExplosive2", 0.0
    DamageFactor "C7MineBoom", 0.0	
    DamageFactor "RMR-BigPlasma", 0.85			
    DamageFactor "EradCoopGreenFire", 0.0	
	DamageFactor "BCannonProj", 0.75	
	DamageFactor "SWNukeCoop", 0.0		
	DamageFactor "SWRailgunCoop", 0.0
	DamageFactor "SWFlame2Coop", 0.0	
	DamageFactor "CMDMGSHIT", 0.0	
	BloodType "SamsaraBloodyHellBlood"
    +NOSKIN
    -DONTBLAST
    -THRUSPECIES
    Species "Player"
    Translation "106:106=125:125"
    Player.ColorRange 112, 127    
    States
    {
      Spawn:
        CORV A -1
        Stop
      See:
        CORV ABCD 4
        Loop
      Melee:
      Missile:
        CORV F 6 BRIGHT
        CORV E 12
        Goto Spawn
      Pain:
        CORV G 4
        CORV G 4 A_Pain
        Goto Spawn
	  Pain.LifeLeech:
		TNT1 A 0 A_GiveToTarget("SerpentHealth", random(1,2))
		TNT1 A 0 A_GiveToTarget("HealthBonus",1)
		Goto Pain
      Death:
	    TNT1 A 0 A_SpawnItem("UniversalBloodyHellGoreDeathEffect")
        CORV H 6
        CORV I 6 A_PlayerScream
        CORV JK 6
        CORV L 6 A_NoBlocking
        CORV MNO 6
        CORV P -1
        Stop
      XDeath:
	    TNT1 A 0 A_SpawnItem("UniversalBloodyHellGoreXDeathEffect")
        CORV Q 5 A_PlayerScream
        TNT1 A 0 A_SpawnItemEx("BloodySkull2",0,0,46,random(-1,1),random(-1,1),5)
        CORV R 5 A_NoBlocking
        CORV STUVWX 5
        CORV Y -1
        Stop
      Death.CorvusGrimBallista:
        CORV Q 5 A_PlaySoundEx("*falling",CHAN_VOICE,0,0)
        TNT1 A 0 A_SpawnItemEx("BloodySkull2",0,0,46,random(-1,1),random(-1,1),5)		
        CORV R 5 A_NoBlocking
        CORV STUVWX 5
        CORV Y -1
        Stop
      Death.HexGreenFire:
      Death.QuakeFire:
	  Death.FireExplosive:
	  Death.PhosFireCoop:
      Burn:
        FDTH A 5 BRIGHT A_PlaySound("*burndeath")
        FDTH B 4 BRIGHT
        FDTH C 5 BRIGHT
        FDTH D 4 BRIGHT
        FDTH E 5 BRIGHT
        FDTH F 4 BRIGHT
        FDTH G 5 BRIGHT
        FDTH H 4 BRIGHT
        FDTH I 5 BRIGHT
        FDTH J 4 BRIGHT
        FDTH K 5 BRIGHT
        FDTH L 4 BRIGHT
        FDTH M 5 BRIGHT
        FDTH N 4 BRIGHT
        FDTH O 5 BRIGHT A_NoBlocking
        FDTH P 4 BRIGHT
        FDTH Q 5 BRIGHT
        FDTH R 4 BRIGHT
        ACLO E -1
        stop
      Death.ZorchPistol:
      Death.Zorch:
      Death.ZorchKamikaze:
      Death.PhasingZorch:
      Death.ZorchSpectral:
        CORV G 0 A_Stop
        TNT1 A 0 A_PlaySoundEx("chex/zorch","soundslot5",0,0)
        CORV G 3 A_SpawnItem("ZorchEffectSmall")
        CORV G 3 A_SetTranslucent(0.9,0)
        CORV G 0 A_PlayerScream
        CORV G 3 A_SetTranslucent(0.8,0)
        CORV G 0 A_NoBlocking
        CORV G 3 A_SetTranslucent(0.7,0)
        CORV G 3 A_SetTranslucent(0.6,0)
        CORV G 3 A_SetTranslucent(0.5,0)
        CORV G 3 A_SetTranslucent(0.4,0)
        CORV G 3 A_SetTranslucent(0.3,0)
        CORV G 3 A_SetTranslucent(0.2,0)
        CORV G 3 A_SetTranslucent(0.1,0)
        ACLO E 1 A_SetTranslucent(1,0)
        ACLO E -1
        stop
      CoopModeOn:
        TNT1 A 0
        TNT1 A 0 A_ChangeFlag("THRUSPECIES",1)
        TNT1 A 0 A_ChangeFlag("DONTBLAST",1)
        CORV A 1
        goto Spawn
      Death.DukeDamage:
      Death.DukePistol:
        TNT1 A 0 A_Jump(218,"Death")
        TNT1 A 0 A_GiveToTarget("DukeDMTaunt1",1)
        goto Death
      Death.DukeExplosive:
      Death.DukeSpectral:
        TNT1 A 0 A_JumpIfHealthLower(-5,"DukeGibStart")
        goto Death.DukeDamage
      DukeGibStart:
	    CORV G 1
        TNT1 A 0 A_UnsetSolid
        TNT1 A 0 A_PlaySound("duke/enemygib")
        TNT1 A 0 A_SpawnDebris("DukeGibs1")
        TNT1 A 0 A_SpawnDebris("DukeGibs2")
        TNT1 A 0 A_SpawnDebris("DukeGibs3")
        TNT1 A 0 A_SpawnDebris("DukeGibs4")
        TNT1 A 0 A_SpawnDebris("DukeGibs5")
        TNT1 A 0 A_SpawnDebris("DukeGibs6")
        TNT1 A 0 A_Jump(96,"XDeath")
        TNT1 A 0 A_GiveToTarget("DukeDMTaunt1",1)
        goto XDeath
      Death.DukeIce:
        TNT1 A 0 A_Jump(218,"Ice")
        TNT1 A 0 A_GiveToTarget("DukeDMTaunt1",1)
        goto Ice
      Ice:
      Death.HexenIce:
      Death.Ice:
	    TNT1 A 0 A_PlayWeaponSound("misc/freeze")
        CORV Z 70 A_NoBlocking
        CORV Z 0 A_SetTranslucent(0,0)
        TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("IceChunk2",random(-1,1),random(-1,1),random(8,48),random(-2,2),random(-2,2),random(1,6))
        CORV Z 1 A_PlayWeaponSound("misc/icebreak")
        CORV Z -1
        stop
      Pain.MightyBoot:
	    CORV G 1 ACS_ExecuteAlways(312, 0, 25, 5, 24)
        CORV G 1 A_PlaySoundEx("duke/kickhit","body",0,0)
        TNT1 A 0 A_Recoil(12)
        CORV G 6 A_Pain
        Goto See
	  Pain.MarathonStun:
        TNT1 A 0 A_Stop
	    CORV G 1 ACS_ExecuteAlways(312, 0, 25, 5, 100)
		TNT1 A 0 A_GiveInventory("NothingSpeed")
		TNT1 A 0 A_SetBlend("White",0.9,105)
        CORV G 1 A_PlaySoundEx("marathon/tech50/stun","body",0,0)
        CORV G 6 A_Pain
        Goto See
	  Death.Dynamite:
	  Death.LifeLeech:
	  Death.CalebBullet:
	  Death.CalebNapalm:
	  Death.Tesla:
	  Death.Vodoo:
		TNT1 A 0 A_Jump(218, "Death")
		TNT1 A 0 A_GiveToTarget("CalebDMTaunt1", 1)
		Goto Death  
	  Death.CalebFire:
		TNT1 A 0 A_Jump(218, "Burn")
		TNT1 A 0 A_GiveToTarget("CalebDMTaunt1", 1)
		Goto Burn	  
      Death.EradDisc:
	  Death.EradShock:
	  Death.EradPiercingDart:
	  Death.EradExplosive2:	  
		TNT1 A 0 A_Jump(218, "Death")
		TNT1 A 0 A_GiveToTarget("EleenaDMTaunt1", 1)
		Goto Death
	  Death.EradGreenFire:
	  Death.EradCoopGreenFire:	  
		TNT1 A 0 A_Jump(218, "Burn")
		TNT1 A 0 A_GiveToTarget("EleenaDMTaunt1", 1)
		Goto Burn
	  Death.EradExplosive:
	  Death.EradNova:
		TNT1 A 0 A_Jump(218, "XDeath")
		TNT1 A 0 A_GiveToTarget("EleenaGibTaunt", 1)
		Goto XDeath
	  Death.RottExplosive:
		TNT1 A 0 A_SpawnItemEx("RottGibbedMan",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE)
		BLAN A 1
		TNT1 A 0 A_Jump(128,2)
		TNT1 A 0 A_GiveToTarget("LudicrousGibsItem", 1)
		TNT1 AA 0
		BLAN A 5 A_NoBlocking
		Stop   
	  Death.RottDisintegrate:
		TNT1 A 0 A_Scream
		TNT1 A 0 A_UnsetSolid
		TNT1 A 5 A_SpawnItem("RGodModeDeath")
		BLAN A -1 A_Fall
		stop	
	  Death.RottFire:
	    TNT1 A 0 A_Scream
		ske7 A 0 A_playsound("gen/burned") 
		ske7 ABCDEFGHIJKLMNOPQRSTUVWXYZ 2 bright
		skex ABCDEFGHIJKL 2 bright
		skex M 0 A_playsound("gen/skel") 
		skex MNOPQRSTUV 3 bright
		skex V -1 a_fall    
	    Stop      
	  Disintegrate:
        TNT1 A 0 A_GiveInventory("EnemyZorched",1)
        TNT1 A 0 A_NoBlocking
        TNT1 A 0 A_ChangeFlag("DONTGIB",1)
        TNT1 A 0 A_SpawnItemEx("CorvusDisintegrate",0,0,5,0,0,0,0,32)
        ACLO E 5 A_PlayerScream
        ACLO E -1
        stop
	  Death.Nuke:
		ACLO E 0 A_UnsetSolid
		ACLO E 0 A_PlayerScream
		ACLO E 0 A_NoBlocking
		ACLO E 5 A_SpawnItemEx("CorvusNuke")
		ACLO E -1
		stop
    }
}

Actor CorvusNuke
{
	-SOLID
	+NOBLOCKMAP
	+CLIENTSIDEONLY
	states
	{
	Spawn:
		NK21 ABCDEFG 5 bright
		stop
	}
}

actor CorvusDisintegrate : ZombiemanDisintegrate 
{
   States
   {
      Spawn:
       TNT1 A 0
       CORV H 3 A_SpawnItem("DisintEffectSmall",0,10)
       CORV H 3 A_SetTranslucent(0.9,0)
       CORV H 3 A_SetTranslucent(0.8,0)
       CORV H 3 A_SetTranslucent(0.7,0)
       CORV I 3 A_SetTranslucent(0.6,0)
       CORV I 3 A_SetTranslucent(0.5,0)
       CORV I 2 A_SetTranslucent(0.4,0)
       CORV I 2 A_SetTranslucent(0.3,0)
       CORV J 2 A_SetTranslucent(0.2,0)
       CORV J 2 A_SetTranslucent(0.1,0)
        Stop
   }
}

actor BloodySkull2
{
    +CLIENTSIDEONLY
    Radius 4
    Height 4
    Gravity 0.25
    +NOBLOCKMAP
    +DROPOFF
    //+DOOMBOUNCE
    //+LOWGRAVITY
    +CANNOTPUSH
    -SKYEXPLODE
    +NOBLOCKMONST
    -SOLID
    +CLIENTSIDEONLY
    //BounceCount 4
    //BounceFactor 0.45
    States
    {
      Spawn:
        BSKL A 0
        BSKL ABCDE 5 A_CheckFloor("Death")
        Goto Spawn+1

      Death:
        TNT1 A 0 A_Stop
        BSKL F -1
        stop
    }
}

actor CorvusClass : inventory { -INVBAR +INVENTORY.UNDROPPABLE inventory.maxamount 1 }