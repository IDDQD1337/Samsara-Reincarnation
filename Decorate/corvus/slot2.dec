actor SamsaraHereticMaceAttackCycle : Counter { Inventory.MaxAmount 8 }

actor " Firemace " : Weapon
{
    Weapon.SelectionOrder 1400
    Inventory.PickUpSound "heretic/weaponget"
    Weapon.AmmoUse 1
    Weapon.AmmoGive1 10
    Weapon.SlotNumber 2
    Weapon.YAdjust 16
    Weapon.AmmoType "Shell"
    //+AMMO_OPTIONAL
    +INVENTORY.UNDROPPABLE
    +NOALERT
    Inventory.PickupMessage "You got the Firemace!"
    Obituary "$SAMSARA_CORVUS_OB_SLOT2_1"
    Inventory.RestrictedTo "CorvusPlayer"
    Tag "Firemace"
    States
    {
      Spawn:
        WMCE A -1
        Stop

      Ready:
		TNT1 A 0 A_TakeInventory("CorvusHoldingSlot")
		TNT1 A 0 A_GiveInventory("CorvusHoldingSlot")
        MACE A 1 A_WeaponReady
        loop

      Deselect:
		MACE A 1 A_Lower
        loop

      Select:
        MACE A 1 A_Raise
        loop

      // Unpowered
      Fire:
        TNT1 A 0 A_JumpIfInventory("PowerHereticTome",1,"FirePowered")
		TNT1 A 0 A_JumpIfInventory("PowerHereticTomePerma",1,"FirePowered")
        TNT1 A 0 A_AlertMonsters
        MACE B 4 A_GiveInventory("CorvusShootCloneTrigger")
        goto HoldStart

      Hold:
        TNT1 A 0 A_JumpIfInventory("PowerHereticTome",1,"FirePowered")
		TNT1 A 0 A_JumpIfInventory("PowerHereticTomePerma",1,"FirePowered")
        TNT1 A 0 A_AlertMonsters
      HoldStart:
        TNT1 A 0 A_TakeInventory("SamsaraTakingAmmo")
        TNT1 A 0 A_TakeInventory("SamsaraHereticMaceAttackCycle") // reset
      HoldCycleStart:
        TNT1 A 0 A_GiveInventory("SamsaraHereticMaceAttackCycle")
        MACE F 0 A_JumpIfInventory("SamsaraHereticMaceAttackCycle",0,"HoldCycleTakeAmmo")
        MACE E 0 A_JumpIfInventory("SamsaraHereticMaceAttackCycle",7,"HoldCycleAttackCheck")
        MACE D 0 A_JumpIfInventory("SamsaraHereticMaceAttackCycle",6,"HoldCycleAttackCheck")
        MACE C 0 A_JumpIfInventory("SamsaraHereticMaceAttackCycle",5,"HoldCycleAttackCheck")
        MACE F 0 A_JumpIfInventory("SamsaraHereticMaceAttackCycle",4,"HoldCycleAttackCheck")
        MACE E 0 A_JumpIfInventory("SamsaraHereticMaceAttackCycle",3,"HoldCycleAttackCheck")
        MACE D 0 A_JumpIfInventory("SamsaraHereticMaceAttackCycle",2,"HoldCycleAttackCheck")
        MACE C 0
        goto HoldCycleAttackCheck

      HoldCycleTakeAmmo:
        "####" "#" 0 A_GiveInventory("SamsaraTakingAmmo")
      HoldCycleAttackCheck:
        "####" "#" 0 A_Jump(28,2) // Has about an 11% chance to fire a larger ball up.
        "####" "#" 0 A_GiveInventory("SamsaraHereticMaceAttackHandler1")
        goto HoldCycleJump
        "####" "#" 3 A_GiveInventory("SamsaraHereticMaceAttackHandler2") // this part of the code had no offset changes
        goto HoldCycleFinish

      HoldCycleJump:
        "####" "#" 0 A_Jump(256,"HoldCycle_N2_32","HoldCycle_N2_33","HoldCycle_N2_34","HoldCycle_N2_35","HoldCycle_N1_32","HoldCycle_N1_33","HoldCycle_N1_34","HoldCycle_N1_35","HoldCycle_0_32","HoldCycle_0_33","HoldCycle_0_34","HoldCycle_0_35","HoldCycle_1_32","HoldCycle_1_33","HoldCycle_1_34","HoldCycle_1_35")
      HoldCycle_N2_32:
        "####" "#" 3 Offset(-2,32)
        goto HoldCycleFinish
      HoldCycle_N2_33:
        "####" "#" 3 Offset(-2,33)
        goto HoldCycleFinish
      HoldCycle_N2_34:
        "####" "#" 3 Offset(-2,34)
        goto HoldCycleFinish
      HoldCycle_N2_35:
        "####" "#" 3 Offset(-2,35)
        goto HoldCycleFinish
      HoldCycle_N1_32:
        "####" "#" 3 Offset(-1,32)
        goto HoldCycleFinish
      HoldCycle_N1_33:
        "####" "#" 3 Offset(-1,33)
        goto HoldCycleFinish
      HoldCycle_N1_34:
        "####" "#" 3 Offset(-1,34)
        goto HoldCycleFinish
      HoldCycle_N1_35:
        "####" "#" 3 Offset(-1,35)
        goto HoldCycleFinish
      HoldCycle_0_32:
        "####" "#" 3 Offset(0,32)
        goto HoldCycleFinish
      HoldCycle_0_33:
        "####" "#" 3 Offset(0,33)
        goto HoldCycleFinish
      HoldCycle_0_34:
        "####" "#" 3 Offset(0,34)
        goto HoldCycleFinish
      HoldCycle_0_35:
        "####" "#" 3 Offset(0,35)
        goto HoldCycleFinish
      HoldCycle_1_32:
        "####" "#" 3 Offset(1,32)
        goto HoldCycleFinish
      HoldCycle_1_33:
        "####" "#" 3 Offset(1,33)
        goto HoldCycleFinish
      HoldCycle_1_34:
        "####" "#" 3 Offset(1,34)
        goto HoldCycleFinish
      HoldCycle_1_35:
        "####" "#" 3 Offset(1,35)
        goto HoldCycleFinish

      HoldCycleFinish:
        TNT1 A 0 A_JumpIfInventory("SamsaraHereticMaceAttackCycle",0,1)
        goto HoldCycleStart
        MACE C 4 A_ReFire
        MACE DEFB 4
        goto Ready

      // Powered
      FirePowered:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Shell",5,1)
        goto Nothing
        TNT1 A 0 A_AlertMonsters
        MACE B 4 A_GiveInventory("CorvusShootCloneTrigger")
        MACE D 4 A_GiveInventory("SamsaraHereticMacePoweredAttackHandler")
        MACE B 4
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Shell",5,1)
        goto Nothing
        MACE A 8 A_ReFire
        goto Ready

      Nothing:
        TNT1 A 0 A_ClearReFire
        TNT1 A 0 A_SelectWeapon("Elven Wand")
        goto Deselect
    }
}

actor SamsaraHereticMaceAttackHandler1 : Trigger
{
    States
    {
      // Ammo check stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraTakingAmmo",1,1)
        goto PickupAttack
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Shell",1,"PickupTakeAmmo")
        stop

      // Ammo taking stuff
      PickupTakeAmmo:
        TNT1 A 0 A_TakeInventory("Shell",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("MaceFX12",random(-5,5),false,0,0,0,2) //A_FireMacePL1
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("MaceFX1Coop",random(-5,5),false,0,0,0,2)
        stop
    }
}

actor SamsaraHereticMaceAttackHandler2 : Trigger
{
    States
    {
      // Ammo check stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraTakingAmmo",1,1)
        goto PickupAttack
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Shell",1,"PickupTakeAmmo")
        stop

      // Ammo taking stuff
      PickupTakeAmmo:
        TNT1 A 0 A_TakeInventory("Shell",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("MaceFX22",0,false,0,2,0,10)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("MaceFX2Coop",0,false,0,2,0,10)
        stop
    }
}

actor MaceFX12 : MaceFX1 replaces MaceFX1
{
    -NOGRAVITY
    Gravity 0.125
    VSpeed 0.5
    DamageType "Mace"
    Damage 3 // Originally 2
    Decal BaronScorch
    //+STRIFEDAMAGE
    Obituary "$SAMSARA_CORVUS_OB_SLOT2_1"
}

actor MaceFX1Coop : MaceFX12
{
    Species "Player"
    +THRUSPECIES
}

actor MaceFX22 : MaceFX2 replaces MaceFX2
{
    -NOGRAVITY
    Gravity 0.125
    VSpeed 0.5
    DamageType "Mace"
    Damage 4 // Originally 6
    SeeSound "weapons/maceshoot"
    Decal BaronScorch
    //+STRIFEDAMAGE
    Obituary "$SAMSARA_CORVUS_OB_SLOT2_1"
}

actor MaceFX2Coop : MaceFX22
{
    Species "Player"
    +THRUSPECIES
}

actor MaceFX32 : MaceFX3 replaces MaceFX3
{
    DamageType "Mace"
    Damage 4
    Decal BaronScorch
    //+STRIFEDAMAGE
    Obituary "$SAMSARA_CORVUS_OB_SLOT2_1"
}

actor SamsaraHereticMacePoweredAttackHandler : Trigger
{
    States
    {
      // Ammo check stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Shell",5,"PickupTakeAmmo")
        stop

      // Ammo taking stuff
      PickupTakeAmmo:
        TNT1 A 0 A_TakeInventory("Shell",5)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("weapons/maceshoot",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("MaceFX42",0,false)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("MaceFX43",0,false)
        stop
    }
}

actor MaceFX42 : MaceFX4// replaces MaceFX4
{
    DamageType "Mace"
    Decal BFGScorch
    +FOILINVUL
    Obituary "$SAMSARA_CORVUS_OB_SLOT2_2"
    Radius 12
    Height 8
    Speed 7
    Damage (1000000)//18
    Gravity 0.125
    Projectile
    -NOGRAVITY
    +TELESTOMP
    +THRUGHOST
    -NOTELEPORT
    BounceType "Hexen" // Oddly enough, not Heretic bounce.
    BounceSound "weapons/macestop"
    BounceCount 8
    BounceFactor 1.0
    States
    {
      Spawn:
        FX02 E -1
        stop

      Death:
        TNT1 A 0 A_Stop
        FX02 C 4 A_PlaySound("weapons/maceexplode")
        FX02 GHIJ 4 Bright
        Stop
    }
}

actor MaceFX43 : MaceFX4
{
    +THRUSPECIES
    Species "Player"
    DamageType "Mace"
    Decal BFGScorch
    +FOILINVUL
}

actor MaceFX23 : MaceFX22
{
	Scale 1.5
	Radius 9
	Height 6
	Damage 7
	Speed 9
}

actor MaceFX23_Silent : MaceFX23 { SeeSound "" }
