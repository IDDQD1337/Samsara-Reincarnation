actor HereticGauntletHit : Boolean {}
actor SamsaraHereticGauntletsAttackCycle : Counter { Inventory.MaxAmount 3 }

actor "Gauntlets of the Necromancer" : Gauntlets
{
    Weapon.SlotNumber 1
    Weapon.SlotPriority 0
    +BLOODSPLATTER
    Weapon.SelectionOrder 2300
    +WEAPON.WIMPY_WEAPON
    +WEAPON.MELEEWEAPON
    +INVENTORY.UNDROPPABLE
    Weapon.Kickback 0
    Weapon.YAdjust 16
    Inventory.PickUpSound "heretic/weaponget"
    Weapon.UpSound "weapons/gauntletsactivate"
    Inventory.PickupMessage "You got the Gauntlets of the Necromancer!"
    Obituary "$SAMSARA_CORVUS_OB_SLOT1"
    Inventory.RestrictedTo "CorvusPlayer"
    Tag "Gauntlets of the Necromancer"
    States
    {
      Spawn:
        WGNT A -1
        Stop

      // Unpowered
      Ready:
		TNT1 A 0 A_TakeInventory("CorvusHoldingSlot")
        TNT1 A 0 A_JumpIfInventory("PowerHereticTome",1,"ReadyPowered")
		TNT1 A 0 A_JumpIfInventory("PowerHereticTomePerma",1,"ReadyPowered")
        GAUN A 1 A_WeaponReady
        loop

      Deselect:
        TNT1 A 0 A_JumpIfInventory("PowerHereticTome",1,"DeselectPowered")
		TNT1 A 0 A_JumpIfInventory("PowerHereticTomePerma",1,"DeselectPowered")
        GAUN A 1 A_Lower
        loop

      Select:
        TNT1 A 0 A_JumpIfInventory("PowerHereticTome",1,"SelectPowered")
		TNT1 A 0 A_JumpIfInventory("PowerHereticTomePerma",1,"SelectPowered")
        GAUN A 1 A_Raise
        loop

      Fire:
		TNT1 A 0 A_GiveInventory("CorvusShootCloneTrigger")
        TNT1 A 0 A_PlaySound("weapons/gauntletsuse",CHAN_WEAPON)
		TNT1 A 0 A_JumpIfInventory("PowerHereticTome",1,"FirePowered")
		GAUN B 4 A_JumpIfInventory("PowerHereticTomePerma",1,"FirePowered")
        GAUN C 4
      Hold:
        TNT1 A 0 A_TakeInventory("SamsaraHereticGauntletsAttackCycle") // reset
        TNT1 A 0 A_JumpIfInventory("PowerHereticTome",1,"HoldPowered")
		TNT1 A 0 A_JumpIfInventory("PowerHereticTomePerma",1,"HoldPowered")
      HoldCycleStart:
        TNT1 A 0 A_GiveInventory("SamsaraHereticGauntletsAttackCycle")
        TNT1 A 0 A_GiveInventory("SamsaraHereticGauntletsAttackHandler")
        GAUN F 0 A_JumpIfInventory("SamsaraHereticGauntletsAttackCycle",0,"HoldCycleJump")
        GAUN E 0 A_JumpIfInventory("SamsaraHereticGauntletsAttackCycle",2,"HoldCycleJump")
        GAUN D 0
      HoldCycleJump:
        "####" "#" 0 A_Jump(256,"HoldCycle_0_30","HoldCycle_0_31","HoldCycle_0_32","HoldCycle_1_30","HoldCycle_1_31","HoldCycle_1_32","HoldCycle_2_30","HoldCycle_2_31","HoldCycle_2_32")
      HoldCycle_0_30:
        "####" "#" 4 Bright Offset(0,30)
        goto HoldCycleFinish
      HoldCycle_0_31:
        "####" "#" 4 Bright Offset(0,31)
        goto HoldCycleFinish
      HoldCycle_0_32:
        "####" "#" 4 Bright Offset(0,32)
        goto HoldCycleFinish
      HoldCycle_1_30:
        "####" "#" 4 Bright Offset(1,30)
        goto HoldCycleFinish
      HoldCycle_1_31:
        "####" "#" 4 Bright Offset(1,31)
        goto HoldCycleFinish
      HoldCycle_1_32:
        "####" "#" 4 Bright Offset(1,32)
        goto HoldCycleFinish
      HoldCycle_2_30:
        "####" "#" 4 Bright Offset(2,30)
        goto HoldCycleFinish
      HoldCycle_2_31:
        "####" "#" 4 Bright Offset(2,31)
        goto HoldCycleFinish
      HoldCycle_2_32:
        "####" "#" 4 Bright Offset(2,32)
        goto HoldCycleFinish

      HoldCycleFinish:
        TNT1 A 0 A_JumpIfInventory("SamsaraHereticGauntletsAttackCycle",0,1)
        goto HoldCycleStart
        GAUN C 4 Offset(0,32) A_ReFire
        GAUN B 4 A_Light0
        goto Ready

      // Powered
      ReadyPowered:
        GAUN GHI 4 A_WeaponReady
        goto Ready

      DeselectPowered:
        GAUN G 1 A_Lower
        goto Deselect

      SelectPowered:
        GAUN G 1 A_Raise
        goto Select

      FirePowered:
        GAUN JK 4
        goto Hold

      HoldPowered:
      HoldCycleStartPowered:
        TNT1 A 0 A_GiveInventory("SamsaraHereticGauntletsAttackCycle")
        TNT1 A 0 A_GiveInventory("SamsaraHereticGauntletsPoweredAttackHandler")
        GAUN N 0 A_JumpIfInventory("SamsaraHereticGauntletsAttackCycle",0,"HoldCycleJumpPowered")
        GAUN M 0 A_JumpIfInventory("SamsaraHereticGauntletsAttackCycle",2,"HoldCycleJumpPowered")
        GAUN L 0
      HoldCycleJumpPowered:
        "####" "#" 0 A_Jump(256,"HoldCyclePowered_0_30","HoldCyclePowered_0_31","HoldCyclePowered_0_32","HoldCyclePowered_1_30","HoldCyclePowered_1_31","HoldCyclePowered_1_32","HoldCyclePowered_2_30","HoldCyclePowered_2_31","HoldCyclePowered_2_32")
      HoldCyclePowered_0_30:
        "####" "#" 4 Bright Offset(0,30)
        goto HoldCycleFinishPowered
      HoldCyclePowered_0_31:
        "####" "#" 4 Bright Offset(0,31)
        goto HoldCycleFinishPowered
      HoldCyclePowered_0_32:
        "####" "#" 4 Bright Offset(0,32)
        goto HoldCycleFinishPowered
      HoldCyclePowered_1_30:
        "####" "#" 4 Bright Offset(1,30)
        goto HoldCycleFinishPowered
      HoldCyclePowered_1_31:
        "####" "#" 4 Bright Offset(1,31)
        goto HoldCycleFinishPowered
      HoldCyclePowered_1_32:
        "####" "#" 4 Bright Offset(1,32)
        goto HoldCycleFinishPowered
      HoldCyclePowered_2_30:
        "####" "#" 4 Bright Offset(2,30)
        goto HoldCycleFinishPowered
      HoldCyclePowered_2_31:
        "####" "#" 4 Bright Offset(2,31)
        goto HoldCycleFinishPowered
      HoldCyclePowered_2_32:
        "####" "#" 4 Bright Offset(2,32)
        goto HoldCycleFinishPowered

      HoldCycleFinishPowered:
        TNT1 A 0 A_JumpIfInventory("SamsaraHereticGauntletsAttackCycle",0,1)
        goto HoldCycleStartPowered
        GAUN K 4 Offset(0,32) A_ReFire
        GAUN J 4 A_Light0
        goto Ready
    }
}

actor SamsaraHereticGauntletsAttackHandler : Trigger
{
    States
    {
      // Attack stuff
      Pickup:
        TNT1 A 0 A_TakeInventory("HereticGauntletHit")
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupCoop")
      PickupDM:
        TNT1 A 0 A_Saw("","",random(1,8)*2,"GauntletPuff1",SF_NORANDOM|SF_RANDOMLIGHTBOTH) //A_GauntletAttack(0)
        goto PickupFinish

      PickupCoop:
        TNT1 A 0 A_Saw("","",random(1,8)*2,"GauntletPuff1Coop",SF_NORANDOM|SF_RANDOMLIGHTBOTH)
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 A_JumpIfInventory("HereticGauntletHit",1,2)
        TNT1 A 0 A_PlaySound("weapons/gauntletson",CHAN_AUTO)
        stop
        TNT1 A 0 A_PlaySound("weapons/gauntletshit",CHAN_AUTO)
        stop
    }
}

actor GauntletPuff1Coop : GauntletPuff1
{
    Species "Player"
    +THRUSPECIES
    +MTHRUSPECIES
    +PUFFGETSOWNER
    States
    {
        Spawn:
        Melee:
        XDeath:
            TNT1 A 0
            TNT1 A 0 A_GiveToTarget("HereticGauntletHit")
        Crash:
            Goto Super::Spawn
    }
}

actor SamsaraHereticGauntletsPoweredAttackHandler : Trigger
{
    States
    {
      // Attack stuff
      Pickup:
        TNT1 A 0 A_TakeInventory("HereticGauntletHit")
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupCoop")
      PickupDM:
        TNT1 A 0 A_Saw("","",random(1,8)*2,"GauntletPuff2",SF_NORANDOM|SF_RANDOMLIGHTBOTH,256,1.40625,0,0.5) //A_GauntletAttack(1)
        goto PickupFinish

      PickupCoop:
        TNT1 A 0 A_Saw("","",random(1,8)*2,"GauntletPuff2Coop",SF_NORANDOM|SF_RANDOMLIGHTBOTH,256,1.40625,0,0.5)
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 A_JumpIfInventory("HereticGauntletHit",1,2)
        TNT1 A 0 A_PlaySound("weapons/gauntletson",CHAN_AUTO)
        stop
        TNT1 A 0 A_PlaySound("weapons/gauntletspowhit",CHAN_AUTO)
        stop
    }
}

actor GauntletPuff2Coop : GauntletPuff2
{
    Species "Player"
    +THRUSPECIES
    +MTHRUSPECIES
    +PUFFGETSOWNER
    States
    {
        Spawn:
        Melee:
        XDeath:
            TNT1 A 0
            TNT1 A 0 A_GiveToTarget("HereticGauntletHit")
        Crash:
            Goto Super::Spawn
    }
}

ACTOR GauntletPuff3
{
	Radius 20
	Height 16
	+NOBLOCKMAP
	+NOGRAVITY
	+NOTELEPORT
	+CANNOTPUSH
	+FORCEPAIN
	+NOINTERACTION
	+PUFFONACTORS
	RenderStyle Add
	States
	{
		Spawn:
		Melee:
		XDeath:
            TNT1 A 0
            TNT1 A 0 A_GiveToTarget("HereticGauntletHit")
			H105 PQR 12 Bright
			Stop
	}
}
