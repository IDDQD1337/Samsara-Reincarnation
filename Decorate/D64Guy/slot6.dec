actor " Plasma Rifle " : PlasmaRifle
{
    Weapon.SlotNumber 6
    +INVENTORY.UNDROPPABLE
    Inventory.RestrictedTo "Doom64GuyPlayer"		
    States
    {
      Ready:
        64PR B 0 A_PlaySoundEx("doom64guy/plasmaidle", "SoundSlot6", 1)
	  ReadyLoop:	
        64PR BCD 2 A_WeaponReady
        Goto ReadyLoop

      Deselect:
        64PR A 0 A_StopSound(6)
        64PR A 1 A_Lower
        Goto Deselect+1

      Select:
        64PR A 0 A_PlaySoundEx("doom64guy/plasmaidle", "SoundSlot6", 1)
        64PR A 1 A_Raise
        Goto Select+1

      Fire:
        TNT1 A 0 A_JumpIfInventory("CoopModeOn", 1, "CoopFire")
        64PR A 0 A_GunFlash
        64PR A 5 A_FireCustomMissile("Doom64PlasmaBall")
        TNT1 A 0 A_Refire
        goto ReadyLoop

      CoopFire:
        64PR A 0 A_GunFlash
        64PR A 5 A_FireCustomMissile("Doom64PlasmaBall2")
        TNT1 A 0 A_Refire
        goto ReadyLoop

      Flash:
        64RF A 4 Bright
        goto Lightdone

      Spawn:
        64PR Z -1
        Stop
    }
}

actor Doom64PlasmaBall : PlasmaBall
{
    Decal Doom64PlasmaScorch
    RenderStyle Normal
    Alpha 1
    Scale 0.75
    +BLOODLESSIMPACT
    SeeSound "doom64guy/plasmaf"
    DeathSound "doom64guy/plasmax"
    Obituary "%k \cpfried\c- %o \cpwith the Plasma Gun.\c-"
    DamageType "DoomPlasma"
    //Damage 5
    States
    {
      Spawn:
        64PB AB 3 bright
        loop

      Death:
        64PB CDEFGH 2 bright
        stop
    }
}

actor Doom64PlasmaBall2 : Doom64PlasmaBall { +THRUSPECIES Species "Player" }