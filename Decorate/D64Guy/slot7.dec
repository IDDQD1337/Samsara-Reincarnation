Actor " B.F.G. 9000 " : BFG9000
{
    //Weapon.SlotNumber 7
	Weapon.BobSpeed 0.8	
    DamageType "BFG"
    Obituary "%k \cpcaught\c- %o \cpin the B.F.G. 9000's radiation.\c-"
    +INVENTORY.UNDROPPABLE
    -WEAPON.NOAUTOFIRE
    Inventory.RestrictedTo "Doom64GuyPlayer"		
    States
    {
      Ready:
        64BF A 1 A_WeaponReady
        loop

      Deselect:
        64BF A 1 A_Lower
        loop

      Select:
        64BF A 1 A_Raise
        loop

      Fire:
        64BF A 0 A_PlayWeaponSound("doom64guy/bfgf")
        64BF A 20
        64BF A 10 A_GunFlash
        64BF B 5
        TNT1 A 0 A_JumpIfInventory("CoopModeOn", 1, "CoopMid")
        TNT1 A 0 A_JumpIfInventory("DoomNoBFGAim", 1, "NoAimMid")
        TNT1 A 0 A_FireCustomMissile("Doom64BFGBall")
        goto FireEnd

      NoAimMid:
        TNT1 A 0 A_JumpIfInventory("RuneSpread", 1, "NoAimSpread")
        TNT1 A 0 A_FireCustomMissile("BFGAmmoWaster")
        TNT1 A 0 A_SpawnItemEx("Doom64BFGBall", 0, 0, 32, 25)
        goto FireEnd

      NoAimSpread:
        TNT1 A 0 A_FireCustomMissile("BFGAmmoWaster")
        TNT1 A 0 A_SpawnItemEx("Doom64BFGBall", 0, 0, 32, 25, 0, 0, -15)
        TNT1 A 0 A_SpawnItemEx("Doom64BFGBall", 0, 0, 32, 25)
        TNT1 A 0 A_SpawnItemEx("Doom64BFGBall", 0, 0, 32, 25, 0, 0,  15)
        goto FireEnd

      CoopMid:
        TNT1 A 0 A_JumpIfInventory("DoomNoBFGAim", 1, "NoAimCoop")
        TNT1 A 0 A_FireCustomMissile("Doom64BFGBall2")
        goto FireEnd

      NoAimCoop:
        TNT1 A 0 A_JumpIfInventory("RuneSpread", 1, "NoAimCoopSpread")
        TNT1 A 0 A_FireCustomMissile("BFGAmmoWaster")
        TNT1 A 0 A_SpawnItemEx("Doom64BFGBall2", 0, 0, 32, 25)
        goto FireEnd

      NoAimCoopSpread:
        TNT1 A 0 A_FireCustomMissile("BFGAmmoWaster")
        TNT1 A 0 A_SpawnItemEx("Doom64BFGBall2", 0, 0, 32, 25, 0, 0, -15)
        TNT1 A 0 A_SpawnItemEx("Doom64BFGBall2", 0, 0, 32, 25)
        TNT1 A 0 A_SpawnItemEx("Doom64BFGBall2", 0, 0, 32, 25, 0, 0,  15)
        goto FireEnd

      FireEnd:
        64BF B 10
        64BF B 25 A_Refire
        goto Ready

      Flash:
        64BM A 11 bright // Apparently Zandronum has a different method for handling flash timing or something.
        64BM B 5 bright
        64BM C 5 bright
        Goto LightDone

      Spawn:
        64BF Z -1
        stop
    }
}

actor Doom64BFGColorFlash : CustomInventory
{
    +INVENTORY.ALWAYSPICKUP
    +INVENTORY.AUTOACTIVATE
    +QUIET
    -COUNTITEM
    -INVBAR

    States
    {
      Pickup:
      Use:
        TNT1 A 0 A_SetBlend("Green",0.45,20)
        stop
    }
}

actor Doom64BFGBall : BFGBall
{
    DamageType "BFG"
    Renderstyle Normal
    scale 0.75
    Decal BFGLightning
    +BRIGHT
    +DONTREFLECT
    +BLOODLESSIMPACT
    DeathSound "doom64guy/bfgx"
    Obituary "%k \cpvaporized\c- %o \cpwith the B.F.G. 9000.\c-"
    States
    {
      Spawn:
        64BB AB 2 bright
        loop
        
      Death:
        64BB C 0 A_GiveToTarget("Doom64BFGColorFlash",1)
        64BB C 0 A_TakeFromTarget("Doom64BFGColorFlash",1)
        64BB C 8 bright
        64BB D 7 bright A_FadeOut(0.142857)
        64BB E 0 bright A_FadeOut(0.142857)
        64BB E 3 bright A_BFGSpray("Doom64BFGSplash")
        64BB F 3 bright A_FadeOut(0.142857)
        64BB G 3 bright A_FadeOut(0.142857)
        64BB H 3 bright A_FadeOut(0.142857)
        stop
    }
}

actor Doom64BFGBall2 : Doom64BFGBall { +THRUSPECIES Species "Player" }

actor Doom64BFGSplash
{
    +NOBLOCKMAP
    +CLIENTSIDEONLY
    +NOGRAVITY
    RenderStyle Normal
    States
    {
      Spawn:
        64B2 ABCDEF 3 bright
        stop
    }
}