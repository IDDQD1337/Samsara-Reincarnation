actor "   Rocket Launcher   " : RocketLauncher
{
    Weapon.SlotNumber 5
    +INVENTORY.UNDROPPABLE
    -WEAPON.NOAUTOFIRE
    Inventory.RestrictedTo "Doom64GuyPlayer"		
    States
    {
      Ready:
        64ML A 1 A_WeaponReady
        Loop

      Deselect:
        64ML A 1 A_Lower
        Loop

      Select:
        64ML A 1 A_Raise
        Loop

      Fire:
        64ML B 8 A_GunFlash
        Goto FireMid

      FireMid:
        TNT1 A 0 A_JumpIfInventory("CoopModeOn", 1, "CoopMid")
        TNT1 A 0 A_FireCustomMissile("Doom64Rocket")
        goto Animation

      CoopMid:
        TNT1 A 0 A_FireCustomMissile("Doom64Rocket2")
        goto Animation
      
      Animation:
        64ML B 0 A_Recoil(2)
        64ML B 0 ACS_ExecuteAlways(312, 0, 5, 4, 15)
        64ML B 13
        64ML B 0 A_Refire
        Goto Ready
        
      Flash:
        64MM A 2 bright
        64MM BCD 4 bright
        Goto LightDone

      Spawn:
        64ML Z -1
        Stop
    }
}

actor Doom64Rocket : Rocket
{
    Decal Scorch
    SeeSound "doom64guy/rocklf"
    DeathSound "doom64guy/rocklx"
    -FIREDAMAGE
    -DEHEXPLOSION
    -ROCKETTRAIL
    +BLOODLESSIMPACT
    DamageType "Explosive"
    Scale 0.75
    Obituary "%k \cpsplattered\c- %o\cp's brains with the Rocket Launcher.\c-"
    States
    {
      Spawn:
        64MI AA 2 bright A_SpawnItemEx("Doom64RocketSmokeTrail")
        Loop

      Death:
        64MI B 8 bright A_Explode
        64MI C 6 bright A_FadeOut(0.2)
        64MI D 3 bright A_FadeOut(0.2)
        64MI EF 3 bright A_FadeOut(0.2)
        Stop
    }
}

actor Doom64Rocket2 : Doom64Rocket { +THRUSPECIES Species "Player" }

actor Doom64RocketSmokeTrail : RocketSmokeTrail
{
    +CLIENTSIDEONLY
    RenderStyle Translucent
    Alpha 0.5
    VSpeed 0
    Scale 0.75
    States
    {
    Spawn:
        64RS ABCDE 2 bright
        stop
    }
}
