actor "   Shotgun   " : Shotgun
{
    Weapon.AmmoType "Shell"
    Decal BulletChip
    Weapon.SlotNumber 2
    +THRUGHOST
    +INVENTORY.UNDROPPABLE
    AttackSound ""
    Obituary "%k \cpblasted\c- %o \cpwith the Shotgun.\c-"
    Inventory.RestrictedTo "Doom64GuyPlayer"		
    States
    {
      Ready:
        64SG A 1 A_WeaponReady
        Loop

      Deselect:
        64SG A 1 A_Lower
        Loop

      Select:
        64SG A 1 A_Raise
        Loop

      Fire:
        64SG A 0 A_PlayWeaponSound("doom64guy/shotgf")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn", 1, "CoopFire")
        TNT1 A 0 A_FireBullets (5.6, 0, 7, 5, "Doom64BulletPuff")
        goto Animation

      CoopFire:
        TNT1 AAAAAA 0 A_FireCustomMissile("Doom64Bullet", random(-560, 560)/100.00, 0, 0, 1, 0)
        TNT1 A      0 A_FireCustomMissile("Doom64Bullet", random(-560, 560)/100.00, 1, 0, 1, 0)
        goto Animation

      Animation:
        64SG A 0 A_GunFlash
        64SG A 0 ACS_ExecuteAlways(312, 0, 4, 3, 17)
        64SG A 5
        64SG C 21
        64SG B 5
        64SG A 7
        64SG A 7 A_ReFire
        Goto Ready

      Flash:
        64SM A 2 bright
        64SM A 2 bright
        Goto LightDone

      Spawn:
        64SG Z -1
        Stop
    }
}
