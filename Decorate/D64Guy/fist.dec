actor "  Fist  " : Weapon
{
    Weapon.SlotNumber 0
    Weapon.SelectionOrder 3700
    Weapon.Kickback 100
    +INVENTORY.UNDROPPABLE
    +WEAPON.WIMPY_WEAPON
    +WEAPON.MELEEWEAPON
    +WEAPON.NOALERT
    Obituary "%k \cpbeat\c- %o \cpsenseless.\c-"
    Inventory.RestrictedTo "Doom64GuyPlayer"	
    States
    {
      Ready:
        64FS A 1 A_WeaponReady
        loop

      Deselect:
        64FS A 1 A_Lower
        loop

      Select: 
        64FS A 1 A_Raise
        loop

      Fire:
        64FS E 0 A_JumpIfInventory("PunchDrunkZerk", 1, "StartBerserk")
        64FS E 0 A_JumpIfInventory("PowerChainsawBerserk", 1, "StartBerserk")
        64FS B 5
        Goto Fire2
		
      StartBerserk:
        64FS B 4
        Goto Fire2

      Fire2:
        64FS E 0 A_JumpIfInventory("PunchDrunkZerk", 1, "FirePDBerserk")
        64FS E 0 A_JumpIfInventory("PowerChainsawBerserk", 1, "FireBerserk")
        64FS E 0 A_CustomPunch(random(1, 10)*2, 1, 0, "Doom64PunchPuff")
        goto FireEnd

      FireBerserk:
        64FS E 0 A_CustomPunch(random(10, 20)*12, 1, 0, "Doom64PunchPuff2")
        goto FireEndBerserk

      FirePDBerserk:
        64FS E 0 A_CustomPunch(random(10, 20)*4, 1, 0, "Doom64PunchPuff2")
        goto FireEndBerserk

      FireEnd:
        64FS C 5
        64FS D 5
        64FS C 5
        64FS B 5 A_ReFire
        goto Ready
		
      FireEndBerserk:
        64FS C 4
        64FS D 5
        64FS C 4
        64FS B 5 A_ReFire
        goto Ready
    }
}

actor Doom64PunchPuff : Doom64BulletPuff
{
    DamageType "DoomFist"
    States
    {
      Spawn:
      Crash:
        TNT1 A 0 A_PlaySound("doom64guy/wallpunch2")
        64PF CDEF 4
        Stop

      Melee:
        TNT1 A 0 A_PlaySound("doom64guy/punch")
        stop
    }
}

actor Doom64PunchPuff2 : Doom64BulletPuff
{
    DamageType "DoomFist"
    States
    {
      Spawn:
      Crash:
        TNT1 A 0 A_PlaySound("doom64guy/wallpunch")
        64PF CDEF 4
        stop

      Melee:
        TNT1 A 0 A_PlaySound("doom64guy/punch2")
        stop
    }
}