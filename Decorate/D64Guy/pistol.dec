actor "  Pistol  " : Weapon// : Pistol
{
    Weapon.SelectionOrder 1900
    Decal BulletChip
    Weapon.SlotNumber 1  
    Weapon.SlotPriority 1
	Weapon.BobSpeed 0.8
    Weapon.AmmoUse 0
    Weapon.AmmoGive 0
    Weapon.AmmoType ""
    +WEAPON.WIMPY_WEAPON
    +INVENTORY.UNDROPPABLE
    DamageType Pistol
    Inventory.Pickupmessage "i eat you too"
    Obituary "%k \cpcapped\c- %o \cpwith a pistol.\c-"
    Inventory.RestrictedTo "Doom64GuyPlayer"		
    States
    {
      Ready:
        64PS A 1 A_WeaponReady
        Loop

      Deselect:
        64PS A 1 A_Lower
        Loop

      Select:
        64PS A 1 A_Raise
        Loop

      Fire:
		TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,"CheckAmmo")
	  Fire2:		  
        TNT1 A 0 A_JumpIfInventory("CoopModeOn", 1, "CoopFire")
        TNT1 A 0 A_FireBullets (5.6, 0, 1, 5, "Doom64BulletPuff2")
        TNT1 A 0 A_PlayWeaponSound("doom64guy/pistol")
        goto Animation

      CoopFire:
        TNT1 A 0 A_FireCustomMissile("Doom64Bullet2", 0, 1, 0, 1)
        TNT1 A 0 A_PlayWeaponSound("doom64guy/pistol")
        goto Animation
		
      Animation:		
        64PS B 4 BRIGHT A_GunFlash
        64PS C 9
        64PS B 3 A_ReFire
        Goto Ready

      Hold:
		TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,"CheckAmmoHold")
	  Hold2:
        TNT1 A 0 A_JumpIfInventory("CoopModeOn", 1, "CoopHold")
        TNT1 A 0 A_FireBullets (5.6, 0, -1, 5, "Doom64BulletPuff2")
        TNT1 A 0 A_PlayWeaponSound("doom64guy/pistol")
        Goto Animation
        
      CoopHold:
        TNT1 A 0 A_FireCustomMissile("Doom64Bullet2", random(-560,560)/100.00, 1, 0, 1)
        TNT1 A 0 A_PlayWeaponSound("doom64guy/pistol")
        Goto Animation
        
      Flash:
        64PM A 4 Bright
        TNT1 A 2
        Goto LightDone

	CheckAmmo:
		TNT1 A 0 A_JumpIfInventory("Clip",1,2)
		TNT1 A 0 A_SelectWeapon("  Fist  ")
		goto Deselect
		TNT1 A 0 A_TakeInventory("Clip",1)
		goto Fire2	

	CheckAmmoHold:
		TNT1 A 0 A_JumpIfInventory("Clip",1,2)
		TNT1 A 0 A_SelectWeapon("  Fist  ")
		goto Deselect
		TNT1 A 0 A_TakeInventory("Clip",1)
		goto Hold2	

      /*Spawn:
        64PS Z -1
        Stop*/
    }
}

actor Doom64BulletPuff2 : Doom64BulletPuff { DamageType Pistol }

actor Doom64Bullet2: DoomBullet2
{
    Decal BulletChip
    States
    {
	  Crash:
      Death:
        TNT1 A 1 A_SpawnItem("Doom64BulletPuff2")
        stop
    }
}
