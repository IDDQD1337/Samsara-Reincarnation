actor DescentFusion : DescentWeaponPrimer
{
	Weapon.SlotNumber 5
	Weapon.AmmoType1 "Cell"
	Weapon.AmmoType2 "RocketAmmo"
	Weapon.AmmoUse1 5
	Weapon.AmmoUse2 1
	Weapon.AmmoGive1 50
	Weapon.AmmoGive2 3
	Inventory.PickupMessage "Fusion Cannon and Mega Missiles!"
	Tag "Fusion Cannon and Mega Missiles"
	Inventory.RestrictedTo "DescentPyroGX"
	+FLOATBOB
	+WEAPON.EXPLOSIVE
	States
	{
		Spawn:
			DS50 ABCDEFGHIJKLMNO 2
			Loop
		SetPrimary:
			TNT1 A 0 A_TakeInventory("DescentPrimaryCounter",65536)
			TNT1 A 0 A_GiveInventory("DescentPrimaryCounter",5)
			Goto Super::SetPrimary
		SetSecondary:
			TNT1 A 0 A_TakeInventory("DescentMissileCamera",256)
			TNT1 A 0 A_TakeInventory("DescentSecondaryCounter",65536)
			TNT1 A 0 A_GiveInventory("DescentSecondaryCounter",5)
			Goto Super::SetSecondary
		Fire:
			TNT1 A 1
			Goto Ready
	}
}

actor DescentFusionProjectile
{
	Damage (50)
	Speed 32
	Projectile
	Renderstyle Add
	+FORCEXYBILLBOARD
	+FORCERADIUSDMG
	+BRIGHT
	var int user_charge;
	States
	{
		Spawn:
			DS00 A 1
			Loop
		Death:
			DS00 A 0 A_SetUserVar("user_charge",CallACS("DescentChargeAmount"))
			DS00 A 0 A_TakeFromTarget("DescentChargeToken",256)
			DS00 A 0 A_PlaySound("Descent/Laser/Explode",CHAN_5)
			DS00 A 0 A_Explode(50+user_charge*10,120)
			DS00 A 0 A_SpawnItemEx("DescentFusionEffect")
			Stop
	}
}

actor DescentFusionProjectileCoop : DescentFusionProjectile 
{ 
	+THRUSPECIES 
	Species "Player" 
	States
	{
		Death:
			DS00 A 0 A_SetUserVar("user_charge",CallACS("DescentChargeAmount"))
			DS00 A 0 A_TakeFromTarget("DescentChargeToken",256)
			DS00 A 0 A_PlaySound("Descent/Laser/Explode",CHAN_5)
			DS00 A 0 A_Explode(75+user_charge*25,120)
			DS00 A 0 A_SpawnItemEx("DescentFusionEffect")
			Stop
	}	
}

actor DescentFusionEffect
{
	+NOINTERACTION
	+NOGRAVITY
	+FORCEXYBILLBOARD
	+BRIGHT
	Scale 2.0
	States
	{
		Spawn:
			DS05 ABCDEFGHIJ 2
			Stop
	}
}
