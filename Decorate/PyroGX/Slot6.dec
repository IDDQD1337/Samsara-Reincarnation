Actor DescentHelix : DescentWeaponPrimer
{
	Weapon.SlotNumber 6
	Weapon.AmmoType1 "Clip"
	Weapon.AmmoType2 "Shell"
	Weapon.AmmoUse1 5
	Weapon.AmmoUse2 2
	Weapon.AmmoGive1 50
	Weapon.AmmoGive2 2
	Inventory.PickupMessage "Helix Cannon and Mercury Missiles!"
	Tag "Helix Cannon and Mercury Missiles"
	Inventory.RestrictedTo "DescentPyroGX"
	+FLOATBOB
	States
	{
		Spawn:
			DS52 ABCDEFGHIJKLMNOP 2
			Loop
		SetPrimary:
			TNT1 A 0 A_TakeInventory("DescentPrimaryCounter",65536)
			TNT1 A 0 A_GiveInventory("DescentPrimaryCounter",6)
			Goto Super::SetPrimary
		SetSecondary:
			TNT1 A 0 A_TakeInventory("DescentMissileCamera",256)
			TNT1 A 0 A_TakeInventory("DescentSecondaryCounter",65536)
			TNT1 A 0 A_GiveInventory("DescentSecondaryCounter",6)
			Goto Super::SetSecondary
		Fire:
			TNT1 A 1
			Goto Ready
	}
}

Actor DescentHelixProjectile : DescentLaserProjectile
{
	Damage (10)
	Speed 20
	Scale 0.1
	States
	{
		Spawn:
			DS08 ABC 1
			Loop
		Death:
			DS00 A 0 A_PlaySound("Descent/Laser/Explode",CHAN_5, 0.3)
			DS00 A 0 A_SpawnItemEx("DescentHelixPuff")
			Stop
	}
}
Actor DescentHelixProjectileCoop : DescentHelixProjectile { Damage (18) +THRUSPECIES Species "Player" }

Actor DescentHelixRotation : DescentCounter { Inventory.MaxAmount 3600 }

Actor DescentMercuryMissile : FastProjectile
{
	Damage (0)
	Radius 4
	Height 8
	Speed 80
	+FORCEXYBILLBOARD
	+FORCERADIUSDMG
	+SKYEXPLODE
	+BRIGHT
	States
	{
		Spawn:
			DS00 A 0
			DS00 A 0 A_GiveToTarget("DescentMissileCamera",1)
			DS00 A 0 ACS_NamedExecuteWithResult("DescentMissileCamera")
		SpawnLoop:
			DS00 A 1 A_SpawnItemEx("DescentMercuryMissileTrail")
			Loop
		Death:
			DS00 A 0 A_RemoveMaster
			DS00 A 0 A_Explode(110,64,0,1)
			DS00 A 0 A_GiveToTarget("DescentMissileStaticGiver",1)
			DS00 A 0 A_TakeFromTarget("DescentMissileCamera",1)
			DS00 A 0 A_PlaySound("Descent/Laser/Explode",CHAN_5)
			DS00 A 0 A_SpawnItemEx("DescentExplosion",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
			Stop
	}
}
Actor DescentMercuryMissileCoop : DescentMercuryMissile { +THRUSPECIES Species "Player" }

Actor DescentMercuryMissileTrail
{
	+NOINTERACTION
	+NOGRAVITY
	+FORCEXYBILLBOARD
	+BRIGHT
	Scale 0.5
	States
	{
		Spawn:
			TNT1 A 2
			DS01 ABCDE 2
			Stop
	}
}