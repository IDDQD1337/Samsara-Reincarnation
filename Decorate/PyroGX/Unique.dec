Actor DescentPlasma : DescentWeaponPrimer
{
	Weapon.SlotNumber 8
	Weapon.AmmoType1 "Clip"
	Weapon.AmmoType2 "Shell"
	Weapon.AmmoUse1 1
	Weapon.AmmoUse2 1
	Weapon.AmmoGive1 50
	Weapon.AmmoGive2 4
	Inventory.PickupMessage "Plasma Cannon and Proximity Bombs!"
	Tag "Plasma Cannon and Proximity Bombs"
	Inventory.RestrictedTo "DescentPyroGX"
	+FLOATBOB
	States
	{
		Spawn:
			DS56 ABCDEFGHIJKLMNO 2
			Loop
		SetPrimary:
			TNT1 A 0 A_TakeInventory("DescentPrimaryCounter",65536)
			TNT1 A 0 A_GiveInventory("DescentPrimaryCounter",8)
			Goto Super::SetPrimary
		SetSecondary:
			TNT1 A 0 A_TakeInventory("DescentMissileCamera",256)
			TNT1 A 0 A_TakeInventory("DescentSecondaryCounter",65536)
			TNT1 A 0 A_GiveInventory("DescentSecondaryCounter",8)
			Goto Super::SetSecondary
		Fire:
			TNT1 A 1
			Goto Ready
	}
}

Actor DescentPlasmaProjectile : DescentLaserProjectile
{
	Damage (15)
	Speed 20
	Projectile
	Scale 0.25
	+FORCEXYBILLBOARD
	States
	{
		Spawn:
			DS07 ABCDEF 1 Bright
			Loop
		Death:
			DS00 A 0 A_PlaySound("Descent/Laser/Explode",CHAN_5)
			DS00 A 0 A_SpawnItemEx("DescentPlasmaPuff")
			Stop
	}
}
Actor DescentPlasmaProjectileCoop : DescentPlasmaProjectile { +THRUSPECIES Species "Player" }

Actor DescentProximityBomb
{
	Height 32
	Radius 16
	Health 100
	Mass 65536
	+NOBLOOD
	+SHOOTABLE
	+FRIENDLY
	+ISMONSTER
	-COUNTKILL
	+NOGRAVITY
	+LOOKALLAROUND
	+FORCEXYBILLBOARD
	+DONTFALL
	+FORCERADIUSDMG
	States
	{
		Spawn:
			DS15 A 0 NoDelay A_CopyFriendliness(AAPTR_Master)
			DS15 A 35 
		SpawnLoop:
			DS15 ABCDEFGHIJ 2 A_LookEx(0,0,64,0,0)
			Loop
		See:
		Death:
			DS02 A 0 A_PlaySound("Descent/Laser/Explode",CHAN_5)
			DS02 A 0 A_CustomMissile("DescentMineExplosion")
			DS00 A 0 A_SpawnItemEx("DescentExplosion",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
			Stop
	}
}

Actor DescentPhoenix : DescentWeaponPrimer
{
	Weapon.SlotNumber 9
	Weapon.AmmoType1 "Cell"
	Weapon.AmmoType2 "Shell"
	Weapon.AmmoUse1 1
	Weapon.AmmoUse2 1
	Weapon.AmmoGive1 50
	Weapon.AmmoGive2 4
	Inventory.PickupMessage "Phoenix Cannon and Smart Mines!"
	Tag "Phoenix Cannon and Smart Mines"
	Inventory.RestrictedTo "DescentPyroGX"
	+WEAPON.AMMO_OPTIONAL
	+FLOATBOB
	States
	{
		Spawn:
			DS55 ABCDEFGHIJKLMNOP 2
			Loop		
		SetPrimary:
			TNT1 A 0 A_TakeInventory("DescentPrimaryCounter",65536)
			TNT1 A 0 A_GiveInventory("DescentPrimaryCounter",9)
			Goto Super::SetPrimary
		SetSecondary:
			TNT1 A 0 A_TakeInventory("DescentMissileCamera",256)
			TNT1 A 0 A_TakeInventory("DescentSecondaryCounter",65536)
			TNT1 A 0 A_GiveInventory("DescentSecondaryCounter",9)
			Goto Super::SetSecondary
		Fire:
			TNT1 A 1
			Goto Ready
	}
}

Actor DescentPhoenixProjectile : DescentLaserProjectile
{
	Damage (15)
	Speed 20
	Projectile
	DamageType "Fire"
	BounceType "Doom"
	BounceCount 3
	Scale 0.25
	+FORCEXYBILLBOARD
	States
	{
		Spawn:
			DS14 ABC 1 Bright
			Loop
		Death:
			DS00 A 0 A_PlaySound("Descent/Laser/Explode",CHAN_5)
			DS00 A 0 A_SpawnItemEx("DescentGaussPuff")
			Stop
	}
}
Actor DescentPhoenixProjectileCoop : DescentPhoenixProjectile { +THRUSPECIES Species "Player" }

Actor DescentSmartMine
{
	Height 32
	Radius 16
	Health 100
	Mass 65536
	+NOBLOOD
	+SHOOTABLE
	+FRIENDLY
	+ISMONSTER
	-COUNTKILL
	+NOGRAVITY
	+LOOKALLAROUND
	+FORCEXYBILLBOARD
	+DONTFALL
	+FORCERADIUSDMG
	States
	{
		Spawn:
			DS15 A 0 NoDelay A_CopyFriendliness(AAPTR_Master)
			DS16 A 35
		SpawnLoop:
			DS16 ABCDE 2 A_LookEx(0,0,64,0,0)
			Loop
		See:
		Death:
			DS02 A 0 A_PlaySound("Descent/Laser/Explode",CHAN_5)
			DS02 A 0 A_CustomMissile("DescentMineExplosion")
			DS02 A 0 A_Jump(256,1,2,3,4,5,6,7)
			DS02 AAAAAAAAAAAA 0 A_CustomMissile("DescentSmartMineBomblet",0,0,random(0,359),CMF_TRACKOWNER|CMF_AIMDIRECTION,random(0,359))
			DS00 A 0 A_SpawnItemEx("DescentExplosion",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
			Stop
	}
}
Actor DescentSmartMineCoop : DescentSmartMine
{
	States
	{
		See:
		Death:
			DS02 A 0 A_PlaySound("Descent/Laser/Explode",CHAN_5)
			DS02 A 0 A_CustomMissile("DescentMineExplosion")
			DS02 A 0 A_Jump(256,1,2,3,4,5,6,7)
			DS02 AAAAAAAAAAAA 0 A_CustomMissile("DescentSmartMineBombletCoop",0,0,random(0,359),CMF_TRACKOWNER|CMF_AIMDIRECTION,random(0,359))
			DS00 A 0 A_SpawnItemEx("DescentExplosion",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
			Stop
	}
}

Actor DescentMineExplosion
{
	Projectile
	Speed 0
	Damage (0)
	+BLOODLESSIMPACT
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 1 A_Explode(200,150,XF_HURTSOURCE,1)
			Stop
	}
}

Actor DescentSmartMineBomblet : DescentMissile 
{
	ExplosionDamage 30
	ExplosionRadius 50
	Speed 8
	+SEEKERMISSILE
	-SCREENSEEKER
	States
	{
		Spawn:
			DS17 A 0
			DS17 A 0 A_TransferPointer(AAPTR_Target,AAPTR_Default,AAPTR_FriendPlayer,AAPTR_Target)
			DS17 A 0 A_CopyFriendliness(AAPTR_Target)
			DS17 A 0 A_ReArrangePointers(AAPTR_Target,AAPTR_Master,AAPTR_NULL)
		SpawnLoop:
			DS17 ABCDEF 1 Bright A_SeekerMissile(8,8,SMF_LOOK|SMF_PRECISE)
			Loop
		Death:
			//DS00 A 0 A_SetScale(1.0, 1.0)
			DS00 A 0 A_Explode(-1,-1,0,1)
			DS00 A 0 A_PlaySound("Descent/Laser/Explode",CHAN_5)
			DS18 ABCDEFGH 2 Bright
			Stop
	}
}
Actor DescentSmartMineBombletCoop : DescentSmartMineBomblet { +THRUSPECIES Species "Player" }

Actor DescentQuadLaserPickup : CustomInventory
{
	-INVENTORY.INVBAR
	+FLOATBOB
	States
	{
		Spawn:
			DS57 ABCDEFGHIJKLMNO 2
			Loop
		Pickup:
			TNT1 A 0 A_GiveInventory("DescentQuadLaser",1)
			stop
		Use:
			TNT1 A 0
			fail
	}
}