Actor DescentSpreadFire : DescentWeaponPrimer
{
	Weapon.SlotNumber 4
	Weapon.AmmoType1 "Clip"
	Weapon.AmmoType2 "Shell"
	Weapon.AmmoUse1 2
	Weapon.AmmoUse2 1
	Weapon.AmmoGive1 50
	Weapon.AmmoGive2 4
	Inventory.PickupMessage "Spreadfire Cannon and Guided Missiles!"
	Tag "Spreadfire Cannon and Guided Missiles"
	Inventory.RestrictedTo "DescentPyroGX"
	+FLOATBOB
	States
	{
		Spawn:
			DS58 ABCDEFGHIJKLMNO 2
			Loop
		SetPrimary:
			TNT1 A 0 A_TakeInventory("DescentPrimaryCounter",65536)
			TNT1 A 0 A_GiveInventory("DescentPrimaryCounter",4)
			Goto Super::SetPrimary
		SetSecondary:
			TNT1 A 0 A_TakeInventory("DescentMissileCamera",256)
			TNT1 A 0 A_TakeInventory("DescentSecondaryCounter",65536)
			TNT1 A 0 A_GiveInventory("DescentSecondaryCounter",4)
			Goto Super::SetSecondary
		Fire:
			TNT1 A 1
			Goto Ready
	}
}

Actor DescentSpreadFireProjectile : DescentLaserProjectile
{
	Damage (10)
	Speed 20
	Projectile
	Scale 0.1
	+FORCEXYBILLBOARD
	States
	{
		Spawn:
			DS03 A 1
			Loop
		Death:
			DS00 A 0 A_PlaySound("Descent/Laser/Explode",CHAN_5, 0.5)
			DS00 A 0 A_SpawnItemEx("DescentSpreadFirePuff")
			Stop
	}
}
Actor DescentSpreadFireProjectileCoop : DescentSpreadFireProjectile { +THRUSPECIES Species "Player" }

Actor DescentGuidedController : DescentCounter {}

Actor DescentGuidedMissile : DescentMissile 
{
	ExplosionDamage 120
	ExplosionRadius 120
	Speed 12
	+SCREENSEEKER
	+SEEKERMISSILE
	States
	{
		Spawn:
			DS00 A 0
			DS00 A 0 A_GiveToTarget("DescentMissileCamera",1)
			DS00 A 0 A_GiveToTarget("DescentGuidedController",1)
			DS00 A 0 ACS_NamedExecuteWithResult("DescentMissileCamera")
			DS00 A 0 ACS_NamedExecuteWithResult("DescentGuidedMissile")
			DS00 A 0 A_GiveInventory("DescentGuidedController",1)
		SpawnLoop:
			DS00 A 1
			DS00 A 0 A_JumpIfInTargetInventory("DescentGuidedController",1,1)
			Goto SpawnHoming
			DS00 A 0
			Loop
		SpawnHoming:
			DS00 A 0 A_TakeInventory("DescentGuidedController",1)
			DS00 A 1 A_SeekerMissile(2,2,SMF_LOOK|SMF_PRECISE)
			Loop
		Death:
			DS00 A 0 A_ChangeVelocity(0,0,0,CVF_REPLACE)
			DS00 A 0 A_RemoveMaster
			DS00 A 0 A_Explode(-1,-1,0,1)
			DS00 A 0 A_GiveToTarget("DescentMissileStaticGiver",1)
			DS00 A 0 A_TakeFromTarget("DescentMissileCamera",1)
			DS00 A 0 A_PlaySound("Descent/Laser/Explode",CHAN_5)
			DS00 A 0 A_SpawnItemEx("DescentExplosion",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
			Stop
		StopSky:
			TNT1 A 1
			Stop	
	}
}
Actor DescentGuidedMissileCoop : DescentGuidedMissile { +THRUSPECIES Species "Player" }
