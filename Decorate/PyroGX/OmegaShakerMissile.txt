Actor DescentOmega : DescentWeaponPrimer
{
	Weapon.SlotNumber 7
	Weapon.AmmoType1 "Cell"
	Weapon.AmmoType2 "RocketAmmo"
	Weapon.AmmoUse1 2
	Weapon.AmmoUse2 5
	Weapon.AmmoGive1 50
	Weapon.AmmoGive2 5
	Inventory.PickupMessage "Omega Cannon and Earthshaker Missiles!"
	Tag "Omega Cannon and Earthshaker Missiles"
	Inventory.RestrictedTo "DescentPyroGX"
	+FLOATBOB
	States
	{
		Spawn:
			DS54 ABCDEFGHIJKLMNO 2
			Loop
		SetPrimary:
			TNT1 A 0 A_TakeInventory("DescentPrimaryCounter",65536)
			TNT1 A 0 A_GiveInventory("DescentPrimaryCounter",7)
			Goto Super::SetPrimary
		SetSecondary:
			TNT1 A 0 A_TakeInventory("DescentMissileCamera",256)
			TNT1 A 0 A_TakeInventory("DescentSecondaryCounter",65536)
			TNT1 A 0 A_GiveInventory("DescentSecondaryCounter",7)
			Goto Super::SetSecondary
		Fire:
			TNT1 A 1
			Goto Ready
	}
}

Actor DescentOmegaProjectile : FastProjectile
{
	Damage (50)
	Speed 256
	Projectile
	+SEEKERMISSILE
	+SCREENSEEKER
	+FORCEXYBILLBOARD
	+SKYEXPLODE
	States
	{
		Spawn:
			TNT1 AAA 1 A_SeekerMissile(90,90,SMF_PRECISE|SMF_LOOK,256,3)
			Goto Death+1
		Death:
			TNT1 A 0 A_SpawnItemEx("DescentOmegaPuff")
			TNT1 A 1 ACS_NamedExecuteWithResult("DescentOmegaLaser")
			Stop
	}
}

Actor DescentOmegaEffect
{
	+NOINTERACTION
	+NOGRAVITY
	+FORCEXYBILLBOARD
	Scale 0.25
	States
	{
		Spawn:
			DS10 ABC 2 Bright
			Stop
	}
}

Actor DescentShakerMissile : DescentMissile 
{
	ExplosionDamage 500
	ExplosionRadius 250
	Radius 8
	Height 16
	Speed 12
	+SCREENSEEKER
	+SEEKERMISSILE
	States
	{
		Spawn:
			DS00 A 0
			DS00 A 0 A_GiveToTarget("DescentMissileCamera",1)
			DS00 A 0 ACS_NamedExecuteWithResult("DescentMissileCamera")
		SpawnLoop:
			DS00 A 1 Bright A_SpawnItemEx("DescentMercuryMissileTrail")
			Loop
		Death:
			DS00 A 0 A_SetScale(2.0,2.0)
			DS00 A 0 A_RemoveMaster
			DS00 A 0 A_Quake(9,35,0,1024,"Descent/Quake")
			DS00 A 0 A_Explode(-1,-1,0,1)
			DS00 A 0 A_GiveToTarget("DescentMissileStaticGiver",1)
			DS00 A 0 A_TakeFromTarget("DescentMissileCamera",1)
			DS00 A 0 A_PlaySound("Descent/EarthShaker/Explode",CHAN_5)
			DS00 AAAAAA 0 A_CustomMissile("DescentShakerBomblet",0,0,random(0,359),CMF_TRACKOWNER|CMF_AIMDIRECTION,random(0,359))
			DS12 ABCDEFGHIJKLM 2 Bright A_RadiusGive("DescentShakerGiver",512,RGF_PLAYERS|RGF_NOSIGHT,1)
			Stop	
	}
}

Actor DescentShakerBomblet : DescentMissile 
{
	ExplosionDamage 500
	ExplosionRadius 250
	Speed 12
	+SCREENSEEKER
	+SEEKERMISSILE
	States
	{
		Spawn:
			DS00 A 1 Bright A_SeekerMissile(8,8,SMF_LOOK|SMF_PRECISE)
			DS00 A 0 A_SpawnItemEx("DescentMercuryMissileTrail")
			Loop
		Death:
			DS00 A 0 A_SetScale(2.0,2.0)
			DS00 A 0 A_Quake(9,35,0,1024,"Descent/Quake")
			DS00 A 0 A_Explode(-1,-1,0,1)
			DS00 A 0 A_PlaySound("Descent/EarthShaker/Explode",CHAN_5)
			DS12 ABCDEFGHIJKLM 2 Bright A_RadiusGive("DescentShakerGiver",512,RGF_PLAYERS|RGF_NOSIGHT,1)
			Stop
	}
}

Actor DescentShakerGiver : Trigger
{
	States
	{
		Pickup:
			TNT1 A 0 A_SetPitch(pitch+frandom(-10.0,10.0))
			TNT1 A 0 A_SetAngle(angle+frandom(-5.0,5.0))
			stop
	}
}
