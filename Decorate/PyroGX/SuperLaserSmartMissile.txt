Actor DescentSuperLaser : DescentWeaponPrimer
{
	Weapon.SlotNumber 1
	Weapon.AmmoType1 "Clip"
	Weapon.AmmoType2 "Shell"
	Weapon.AmmoUse1 1
	Weapon.AmmoUse2 1
	Weapon.AmmoGive1 50
	Weapon.AmmoGive2 4
	Inventory.PickupMessage "Super Laser Cannon and Smart Missiles!"
	Tag "Super Laser Cannon and Smart Missiles"
	+FLOATBOB
	States
	{
		Spawn:
			DS60 ABCDEFGHIJKLMNO 2
			Loop
		SetPrimary:
			TNT1 A 0 A_TakeInventory("DescentPrimaryCounter",65536)
			TNT1 A 0 A_GiveInventory("DescentPrimaryCounter",1)
			Goto Super::SetPrimary
		SetSecondary:
			TNT1 A 0 A_TakeInventory("DescentMissileCamera",256)
			TNT1 A 0 A_TakeInventory("DescentSecondaryCounter",65536)
			TNT1 A 0 A_GiveInventory("DescentSecondaryCounter",1)
			Goto Super::SetSecondary
		Fire:
			TNT1 A 1
			Goto Ready
	}
}

Actor DescentSmartMissile : DescentMissile 
{
	+SCREENSEEKER
	+SEEKERMISSILE
	States
	{
		Spawn:
			DS00 A 0
			DS00 A 0 A_GiveToTarget("DescentMissileCamera",1)
			DS00 A 0 ACS_NamedExecuteWithResult("DescentMissileCamera")
		SpawnLoop:
			DS00 A 1 Bright
			Loop
		Death:
			DS00 A 0 A_RemoveMaster
			DS00 A 0 A_Explode(-1,-1,0,1)
			DS00 A 0 A_GiveToTarget("DescentMissileStaticGiver",1)
			DS00 A 0 A_TakeFromTarget("DescentMissileCamera",1)
			DS00 A 0 A_PlaySound("Descent/Laser/Explode",CHAN_5)
			DS00 AAAAAA 0 A_CustomMissile("DescentSmartBomblet",0,0,random(0,359),CMF_TRACKOWNER|CMF_AIMDIRECTION,random(0,359))
			DS02 ABCDEFGHIJKL 2 Bright
			Stop
	}
}

Actor DescentSmartBomblet : DescentMissile 
{
	ExplosionDamage 30
	ExplosionRadius 50
	Scale 0.25
	Speed 8
	+SCREENSEEKER
	+SEEKERMISSILE
	States
	{
		Spawn:
			DS07 ABCDEF 1 Bright A_SeekerMissile(8,8,SMF_LOOK|SMF_PRECISE)
			Loop
		Death:
			DS00 A 0 A_SetScale(1.0, 1.0)
			DS00 A 0 A_Explode(-1,-1,0,1)
			DS00 A 0 A_PlaySound("Descent/Laser/Explode",CHAN_5)
			DS02 ABCDEFGHIJKL 2 Bright
			Stop
	}
}