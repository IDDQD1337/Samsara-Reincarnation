Actor RTCW_Luger : RTCW_Weapon
{
	Weapon.SlotNumber 1
	Weapon.SlotPriority 2
	Weapon.SelectionOrder 2000
	Weapon.AmmoUse 1
	Weapon.AmmoType "RTCW_LugerMagazine"
	Obituary "%o was killed (luger) by %k."
	Inventory.Pickupmessage "Luger P08"
	Inventory.PickupSound "RTCW/WeaponPickup"
	Tag "Luger"
	+WEAPON.NOALERT
	+WEAPON.AMMO_OPTIONAL
	+WEAPON.WIMPY_WEAPON
	States
	{
		Spawn:
			RW98 A -1
			Stop
		Deselect:
			RW00 V 0 A_PlaySound("RTCW/Change",CHAN_7)
			RW00 V 0 A_JumpIfInventory("RTCW_LugerSilencerAttached",1,"DeselectSilencer")
			RW00 GHIJK 2
			Goto Super::Deselect
		DeselectSilencer:
			RW04 GHIJK 2
			Goto Super::Deselect
		Ready:
			RW00 A 0 A_JumpIfInventory("RTCW_LugerSilencerAttached",1,"ReadySilencer")
			RW00 A 0 A_TakeInventory("RTCW_WeaponToken",256)
			RW00 A 0 A_GiveInventory("RTCW_WeaponToken",2)
			RW00 A 0
			RW00 LMNO 2
		ReadyLoop:
			RW00 A 0 A_JumpIfInventory("RTCW_LugerSilencerAttached",1,"ReadySilencedLoop")
			RW00 A 0 A_JumpIfInventory("RTCW_Kicking",1,"Kicking")
			RW00 A 1 A_WeaponReady(WRF_ALLOWRELOAD)		
			Loop
		ReadySilencer:
			RW04 A 0 A_TakeInventory("RTCW_WeaponToken",256)
			RW04 A 0 A_GiveInventory("RTCW_WeaponToken",3)
			RW04 LMNO 2
		ReadySilencedLoop:
			RW04 A 0 A_JumpIfInventory("RTCW_LugerSilencerAttached",1,1)
			Goto ReadyLoop
			RW04 A 0 A_JumpIfInventory("RTCW_Kicking",1,"Kicking")
			RW04 A 1 A_WeaponReady(WRF_ALLOWRELOAD)
			Loop
		NoAmmo:
			RW00 A 0 A_PlaySound("RTCW/NoAmmo",CHAN_5)
			RW00 A 17
			TNT1 A 0 A_SelectWeapon("RTCW_Knife")
			Goto Deselect
		NoAmmoSilenced:
			RW00 A 0 A_PlaySound("RTCW/NoAmmo",CHAN_5)
			RW04 A 17
			TNT1 A 0 A_SelectWeapon("RTCW_Knife")
			Goto Deselect
		Fire:
			RW00 B 0 A_JumpIfInventory("RTCW_LugerSilencerAttached",1,"FireSilenced")
			RW00 B 0 A_JumpIfInventory("RTCW_LugerMagazine",1,1)
			Goto Reload
			RW00 B 0 A_GunFlash("FireFlash")
			RW00 B 0 A_FireBullets(0,0,1,0,"SamsaraBlankPuff",FBF_NORANDOM|FBF_NOFLASH)
			RW00 B 0 A_FireCustomMissile("RTCW_Casing",-random(85,105),0,8,4,0,-4)
			RW00 B 0 A_GiveInventory("SamsaraRTCWLugerAttackHandler")
			RW00 BCDEFG 2
			Goto ReadyLoop
		FireSilenced:
			RW04 B 0 A_JumpIfInventory("RTCW_LugerMagazine",1,1)
			Goto Reload
			RW00 B 0 A_FireBullets(0,0,1,0,"SamsaraBlankPuff",FBF_NORANDOM|FBF_NOFLASH)
			RW00 B 0 A_FireCustomMissile("RTCW_Casing",-random(85,105),0,8,4,0,-4)
			RW00 B 0 A_GiveInventory("SamsaraRTCWLugerAttackHandler2")
			RW04 BCDEFG 2
			Goto ReadySilencedLoop
		FireFlash:
			RW03 B 2 Bright
			Stop
		AltFire:
			RW02 I 0 A_JumpIfInventory("RTCW_LugerSilencerAttached",1,"RemoveSilencer")
			RW02 I 0 A_PlaySound("RTCW/Luger/SilencerAttach",CHAN_6)
			RW02 I 0 A_GiveInventory("RTCW_LugerSilencerAttached",1)
			RW02 IJKL 2
			RW02 M 0
			RW05 "[\]" 2
			RW06 ABCDEFGHIJKLMNOPQRSTUV 2
			RW04 A 0 A_TakeInventory("RTCW_WeaponToken",256)
			RW04 A 0 A_GiveInventory("RTCW_WeaponToken",3)
			Goto ReadySilencedLoop
		RemoveSilencer:
			RW06 W 0 A_PlaySound("RTCW/Luger/SilencerRemove",CHAN_6)
			RW06 W 0 A_TakeInventory("RTCW_LugerSilencerAttached",1)
			RW06 "WXYZ[\]" 2
			RW07 ABCDEFGHIJKL 2
			RW07 M 0
			RW00 A 0 A_TakeInventory("RTCW_WeaponToken",256)
			RW00 A 0 A_GiveInventory("RTCW_WeaponToken",2)
			Goto ReadyLoop
		Reload:
			TNT1 A 0 A_JumpIfInventory("RTCW_LugerMagazine",0,"ReadyLoop")
			TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,"ReloadAmmoCheck")
			goto ReloadSuccess
			TNT1 A 0
			goto ReadyLoop
		ReloadAmmoCheck:
			TNT1 A 0 A_JumpIfInventory("RTCW_LugerMagazine",0,"ReadyLoop")
			TNT1 A 0 A_JumpIfInventory("Clip",1,"ReloadSuccess")
			RW00 A 0 A_JumpIfInventory("RTCW_LugerSilencerAttached",1,"NoAmmoSilenced")
			goto NoAmmo
			TNT1 A 0
			goto ReadyLoop			
		ReloadSuccess:
			TNT1 A 0 //A_JumpIfInventory("RTCW_LugerMagazine",1,1)
			goto ReloadSuccessNoAmmo		
			RW00 A 0 A_PlaySound("RTCW/Luger/Reload",CHAN_6)
			RW00 A 0 A_JumpIfInventory("RTCW_LugerSilencerAttached",1,"ReloadSuccessSilenced")
			RW00 "QRSTUVWXYZ[\]" 2
			RW01 ABCDEFGHIJKLM 2
			RW04 A 0 A_JumpIfInventory("PistolModeON", 1, "ReloadAmmo")
			goto SetAmmo
		ReloadSuccessNoAmmo:	
			RW00 A 0 A_PlaySound("RTCW/Luger/Reload",CHAN_6)
			RW00 A 0 A_JumpIfInventory("RTCW_LugerSilencerAttached",1,"ReloadSuccessSilenced")
			RW00 "QRSTUVWXYZ[\]" 2
			RW01 ABCDEFGHIJKLM 2
			RW01 N 0 A_PlaySound("RTCW/Luger/Reload2",CHAN_6)
			RW01 NOPQRSTUVWXYZ 1
			RW04 A 0 A_JumpIfInventory("PistolModeON", 1, "ReloadAmmo")
			goto SetAmmo				
		ReloadSuccessSilenced:
			TNT1 A 0 //A_JumpIfInventory("RTCW_LugerMagazine",1,1)
			goto ReloadSuccessNoAmmoSilenced			
			RW04 "QRSTUVWXYZ[\]" 2
			RW05 ABCDEFGHIJKLM 2
			RW04 A 0 A_JumpIfInventory("PistolModeON", 1, "ReloadAmmo")
			goto SetAmmo
		ReloadSuccessNoAmmoSilenced:			
			RW04 "QRSTUVWXYZ[\]" 2
			RW05 ABCDEFGHIJKLM 2
			RW05 N 0 A_PlaySound("RTCW/Luger/Reload2",CHAN_6)
			RW05 NOPQRSTUVWXYZ 1
			RW04 A 0 A_JumpIfInventory("PistolModeON", 1, "ReloadAmmo")
			goto SetAmmo	
		SetAmmo:
			TNT1 A 0 A_GiveInventory("RTCW_LugerMagazine",8)
			Goto ReadyLoop
		ReloadAmmo:
			TNT1 A 0 A_JumpIfInventory("RTCW_LugerMagazine",0,"ReadyLoop")
			TNT1 A 0 A_JumpIfInventory("Clip",1,1)
			goto ReadyLoop
			TNT1 A 0 A_GiveInventory("RTCW_LugerMagazine",1)
			TNT1 A 0 A_TakeInventory("Clip",1,TIF_NOTAKEINFINITE)
			Loop
		Kicking:
			TNT1 A 0 A_Gunflash("KickingFoot")
		KickingDelay:
			RW00 A 0 A_JumpIfInventory("RTCW_LugerSilencerAttached",1,"KickingSilencedDelay")
			RW00 A 18
			RW00 A 0 A_TakeInventory("RTCW_Kicking",1)
			Goto ReadyLoop
		KickingSilencedDelay:
			RW04 A 18
			RW04 A 0 A_TakeInventory("RTCW_Kicking",1)
			Goto ReadyLoop
	}
}

//Was 6 damage, now 12
Actor RTCW_LugerTracer : RTCW_Tracer
{ 
	States
	{
		Crash:
		Death:
			TNT1 A 0 A_Explode(CallACS("RTCW_BulletDamage",15),8,!XF_HURTSOURCE,0,8)
			Goto Super::Death
		XDeath:
			TNT1 A 0 A_Explode(CallACS("RTCW_BulletDamage",15),8,!XF_HURTSOURCE,0,8)
			Goto Super::XDeath
	}
}

Actor RTCW_LugerTracerCoop : RTCW_LugerTracer { +THRUSPECIES Species "Player" }

Actor RTCW_LugerMagazine : Ammo
{
	Inventory.MaxAmount 8
	Ammo.BackpackMaxAmount 8
}

Actor RTCW_LugerSilencerAttached : Inventory { +INVENTORY.UNDROPPABLE Inventory.MaxAmount 1 Inventory.InterHubAmount 1 }

actor SamsaraRTCWLugerAttackHandler : Trigger
{
	States
	{
		// Ammo check stuff
		Pickup:
			TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"PickupCheckAmmoReloadable")
		PickupCheckAmmoNonReloadable:
			TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
			Goto PickupAttack
			TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupTakeAmmoNonReloadable")
			TNT1 A 0 A_JumpIfInventory("Clip",1,"PickupTakeAmmoNonReloadable")
			Stop
		PickupCheckAmmoReloadable:
			TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"PickupAttack")
			TNT1 A 0 A_JumpIfInventory("RTCW_LugerMagazine",1,"PickupTakeAmmoReloadable")
			Stop

		// Attack stuff
		PickupAttack:
			TNT1 A 0 A_GiveInventory("RTCW_FiringToken",1)
			TNT1 A 0 A_GiveInventory("RTCW_BlazkoWeaponAlert",1)
			TNT1 A 0 A_PlaySound("RTCW/Luger/Fire",CHAN_Weapon)
			TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
			TNT1 A 0 A_FireCustomMissile("RTCW_LugerTracer",frandom(-2.0,2.0),0,0,4,0,frandom(-2.0,2.0))
			Stop
		PickupAttackCoop:
			TNT1 A 0 A_FireCustomMissile("RTCW_LugerTracerCoop",frandom(-2.0,2.0),0,0,4,0,frandom(-2.0,2.0))
			Stop

		// Ammo taking stuff
		PickupTakeAmmoNonReloadable:
			TNT1 A 0 A_TakeInventory("Clip",1,TIF_NOTAKEINFINITE)
			Goto PickupAttack
		PickupTakeAmmoReloadable:
			TNT1 A 0 A_TakeInventory("RTCW_LugerMagazine",1)
			Goto PickupAttack
	}
}

actor SamsaraRTCWLugerAttackHandler2 : Trigger
{
	States
	{
		// Ammo check stuff
		Pickup:
			TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"PickupCheckAmmoReloadable")
		PickupCheckAmmoNonReloadable:
			TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
			Goto PickupAttack
			TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupTakeAmmoNonReloadable")
			TNT1 A 0 A_JumpIfInventory("Clip",1,"PickupTakeAmmoNonReloadable")
			Stop
		PickupCheckAmmoReloadable:
			TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"PickupAttack")
			TNT1 A 0 A_JumpIfInventory("RTCW_LugerMagazine",1,"PickupTakeAmmoReloadable")
			Stop

		// Attack stuff
		PickupAttack:
			TNT1 A 0 A_PlaySound("RTCW/Luger/SilencerFire",CHAN_Weapon)
			TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
			TNT1 A 0 A_FireCustomMissile("RTCW_LugerTracer",frandom(-3.0,3.0),0,0,4,0,frandom(-3.0,3.0))
			Stop
		PickupAttackCoop:
			TNT1 A 0 A_FireCustomMissile("RTCW_LugerTracerCoop",frandom(-3.0,3.0),0,0,4,0,frandom(-3.0,3.0))
			Stop

		// Ammo taking stuff
		PickupTakeAmmoNonReloadable:
			TNT1 A 0 A_TakeInventory("Clip",1,TIF_NOTAKEINFINITE)
			Goto PickupAttack
		PickupTakeAmmoReloadable:
			TNT1 A 0 A_TakeInventory("RTCW_LugerMagazine",1)
			Goto PickupAttack
	}
}
