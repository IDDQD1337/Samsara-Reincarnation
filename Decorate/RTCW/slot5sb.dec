actor RTCW_M1GarandGrenadeModeSelected : Boolean {}
actor RTCW_M1GarandGrenadeNotLoaded : Boolean {}

Actor RTCW_M1Garand : RTCW_Weapon
{
	Weapon.SlotNumber 5
	Weapon.SlotPriority 2
	Weapon.SelectionOrder 800
	Weapon.AmmoType1 "RTCW_M1GarandMagazine"
	Weapon.AmmoUse1 1
	Weapon.AmmoType2 "Shell"
	Weapon.AmmoGive2 4
	Obituary "%o was killed (garand) by %k."
	Inventory.Pickupmessage "M1 Garand"
	Inventory.PickupSound "RTCW/WeaponPickup"
	Tag "M1 Garand"
	+WEAPON.NOALERT
	+WEAPON.AMMO_OPTIONAL
	+WEAPON.NOAUTOFIRE
	States
	{
		Spawn:
			RW98 A -1
			Stop

        ReadyActual:
			TNT1 A 0 A_JumpIfInventory("RTCW_M1GarandGrenadeModeSelected",1,"ReadyActualGrenade")
            TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReadyActualReloadable")
        ReadyActualNonReloadable:
			RW00 A 0 A_JumpIfInventory("RTCW_Kicking",1,"Kicking")
            RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,3)
            RW00 A 0 A_JumpIfInventory("RocketAmmo",1,2)
			RW00 A 1 A_WeaponReady(WRF_NOSECONDARY)
			goto ReadyActualAmmoCheck
			RW00 A 1 A_WeaponReady
			goto ReadyActualAmmoCheck

        ReadyActualReloadable:
			RW00 A 0 A_JumpIfInventory("RTCW_Kicking",1,"Kicking")
            RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,4)
            RW00 A 0 A_JumpIfInventory("RTCW_M1GarandGrenade",1,3)
            RW00 A 0 A_JumpIfInventory("RocketAmmo",1,2)
			RW00 A 1 A_WeaponReady(WRF_NOSECONDARY|WRF_ALLOWRELOAD)
			goto ReadyActualAmmoCheck
			RW00 A 1 A_WeaponReady(WRF_ALLOWRELOAD)
			goto ReadyActualAmmoCheck

        ReadyActualAmmoCheck:
            TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,1)
            goto ReadyActual
            TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"ReadyActual")
            TNT1 A 0 A_JumpIfInventory("RTCW_M1GarandMagazine",1,"ReadyActual")
            goto ReloadCheck

        ReadyActualGrenade:
            TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReadyActualGrenadeReloadable")
		ReadyActualGrenadeNonReloadable:
            TNT1 A 0 A_JumpIfInventory("RTCW_M1GarandGrenadeNotLoaded",1,"ReadyActualGrenadeNonReloadableNoAmmo")
            TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReadyActualGrenadeNonReloadableAmmo")
            TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,"ReadyActualGrenadeNonReloadableAmmo")
            goto ReadyActualGrenadeNonReloadableNoAmmo

        ReadyActualGrenadeNonReloadableNoAmmo:
			RW00 A 0 A_GiveInventory("RTCW_M1GarandGrenadeNotLoaded")
			RW00 A 0 A_TakeInventory("RTCW_M1GarandGrenadeModeSelected")
			goto ReadyActual

        ReadyActualGrenadeNonReloadableAmmo:
			RW04 T 0 A_TakeInventory("RTCW_M1GarandGrenadeNotLoaded")
			RW04 T 0 A_JumpIfInventory("RTCW_Kicking",1,"KickingGrenade")
			RW04 T 1 A_WeaponReady
			goto ReadyActual

        ReadyActualGrenadeReloadable:
            TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"ReadyActualGrenadeReloadableAmmo")
            TNT1 A 0 A_JumpIfInventory("RTCW_M1GarandGrenade",1,"ReadyActualGrenadeReloadableAmmo")
            goto ReadyActualGrenadeReloadableNoAmmo

        ReadyActualGrenadeReloadableNoAmmo:
			RW00 A 0 A_GiveInventory("RTCW_M1GarandGrenadeNotLoaded")
			RW00 A 0 A_TakeInventory("RTCW_M1GarandGrenadeModeSelected")
			goto ReadyActual

        ReadyActualGrenadeReloadableAmmo:
			RW04 T 0 A_TakeInventory("RTCW_M1GarandGrenadeNotLoaded")
			RW04 T 0 A_JumpIfInventory("RTCW_Kicking",1,"KickingGrenade")
			RW04 T 1 A_WeaponReady(WRF_ALLOWRELOAD)
			goto ReadyActual

		Deselect:
			RW01 A 0 A_JumpIfInventory("RTCW_M1GarandGrenade",1,"DeselectGrenade")
			RW01 A 2 A_PlaySound("RTCW/Change",CHAN_7)
			RW01 BCDE 2
			Goto Super::Deselect
		
		DeselectGrenade:
			RW05 T 2 A_PlaySound("RTCW/Change",CHAN_7)
			RW05 UVWX 2
			Goto Super::Deselect

		Ready: // our "Select" state
			RW00 A 0 A_TakeInventory("RTCW_WeaponToken",256)
			RW01 F 0 A_GiveInventory("RTCW_WeaponToken",21)
			RW01 F 0 A_JumpIfInventory("RTCW_M1GarandGrenade",1,"RaiseGrenade")
			RW01 FGHIJKL 2
            goto ReadyActual
		
		RaiseGrenade:
			RW05 "]" 2
			RW06 ABCD 2
            goto ReadyActual

		Fire:
			RW00 B 0 A_JumpIfInventory("RTCW_M1GarandGrenadeModeSelected",1,"FireGrenade")
            RW00 B 0 A_JumpIfInventory("SamsaraReloadMode",1,"FireReloadable")
        FireNonReloadable:
            RW00 B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart")
            RW00 B 0 A_JumpIfInventory("Shell",1,"FireStart")
            goto FireDry

        FireReloadable:
            RW00 B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireStart")
            RW00 B 0 A_JumpIfInventory("RTCW_M1GarandMagazine",1,"FireStart")
        FireReloadableDryCheck:
            RW00 B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReloadStart")
            RW00 B 0 A_JumpIfInventory("Shell",1,"ReloadStart")
            goto FireDry

        FireDry:
            RW00 A 17 A_PlaySound("RTCW/NoAmmo",CHAN_5)
			TNT1 A 0 A_SelectWeapon("RTCW_Knife")
			Goto Deselect

        FireUnderwater:
            RW00 A 17 A_PlaySound("RTCW/UnderwaterFire",CHAN_5)
            goto ReadyActual

		FireStart:
            RW00 B 0 A_JumpIfInventory("SamsaraCurrentWaterLevel",0,"FireUnderwater")
			RW00 B 0 A_GiveInventory("RTCW_FiringToken",1)
			RW00 B 0 A_GiveInventory("RTCW_BlazkoWeaponAlert",1)
			RW00 B 0 A_SpawnItemEx("RTCW_Casing",4,-16*cos(-pitch),38+(16*sin(-pitch)),1,0,2*cos(-pitch),-random(85,95),SXF_TRANSFERPITCH)
			RW00 B 0 A_GunFlash("FireFlash")
			RW00 B 0 A_JumpIfInventory("RTCW_M1GarandMagazine",2,1)
			Goto FireLast
			RW00 B 2 A_GiveInventory("SamsaraRTCWM1GarandAttackHandler")
			RW00 CDEF 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        FireFinish:
			RW00 A 0 A_ReFire("FireFinishAuto")
            RW00 AAAAA 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOSECONDARY)
            goto ReadyActual
			
		FireLast:
			RW01 L 2 A_GiveInventory("SamsaraRTCWM1GarandAttackHandler")
			RW01 MNOP 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			Goto ReloadCheck
			
        FireFinish:
			RW00 A 0 A_ReFire("FireFinishAuto")
            RW00 AAAAA 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOSECONDARY)
            goto ReadyActual

		FireFinishAuto:
			RW00 A 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOSECONDARY)
			RW00 A 0 A_ReFire
			RW00 A 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOSECONDARY)
			RW00 A 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOSECONDARY)
			RW00 A 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOSECONDARY)
			goto ReadyActual

		FireFlash:
			RW07 B 0 A_Jump(256,1,2)
			RW07 B 2 Bright
			stop
			RW08 B 2 Bright
			stop

		FireGrenade:
            RW02 O 0 A_JumpIfInventory("SamsaraReloadMode",1,"FireGrenadeReloadable")
        FireGrenadeNonReloadable:
            RW02 O 0 A_JumpIfInventory("RTCW_M1GarandGrenadeNotLoaded",1,"FireDryGrenade")
            RW02 O 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStartGrenade")
            RW02 O 0 A_JumpIfInventory("RocketAmmo",1,"FireStartGrenade")
            goto FireDryGrenade

        FireGrenadeReloadable:
            RW02 O 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireStartGrenade")
            RW02 O 0 A_JumpIfInventory("RTCW_M1GarandGrenade",1,"FireStartGrenade")
            goto FireDryGrenade

        FireDryGrenade:
            RW00 A 17 A_PlaySound("RTCW/NoAmmo",CHAN_5)
            TNT1 A 0 A_TakeInventory("RTCW_M1GarandGrenadeModeSelected")
			goto ReadyActual

		FireStartGrenade:
            RW02 O 0 A_JumpIfInventory("SamsaraCurrentWaterLevel",0,"FireUnderwater")
			//RW02 O 0 A_GiveInventory("RTCW_FiringToken",1)
			RW02 O 0 A_GiveInventory("RTCW_BlazkoWeaponAlert",1)
        FireLastShotCalcGrenade:
            RW00 A 0 A_JumpIfInventory("SamsaraReloadMode",1,1)
            goto FireContinueGrenadeLast
            RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireContinueGrenade")
            RW00 A 0 A_JumpIfInventory("RTCW_M1GarandGrenade",2,"FireContinueGrenade")
            goto FireContinueGrenadeLast

        FireContinueGrenade:
			RW02 S 2 A_GiveInventory("SamsaraRTCWM1GarandGrenadeAttackHandler")
			RW02 "TUVWXYZ[" 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
            RW00 A 0
			goto ReadyActual

        FireContinueGrenadeLast:
			RW02 O 0 A_PlaySound("RTCW/NoAmmo",CHAN_5)
			RW02 S 2 A_GiveInventory("SamsaraRTCWM1GarandGrenadeAttackHandler")
			RW02 "TUVWXYZ[" 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
            RW00 A 0 A_TakeInventory("RTCW_M1GarandGrenadeModeSelected")
			goto ReadyActual

		//-_-
		AltFire:
			RW02 U 0 A_JumpIfInventory("RTCW_M1GarandGrenadeModeSelected",1,"AltFireRemove")
        AltFireAttach:
            RW02 U 0 A_PlaySound("RTCW/M1Garand/GrenadeOn",CHAN_6)
			RW02 "\]" 1 A_GiveInventory("RTCW_M1GarandGrenadeModeSelected")
			RW03 A 2
			RW03 B 1
			RW03 C 2
			RW03 D 1
			RW03 E 2
			RW03 F 1
			RW03 G 2
			RW03 H 1
			RW03 I 2
			RW03 J 1
			RW03 K 2
			RW03 L 1
			RW03 M 2
			RW03 N 1
			RW03 O 2
			RW03 P 1
			RW03 Q 2
			RW03 R 1
			RW03 S 2
			RW03 T 1
			RW03 U 2
			RW03 V 1
			RW03 W 2
			RW03 X 1
			RW03 Y 2
			RW03 Z 1
			RW03 "[" 2
			RW03 "\" 1
			RW03 "]" 2
			RW04 A 1
			RW04 B 2
			RW04 C 1
			RW04 D 2
			RW04 E 1
			RW04 F 2
			RW04 G 1
			RW04 H 2
			RW04 I 1
			RW04 J 2
			RW04 K 1
			RW04 L 2
			RW04 M 1
			RW04 N 2
			RW04 O 1
			RW04 P 2
			RW04 Q 1
			RW04 R 2
			RW04 S 1
			RW04 T 0 A_TakeInventory("RTCW_M1GarandGrenadeNotLoaded")
            RW04 T 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReloadAmmoGrenade")
			goto ReloadDoneGrenade

		AltFireRemove:
			RW04 T 0 A_PlaySound("RTCW/M1Garand/GrenadeOff",CHAN_6)
			RW04 T 1 A_TakeInventory("RTCW_M1GarandGrenadeModeSelected")
			RW04 U 2
			RW04 V 1
			RW04 W 2
			RW04 X 1
			RW04 Y 2
			RW04 Z 1
			RW04 "[" 2
			RW04 "\" 1
			RW04 "]" 2
			RW05 A 1
			RW05 B 2
			RW05 C 1
			RW05 D 2
			RW05 E 1
			RW05 F 2
			RW05 G 1
			RW05 H 2
			RW05 I 1
			RW05 J 2
			RW05 K 1
			RW05 L 2
			RW05 M 1
			RW05 N 2
			RW05 O 1
			RW05 P 2
			RW05 Q 1
			RW05 R 2
			RW05 S 1
            RW00 A 0
			goto ReadyActual

        Reload:
            RW00 A 0 A_JumpIfInventory("RTCW_M1GarandGrenadeModeSelected",1,"ReloadCheckGrenade")
        ReloadCheck:
            RW00 A 0 A_JumpIfInventory("RTCW_M1GarandMagazine",0,"ReadyActual")
            RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
            RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReloadStart")
            RW00 A 0 A_JumpIfInventory("Shell",1,"ReloadStart")
            goto ReadyActual

		ReloadStart:
			RW01 P 2 A_PlaySound("RTCW/M1Garand/Reload",CHAN_6)
			RW01 "QRSTUVWXYZ[\]" 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW02 ABCDEF 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW00 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReloadAmmo")
            goto ReloadDone

        ReloadDone:
            RW00 A 0
			goto ReadyActual

        ReloadAmmo:
            TNT1 A 0 A_JumpIfInventory("RTCW_M1GarandMagazine",0,"ReloadDone")
            TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
            TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
            TNT1 A 0 A_JumpIfInventory("Shell",1,1)
            goto ReloadDone
            TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
            TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
            TNT1 A 0 A_TakeInventory("Shell",1)
            TNT1 A 0 A_GiveInventory("RTCW_M1GarandMagazine")
            loop

        ReloadCheckGrenade:
            RW04 J 0 A_JumpIfInventory("RTCW_M1GarandGrenade",0,"ReadyActual")
            RW04 J 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
            RW04 J 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"AltFireAttach")
            RW04 J 0 A_JumpIfInventory("RocketAmmo",1,"AltFireAttach")
            goto ReadyActual

        ReloadDoneGrenade:
            RW04 J 0
			goto ReadyActual

        ReloadAmmoGrenade:
            TNT1 A 0 A_JumpIfInventory("RTCW_M1GarandGrenade",0,"ReloadDoneGrenade")
            TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
            TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
            TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,1)
            goto ReloadDoneGrenade
            TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
            TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
            TNT1 A 0 A_TakeInventory("RocketAmmo",1)
            TNT1 A 0 A_GiveInventory("RTCW_M1GarandGrenade")
            loop

		Kicking:
			TNT1 A 0 A_GunFlash("KickingFoot")
		KickingDelay:
			RW00 A 18 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW00 A 0 A_TakeInventory("RTCW_Kicking",1)
			goto ReadyActual

		KickingGrenade:
			TNT1 A 0 A_GunFlash("KickingFoot")
		KickingDelayGrenade:
			RW04 J 18 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			RW04 J 0 A_TakeInventory("RTCW_Kicking",1)
			goto ReadyActual
	}
}

actor SamsaraRTCWM1GarandAttackHandler : Trigger
{
    States
    {
      // Ammo check stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"PickupCheckAmmoReloadable")
      PickupCheckAmmoNonReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupTakeAmmoNonReloadable")
        TNT1 A 0 A_JumpIfInventory("Shell",1,"PickupTakeAmmoNonReloadable")
        stop

      PickupCheckAmmoReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("RTCW_M1GarandMagazine",1,"PickupTakeAmmoReloadable")
        stop

      // Ammo taking stuff
      PickupTakeAmmoNonReloadable:
        TNT1 A 0 A_TakeInventory("Shell",1,TIF_NOTAKEINFINITE)
        goto PickupAttack

      PickupTakeAmmoReloadable:
        TNT1 A 0 A_TakeInventory("RTCW_M1GarandMagazine",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
		TNT1 A 0 A_JumpIfInventory("RTCW_M1GarandMagazine",1,2)
		TNT1 A 0 A_PlaySound("RTCW/M1Garand/FireLast",CHAN_WEAPON)
		Goto PickupAttack+3
        TNT1 A 0 A_PlaySound("RTCW/M1Garand/Fire",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("RTCW_M1GarandTracer",frandom(-2.5,2.5),false,4,4,0,frandom(-2.5,2.5))
        goto PickupFinish

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("RTCW_M1GarandTracerCoop",frandom(-2.5,2.5),false,4,4,0,frandom(-2.5,2.5))
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 ACS_NamedExecuteWithResult("RTCW_Recoil", 2)
        stop
    }
}

actor SamsaraRTCWM1GarandGrenadeAttackHandler : Trigger
{
    States
    {
      // Ammo check stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"PickupCheckAmmoReloadable")
      PickupCheckAmmoNonReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupTakeAmmoNonReloadable")
        TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,"PickupTakeAmmoNonReloadable")
        stop

      PickupCheckAmmoReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("RTCW_M1GarandGrenade",1,"PickupTakeAmmoReloadable")
        stop

      // Ammo taking stuff
      PickupTakeAmmoNonReloadable:
        TNT1 A 0 A_TakeInventory("RocketAmmo",1,TIF_NOTAKEINFINITE)
        goto PickupAttack

      PickupTakeAmmoReloadable:
        TNT1 A 0 A_TakeInventory("RTCW_M1GarandGrenade",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("RTCW/M1Garand/FireGrenade",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("RTCW_RiflePineappleProjectile",0,false,8,4)
        goto PickupFinish

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("RTCW_RiflePineappleProjectileCoop",0,false,8,4)
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 A_GiveInventory("RTCW_M1GarandGrenadeNotLoaded")
        TNT1 A 0 ACS_NamedExecuteWithResult("RTCW_Recoil", 2)
        stop
    }
}

actor RTCW_M1GarandMagazine : Ammo
{
    Inventory.MaxAmount 8
    +IGNORESKILL
}

actor RTCW_M1GarandGrenade : Ammo
{
    Inventory.MaxAmount 1
    +IGNORESKILL
}

Actor RTCW_M1GarandTracer : RTCW_Tracer
{ 
	States
	{
		Crash:
		Death:
			TNT1 A 0 A_Explode(CallACS("RTCW_BulletDamage",100),8,!XF_HURTSOURCE,0,8)
			Goto Super::Death
		XDeath:
			TNT1 A 0 A_Explode(CallACS("RTCW_BulletDamage",100),8,!XF_HURTSOURCE,0,8)
			Goto Super::XDeath
	}
}

Actor RTCW_M1GarandTracerCoop : RTCW_M1GarandTracer { +THRUSPECIES Species "Player" }

Actor RTCW_RiflePineappleProjectile : RTCW_PineappleProjectile
{ 
	Speed 24 
	ReactionTime 0
	SeeSound "RTCW/K43/GrenadeFly"
	States
	{
		Bounce.Actor.Creature:
			TNT1 A 0 A_GiveInventory("RTCW_GrenadeTimer",4)
			TNT1 A 0 A_CountDown
			Goto Detonate
	}
}

Actor RTCW_RiflePineappleProjectileCoop : RTCW_PineappleProjectileCoop
{ 
	Speed 24 
	ReactionTime 0
	SeeSound "RTCW/K43/GrenadeFly"
	States
	{
		Bounce.Actor.Creature:
			TNT1 A 0 A_GiveInventory("RTCW_GrenadeTimer",4)
			TNT1 A 0 A_CountDown
			Goto Detonate
	}
}
