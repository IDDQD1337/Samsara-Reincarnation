actor RTCW_BARAlt : Boolean {}
actor RTCW_BARFireFrame : Counter { Inventory.MaxAmount 3 }

Actor RTCW_BAR : RTCW_Weapon
{
	Weapon.SlotNumber 4
	Weapon.SlotPriority 2
	Weapon.SelectionOrder 300
	Weapon.AmmoType1 "RTCW_BARMagazine"
	Weapon.AmmoUse1 1
	Weapon.AmmoType2 "Clip"
	Weapon.AmmoGive2 10
	Obituary "%o was killed (BAR) by %k."
	Inventory.Pickupmessage "BAR"
	Inventory.PickupSound "RTCW/WeaponPickup"
	Tag "Browning Automatic Rifle"
	+WEAPON.NOALERT
	+WEAPON.AMMO_OPTIONAL
	States
	{
		Spawn:
			RW98 A -1
			Stop

        ReadyActual:
			RW00 A 0 A_TakeInventory("RTCW_BARFireFrame")
			RW00 A 0 A_TakeInventory("RTCW_FG42Spread")
			RW00 A 0 A_JumpIfInventory("RTCW_Kicking",1,"Kicking")
            RW00 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReadyActualReloadable")
        ReadyActualNonReloadable:
			RW00 A 1 A_WeaponReady
			goto ReadyActualAmmoCheck

        ReadyActualReloadable:
			RW00 A 1 A_WeaponReady(WRF_ALLOWRELOAD)
			goto ReadyActualAmmoCheck

        ReadyActualAmmoCheck:
            RW00 A 0 A_JumpIfInventory("SamsaraReloadMode",1,1)
            goto ReadyActual
            RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"ReadyActual")
            RW00 A 0 A_JumpIfInventory("RTCW_BARMagazine",1,"ReadyActual")
            goto ReloadCheck

		Deselect:
			RW00 U 0 A_TakeInventory("RTCW_BARFireFrame")
			RW00 U 0 A_PlaySound("RTCW/Change",CHAN_7)
			RW00 U 1 A_TakeInventory("RTCW_FG42Spread")
			RW00 "VWXYZ[\]" 1
			RW01 A 1
			Goto Super::Deselect

		Ready: // our "Select" state
			RW00 A 0 A_TakeInventory("RTCW_BARFireFrame")
			RW00 A 0 A_TakeInventory("RTCW_WeaponToken",256)
			RW00 A 0 A_GiveInventory("RTCW_WeaponToken",19)
			RW01 B 1 A_TakeInventory("RTCW_FG42Spread")
			RW01 CEFGHJKLMOPQSTUW 1
            goto ReadyActual

		Fire:
            RW00 A 0 A_TakeInventory("RTCW_BARAlt")
        FireAmmoCheck:
            RW00 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"FireAmmoCheckReloadable")
        FireAmmoCheckNonReloadable:
            RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart")
            RW00 A 0 A_JumpIfInventory("Clip",1,"FireStart")
            goto FireDry

        FireAmmoCheckReloadable:
            RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireStart")
            RW00 A 0 A_JumpIfInventory("RTCW_BARMagazine",1,"FireStart")
        FireAmmoCheckReloadableDryCheck:
            RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReloadStart")
            RW00 A 0 A_JumpIfInventory("Clip",1,"ReloadStart")
            goto FireDry

        FireDry:
			RW00 A 0 A_PlaySound("RTCW/NoAmmo",CHAN_5)
            RW00 A 17 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			TNT1 A 0 A_SelectWeapon("RTCW_Knife")
			Goto Deselect

        FireUnderwater:
			RW00 A 0 A_PlaySound("RTCW/UnderwaterFire",CHAN_5)
            RW00 A 17 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
            goto ReadyActual
			
		FireStart:
            RW00 A 1 A_JumpIfInventory("SamsaraCurrentWaterLevel",0,"FireUnderwater")
			RW00 B 0 A_GiveInventory("RTCW_BlazkoWeaponAlert",1)
            RW00 B 0 A_JumpIfInventory("SamsaraReloadMode",1,"FireStartReloadable")
        FireStartNonReloadable:
            RW00 B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireAttack")
            RW00 B 0 A_JumpIfInventory("Clip",1,"FireAttack")
            goto FireContinue

        FireStartReloadable:
            RW00 B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireAttack")
            RW00 B 0 A_JumpIfInventory("RTCW_BARMagazine",1,"FireAttack")
            goto FireContinue

        FireAttack:
			RW00 B 0 A_SpawnItemEx("RTCW_Casing",4,-10*cos(-pitch),38+(10*sin(-pitch)),1,0,2*cos(-pitch),-random(85,95),SXF_TRANSFERPITCH)
		FireContinue:
			RW00 B 0 A_GiveInventory("RTCW_FiringToken",1)
			RW00 B 0 A_JumpIfInventory("RTCW_BARFireFrame",3,"FireAnim4")
			RW00 B 0 A_JumpIfInventory("RTCW_BARFireFrame",2,"FireAnim3")
			RW00 B 0 A_JumpIfInventory("RTCW_BARFireFrame",1,"FireAnim2")
			goto FireAnim1
			
        FireAnim1:
			RW00 B 0 A_GunFlash("FireFlash1")
			RW00 B 0 A_GiveInventory("SamsaraRTCWBARAttackHandler")
			RW00 B 2 
			RW00 C 2 
			RW00 A 0 A_JumpIfInventory("RTCW_BARAlt",1,"AltFireContinue")
            goto FireFinish
			
		FireAnim2:
			RW00 D 0 A_GunFlash("FireFlash2")
			RW00 D 0 A_GiveInventory("SamsaraRTCWBARAttackHandler")
			RW00 D 2 
			RW00 E 2 
			RW00 A 0 A_JumpIfInventory("RTCW_BARAlt",1,"AltFireContinue")
            goto FireFinish
			
		FireAnim3:
			RW00 F 0 A_GunFlash("FireFlash3")
			RW00 F 0 A_GiveInventory("SamsaraRTCWBARAttackHandler")
			RW00 F 2 
			RW00 G 2 
			RW00 A 0 A_JumpIfInventory("RTCW_BARAlt",1,"AltFireContinue")
            goto FireFinish
			
		FireAnim4:
			RW00 H 0 A_GunFlash("FireFlash4")
			RW00 H 0 A_GiveInventory("SamsaraRTCWBARAttackHandler")
			RW00 H 2 
			RW00 I 2 
			RW00 A 0 A_JumpIfInventory("RTCW_BARAlt",1,"AltFireContinue")
            goto FireFinish

        FireFinish:
			RW00 A 0 A_JumpIfInventory("RTCW_BARFireFrame",0,2)
            RW00 A 0 A_GiveInventory("RTCW_BARFireFrame")
            goto FireFinishReFire
            RW00 A 0 A_TakeInventory("RTCW_BARFireFrame")
        FireFinishReFire:
			RW00 A 0 A_ReFire
			goto ReadyActual

		AltFire:
			RW00 A 0 A_TakeInventory("RTCW_BARFireFrame")
			RW00 A 0 A_GiveInventory("RTCW_BARAlt")
            goto FireAmmoCheck

		AltFireContinue:
			RW00 A 3
			goto FireFinishReFire

		FireFlash1:
			RW04 B 0 A_Jump(256,1,2)
			RW04 B 2 Bright
			stop
			RW05 B 2 Bright
			stop
			
		FireFlash2:
			RW04 D 0 A_Jump(256,1,2)
			RW04 D 2 Bright
			stop
			RW05 D 2 Bright
			stop
			
		FireFlash3:
			RW04 F 0 A_Jump(256,1,2)
			RW04 F 2 Bright
			stop
			RW05 F 2 Bright
			stop
			
		FireFlash4:
			RW04 H 0 A_Jump(256,1,2)
			RW04 H 2 Bright
			stop
			RW05 H 2 Bright
			stop

        Reload:
        ReloadCheck:
            RW00 A 0 A_JumpIfInventory("RTCW_BARMagazine",0,"ReadyActual")
            RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
            RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReloadStart")
            RW00 A 0 A_JumpIfInventory("Clip",1,"ReloadStart")
            goto ReadyActual

		ReloadStart:
			RW01 Y 0 A_PlaySound("RTCW/BAR/Reload",CHAN_6)
			RW01 "YZ[\]" 2
			RW02 "ABCDEFGHIJKLMNOPQRSTUVWXYZ[\]" 2
			RW03 AB 2
			RW00 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReloadAmmo")
            goto ReloadDone

        ReloadDone:
            RW00 A 0
			goto ReadyActual

        ReloadAmmo:
            TNT1 A 0 A_JumpIfInventory("RTCW_BARMagazine",0,"ReloadDone")
            TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
            TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
            TNT1 A 0 A_JumpIfInventory("Clip",1,1)
            goto ReloadDone
            TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
            TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
            TNT1 A 0 A_TakeInventory("Clip",1)
            TNT1 A 0 A_GiveInventory("RTCW_BARMagazine")
            loop

		Kicking:
			TNT1 A 0 A_GunFlash("KickingFoot")
		KickingDelay:
			RW00 A 18 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			RW00 A 0 A_TakeInventory("RTCW_Kicking",1)
			goto ReadyActual
	}
}

actor SamsaraRTCWBARAttackHandler : Trigger
{
    States
    {
      // Ammo check stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"PickupCheckAmmoReloadable")
      PickupCheckAmmoNonReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupTakeAmmoNonReloadable")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"PickupTakeAmmoNonReloadable")
        stop

      PickupCheckAmmoReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("RTCW_BARMagazine",1,"PickupTakeAmmoReloadable")
        stop

      // Ammo taking stuff
      PickupTakeAmmoNonReloadable:
        TNT1 A 0 A_TakeInventory("Clip",1,TIF_NOTAKEINFINITE)
        goto PickupAttack

      PickupTakeAmmoReloadable:
        TNT1 A 0 A_TakeInventory("RTCW_BARMagazine",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("RTCW/BAR/Fire",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("RTCW_BARAlt",1,"PickupAttackSecondary")
      // Primary
      PickupAttackPrimary:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackPrimaryCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackPrimaryCoop")
      PickupAttackPrimaryDM:
        TNT1 A 0 A_FireCustomMissile("RTCW_BARTracer",CallACS("RTCW_Decorate",1,1)*frandom(-1.0,1.0),false,4,4,0,CallACS("RTCW_Decorate",1,1)*frandom(-1.0,1.0))
        goto PickupFinish

      PickupAttackPrimaryCoop:
        TNT1 A 0 A_FireCustomMissile("RTCW_BARTracerCoop",CallACS("RTCW_Decorate",1,1)*frandom(-1.0,1.0),false,4,4,0,CallACS("RTCW_Decorate",1,1)*frandom(-1.0,1.0))
        goto PickupFinish

      // Secondary
      PickupAttackSecondary:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackSecondaryCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackSecondaryCoop")
      PickupAttackSecondaryDM:
        TNT1 A 0 A_FireCustomMissile("RTCW_BARTracer",CallACS("RTCW_Decorate",1,1)*frandom(-0.1,0.1),false,4,4,0,CallACS("RTCW_Decorate",1,1)*frandom(-0.1,0.1))
        goto PickupFinish

      PickupAttackSecondaryCoop:
        TNT1 A 0 A_FireCustomMissile("RTCW_BARTracerCoop",CallACS("RTCW_Decorate",1,1)*frandom(-0.1,0.1),false,4,4,0,CallACS("RTCW_Decorate",1,1)*frandom(-0.1,0.1))
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
		TNT1 A 0 ACS_NamedExecuteWithResult("RTCW_Recoil", 1)
        TNT1 A 0 A_GiveInventory("RTCW_FG42Spread")
        stop
    }
}

actor RTCW_BARMagazine : Ammo
{
    Inventory.MaxAmount 20
    +IGNORESKILL
}

//Was 19 damage
Actor RTCW_BARTracer : RTCW_Tracer
{ 
	States
	{
		Crash:
		Death:
			TNT1 A 0 A_Explode(CallACS("RTCW_BulletDamage",30),8,!XF_HURTSOURCE,0,8)
			Goto Super::Death
		XDeath:
			TNT1 A 0 A_Explode(CallACS("RTCW_BulletDamage",30),8,!XF_HURTSOURCE,0,8)
			Goto Super::XDeath
	}
}

Actor RTCW_BARTracerCoop : RTCW_BARTracer { +THRUSPECIES Species "Player" }
