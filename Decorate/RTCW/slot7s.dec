actor RTCW_FlamethrowerStreamAmmo : Boolean {}

Actor RTCW_Flamethrower : RTCW_Weapon
{
	Weapon.SlotNumber 7
	Weapon.SlotPriority 1
	Weapon.SelectionOrder 900
	Weapon.AmmoUse 1
	Weapon.AmmoGive 50
	Weapon.AmmoType "Cell"
	Obituary "%o was killed (flamethrower) by %k."
	Inventory.Pickupmessage "Flamethrower"
	Inventory.PickupSound "RTCW/WeaponPickup"
	Tag "Flamethrower"
	+WEAPON.AMMO_OPTIONAL
	+WEAPON.NOALERT
	States
	{
		Spawn:
			RW98 A -1
			Stop

		ReadyActual:
			RW00 A 0 A_StopSound(CHAN_WEAPON)
			RW00 A 0 A_JumpIfInventory("RTCW_Kicking",1,"Kicking")
			RW00 A 1 A_WeaponReady
			Loop

		Deselect:
			RW00 A 0 A_PlaySound("RTCW/Change",CHAN_7)
			RW00 A 0 A_StopSound(CHAN_WEAPON)
			RW00 G 2 A_GunFlash("NozzleStop")
			RW00 HIJK 2
			Goto Super::Deselect

		Ready: // our "Select" state
			RW00 A 0 A_TakeInventory("RTCW_WeaponToken",256)
			RW00 L 2 A_GiveInventory("RTCW_WeaponToken",16)
			RW00 MNO 2
			RW00 A 0 A_GunFlash("Nozzle")
            goto ReadyActual

        Nozzle:
            RW01 AAABBBCCCDDDEEEFFFGGGHHH 1 Bright A_JumpIfInventory("SamsaraCurrentWaterLevel",0,"NozzleUnderwater")
            loop

        NozzleUnderwater:
            TNT1 A 1
            goto Nozzle

        NozzleStop:
            TNT1 A 1
            stop

		Fire:
            RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart")
            RW00 A 0 A_JumpIfInventory("Cell",1,"FireStart")
            goto FireDry

        FireDry:
            RW00 A 0 A_StopSound(CHAN_WEAPON)
			RW00 A 0 A_PlaySound("RTCW/NoAmmo",CHAN_5)
            RW00 A 17 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			TNT1 A 0 A_SelectWeapon("RTCW_Luger")
			Goto Deselect

        FireUnderwater:
            RW00 A 0 A_StopSound(CHAN_WEAPON)
            RW00 A 0 A_PlaySound("RTCW/UnderwaterFire",CHAN_5)
            RW00 A 17 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
            goto ReadyActual

        FireStart:
            RW00 A 0 A_JumpIfInventory("SamsaraCurrentWaterLevel",0,"FireUnderwater")
			RW00 A 0 A_GiveInventory("RTCW_BlazkoWeaponAlert")
			RW00 A 0 A_PlaySound("RTCW/Flamethrower/Start",CHAN_5)
			RW00 A 0 A_PlaySound("RTCW/Flamethrower/Stream",CHAN_WEAPON,1.0,true)
            goto FireHoldContinue

		FireHold:
            RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireHoldStart")
            RW00 A 0 A_JumpIfInventory("Cell",1,"FireHoldStart")
            goto FireDry

		FireHoldStart:
            RW00 A 0 A_JumpIfInventory("SamsaraCurrentWaterLevel",0,"FireUnderwater")
			RW00 A 0 A_GiveInventory("RTCW_BlazkoWeaponAlert")
		FireHoldContinue:
			RW00 A 0 A_GiveInventory("SamsaraRTCWFlamethrowerAttackHandler")
            RW00 A 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW00 A 0 A_ReFire("FireHold")
			goto ReadyActual

		Kicking:
			TNT1 A 0 A_GunFlash("KickingFoot")
		KickingDelay:
			RW00 A 18 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW00 A 0 A_TakeInventory("RTCW_Kicking",1)
			RW00 A 0 A_GunFlash("Nozzle")
			goto ReadyActual
	}
}

actor SamsaraRTCWFlamethrowerAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Cell",1,1)
        stop
        TNT1 A 0 A_JumpIfInventory("RTCW_FlamethrowerStreamAmmo",1,2)
        TNT1 A 0 A_GiveInventory("RTCW_FlamethrowerStreamAmmo")
        goto PickupAttack
        TNT1 A 0 A_TakeInventory("Cell",1)
        TNT1 A 0 A_TakeInventory("RTCW_FlamethrowerStreamAmmo")
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("RTCW_FlamethrowerFire",frandom(-0.25,0.25),false,4,7,0,frandom(-0.25,0.25))
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("RTCW_FlamethrowerFireCoop",frandom(-0.25,0.25),false,4,7,0,frandom(-0.25,0.25))
        stop
    }
}

Actor RTCW_FlamethrowerFire : FastProjectile
{
	Speed 192
	Radius 2
	Height 4
	Damage (0)
	DamageType "RTCWFire"
	+SKYEXPLODE
	+DONTSPLASH
	+CANNOTPUSH
	+BLOODLESSIMPACT
	+HITTRACER
	States
	{
		Spawn:
			TNT1 A 2
			Goto Death	
		Death:
			//TNT1 A 0 A_CustomMissile("RTCW_FlamethrowerEndPoint",0)
			TNT1 A 0 ACS_NamedExecuteWithResult("RTCW_FlamethrowerStreamRotate",16,36)
			TNT1 A 2
			stop
	}
}

Actor RTCW_FlamethrowerFireCoop : RTCW_FlamethrowerFire { +THRUSPECIES Species "Player" }

Actor RTCW_FlamethrowerEndPoint
{
	Height 8
	Radius 4
	Scale 0.25
	Damage (0)
	Renderstyle Translucent
	DamageType "RTCWFire"
	ExplosionDamage 0
	Projectile
	+THRUACTORS
	+NODAMAGETHRUST
	+FORCEXYBILLBOARD
	+FORCERADIUSDMG
	States
	{
		Spawn:
		Death:
			TNT1 A 0 NoDelay ACS_NamedExecuteWithResult("RTCW_FlamethrowerFireScale")
			RW03 P 0 A_PlaySound("RTCW/Flamethrower/Fire",CHAN_BODY,0.5)
			RW03 CD 2 Bright
			TNT1 A 0 A_GiveInventory("RTCW_FlamethrowerIgniteShooter")
			RW03 EF 2 Bright
			TNT1 A 0 A_GiveInventory("RTCW_FlamethrowerIgniteShooter")
			RW03 GH 2 Bright
			TNT1 A 0 A_GiveInventory("RTCW_FlamethrowerIgniteShooter")
			RW03 IJ 2 Bright
			TNT1 A 0 A_GiveInventory("RTCW_FlamethrowerIgniteShooter")
			RW03 KL 2 Bright
			TNT1 A 0 A_GiveInventory("RTCW_FlamethrowerIgniteShooter")
			RW03 MN 2 Bright
			TNT1 A 0 A_GiveInventory("RTCW_FlamethrowerIgniteShooter")
			RW03 OP 2 Bright
			TNT1 A 0 A_GiveInventory("RTCW_FlamethrowerIgniteShooter")
			RW03 QR 2 Bright
			TNT1 A 0 A_GiveInventory("RTCW_FlamethrowerIgniteShooter")
			RW03 ST 2 Bright
			TNT1 A 0 A_GiveInventory("RTCW_FlamethrowerIgniteShooter")
			RW03 UV 2 Bright
			TNT1 A 0 A_GiveInventory("RTCW_FlamethrowerIgniteShooter")
			RW03 WX 2 Bright
			TNT1 A 0 A_GiveInventory("RTCW_FlamethrowerIgniteShooter")
			RW03 YZ 2 Bright
			TNT1 A 0 A_GiveInventory("RTCW_FlamethrowerIgniteShooter")
			RW04 AB 2 Bright
			TNT1 A 0 A_GiveInventory("RTCW_FlamethrowerIgniteShooter")
			RW04 CD 2 Bright
			TNT1 A 0 A_GiveInventory("RTCW_FlamethrowerIgniteShooter")
			RW04 EF 2 Bright
			TNT1 A 0 A_GiveInventory("RTCW_FlamethrowerIgniteShooter")
			RW04 GH 2 Bright
			TNT1 A 0 A_GiveInventory("RTCW_FlamethrowerIgniteShooter")
			RW04 IJ 2 Bright
			TNT1 A 0 A_GiveInventory("RTCW_FlamethrowerIgniteShooter")
			RW04 KL 2 Bright
			TNT1 A 0 A_GiveInventory("RTCW_FlamethrowerIgniteShooter")
			RW04 MN 2 Bright
			TNT1 A 0 A_GiveInventory("RTCW_FlamethrowerIgniteShooter")
			RW04 OP 2 Bright
			TNT1 A 0 A_GiveInventory("RTCW_FlamethrowerIgniteShooter")
			RW04 QR 2 Bright
			TNT1 A 0 A_GiveInventory("RTCW_FlamethrowerIgniteShooter")
			RW04 S 2 Bright A_GiveInventory("RTCW_FlamethrowerIgniteShooter")
			RW04 S 0 A_StopSound(CHAN_BODY)
			Stop
	}
}

Actor RTCW_FlamethrowerIgniteShooter : Trigger
{
	States
	{
		Pickup:
			TNT1 A 0 A_SetAngle(5*random(0,6))
			TNT1 A 0 A_CustomMissile("RTCW_FlamethrowerIgniteTracer",-16,0,0,CMF_AIMDIRECTION|CMF_TRACKOWNER)
			TNT1 A 0 A_CustomMissile("RTCW_FlamethrowerIgniteTracer",-16,0,45,CMF_AIMDIRECTION|CMF_TRACKOWNER)
			TNT1 A 0 A_CustomMissile("RTCW_FlamethrowerIgniteTracer",-16,0,90,CMF_AIMDIRECTION|CMF_TRACKOWNER)
			TNT1 A 0 A_CustomMissile("RTCW_FlamethrowerIgniteTracer",-16,0,135,CMF_AIMDIRECTION|CMF_TRACKOWNER)
			TNT1 A 0 A_CustomMissile("RTCW_FlamethrowerIgniteTracer",-16,0,180,CMF_AIMDIRECTION|CMF_TRACKOWNER)
			TNT1 A 0 A_CustomMissile("RTCW_FlamethrowerIgniteTracer",-16,0,225,CMF_AIMDIRECTION|CMF_TRACKOWNER)
			TNT1 A 0 A_CustomMissile("RTCW_FlamethrowerIgniteTracer",-16,0,270,CMF_AIMDIRECTION|CMF_TRACKOWNER)
			TNT1 A 0 A_CustomMissile("RTCW_FlamethrowerIgniteTracer",-16,0,315,CMF_AIMDIRECTION|CMF_TRACKOWNER)
			stop
	}
}

Actor RTCW_FlamethrowerIgnition : Counter { Inventory.MaxAmount 50 }
Actor RTCW_FlamethrowerStacker : Counter { Inventory.MaxAmount 32 }
Actor RTCW_FlamethrowerHitTarget : Boolean {}
Actor RTCW_FlamethrowerBurnt : Boolean {}

Actor RTCW_FlamethrowerIgniteTracer
{
	Height 64
	Radius 32
	Speed 32
	Projectile
	+HITTRACER
	States
	{
		Spawn:
			TNT1 A 2
			Stop
		Death:
			TNT1 A 1
			Stop	
		Crash:
		XDeath:
			TNT1 A 2 ACS_NamedExecuteWithResult("RTCW_FlamethrowerTracer")
			Stop
	}
}

Actor RTCW_FlamethrowerBurner
{
	Height 4
	Radius 2
	Speed 0
	Damage (0)
	Projectile
	DamageType "RTCWFire"
	+THRUACTORS
	+NODAMAGETHRUST
	+FORCEPAIN
	var int user_radius;
	var int user_height;
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 ACS_NamedExecuteWithResult("RTCW_BurnHandler")
		Death:
			TNT1 A 0 A_SetUserVar("user_radius",CallACS("RTCW_Decorate",45))
			TNT1 A 0 A_SetUserVar("user_height",CallACS("RTCW_Decorate",45,1))
			TNT1 A 0 A_Explode(CallACS("RTCW_Decorate",46),16,!XF_HURTSOURCE,0,16)
			TNT1 A 0 A_ChangeFlag("PAINLESS",1)
			TNT1 A 0 A_ChangeFlag("FORCEPAIN",0)
		SpawnLoop:
			TNT1 A 0 A_CheckFlag("SHOOTABLE",1,AAPTR_Tracer)
			Goto Remove
			TNT1 A 0 A_CheckFlag("CORPSE","Remove",AAPTR_Tracer)
			TNT1 A 0 A_SpawnItemEx("RTCW_FlamethrowerFireVisual",random(-user_radius/2,user_radius/2),random(-user_radius/2,user_radius/2),random(0,user_height),0,0,0,0,SXF_NOCHECKPOSITION)
			TNT1 A 0 A_Explode(CallACS("RTCW_Decorate",46),16,!XF_HURTSOURCE,0,16)
			TNT1 A 1 A_Warp(AAPTR_Tracer,0,0,0,0,WARPF_INTERPOLATE|WARPF_NOCHECKPOSITION)
			TNT1 A 0 A_CheckFlag("SHOOTABLE",1,AAPTR_Tracer)
			Goto Remove
			TNT1 A 0 A_CheckFlag("CORPSE","Remove",AAPTR_Tracer)
			TNT1 A 0 A_Explode(CallACS("RTCW_Decorate",46),16,!XF_HURTSOURCE,0,16)
			TNT1 A 1 A_Warp(AAPTR_Tracer,0,0,0,0,WARPF_INTERPOLATE|WARPF_NOCHECKPOSITION)
			TNT1 A 0 A_CheckFlag("SHOOTABLE",1,AAPTR_Tracer)
			Goto Remove
			TNT1 A 0 A_CheckFlag("CORPSE","Remove",AAPTR_Tracer)
			TNT1 A 0 A_Explode(CallACS("RTCW_Decorate",46),16,!XF_HURTSOURCE,0,16)
			TNT1 A 1 A_Warp(AAPTR_Tracer,0,0,0,0,WARPF_INTERPOLATE|WARPF_NOCHECKPOSITION)
			TNT1 A 0 A_CheckFlag("SHOOTABLE",1,AAPTR_Tracer)
			Goto Remove
			TNT1 A 0 A_CheckFlag("CORPSE","Remove",AAPTR_Tracer)
			TNT1 A 0 A_Explode(CallACS("RTCW_Decorate",46),16,!XF_HURTSOURCE,0,16)
			TNT1 A 1 A_Warp(AAPTR_Tracer,0,0,0,0,WARPF_INTERPOLATE|WARPF_NOCHECKPOSITION)
			Loop
		Remove:
			TNT1 A 0 A_TakeInventory("RTCW_FlamethrowerIgnition",65536,0,AAPTR_Tracer)
			TNT1 A 0 A_TakeInventory("RTCW_FlamethrowerHitTarget",1,0,AAPTR_Tracer)
			TNT1 A 0 A_TakeInventory("RTCW_FlamethrowerBurnt",1,0,AAPTR_Tracer)
			Stop
	}
}

Actor RTCW_FlamethrowerStartBurn : Trigger
{
	States
	{
		Pickup:
			TNT1 A 0 A_PrintBold("Burn baby burn")
			Stop
	}
}

Actor RTCW_FlamethrowerEndPointHarmless : RTCW_FlamethrowerEndPoint
{
	States
	{
		Spawn:
		Death:
			TNT1 A 0 NoDelay ACS_NamedExecuteWithResult("RTCW_FlamethrowerFireScale")
			RW03 CDEFGHIJKLMNOPQRSTUVWXYZ 2 Bright
			RW04 ABCDEFGHIJKLMNOPQRS 2 Bright
			Stop
	}
}

Actor RTCW_FlameStreamSegment
{
	+NOINTERACTION
	+NOBLOCKMAP
	+CLIENTSIDEONLY
	Radius 1
	Height 1
	Renderstyle Add
	Alpha 1.0
	States
	{
		Spawn:
			RW00 A 2 Bright
			stop
	}
}

Actor RTCW_FlameStreamSegment1 : RTCW_FlameStreamSegment { Alpha 0.15 }
Actor RTCW_FlameStreamSegment2 : RTCW_FlameStreamSegment { Alpha 0.3 States { Spawn: RW00 A 1 Bright A_Stop Goto Super::Spawn } }
Actor RTCW_FlameStreamSegment3 : RTCW_FlameStreamSegment { Alpha 0.45 States { Spawn: RW00 A 2 Bright A_Stop Goto Super::Spawn } }
Actor RTCW_FlameStreamSegment4 : RTCW_FlameStreamSegment { Alpha 0.6 States { Spawn: RW00 A 3 Bright A_Stop Goto Super::Spawn } }
Actor RTCW_FlameStreamSegment5 : RTCW_FlameStreamSegment { Alpha 0.75 States { Spawn: RW00 A 4 Bright A_Stop Goto Super::Spawn } }
Actor RTCW_FlameStreamSegment6 : RTCW_FlameStreamSegment { States { Spawn: RW00 A 5 Bright A_Stop Goto Super::Spawn } }
Actor RTCW_FlameStreamSegment7 : RTCW_FlameStreamSegment { States { Spawn: RW00 A 6 Bright A_Stop Goto Super::Spawn }}
Actor RTCW_FlameStreamSegment8 : RTCW_FlameStreamSegment { States { Spawn: RW00 A 7 Bright A_Stop Goto Super::Spawn }}
Actor RTCW_FlameStreamSegmentBlue : RTCW_FlameStreamSegment {}

Actor RTCW_FlamethrowerFireScript : Inventory { +INVENTORY.UNDROPPABLE Inventory.MaxAmount 1 Inventory.InterHubAmount 0 }

Actor RTCW_FlamethrowerFireVisual
{
	Scale 0.667
	Renderstyle Translucent
	+NOGRAVITY
	+NOINTERACTION
	+FORCEXYBILLBOARD
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 3
			RW03 P 0 A_PlaySound("RTCW/Flamethrower/Fire",CHAN_BODY,0.5)
			RW03 PQRSTUVWXYZ 2 Bright
			RW04 ABCDEFGHIJKLMNOPQRS 2 Bright
			RW04 S 0 A_StopSound(CHAN_BODY)
			Stop
	}
}
