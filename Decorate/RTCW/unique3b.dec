Actor RTCW_Pineapple : RTCW_Weapon
{
	Weapon.SlotNumber 1
	Weapon.SlotPriority 1
	Weapon.SelectionOrder 1900
	Weapon.AmmoType "RTCW_AlliedAmmo3"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 5
	Obituary "%o was killed (gren - am) by %k."
	Inventory.Pickupmessage "Mk2 Grenade"
	Inventory.PickupSound "RTCW/WeaponPickup"
	Tag "Pineapple Grenade"
    +AMMO_OPTIONAL
	+WEAPON.EXPLOSIVE
	+WEAPON.NOALERT
	States
	{
		Spawn:
			RW98 A -1
			stop

		ReadyActual:
            RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReadyActualAmmo")
            RW00 A 0 A_JumpIfInventory("RTCW_AlliedAmmo3",1,"ReadyActualAmmo")
            goto ReadyActualNoAmmo

		ReadyActualNoAmmo:
			TNT1 A 0 A_JumpIfInventory("RTCW_Kicking",1,"KickingNoAmmo")
			TNT1 A 1 A_WeaponReady
			goto ReadyActualNoAmmoCheck

        ReadyActualAmmo:
			RW00 A 0 A_JumpIfInventory("RTCW_Kicking",1,"Kicking")
			RW00 A 1 A_WeaponReady
			goto ReadyActual

        ReadyActualNoAmmoCheck:
            RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReloadStart")
            RW00 A 0 A_JumpIfInventory("RTCW_AlliedAmmo3",1,"ReloadStart")
            goto ReadyActual

		Deselect:
            RW00 G 0 A_JumpIfHealthLower(1,"DeselectInstant")
			RW00 G 0 A_PlaySound("RTCW/Change",CHAN_7)
            RW00 G 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"DeselectAmmo")
            RW00 G 0 A_JumpIfInventory("RTCW_AlliedAmmo3",1,"DeselectAmmo")
            goto DeselectNoAmmo

		DeselectNoAmmo:
			TNT1 AAAAA 2
			goto DeselectInstant

        DeselectAmmo:
			RW00 GHIJK 2
			goto DeselectInstant

		Ready: // our "Select" state
			RW00 A 0 A_TakeInventory("RTCW_WeaponToken",256)
			RW00 A 0 A_GiveInventory("RTCW_WeaponToken",7)
			RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReadyAmmo")
			RW00 A 0 A_JumpIfInventory("RTCW_AlliedAmmo3",1,"ReadyAmmo")
			goto ReadyNoAmmo

        ReadyNoAmmo:
			TNT1 AAAA 2
            goto ReadyActual

        ReadyAmmo:
			RW00 LMNO 2
            goto ReadyActual

		Fire:
            RW00 B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart")
            RW00 B 0 A_JumpIfInventory("RTCW_AlliedAmmo3",1,"FireStart")
            goto FireDry

        FireDry:
			TNT1 A 0 A_PlaySound("RTCW/NoAmmo",CHAN_5)
            TNT1 A 17 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW00 A 0 A_SelectWeapon("RTCW_Luger")
			goto Deselect

        FireStart:
            RW00 B 0 A_TakeInventory("RTCW_GrenadeFuse")
            RW00 B 0 A_TakeInventory("RTCW_GrenadeTimer")
			RW00 B 0 A_PlaySound("RTCW/Grenade/Pulse4",CHAN_5)
            RW00 B 0 A_GiveInventory("RTCW_GrenadeFuse")
            RW00 B 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
            RW00 B 0 A_GiveInventory("RTCW_GrenadeFuse")
            RW00 B 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
            RW00 B 0 A_GiveInventory("RTCW_GrenadeFuse")
            RW00 B 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
            RW00 C 0 A_GiveInventory("RTCW_GrenadeFuse")
            RW00 C 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
            RW00 C 0 A_GiveInventory("RTCW_GrenadeFuse")
            RW00 C 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
            RW00 C 0 A_GiveInventory("RTCW_GrenadeFuse")
            RW00 C 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
		GrenadeHold:
			RW00 C 0 A_JumpIfInventory("RTCW_GrenadeTimer",0,"ReleaseGrenade")
			RW00 C 0 A_JumpIfInventory("RTCW_GrenadeFuse",0,"GrenadeTimerUp")
			RW00 C 0 A_GiveInventory("RTCW_GrenadeFuse")
            RW00 C 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW00 C 0 A_ReFire("GrenadeHold")
			goto ReleaseGrenade

		GrenadeTimerUp:
			RW00 C 0 A_TakeInventory("RTCW_GrenadeFuse")
			RW00 C 0 A_GiveInventory("RTCW_GrenadeTimer")
			RW00 C 0 A_JumpIfInventory("RTCW_GrenadeTimer",0,"GrenadeHold")
			RW00 C 0 A_JumpIfInventory("RTCW_GrenadeTimer",3,4)
			RW00 C 0 A_JumpIfInventory("RTCW_GrenadeTimer",2,2)
			RW00 C 0 A_PlaySound("RTCW/Grenade/Pulse3",CHAN_5)
			goto GrenadeHold
			RW00 C 0 A_PlaySound("RTCW/Grenade/Pulse2",CHAN_5)
			goto GrenadeHold
			RW00 C 0 A_PlaySound("RTCW/Grenade/Pulse1",CHAN_5)
			goto GrenadeHold

		ReleaseGrenade:
			RW00 B 0 A_GiveInventory("RTCW_FiringToken",1)
        ReleaseGrenadeEndCheck:
            RW00 B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReleaseGrenadeEnd")
            RW00 B 0 A_JumpIfInventory("RTCW_AlliedAmmo3",2,"ReleaseGrenadeEnd")
            goto ReleaseGrenadeEndLast

        ReleaseGrenadeEnd:
            RW00 B 0 A_GiveInventory("SamsaraRTCWPineappleAttackHandler")
            RW00 B 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
            TNT1 A 35 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
            goto ReadyActualNoAmmoCheck

        ReleaseGrenadeEndLast:
            RW00 B 0 A_PlaySound("RTCW/NoAmmo",CHAN_5)
            RW00 B 0 A_PlaySound("RTCW/Change",CHAN_7)
            RW00 B 0 A_GiveInventory("SamsaraRTCWPineappleAttackHandler")
            RW00 B 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
            TNT1 A 35 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
            TNT1 A 0 A_SelectWeapon("RTCW_Luger")
            goto DeselectInstant

        ReloadStart:
        ReloadFinish:
            RW00 LMNO 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
            goto ReadyActual

		KickingNoAmmo: // jumps to "with ammo" version for the time being
		Kicking:
			RW00 A 0 A_GunFlash("KickingFoot")
		KickingDelay:
			RW00 A 18 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW00 A 0 A_TakeInventory("RTCW_Kicking",1)
			goto ReadyActual
	}
}

actor SamsaraRTCWPineappleAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("RTCW_AlliedAmmo3",1,1)
        stop
        TNT1 A 0 A_TakeInventory("RTCW_AlliedAmmo3",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("RTCW/Grenade/Throw",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("RTCW_PineappleProjectile",0,false,8,4)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("RTCW_PineappleProjectileCoop",0,false,8,4)
        stop
    }
}

Actor RTCW_PineappleProjectile : RTCW_GrenadeProjectile
{
	Speed 10
	States
	{
		Spawn:
			RW00 A 0 NoDelay A_GiveInventory("RTCW_GrenadeTimer",CallACS("RTCW_Decorate",7))
            RW00 A 0 A_JumpIfInventory("RTCW_GrenadeTimer",0,"StopGrenade")
			RW00 A 0 ThrustThingZ(0,20,0,1)
		SpawnLoop:
			RW00 AAAAAAAAAAAAAAAAAAAAAAAA 1
			RW00 A 0 A_GiveInventory("RTCW_GrenadeTimer",1)
			RW00 A 0 A_JumpIfInventory("RTCW_GrenadeTimer",0,"StopGrenade")
			Goto SpawnLoop
		Bounce:
			RW00 A 0 A_GiveInventory("RTCW_GrenadeCounter",1)
			RW00 A 0 A_JumpIfInventory("RTCW_GrenadeCounter",0,1)
			Goto Bounce+5
			RW00 A 0 A_GiveInventory("RTCW_GrenadeTimer",1)
			RW00 A 0 A_TakeInventory("RTCW_GrenadeCounter")
			RW00 A 0 A_JumpIfInventory("RTCW_GrenadeTimer",0,"StopGrenade")
			RW00 A 1 A_SpawnItemEx("RTCW_GrenadeTrailSmoke")
			Goto Bounce
		OnFloor:
			RW00 B 0 A_GiveInventory("RTCW_GrenadeTimer",1)
			RW00 B 17 A_JumpIfInventory("RTCW_GrenadeTimer",0,"StopGrenade")
			Loop
		StopGrenade:
			RW00 A 0 A_CountDown
			Goto Detonate
		Crash:
		Death:
		XDeath:
			RW00 A 0 A_Stop
			RW00 A 0 A_JumpIfInventory("RTCW_GrenadeTimer",0,1)
			Goto OnFloor
		Detonate:
			RW00 AAAA 0 A_SpawnItemEx("RTCW_PanzerfaustSmokeExplosion",0,0,0,frandom(-0.5,0.5),frandom(-0.5,0.5),1)
			RW00 AAAA 0 A_SpawnItemEx("RTCW_ExplosionSmokeDebris")
			RW00 A 0 A_GiveInventory("RTCW_BlazkoWeaponAlert",1,AAPTR_Target)
			RW00 A 0 A_Explode(375,120,XF_HURTSOURCE,1,0) // 80 is too small // values may need adjustment. hand detonation was 200,300
			RW00 A 0 A_SpawnItemEx("RTCW_PanzerfaustRocketExplosion")
			Stop
	}
}

Actor RTCW_PineappleProjectileCoop : RTCW_PineappleProjectile { +THRUSPECIES Species "Player" }
