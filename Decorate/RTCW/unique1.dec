Actor RTCW_Winchester : RTCW_Weapon
{
	Weapon.SlotNumber 9
	Weapon.SlotPriority 0
	Weapon.SelectionOrder 1100
	Weapon.AmmoType1 "RTCW_ShotgunMagazine"
	Weapon.AmmoUse1 1
	Weapon.AmmoType2 "Shell"
	Weapon.AmmoGive2 3
	Obituary "%o was killed (shotgun) by %k."
	Inventory.Pickupmessage "Winchester M1897"
	Inventory.PickupSound "RTCW/WeaponPickup"
	Tag "Winchester M1897"
	+WEAPON.NOALERT
	+WEAPON.AMMO_OPTIONAL
	States
	{
		Spawn:
			RW98 A -1
			Stop

        ReadyActual:
			RW00 A 0 A_JumpIfInventory("RTCW_Kicking",1,"Kicking")
            RW00 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReadyActualReloadable")
        ReadyActualNonReloadable:
			RW00 A 1 A_WeaponReady
			goto ReadyActualAmmoCheck

        ReadyActualReloadable:
			RW00 A 1 A_WeaponReady(WRF_ALLOWRELOAD)
			goto ReadyActualAmmoCheck

        ReadyActualAmmoCheck:
            RW00 A 0 A_JumpIfInventory("SamsaraReloadMode",1,1)
            goto ReadyActual
            RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"ReadyActual")
            RW00 A 0 A_JumpIfInventory("RTCW_ShotgunMagazine",1,"ReadyActual")
            goto ReloadCheck

		Deselect:
			RW00 I 0 A_PlaySound("RTCW/Change",CHAN_7)
			RW00 IJKLM 2
			Goto Super::Deselect

		Ready: // our "Select" state
			RW00 A 0 A_TakeInventory("RTCW_WeaponToken",256)
			RW00 A 0 A_GiveInventory("RTCW_WeaponToken",24)
			RW00 OPQRST 2
            goto ReadyActual

		Fire:
            RW00 B 0 A_JumpIfInventory("SamsaraReloadMode",1,"FireReloadable")
        FireNonReloadable:
            RW00 B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart")
            RW00 B 0 A_JumpIfInventory("Shell",1,"FireStart")
            goto FireDry

        FireReloadable:
            RW00 B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireStart")
            RW00 B 0 A_JumpIfInventory("RTCW_ShotgunMagazine",1,"FireStart")
        FireReloadableDryCheck:
            RW00 B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReloadStart")
            RW00 B 0 A_JumpIfInventory("Shell",1,"ReloadStart")
            goto FireDry

        FireDry:
            RW00 A 17 A_PlaySound("RTCW/NoAmmo",CHAN_5)
			TNT1 A 0 A_SelectWeapon("RTCW_Knife")
			Goto Deselect

        FireUnderwater:
            RW00 A 17 A_PlaySound("RTCW/UnderwaterFire",CHAN_5)
            goto ReadyActual

		FireStart:
            RW00 U 0 A_JumpIfInventory("SamsaraCurrentWaterLevel",0,"FireUnderwater")
			RW00 U 0 A_GiveInventory("RTCW_FiringToken",1)
			RW00 U 0 A_GiveInventory("RTCW_BlazkoWeaponAlert",1)
			RW00 U 0 A_GunFlash("FireFlash")
			RW00 U 2 A_GiveInventory("SamsaraRTCWShotgunAttackHandler")
			RW00 "VWXYZ[" 2
			RW00 "\" 0 A_JumpIfInventory("RTCW_ShotgunMagazine",1,1)
			Goto FireLastShot
			RW00 "\" 0 A_SpawnItemEx("RTCW_CasingShell",4,-6*cos(-pitch),32+(6*sin(-pitch)),1,0,0,-random(85,95),SXF_TRANSFERPITCH)
			RW00 "\]" 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			RW01 ABCDEFGHIJ 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			Goto ReadyActual
			
		FireLastShot:
			RW00 "\" 8 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			Goto Reload
			
		FireFlash:
			TNT1 A 0 A_Jump(256,1,2)
			RW05 U 2 Bright
			Stop
			RW06 U 2 Bright
			Stop

        Reload:
        ReloadCheck:
            RW00 A 0 A_JumpIfInventory("RTCW_ShotgunMagazine",0,"ReadyActual")
            RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
            RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReloadStart")
            RW00 A 0 A_JumpIfInventory("Shell",1,"ReloadStart")
            goto ReadyActual

		ReloadStart:
			TNT1 A 0 A_JumpIfInventory("RTCW_ShotgunMagazine",1,1)
			goto ReloadFirst
			RW02 "]" 1
			RW03 ABCDEFG 1
			RW03 H 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReloadAmmo")
            goto ReloadDone
		ReloadAmmoTransition:
			RW02 MLKJH 1
			RW03 F 0
		ReloadAmmo:
            TNT1 A 0 A_JumpIfInventory("RTCW_ShotgunMagazine",0,"ReloadDone")
            TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
            TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
            TNT1 A 0 A_JumpIfInventory("Shell",1,1)
            goto ReloadDone
			RW03 H 0 A_Refire("ReloadDone")
			RW03 H 0 A_PlaySound("RTCW/Shotgun/AddShell",CHAN_6)
			RW03 HJLN 1
			RW03 O 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
            RW03 O 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
            RW03 O 0 A_TakeInventory("Shell",1)
            RW03 O 0 A_GiveInventory("RTCW_ShotgunMagazine")
			RW03 "OQSUWY[]" 1
            loop
		ReloadDone:
			RW04 ABCDEFGHIJ 1
			RW04 K 8 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			Goto ReadyActual
			
		ReloadFirst:
			RW01 LMNOPQRST 1
			RW01 U 0 A_PlaySound("RTCW/Shotgun/AddShell",CHAN_6)
			RW01 "UVWXYZ[\]" 1
		ReloadingFirstShell:
			RW02 A 0 A_JumpIfInventory("RTCW_ShotgunMagazine",1,"ReloadFirstFinish")
            RW02 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
            RW02 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
            RW02 A 0 A_JumpIfInventory("Shell",1,1)
            Goto ReloadDone
            RW02 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
            RW02 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
            RW02 A 0 A_TakeInventory("Shell",1)
            RW02 A 0 A_GiveInventory("RTCW_ShotgunMagazine")
            Loop
		ReloadFirstFinish:
			RW02 ABCDEFGHIJKLM 1
			RW02 N 0 A_PlaySound("RTCW/Shotgun/Reload",CHAN_6)
			RW02 N 0 A_SpawnItemEx("RTCW_CasingShell",4,-6*cos(-pitch),32+(6*sin(-pitch)),1,0,0,-random(85,95),SXF_TRANSFERPITCH)
			RW02 NOPQRSTUV 1
			RW02 W 0 A_JumpIfInventory("Shell",1,"ReloadAmmoTransition")
			RW02 W 8 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
            goto ReadyActual

		Kicking:
			TNT1 A 0 A_GunFlash("KickingFoot")
		KickingDelay:
			RW00 A 18 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			RW00 A 0 A_TakeInventory("RTCW_Kicking",1)
			goto ReadyActual
	}
}

actor SamsaraRTCWShotgunAttackHandler : Trigger
{
    States
    {
      // Ammo check stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"PickupCheckAmmoReloadable")
      PickupCheckAmmoNonReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupTakeAmmoNonReloadable")
        TNT1 A 0 A_JumpIfInventory("Shell",1,"PickupTakeAmmoNonReloadable")
        stop

      PickupCheckAmmoReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("RTCW_ShotgunMagazine",1,"PickupTakeAmmoReloadable")
        stop

      // Ammo taking stuff
      PickupTakeAmmoNonReloadable:
        TNT1 A 0 A_TakeInventory("Shell",1,TIF_NOTAKEINFINITE)
        goto PickupAttack

      PickupTakeAmmoReloadable:
        TNT1 A 0 A_TakeInventory("RTCW_ShotgunMagazine",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
		TNT1 A 0 A_JumpIfInventory("RTCW_ShotgunMagazine",1,2)
		TNT1 A 0 A_PlaySound("RTCW/Shotgun/FireLast",CHAN_WEAPON)
		Goto PickupAttack+3
        TNT1 A 0 A_PlaySound("RTCW/Shotgun/Fire",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 AAAAAAAAAAAA 0 A_FireCustomMissile("RTCW_ShotgunTracer",frandom(-6.0,6.0),false,4,4,0,frandom(-6.0,6.0))
        goto PickupFinish

      PickupAttackCoop:
        TNT1 AAAAAAAAAAAA 0 A_FireCustomMissile("RTCW_ShotgunTracerCoop",frandom(-6.0,6.0),false,4,4,0,frandom(-6.0,6.0))
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0
        stop
    }
}

actor RTCW_ShotgunMagazine : Ammo
{
    Inventory.MaxAmount 6
    +IGNORESKILL
}

Actor RTCW_ShotgunTracer : RTCW_Tracer
{
	States
	{
		Crash:
		Death:
			TNT1 A 0 A_Explode(CallACS("RTCW_BulletDamage",14),8,!XF_HURTSOURCE,0,8)
			Goto Super::Death
		XDeath:
			TNT1 A 0 ACS_NamedExecuteWithResult("RTCW_Decorate",47)
			TNT1 A 0 A_Explode(CallACS("RTCW_BulletDamage",14),8,!XF_HURTSOURCE,0,8)
			Goto Super::XDeath
	}
}

Actor RTCW_ShotgunTracerCoop : RTCW_ShotgunTracer { +THRUSPECIES Species "Player" }

Actor RTCW_CasingShell : RTCW_Casing {} 