actor RTCW_ColtDualWield : Boolean {}
actor RTCW_ColtFiredLeft : Boolean {}

Actor RTCW_Colt : RTCW_Weapon
{
	Weapon.SlotNumber 1
	Weapon.SlotPriority 3
	Weapon.SelectionOrder 1600
	Weapon.AmmoUse 1
	Weapon.AmmoType "RTCW_ColtMagazine"
	Obituary "%o was killed (colt) by %k."
	Inventory.Pickupmessage "Colt"
	Inventory.PickupSound "RTCW/WeaponPickup"
	Tag "Colt"
	+WEAPON.NOALERT
	+WEAPON.AMMO_OPTIONAL
	+WEAPON.WIMPY_WEAPON
	States
	{
		Spawn:
			RW98 A -1
			Stop

        // Single
        ReadyActual:
			RW00 A 0 A_JumpIfInventory("RTCW_Kicking",1,"Kicking")
            RW00 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReadyActualReloadable")
        ReadyActualNonReloadable:
            RW00 A 0 A_JumpIfInventory("RTCW_ColtDualWield",1,"ReadyActualDualNonReloadable")
			RW00 A 1 A_WeaponReady
			goto ReadyActualAmmoCheck

        ReadyActualReloadable:
            RW00 A 0 A_JumpIfInventory("RTCW_ColtDualWield",1,"ReadyActualDualReloadable")
			RW00 A 1 A_WeaponReady(WRF_ALLOWRELOAD)
			goto ReadyActualAmmoCheck

        ReadyActualAmmoCheck:
            RW00 A 0 A_JumpIfInventory("SamsaraReloadMode",1,1)
            goto ReadyActual
            RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"ReadyActual")
            RW00 A 0 A_JumpIfInventory("RTCW_ColtMagazine",1,"ReadyActual")
            goto ReloadCheck

		Deselect:
			RW00 V 0 A_PlaySound("RTCW/Change",CHAN_7)
			RW00 G 2 A_JumpIfInventory("RTCW_ColtDualWield",1,"DeselectDual")
			RW00 HIJK 2
			Goto Super::Deselect

		Ready: // our "Select" state
			RW00 A 0 A_TakeInventory("RTCW_WeaponToken",256)
			RW00 A 0 A_JumpIfInventory("RTCW_ColtDualWield",1,"ReadyDual")
			RW00 L 2 A_GiveInventory("RTCW_WeaponToken",4)
			RW00 MNO 2
            goto ReadyActual

		Fire:
			RW00 B 0 A_JumpIfInventory("RTCW_ColtDualWield",1,"FireDual")
            RW00 B 0 A_JumpIfInventory("SamsaraReloadMode",1,"FireReloadable")
        FireNonReloadable:
            RW00 B 0 A_JumpIfInventory("PistolModeOn",1,1)
            goto FireStart
            RW00 B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart")
            RW00 B 0 A_JumpIfInventory("Clip",1,"FireStart")
            goto FireDry

        FireReloadable:
            RW00 B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireStart")
            RW00 B 0 A_JumpIfInventory("RTCW_ColtMagazine",1,"FireStart")
        FireReloadableDryCheck:
            RW00 B 0 A_JumpIfInventory("PistolModeOn",1,1)
            goto ReloadStart
            RW00 B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReloadStart")
            RW00 B 0 A_JumpIfInventory("Clip",1,"ReloadStart")
            goto FireDry

        FireDry:
            RW00 A 0 A_PlaySound("RTCW/NoAmmo",CHAN_5)
			RW00 A 17 A_JumpIfInventory("RTCW_ColtDualWield",1,"FireDualDry")
        FireDryFinish:
			TNT1 A 0 A_SelectWeapon("RTCW_Knife")
			Goto Deselect

		FireStart:
			RW00 B 0 A_GiveInventory("RTCW_FiringToken",1)
			RW00 B 0 A_GiveInventory("RTCW_BlazkoWeaponAlert",1)
			RW04 B 0 A_FireCustomMissile("RTCW_Casing",-random(85,105),false,8,4,0,-4)
			RW00 B 0 A_GunFlash("FireFlash")
			RW00 B 2 A_GiveInventory("SamsaraRTCWColtRightAttackHandler")
			RW00 CDEFGA 2
			goto ReadyActual

		FireFlash:
			RW03 B 2 Bright
			Stop

        Reload:
        ReloadCheck:
			TNT1 A 0 A_JumpIfInventory("RTCW_ColtDualWield",1,"ReloadCheckDual")
            RW00 A 0 A_JumpIfInventory("RTCW_ColtMagazine",0,"ReadyActual")
            RW00 A 0 A_JumpIfInventory("PistolModeOn",1,1)
            goto ReloadStart
            RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
            RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReloadStart")
            RW00 A 0 A_JumpIfInventory("Clip",1,"ReloadStart")
            goto ReadyActual

		ReloadStart:
			TNT1 A 0 A_PlaySound("RTCW/Colt/Reload",CHAN_6)
			//TNT1 A 0 A_JumpIfInventory("RTCW_ColtMagazine",1,1)
			goto ReloadStartNoAmmo
			RW00 "QRSTUVWXYZ[\]" 2
			RW01 ABCDEFGHIJKLM 2
            RW00 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReloadAmmo")
            goto ReloadDone

		ReloadStartNoAmmo:
			RW00 "QRSTUVWXYZ[\]" 2
			RW01 ABCDEFGH 2
			RW01 IJKLM 1
			RW01 N 2 A_PlaySound("RTCW/Colt/Reload1_2",CHAN_6)
			RW01 OPQRSTUVWXYZ 2
            RW00 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReloadAmmo")
            goto ReloadDone

        ReloadDone:
            RW00 A 0
			goto ReadyActual

        ReloadAmmo:
            TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,"ReloadAmmoReserve")
            TNT1 A 0 A_JumpIfInventory("RTCW_ColtMagazine",0,"ReloadDone")
            TNT1 A 0 A_GiveInventory("RTCW_ColtMagazine")
            loop

        ReloadAmmoReserve:
            TNT1 A 0 A_JumpIfInventory("RTCW_ColtMagazine",0,"ReloadDone")
            TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
            TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
            TNT1 A 0 A_JumpIfInventory("Clip",1,1)
            goto ReloadDone
            TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
            TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
            TNT1 A 0 A_TakeInventory("Clip",1)
            TNT1 A 0 A_GiveInventory("RTCW_ColtMagazine")
            goto ReloadAmmo

		Kicking:
			TNT1 A 0 A_GunFlash("KickingFoot")
		KickingDelay:
			RW00 A 18 A_JumpIfInventory("RTCW_ColtDualWield",1,"KickingDelayDual")
        KickingDelayFinish:
			RW00 A 0 A_TakeInventory("RTCW_Kicking",1)
			goto ReadyActual

        // Dual Init
		AltFire:
			RW01 Z 0 A_PlaySound("RTCW/Change",CHAN_7)
			RW01 Z 0 A_JumpIfInventory("RTCW_ColtDualWield",1,"AltFireSingle")
        AltFireDual:
			RW01 Z 0 A_GiveInventory("RTCW_ColtDualWield")
			RW01 Z 0 A_TakeInventory("RTCW_WeaponToken",256)
			RW01 Z 2 A_GiveInventory("RTCW_WeaponToken",5)
			RW01 "[\]" 2
			RW06 "[\]" 2
			RW07 AB 2
			goto ReadyActual

		AltFireSingle:
			RW06 R 0 A_TakeInventory("RTCW_ColtDualWield")
			RW06 R 0 A_TakeInventory("RTCW_WeaponToken",256)
			RW06 R 2 A_GiveInventory("RTCW_WeaponToken",4)
			RW06 STU 2
			TNT1 A 0
			RW02 JKL 2
			goto ReadyActual

        // Dual
        ReadyActualDualNonReloadable:
			RW04 A 1 A_WeaponReady
			goto ReadyActualAmmoCheckDual

        ReadyActualDualReloadable:
			RW04 A 1 A_WeaponReady(WRF_ALLOWRELOAD)
			goto ReadyActualAmmoCheckDual

        ReadyActualAmmoCheckDual:
            RW00 A 0 A_JumpIfInventory("SamsaraReloadMode",1,1)
            goto ReadyActual
            RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"ReadyActual")
            RW00 A 0 A_JumpIfInventory("RTCW_ColtMagazine",1,"ReadyActual")
            RW00 A 0 A_JumpIfInventory("RTCW_ColtMagazine2",1,"ReadyActual")
            goto ReloadCheck

		DeselectDual:
			RW04 NOPQR 2
			goto Super::Deselect

		ReadyDual:
			RW04 S 2 A_GiveInventory("RTCW_WeaponToken",5)
			RW04 TUVW 2
            goto ReadyActual

		FireDual:
            RW04 B 0 A_JumpIfInventory("SamsaraReloadMode",1,"FireDualReloadable")
        FireDualNonReloadable:
            RW04 B 0 A_JumpIfInventory("PistolModeOn",1,1)
            goto FireDualStart
            RW04 B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireDualStart")
            RW04 B 0 A_JumpIfInventory("Clip",1,"FireDualStart")
            goto FireDry

        FireDualReloadable:
            RW04 B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireDualStart")
            RW04 B 0 A_JumpIfInventory("RTCW_ColtMagazine",1,"FireDualStart")
            RW04 B 0 A_JumpIfInventory("RTCW_ColtMagazine2",1,"FireDualStart")
        FireDualReloadableDryCheck:
            RW04 B 0 A_JumpIfInventory("PistolModeOn",1,1)
            goto ReloadStartDual
            RW04 B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReloadStartDual")
            RW04 B 0 A_JumpIfInventory("Clip",1,"ReloadStartDual")
            goto FireDry

        FireDualDry:
			RW04 A 17
            goto FireDryFinish

        FireDualStart:
            RW04 B 0 A_JumpIfInventory("RTCW_ColtFiredLeft",1,"FireDualCheckRight")
            goto FireDualCheckLeft

		FireDualCheckLeft:
            RW04 B 0 A_JumpIfInventory("SamsaraReloadMode",1,1)
            goto FireDualStartLeft
            RW04 B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireDualStartLeft")
            RW04 B 0 A_JumpIfInventory("RTCW_ColtMagazine2",1,"FireDualStartLeft")
            goto FireDualStartRight

		FireDualStartLeft:
			RW04 B 0 A_GiveInventory("RTCW_FiringToken",1)
			RW04 B 0 A_GiveInventory("RTCW_BlazkoWeaponAlert",1)
            RW04 B 0 A_GiveInventory("RTCW_ColtFiredLeft")
			RW04 B 0 A_FireCustomMissile("RTCW_Casing",-random(85,105),0,-8,4,0,-4)
			RW04 B 0 A_GunFlash("FireFlashDualLeft")
			RW04 B 2 A_GiveInventory("SamsaraRTCWColtLeftAttackHandler")
			RW04 CDEFG 1
            RW04 H 0 A_JumpIfInventory("SamsaraReloadMode",1,"FireDualFinishLeftReloadable")
        FireDualFinishLeftNonReloadable:
            RW04 H 0 A_JumpIfInventory("PistolModeOn",1,1)
            goto FireDualFinishLeftReFire
            RW04 H 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireDualFinishLeftReFire")
            RW04 H 0 A_JumpIfInventory("Clip",1,"FireDualFinishLeftReFire")
            goto FireDualFinishLeft

        FireDualFinishLeftReloadable:
            RW04 H 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireDualFinishLeftReFire")
            RW04 H 0 A_JumpIfInventory("RTCW_ColtMagazine",1,"FireDualFinishLeftReFire") // have magazine ammo in the right colt?
            goto FireDualFinishLeft

        FireDualFinishLeftReFire:
            RW04 H 0 A_ReFire
        FireDualFinishLeft:
            RW04 H 1
            RW04 A 6
			goto ReadyActual

		FireFlashDualLeft:
			RW03 C 2 Bright
			Stop

		FireDualCheckRight:
            RW04 I 0 A_JumpIfInventory("SamsaraReloadMode",1,1)
            goto FireDualStartRight
            RW04 I 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireDualStartRight")
            RW04 I 0 A_JumpIfInventory("RTCW_ColtMagazine",1,"FireDualStartRight")
            goto FireDualStartLeft

		FireDualStartRight:
			RW04 I 0 A_GiveInventory("RTCW_FiringToken",1)
			RW04 I 0 A_GiveInventory("RTCW_BlazkoWeaponAlert",1)
            RW04 I 0 A_TakeInventory("RTCW_ColtFiredLeft")
			RW04 I 0 A_FireCustomMissile("RTCW_Casing",-random(85,105),0,8,4,0,-4)
			RW04 I 0 A_GunFlash("FireFlashDualRight")
			RW04 I 2 A_GiveInventory("SamsaraRTCWColtRightAttackHandler")
			RW04 JKLMN 1
            RW04 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"FireDualFinishRightReloadable")
        FireDualFinishRightNonReloadable:
            RW04 A 0 A_JumpIfInventory("PistolModeOn",1,1)
            goto FireDualFinishRightReFire
            RW04 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireDualFinishRightReFire")
            RW04 A 0 A_JumpIfInventory("Clip",1,"FireDualFinishRightReFire")
            goto FireDualFinishRight

        FireDualFinishRightReloadable:
            RW04 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireDualFinishRightReFire")
            RW04 A 0 A_JumpIfInventory("RTCW_ColtMagazine2",1,"FireDualFinishRightReFire") // have magazine ammo in the left colt?
            goto FireDualFinishRight

        FireDualFinishRightReFire:
            RW04 A 0 A_ReFire
        FireDualFinishRight:
            RW04 A 1 // DoubleFiringSpeed parity
            RW04 A 6
			goto ReadyActual

		FireFlashDualRight:
			RW03 D 2 Bright
			Stop

        ReloadCheckDual:
            RW00 A 0 A_JumpIfInventory("RTCW_ColtMagazine",0,1)
            goto ReloadCheckDualSuccess
            RW00 A 0 A_JumpIfInventory("RTCW_ColtMagazine2",0,"ReadyActual")
        ReloadCheckDualSuccess:
            RW00 A 0 A_JumpIfInventory("PistolModeOn",1,1)
            goto ReloadStartDual
            RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
            RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReloadStartDual")
            RW00 A 0 A_JumpIfInventory("Clip",1,"ReloadStartDual")
            goto ReadyActual

		ReloadStartDual:
			TNT1 A 0 A_PlaySound("RTCW/Colt/Reload2",CHAN_6)
			/*TNT1 A 0 A_JumpIfInventory("RTCW_ColtMagazine2",1,1)
            goto ReloadStartNoAmmoDual
			TNT1 A 0 A_JumpIfInventory("RTCW_ColtMagazine",1,1)*/
			goto ReloadStartNoAmmoDual
			RW04 "YZ[\]" 2
			RW05 ABCDEFGHIJKLMNOPQRSTUV 2
            RW04 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReloadAmmoDualLeft")
            goto ReloadDoneDual

		ReloadStartNoAmmoDual:
			RW04 "YZ[\]" 2
			RW05 ABCDEFGHIJKLMNOPQRSTUV 2
			RW05 W 2 A_PlaySound("RTCW/Colt/Reload2_2",CHAN_6)
			RW05 "XYZ[\]" 2
			RW06 ABCDEFGHIJKLMNO 2
            RW04 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReloadAmmoDualLeft")
            goto ReloadDoneDual

        ReloadDoneDual:
            RW04 A 0
			goto ReadyActual

        ReloadAmmoDualLeft:
            TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,"ReloadAmmoReserveDualLeft")
            TNT1 A 0 A_JumpIfInventory("RTCW_ColtMagazine2",0,"ReloadAmmoDualRight")
            TNT1 A 0 A_GiveInventory("RTCW_ColtMagazine2")
            loop

        ReloadAmmoReserveDualLeft:
            TNT1 A 0 A_JumpIfInventory("RTCW_ColtMagazine2",0,"ReloadAmmoDualRight")
            TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
            TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
            TNT1 A 0 A_JumpIfInventory("Clip",1,1)
            goto ReloadAmmoDualRight
            TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
            TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
            TNT1 A 0 A_TakeInventory("Clip",1)
            TNT1 A 0 A_GiveInventory("RTCW_ColtMagazine2")
            goto ReloadAmmoDualLeft

        ReloadAmmoDualRight:
            TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,"ReloadAmmoReserveDualRight")
            TNT1 A 0 A_JumpIfInventory("RTCW_ColtMagazine",0,"ReloadDoneDual")
            TNT1 A 0 A_GiveInventory("RTCW_ColtMagazine")
            loop

        ReloadAmmoReserveDualRight:
            TNT1 A 0 A_JumpIfInventory("RTCW_ColtMagazine",0,"ReloadDoneDual")
            TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
            TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
            TNT1 A 0 A_JumpIfInventory("Clip",1,1)
            goto ReloadDoneDual
            TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
            TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
            TNT1 A 0 A_TakeInventory("Clip",1)
            TNT1 A 0 A_GiveInventory("RTCW_ColtMagazine")
            goto ReloadAmmoDualRight

		KickingDelayDual:
			RW04 A 18
            goto KickingDelayFinish
	}
}

actor SamsaraRTCWColtRightAttackHandler : Trigger
{
    States
    {
      // Ammo check stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"PickupCheckAmmoReloadable")
      PickupCheckAmmoNonReloadable:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto PickupAttack
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupTakeAmmoNonReloadable")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"PickupTakeAmmoNonReloadable")
        stop

      PickupCheckAmmoReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("RTCW_ColtMagazine",1,"PickupTakeAmmoReloadable")
        stop

      // Ammo taking stuff
      PickupTakeAmmoNonReloadable:
        TNT1 A 0 A_TakeInventory("Clip",1,TIF_NOTAKEINFINITE)
        goto PickupAttack

      PickupTakeAmmoReloadable:
        TNT1 A 0 A_TakeInventory("RTCW_ColtMagazine",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("RTCW/Colt/Fire",CHAN_WEAPON)
        TNT1 A 0 A_FireBullets(0,0,1,0,"SamsaraBlankPuff",FBF_NORANDOM|FBF_NOFLASH)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("RTCW_ColtTracer",frandom(-2.5,2.5),false,0,4,0,frandom(-2.5,2.5))
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("RTCW_ColtTracerCoop",frandom(-2.5,2.5),false,0,4,0,frandom(-2.5,2.5))
        stop
    }
}

actor SamsaraRTCWColtLeftAttackHandler : Trigger
{
    States
    {
      // Ammo check stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"PickupCheckAmmoReloadable")
      PickupCheckAmmoNonReloadable:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto PickupAttack
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupTakeAmmoNonReloadable")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"PickupTakeAmmoNonReloadable")
        stop

      PickupCheckAmmoReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("RTCW_ColtMagazine2",1,"PickupTakeAmmoReloadable")
        stop

      // Ammo taking stuff
      PickupTakeAmmoNonReloadable:
        TNT1 A 0 A_TakeInventory("Clip",1,TIF_NOTAKEINFINITE)
        goto PickupAttack

      PickupTakeAmmoReloadable:
        TNT1 A 0 A_TakeInventory("RTCW_ColtMagazine2",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("RTCW/Colt/Fire",CHAN_WEAPON)
        TNT1 A 0 A_FireBullets(0,0,1,0,"SamsaraBlankPuff",FBF_NORANDOM|FBF_NOFLASH)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("RTCW_ColtTracer",frandom(-2.5,2.5),false,0,-4,0,frandom(-2.5,2.5))
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("RTCW_ColtTracerCoop",frandom(-2.5,2.5),false,0,-4,0,frandom(-2.5,2.5))
        stop
    }
}

actor RTCW_ColtMagazine : Ammo
{
    Inventory.MaxAmount 8
    +IGNORESKILL
}

actor RTCW_ColtMagazine2 : Ammo
{
    Inventory.MaxAmount 8
    +IGNORESKILL
}

//Was 8 damage, now 16
Actor RTCW_ColtTracer : RTCW_Tracer
{ 
	States
	{
		Crash:
		Death:
			TNT1 A 0 A_Explode(CallACS("RTCW_BulletDamage",20),8,!XF_HURTSOURCE,0,8)
			Goto Super::Death
		XDeath:
			TNT1 A 0 A_Explode(CallACS("RTCW_BulletDamage",20),8,!XF_HURTSOURCE,0,8)
			Goto Super::XDeath
	}
}

Actor RTCW_ColtTracerCoop : RTCW_ColtTracer { +THRUSPECIES Species "Player" }
