actor RTCW_GrenadeTimer : Counter { Inventory.MaxAmount 4 }
actor RTCW_GrenadeFuse : Counter { Inventory.MaxAmount 35 }

//Was 6 damage, now 18
Actor RTCW_Grenade : RTCW_Weapon
{
	Weapon.SlotNumber 1
	Weapon.SlotPriority 0 
	Weapon.SelectionOrder 2000
	Weapon.AmmoUse 1
	Weapon.AmmoType "RocketAmmo"
	Weapon.AmmoGive 5
	Obituary "%o was killed (gren - gm) by %k."
	Inventory.Pickupmessage "Grenade"
	Inventory.PickupSound "RTCW/WeaponPickup"
	Tag "Stick Grenade"
    +AMMO_OPTIONAL
	+WEAPON.EXPLOSIVE
	+WEAPON.NOALERT
	States
	{
		Spawn:
			RW98 A -1
			stop

		ReadyActual:
            RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReadyActualAmmo")
            RW00 A 0 A_JumpIfInventory("RocketAmmo",1,"ReadyActualAmmo")
            goto ReadyActualNoAmmo

		ReadyActualNoAmmo:
			TNT1 A 0 A_JumpIfInventory("RTCW_Kicking",1,"KickingNoAmmo")
			TNT1 A 1 A_WeaponReady
			goto ReadyActualNoAmmoCheck

        ReadyActualAmmo:
			RW00 A 0 A_JumpIfInventory("RTCW_Kicking",1,"Kicking")
			RW00 A 1 A_WeaponReady
			goto ReadyActual

        ReadyActualNoAmmoCheck:
            RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReloadStart")
            RW00 A 0 A_JumpIfInventory("RocketAmmo",1,"ReloadStart")
            goto ReadyActual

		Deselect:
			RW00 V 0 A_PlaySound("RTCW/Change",CHAN_7)
            RW00 V 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"DeselectAmmo")
            RW00 V 0 A_JumpIfInventory("RocketAmmo",1,"DeselectAmmo")
            goto DeselectNoAmmo

        DeselectAmmo:
			RW00 GHIJK 2
			Goto Super::Deselect

		DeselectNoAmmo:
			TNT1 AAAAA 2
			Goto Super::Deselect

		Ready: // our "Select" state
			RW00 A 0 A_TakeInventory("RTCW_WeaponToken",256)
			RW00 A 0 A_GiveInventory("RTCW_WeaponToken",6)
        ReadyStart:
			RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReadyAmmo")
			RW00 A 0 A_JumpIfInventory("RocketAmmo",1,"ReadyAmmo")
			goto ReadyNoAmmo

        ReadyNoAmmo:
			TNT1 AAAA 2
            goto ReadyActual

        ReadyAmmo:
			RW00 LMNO 2
            goto ReadyActual

		Fire:
            RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart")
            RW00 A 0 A_JumpIfInventory("RocketAmmo",1,"FireStart")
            goto FireDry

        FireDry:
			TNT1 A 17 A_PlaySound("RTCW/NoAmmo",CHAN_5)
			TNT1 A 0 A_SelectWeapon("RTCW_Knife")
			Goto Deselect

        FireStart:
            RW00 B 0 A_TakeInventory("RTCW_GrenadeFuse")
            RW00 B 0 A_TakeInventory("RTCW_GrenadeTimer")
			RW00 B 3 A_PlaySound("RTCW/Grenade/Pulse4",CHAN_5)
			RW00 C 3
		GrenadeHold:
			RW00 C 0 A_JumpIfInventory("RTCW_GrenadeTimer",0,"ReleaseGrenade")
			RW00 C 0 A_JumpIfInventory("RTCW_GrenadeFuse",0,"GrenadeTimerUp")
			RW00 C 1 A_GiveInventory("RTCW_GrenadeFuse",1)
			RW00 C 0 A_ReFire("GrenadeHold")
			Goto ReleaseGrenade

		GrenadeTimerUp:
			RW00 C 0 A_TakeInventory("RTCW_GrenadeFuse",35)
			RW00 C 0 A_GiveInventory("RTCW_GrenadeTimer",1)
			RW00 C 0 A_JumpIfInventory("RTCW_GrenadeTimer",0,"GrenadeHold")
			RW00 C 0 A_JumpIfInventory("RTCW_GrenadeTimer",3,4)
			RW00 C 0 A_JumpIfInventory("RTCW_GrenadeTimer",2,2)
			RW00 C 0 A_PlaySound("RTCW/Grenade/Pulse3",CHAN_5)
			Goto GrenadeHold
			RW00 C 0 A_PlaySound("RTCW/Grenade/Pulse2",CHAN_5)
			Goto GrenadeHold
			RW00 C 0 A_PlaySound("RTCW/Grenade/Pulse1",CHAN_5)
			Goto GrenadeHold

		ReleaseGrenade:
			RW00 B 0 A_GiveInventory("RTCW_FiringToken",1)
			RW00 B 0 A_GiveInventory("SamsaraRTCWGrenadeAttackHandler")
        ReleaseGrenadeEndCheck:
            RW00 B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReleaseGrenadeEnd")
            RW00 B 0 A_JumpIfInventory("RocketAmmo",1,"ReleaseGrenadeEnd")
            goto ReleaseGrenadeEndLast

        ReleaseGrenadeEndLast:
            RW00 B 0 A_PlaySound("RTCW/NoAmmo",CHAN_5)
            RW00 B 3 A_PlaySound("RTCW/Change",CHAN_7)
            TNT1 A 0 A_TakeInventory("RTCW_GrenadeFuse")
            TNT1 A 35 A_TakeInventory("RTCW_GrenadeTimer")
            TNT1 A 0 A_SelectWeapon("RTCW_Knife")
            goto Super::Deselect

        ReleaseGrenadeEnd:
            RW00 B 3
            TNT1 A 0 A_TakeInventory("RTCW_GrenadeFuse")
            TNT1 A 35 A_TakeInventory("RTCW_GrenadeTimer")
            goto ReadyActualNoAmmoCheck

        ReloadStart:
        ReloadDone:
            RW00 LMNO 2
            goto ReadyActual

		KickingNoAmmo: // jumps to "with ammo" version for the time being
		Kicking:
			TNT1 A 0 A_GunFlash("KickingFoot")
		KickingDelay:
			RW00 A 18
			RW00 A 0 A_TakeInventory("RTCW_Kicking",1)
			goto ReadyActual
	}
}

actor SamsaraRTCWGrenadeAttackHandler : Trigger
{
    States
    {
      // Ammo check stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupTakeAmmo")
        TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,"PickupTakeAmmo")
        stop

      // Ammo taking stuff
      PickupTakeAmmo:
        TNT1 A 0 A_TakeInventory("RocketAmmo",1,TIF_NOTAKEINFINITE)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("RTCW/Grenade/Throw",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("RTCW_GrenadeProjectile",0,false,8,4)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("RTCW_GrenadeProjectileCoop",0,false,8,4)
        stop
    }
}

Actor RTCW_GrenadeProjectile
{
	Height 10
	Radius 5
	Damage (6)
	Speed 12
	Projectile
	Gravity 0.5
	BounceCount 5
	BounceType "Hexen"
	DamageType "RTCWExplosive"
	BounceSound "RTCW/Grenade/Bounce"
	+USEBOUNCESTATE
	+ALLOWBOUNCEONACTORS
	+BOUNCEONACTORS
	+SKYEXPLODE
	-NOGRAVITY
	States
	{
		Spawn:
			RW00 A 0 NoDelay A_GiveInventory("RTCW_GrenadeTimer",CallACS("RTCW_Decorate",7))
            RW00 A 0 A_JumpIfInventory("RTCW_GrenadeTimer",0,"Detonate")
			RW00 A 0 ThrustThingZ(0,20,0,1)
		SpawnLoop:
			RW00 AAAAAAAAAAAAAAAAAAAAAAAA 1
			RW00 A 0 A_GiveInventory("RTCW_GrenadeTimer",1)
			RW00 A 0 A_JumpIfInventory("RTCW_GrenadeTimer",0,"Detonate")
			Goto SpawnLoop
		Bounce:
			RW00 A 0 A_ChangeFlag("USEBOUNCESTATE",0)
			RW00 AAAAAAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("RTCW_GrenadeTrailSmoke")
			RW00 A 0 A_GiveInventory("RTCW_GrenadeTimer",1)
			RW00 A 0 A_JumpIfInventory("RTCW_GrenadeTimer",0,"Detonate")
			Goto Bounce
		OnFloor:
			RW00 B 0 A_GiveInventory("RTCW_GrenadeTimer",1)
			RW00 B 17 A_JumpIfInventory("RTCW_GrenadeTimer",0,"Detonate")
			Loop
		Crash:
		Death:
		XDeath:
			RW00 A 0 A_Stop
			RW00 A 0 A_JumpIfInventory("RTCW_GrenadeTimer",0,1)
			Goto OnFloor
		Detonate:
			RW00 AAAA 0 A_SpawnItemEx("RTCW_PanzerfaustSmokeExplosion",0,0,0,frandom(-0.5,0.5),frandom(-0.5,0.5),1)
			RW00 AAAA 0 A_SpawnItemEx("RTCW_ExplosionSmokeDebris")
			RW00 A 0 A_GiveInventory("RTCW_BlazkoWeaponAlert",1,AAPTR_Target)
			RW00 A 0 A_Explode(200,150,XF_HURTSOURCE,1,0) // values may need adjustment. hand detonation was 240,120
			RW00 A 0 A_SpawnItemEx("RTCW_PanzerfaustRocketExplosion")
			Stop
	}
}

Actor RTCW_GrenadeProjectileCoop : RTCW_GrenadeProjectile { +THRUSPECIES Species "Player" }

Actor RTCW_GrenadeTrailSmoke
{
	Radius 1
	Height 1
	Alpha 0.2
	Scale 0.1
	RenderStyle Shaded
	+NOINTERACTION
	+CLIENTSIDEONLY
	+FORCEXYBILLBOARD
	States
	{
		Spawn:
			RW05 ABCDEFGHIJKLMNOPQRSTUVWX 2
			Stop
	}
}
