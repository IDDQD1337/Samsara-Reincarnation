Actor RTCW_MG42FireFrame : Counter { Inventory.MaxAmount 7 }
Actor RTCW_MG42Heat : Counter { Inventory.MaxAmount 150 }
actor RTCW_MG42CoolLock : Boolean {}

Actor RTCW_MG42 : RTCW_Weapon
{
	Weapon.SlotNumber 6
	Weapon.SelectionOrder 750
	Weapon.AmmoType1 "RTCW_MG42Magazine"
	Weapon.AmmoUse1 1
	Weapon.AmmoType2 "Cell"
	Weapon.AmmoGive2 100
	Obituary "%o was killed (mg42) by %k."
	Inventory.Pickupmessage "Portable MG42"
	Inventory.PickupSound "RTCW/WeaponPickup"
	Tag "Portable MG42"
	+WEAPON.NOALERT
	+WEAPON.AMMO_OPTIONAL
	States
	{
		Spawn:
			RW98 A -1
			Stop

        ReadyActual:
			RW00 A 0 A_TakeInventory("RTCW_MG42FireFrame",7)
			RW00 A 0 A_TakeInventory("RTCW_FG42Spread")
			RW00 A 0 A_TakeInventory("RTCW_MG42CoolLock")
			RW00 A 0 A_JumpIfInventory("RTCW_Kicking",1,"Kicking")
            RW00 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReadyActualReloadable")
        ReadyActualNonReloadable:
			RW00 A 1 A_WeaponReady
			goto ReadyActualAmmoCheck

        ReadyActualReloadable:
			RW00 A 1 A_WeaponReady(WRF_ALLOWRELOAD)
			goto ReadyActualAmmoCheck

        ReadyActualAmmoCheck:
            RW00 A 0 A_JumpIfInventory("SamsaraReloadMode",1,1)
            goto ReadyActual
            RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"ReadyActual")
            RW00 A 0 A_JumpIfInventory("RTCW_MG42Magazine",1,"ReadyActual")
            goto ReloadCheck

		Deselect:
			RW00 T 0 A_TakeInventory("RTCW_MG42FireFrame",7)
			RW00 T 0 A_PlaySound("RTCW/Change",CHAN_7)
			RW00 T 0 A_TakeInventory("RTCW_FG42Spread")
			RW00 T 2 A_TakeInventory("RTCW_MG42CoolLock")
			RW00 "UVWXYZ[\]" 2
			RW01 A 2
			Goto Super::Deselect

		Ready: // our "Select" state
			RW00 A 0 A_TakeInventory("RTCW_WeaponToken",256)
			RW00 A 0 A_GiveInventory("RTCW_WeaponToken",17)
			RW00 A 0 A_TakeInventory("RTCW_FG42Spread")
			RW01 B 2 A_TakeInventory("RTCW_MG42CoolLock")
			RW01 DFHJLNPRTVX 2
            goto ReadyActual

		Fire:
            RW00 B 0 A_JumpIfInventory("SamsaraReloadMode",1,"FireReloadable")
        FireNonReloadable:
            RW00 B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart")
            RW00 B 0 A_JumpIfInventory("Cell",1,"FireStart")
            goto FireDry

        FireReloadable:
            RW00 B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireStart")
            RW00 B 0 A_JumpIfInventory("RTCW_MG42Magazine",1,"FireStart")
        FireReloadableDryCheck:
            RW00 B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReloadStart")
            RW00 B 0 A_JumpIfInventory("Cell",1,"ReloadStart")
            goto FireDry

        FireDry:
			RW00 A 0 A_TakeInventory("RTCW_MG42CoolLock")
            RW00 A 17 A_PlaySound("RTCW/NoAmmo",CHAN_5)
			TNT1 A 0 A_SelectWeapon("RTCW_Knife")
			Goto Deselect

        FireUnderwater:
			RW00 A 0 A_TakeInventory("RTCW_MG42CoolLock")
            RW00 A 17 A_PlaySound("RTCW/UnderwaterFire",CHAN_5)
            goto ReadyActual

		FireStart:
            RW00 B 0 A_JumpIfInventory("SamsaraCurrentWaterLevel",0,"FireUnderwater")
            RW00 B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
			RW00 B 0 A_JumpIfInventory("RTCW_MG42Heat",0,"CoolDown")
            RW00 B 0 A_GiveInventory("RTCW_MG42CoolLock")
			RW00 B 0 A_FireCustomMissile("RTCW_Casing",-random(85,105),0,8,4,0,-4)
			RW00 B 0 A_GiveInventory("SamsaraRTCWMG42AttackHandler")
			RW00 B 0 A_JumpIfInventory("RTCW_MG42FireFrame",7,"Fire8")
			RW00 B 0 A_JumpIfInventory("RTCW_MG42FireFrame",6,"Fire7")
			RW00 B 0 A_JumpIfInventory("RTCW_MG42FireFrame",5,"Fire6")
			RW00 B 0 A_JumpIfInventory("RTCW_MG42FireFrame",4,"Fire5")
			RW00 B 0 A_JumpIfInventory("RTCW_MG42FireFrame",3,"Fire4")
			RW00 B 0 A_JumpIfInventory("RTCW_MG42FireFrame",2,"Fire3")
			RW00 B 0 A_JumpIfInventory("RTCW_MG42FireFrame",1,"Fire2")
			Goto Fire1
		
		Fire1:
			RW00 B 1 A_GunFlash("FireFlash1",GFF_NOEXTCHANGE)
            RW00 C 0
			Goto FireFinish
		Fire2:
			RW00 C 2 A_GunFlash("FireFlash2",GFF_NOEXTCHANGE)
            RW00 D 0
			Goto FireFinish
		Fire3:
			RW00 D 2 A_GunFlash("FireFlash3",GFF_NOEXTCHANGE)
            RW00 E 0
			Goto FireFinish
		Fire4:
			RW00 E 2 A_GunFlash("FireFlash4",GFF_NOEXTCHANGE)
            RW00 F 0
			Goto FireFinish
		Fire5:
			RW00 F 1 A_GunFlash("FireFlash5",GFF_NOEXTCHANGE)
            RW00 G 0
			Goto FireFinish
		Fire6:
			RW00 G 2 A_GunFlash("FireFlash6",GFF_NOEXTCHANGE)
            RW00 H 0
			Goto FireFinish
		Fire7:
			RW00 H 2 A_GunFlash("FireFlash7",GFF_NOEXTCHANGE)
            RW00 I 0
			Goto FireFinish
		Fire8:
			RW00 I 2 A_GunFlash("FireFlash8",GFF_NOEXTCHANGE)
            RW00 B 0
			Goto FireFinish
			
		//This animation is pretty wonky but it's basically how it works in WOLF:ET
		FireLast:
			RW00 JKLMNOPQRS 1
			Goto ReloadCheck
			
		FireFinish:
			RW00 B 0 A_JumpIfInventory("RTCW_MG42FireFrame",0,2)
			RW00 B 0 A_GiveInventory("RTCW_MG42FireFrame",1)
			Goto FireFinish+3
			RW00 B 0 A_TakeInventory("RTCW_MG42FireFrame",7)
			RW00 B 0 A_JumpIfInventory("RTCW_MG42Magazine",1,1)
			Goto FireLast
            RW00 B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
			RW00 B 0 A_JumpIfInventory("RTCW_MG42Heat",0,"CoolDown")
			RW00 B 0 A_ReFire
			goto ReadyActual
			
		FireFlash1:
			RW04 B 2 Bright A_Jump(128,"FlashBreak","FireFlash1_2")
			Stop
		FireFlash1_2:
			RW05 B 2 Bright
			Stop
		FireFlash2:
			RW04 C 2 Bright A_Jump(128,"FlashBreak","FireFlash2_2")
			Stop
		FireFlash2_2:
			RW05 C 2 Bright
			Stop
		FireFlash3:
			RW04 D 2 Bright A_Jump(128,"FlashBreak","FireFlash3_2")
			Stop
		FireFlash3_2:
			RW05 D 2 Bright
			Stop
		FireFlash4:
			RW04 E 2 Bright A_Jump(128,"FlashBreak","FireFlash4_2")
			Stop
		FireFlash4_2:
			RW05 E 2 Bright
			Stop
		FireFlash5:
			RW04 F 2 Bright A_Jump(128,"FlashBreak","FireFlash5_2")
			Stop
		FireFlash5_2:
			RW05 F 2 Bright
			Stop
		FireFlash6:
			RW04 G 2 Bright A_Jump(128,"FlashBreak","FireFlash6_2")
			Stop
		FireFlash6_2:
			RW05 G 2 Bright
			Stop
		FireFlash7:
			RW04 H 2 Bright A_Jump(128,"FlashBreak","FireFlash7_2")
			Stop
		FireFlash7_2:
			RW05 H 2 Bright
			Stop
		FireFlash8:
			RW04 I 2 Bright A_Jump(128,"FlashBreak","FireFlash8_2")
			Stop
		FireFlash8_2:
			RW05 I 2 Bright
			Stop
		
		FlashBreak:
			TNT1 A 1
			Stop
			
		CoolDown:
			RW00 B 0 A_TakeInventory("RTCW_MG42CoolLock")
			RW00 B 0 A_PlaySound("RTCW/MG42/OverHeat",CHAN_5)
			RW00 B 0 A_SpawnItemEx("RTCW_VenomSmoke",cos(pitch)*30,6,38-(sin(pitch)*30),(cos(pitch)*frandom(-0.125,0.125))+(sin(pitch)*0.33),frandom(-0.125,0.125),(cos(pitch)*0.33)-sin(pitch)*frandom(-0.125,0.125))
			RW00 BCDE 1
			RW00 F 0 A_SpawnItemEx("RTCW_VenomSmoke",cos(pitch)*30,6,38-(sin(pitch)*30),(cos(pitch)*frandom(-0.125,0.125))+(sin(pitch)*0.33),frandom(-0.125,0.125),(cos(pitch)*0.33)-sin(pitch)*frandom(-0.125,0.125))
			RW00 FGHI 1
			RW00 B 0 A_SpawnItemEx("RTCW_VenomSmoke",cos(pitch)*30,6,38-(sin(pitch)*30),(cos(pitch)*frandom(-0.125,0.125))+(sin(pitch)*0.33),frandom(-0.125,0.125),(cos(pitch)*0.33)-sin(pitch)*frandom(-0.125,0.125))
			RW00 BCDE 1
			RW00 F 0 A_SpawnItemEx("RTCW_VenomSmoke",cos(pitch)*30,6,38-(sin(pitch)*30),(cos(pitch)*frandom(-0.125,0.125))+(sin(pitch)*0.33),frandom(-0.125,0.125),(cos(pitch)*0.33)-sin(pitch)*frandom(-0.125,0.125))
			RW00 FGHI 1
			RW00 B 0 A_SpawnItemEx("RTCW_VenomSmoke",cos(pitch)*30,6,38-(sin(pitch)*30),(cos(pitch)*frandom(-0.125,0.125))+(sin(pitch)*0.33),frandom(-0.125,0.125),(cos(pitch)*0.33)-sin(pitch)*frandom(-0.125,0.125))
			RW00 BCDE 1
			RW00 F 0 A_SpawnItemEx("RTCW_VenomSmoke",cos(pitch)*30,6,38-(sin(pitch)*30),(cos(pitch)*frandom(-0.125,0.125))+(sin(pitch)*0.33),frandom(-0.125,0.125),(cos(pitch)*0.33)-sin(pitch)*frandom(-0.125,0.125))
			RW00 FGHI 1
			RW00 B 0 A_SpawnItemEx("RTCW_VenomSmoke",cos(pitch)*30,6,38-(sin(pitch)*30),(cos(pitch)*frandom(-0.125,0.125))+(sin(pitch)*0.33),frandom(-0.125,0.125),(cos(pitch)*0.33)-sin(pitch)*frandom(-0.125,0.125))
			RW00 BCDE 1
			RW00 F 0 A_SpawnItemEx("RTCW_VenomSmoke",cos(pitch)*30,6,38-(sin(pitch)*30),(cos(pitch)*frandom(-0.125,0.125))+(sin(pitch)*0.33),frandom(-0.125,0.125),(cos(pitch)*0.33)-sin(pitch)*frandom(-0.125,0.125))
			RW00 FGHI 1
			RW00 B 0 A_SpawnItemEx("RTCW_VenomSmoke",cos(pitch)*30,6,38-(sin(pitch)*30),(cos(pitch)*frandom(-0.125,0.125))+(sin(pitch)*0.33),frandom(-0.125,0.125),(cos(pitch)*0.33)-sin(pitch)*frandom(-0.125,0.125))
			RW00 BCDE 1
			RW00 F 0 A_SpawnItemEx("RTCW_VenomSmoke",cos(pitch)*30,6,38-(sin(pitch)*30),(cos(pitch)*frandom(-0.125,0.125))+(sin(pitch)*0.33),frandom(-0.125,0.125),(cos(pitch)*0.33)-sin(pitch)*frandom(-0.125,0.125))
			RW00 FGHI 1
			RW00 AAA 4
			goto ReadyActual

        Reload:
        ReloadCheck:
            RW00 A 0 A_JumpIfInventory("RTCW_MG42Magazine",0,"ReadyActual")
            RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
            RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReloadStart")
            RW00 A 0 A_JumpIfInventory("Cell",1,"ReloadStart")
            goto ReadyActual

		ReloadStart:
            RW00 A 0 A_TakeInventory("RTCW_MG42CoolLock")
			RW00 A 0 A_PlaySound("RTCW/MG42/Reload",CHAN_6)
			RW01 "YZ[\]" 2
			RW02 "ABCDEFGHIJKLMNOPQRSTUVWXYZ[\]" 2
			RW03 ABC 2
			RW00 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReloadAmmo")
            goto ReloadDone

        ReloadDone:
            RW00 A 17
			goto ReadyActual

        ReloadAmmo:
            TNT1 A 0 A_JumpIfInventory("RTCW_MG42Magazine",0,"ReloadDone")
            TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
            TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
            TNT1 A 0 A_JumpIfInventory("Cell",1,1)
            goto ReloadDone
            TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
            TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
            TNT1 A 0 A_TakeInventory("Cell",1)
            TNT1 A 0 A_GiveInventory("RTCW_MG42Magazine")
            loop

		Kicking:
			TNT1 A 0 A_GunFlash("KickingFoot")
		KickingDelay:
			RW00 A 18
			RW00 A 0 A_TakeInventory("RTCW_Kicking",1)
			goto ReadyActual
	}
}

actor SamsaraRTCWMG42AttackHandler : Trigger
{
    States
    {
      // Ammo check stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"PickupCheckAmmoReloadable")
      PickupCheckAmmoNonReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupTakeAmmoNonReloadable")
        TNT1 A 0 A_JumpIfInventory("Cell",1,"PickupTakeAmmoNonReloadable")
        stop

      PickupCheckAmmoReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("RTCW_MG42Magazine",1,"PickupTakeAmmoReloadable")
        stop

      // Ammo taking stuff
      PickupTakeAmmoNonReloadable:
        TNT1 A 0 A_TakeInventory("Cell",1,TIF_NOTAKEINFINITE)
        goto PickupAttack

      PickupTakeAmmoReloadable:
        TNT1 A 0 A_TakeInventory("RTCW_MG42Magazine",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("RTCW/MG42/Fire",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("RTCW_MG42Tracer",CallACS("RTCW_Decorate",1,1)*frandom(-1.0,1.0),false,6,4,0,CallACS("RTCW_Decorate",1,1)*frandom(-1.0,1.0))
        goto PickupFinish

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("RTCW_MG42TracerCoop",CallACS("RTCW_Decorate",1,1)*frandom(-1.0,1.0),false,6,4,0,CallACS("RTCW_Decorate",1,1)*frandom(-1.0,1.0))
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
		TNT1 A 0 A_GiveInventory("RTCW_FG42Spread")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        TNT1 A 0 A_GiveInventory("RTCW_MG42Heat",2)
        TNT1 A 0 A_JumpIfInventory("SamsaraRecoilIsDisabled", 1, 2)
		TNT1 A 0 A_SetPitch(pitch-0.25)
        TNT1 A 0 A_SetAngle(angle+frandom(-0.25,0.25))
        stop
    }
}

actor RTCW_MG42Magazine : Ammo
{
    Inventory.MaxAmount 150
    +IGNORESKILL
}

Actor RTCW_MG42Tracer : RTCW_Tracer
{
	DamageType "RTCWVenomBullet"
	States
	{
		Crash:
		Death:
			TNT1 A 0 A_Explode(CallACS("RTCW_BulletDamage",18),8,!XF_HURTSOURCE,0,8)
			Goto Super::Death
		XDeath:
			TNT1 A 0 A_Explode(CallACS("RTCW_BulletDamage",18),8,!XF_HURTSOURCE,0,8)
			Goto Super::XDeath
	}
}

Actor RTCW_MG42TracerCoop : RTCW_MG42Tracer { +THRUSPECIES Species "Player" }