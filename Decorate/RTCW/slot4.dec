actor RTCW_FG42Spread : Counter { Inventory.MaxAmount 8 }

Actor RTCW_FG42 : RTCW_Weapon
{
	Weapon.SlotNumber 4
    Weapon.SlotPriority 0
	Weapon.SelectionOrder 750
	Weapon.AmmoType1 "RTCW_FG42Magazine"
	Weapon.AmmoUse1 1
	Weapon.AmmoType2 "Clip"
	Weapon.AmmoGive2 40
	Obituary "%o was killed (fg42) by %k."
	Inventory.Pickupmessage "FG42"
	Inventory.PickupSound "RTCW/WeaponPickup"
	Tag "FG-42"
	+WEAPON.NOALERT
	+WEAPON.AMMO_OPTIONAL
	States
	{
		Spawn:
			RW98 A -1
			Stop

        ReadyActual:
			RW00 A 0 A_TakeInventory("RTCW_FG42Spread")
			RW00 A 0 A_JumpIfInventory("RTCW_Kicking",1,"Kicking")
            RW00 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReadyActualReloadable")
        ReadyActualNonReloadable:
			RW00 A 0 A_JumpIfInventory("RTCW_Scoped",1,2)
			RW00 A 1 A_WeaponReady
			goto ReadyActualAmmoCheck
			TNT1 A 1 A_WeaponReady
			goto ReadyActualAmmoCheck

        ReadyActualReloadable:
			RW00 A 0 A_JumpIfInventory("RTCW_Scoped",1,2)
			RW00 A 1 A_WeaponReady(WRF_ALLOWRELOAD)
			goto ReadyActualAmmoCheck
			TNT1 A 1 A_WeaponReady(WRF_ALLOWRELOAD)
			goto ReadyActualAmmoCheck

        ReadyActualAmmoCheck:
            RW00 A 0 A_JumpIfInventory("SamsaraReloadMode",1,1)
            goto ReadyActual
            RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"ReadyActual")
            RW00 A 0 A_JumpIfInventory("RTCW_FG42Magazine",1,"ReadyActual")
            goto ReloadCheck

		Deselect:
			RW00 V 0 A_PlaySound("RTCW/Change",CHAN_7)
			RW00 V 0 A_TakeInventory("RTCW_Scoped",1)
			RW00 V 0 A_ZoomFactor(1.0)
			RW00 F 2 A_TakeInventory("RTCW_FG42Spread")
			RW00 GHIJKL 2
			Goto Super::Deselect

		Ready: // our "Select" state
			RW00 A 0 A_TakeInventory("RTCW_WeaponToken",256)
			RW00 A 0 A_GiveInventory("RTCW_WeaponToken",12)
			RW00 L 2 A_TakeInventory("RTCW_FG42Spread")
			RW00 MNOP 2
            goto ReadyActual

		Fire:
            RW00 B 0 A_JumpIfInventory("SamsaraReloadMode",1,"FireReloadable")
        FireNonReloadable:
            RW00 B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart")
            RW00 B 0 A_JumpIfInventory("Clip",2,"FireStart")
            goto FireDry

        FireReloadable:
            RW00 B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireStart")
            RW00 B 0 A_JumpIfInventory("RTCW_FG42Magazine",1,"FireStart")
        FireReloadableDryCheck:
            RW00 B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReloadScopeCheck")
            RW00 B 0 A_JumpIfInventory("Clip",2,"ReloadScopeCheck")
            goto FireDry

        FireDry:
            RW00 A 0 A_PlaySound("RTCW/NoAmmo",CHAN_5)
			RW00 A 17 A_JumpIfInventory("RTCW_Scoped",1,1)
            goto FireDryFinish
			TNT1 A 17
			goto FireDryFinish

        FireDryFinish:
			TNT1 A 0 A_SelectWeapon("RTCW_Knife")
			Goto Deselect

        FireUnderwater:
            RW00 A 0 A_PlaySound("RTCW/UnderwaterFire",CHAN_5)
			RW00 A 17 A_JumpIfInventory("RTCW_Scoped",1,1)
            goto ReadyActual
			TNT1 A 17
			goto ReadyActual

		FireStart:
            RW00 B 0 A_JumpIfInventory("SamsaraCurrentWaterLevel",0,"FireUnderwater")
			RW00 B 0 A_GiveInventory("RTCW_FiringToken",1)
			RW00 B 0 A_GiveInventory("RTCW_BlazkoWeaponAlert",1)
			RW00 B 0 A_SpawnItemEx("RTCW_Casing",4,-10*cos(-pitch),38+(10*sin(-pitch)),1,0,2*cos(-pitch),-random(85,95),SXF_TRANSFERPITCH)
			RW00 B 0 A_JumpIfInventory("RTCW_Scoped",1,"FireScoped")
			RW00 B 0 A_GunFlash("FireFlash")
			RW00 B 2 A_GiveInventory("SamsaraRTCWFG42AttackHandler")
			RW00 CD 2
			RW00 E 1
			RW00 F 0 A_ReFire
			goto ReadyActual

		FireScoped:
			TNT1 A 2 A_GiveInventory("SamsaraRTCWFG42AttackHandler")
			TNT1 AA 2
			TNT1 A 1
			TNT1 A 0 A_ReFire
			goto ReadyActual

		FireFlash:
			RW03 B 2 Bright
			Stop

		AltFire:
			RW00 A 0 A_PlaySound("RTCW/Change",CHAN_7)
			RW00 A 0 A_JumpIfInventory("RTCW_Scoped",1,"AltFireUnscope")
		AltFireScope:
			TNT1 A 0 A_GiveInventory("RTCW_Scoped",1)
			TNT1 A 0 ACS_NamedExecuteWithResult("RTCW_Scope")
			TNT1 A 10 A_ZoomFactor(1.63)
			goto ReadyActual

		AltFireUnscope:
			RW00 A 0 A_TakeInventory("RTCW_Scoped",1)
			RW00 A 0 A_ZoomFactor(1.0)
			goto Ready

        Reload:
        ReloadCheck:
            RW00 A 0 A_JumpIfInventory("RTCW_FG42Magazine",0,"ReadyActual")
            RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
            RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReloadScopeCheck")
            RW00 A 0 A_JumpIfInventory("Clip",2,"ReloadScopeCheck")
            goto ReadyActual

        ReloadScopeCheck:
			RW00 A 0 A_JumpIfInventory("RTCW_Scoped",1,1)
            goto ReloadStart
			RW00 A 0 A_PlaySound("RTCW/Change",CHAN_7)
			RW00 A 0 A_TakeInventory("RTCW_Scoped",1)
			RW00 L 2 A_ZoomFactor(1.0)
			RW00 MNOP 2
            goto ReloadCheck

		ReloadStart:
			TNT1 A 0 A_PlaySound("RTCW/FG42/Reload",CHAN_6)
			//TNT1 A 0 A_JumpIfInventory("RTCW_FG42Magazine",1,1)
			goto ReloadStartNoAmmo
			RW00 "QRSTUVWXYZ[\]" 2
			RW01 ABCDEFGHIJKLMN 2
			RW02 DEFG 2
			RW00 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReloadAmmo")
            goto ReloadDone

		ReloadStartNoAmmo:
			RW00 "QRSTUVWXYZ[\]" 2
			RW01 ABCDEFGHIJKLMNOPQR 2
			RW01 S 2 A_PlaySound("RTCW/FG42/Reload2",CHAN_6)
			RW01 "TUVWXYZ[\]" 2
			RW02 ABCDEFG 1
			RW00 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReloadAmmo")
            goto ReloadDone

        ReloadDone:
            RW00 A 0
			goto ReadyActual

        ReloadAmmo:
            TNT1 A 0 A_JumpIfInventory("RTCW_FG42Magazine",0,"ReloadDone")
            TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
            TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
            TNT1 A 0 A_JumpIfInventory("Clip",2,1)
            goto ReloadDone
            TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
            TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
            TNT1 A 0 A_TakeInventory("Clip",2)
            TNT1 A 0 A_GiveInventory("RTCW_FG42Magazine")
            loop

		Kicking:
			TNT1 A 0 A_GunFlash("KickingFoot")
		KickingDelay:
			RW00 A 18 A_JumpIfInventory("RTCW_Scoped",1,1)
			goto KickingDelayFinish
			TNT1 A 18 // not really supposed to see anything, even the kick
			goto KickingDelayFinish

        KickingDelayFinish:
			RW00 A 0 A_TakeInventory("RTCW_Kicking",1)
			goto ReadyActual
	}
}

actor SamsaraRTCWFG42AttackHandler : Trigger
{
    States
    {
      // Ammo check stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"PickupCheckAmmoReloadable")
      PickupCheckAmmoNonReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupTakeAmmoNonReloadable")
        TNT1 A 0 A_JumpIfInventory("Clip",2,"PickupTakeAmmoNonReloadable")
        stop

      PickupCheckAmmoReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("RTCW_FG42Magazine",1,"PickupTakeAmmoReloadable")
        stop

      // Ammo taking stuff
      PickupTakeAmmoNonReloadable:
        TNT1 A 0 A_TakeInventory("Clip",2,TIF_NOTAKEINFINITE)
        goto PickupAttack

      PickupTakeAmmoReloadable:
        TNT1 A 0 A_TakeInventory("RTCW_FG42Magazine",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("RTCW/FG42/Fire",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("RTCW_Scoped",1,"PickupAttackScoped")
      // Unscoped
      PickupAttackUnscoped:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackUnscopedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackUnscopedCoop")
      PickupAttackUnscopedDM:
        TNT1 A 0 A_FireCustomMissile("RTCW_FG42Tracer",CallACS("RTCW_Decorate",1,1)*frandom(-1.0,1.0),false,4,4,0,CallACS("RTCW_Decorate",1,1)*frandom(-1.0,1.0))
        goto PickupFinish

      PickupAttackUnscopedCoop:
        TNT1 A 0 A_FireCustomMissile("RTCW_FG42TracerCoop",CallACS("RTCW_Decorate",1,1)*frandom(-1.0,1.0),false,4,4,0,CallACS("RTCW_Decorate",1,1)*frandom(-1.0,1.0))
        goto PickupFinish

      // Scoped
      PickupAttackScoped:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackScopedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackScopedCoop")
      PickupAttackScopedDM:
        TNT1 A 0 A_FireCustomMissile("RTCW_FG42Tracer",CallACS("RTCW_Decorate",1,1)*frandom(-0.25,0.25),false,0,4,0,CallACS("RTCW_Decorate",1,1)*frandom(-0.25,0.25))
        goto PickupFinish

      PickupAttackScopedCoop:
        TNT1 A 0 A_FireCustomMissile("RTCW_FG42TracerCoop",CallACS("RTCW_Decorate",1,1)*frandom(-0.25,0.25),false,0,4,0,CallACS("RTCW_Decorate",1,1)*frandom(-0.25,0.25))
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 A_GiveInventory("RTCW_FG42Spread")
        TNT1 A 0 A_JumpIfInventory("RTCW_Scoped",1,"PickupFinishScoped")
      PickupFinishUnscoped:
        TNT1 A 0 ACS_NamedExecuteWithResult("RTCW_Recoil", 1)
        stop

      PickupFinishScoped:
        TNT1 A 0 A_GiveInventory("RTCW_AimSpread",1048576)
        TNT1 A 0 A_SetPitch(pitch-0.5)
        TNT1 A 0 A_SetAngle(angle+frandom(-1.0,1.0))
        stop
    }
}

actor RTCW_FG42Magazine : Ammo
{
    Inventory.MaxAmount 20
    +IGNORESKILL
}

//Was 15, now 30
Actor RTCW_FG42Tracer : RTCW_Tracer
{ 
	States
	{
		Crash:
		Death:
			TNT1 A 0 A_Explode(CallACS("RTCW_BulletDamage",35),8,!XF_HURTSOURCE,0,8)
			Goto Super::Death
		XDeath:
			TNT1 A 0 A_Explode(CallACS("RTCW_BulletDamage",35),8,!XF_HURTSOURCE,0,8)
			Goto Super::XDeath
	}
}

Actor RTCW_FG42TracerCoop : RTCW_FG42Tracer { +THRUSPECIES Species "Player" }
