actor RTCW_StG44Alt : Boolean {}

Actor RTCW_StG44 : RTCW_Weapon
{
	Weapon.SlotNumber 4
	Weapon.SlotPriority 1
	Weapon.SelectionOrder 500
	Weapon.AmmoType1 "RTCW_StG44Magazine"
	Weapon.AmmoUse1 1
	Weapon.AmmoType2 "Clip"
	Weapon.AmmoGive2 15
	Obituary "%o was killed (StG44) by %k."
	Inventory.Pickupmessage "StG44"
	Inventory.PickupSound "RTCW/WeaponPickup"
	Tag "StG44"
	+WEAPON.NOALERT
	+WEAPON.AMMO_OPTIONAL
	States
	{
		Spawn:
			RW98 A -1
			Stop

        ReadyActual:
			RW00 A 0 A_TakeInventory("RTCW_StG44Alt",1)
			RW00 A 0 A_TakeInventory("RTCW_FG42Spread")
			RW00 A 0 A_JumpIfInventory("RTCW_Kicking",1,"Kicking")
            RW00 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReadyActualReloadable")
        ReadyActualNonReloadable:
			RW00 A 1 A_WeaponReady
			goto ReadyActualAmmoCheck

        ReadyActualReloadable:
			RW00 A 1 A_WeaponReady(WRF_ALLOWRELOAD)
			goto ReadyActualAmmoCheck

        ReadyActualAmmoCheck:
            RW00 A 0 A_JumpIfInventory("SamsaraReloadMode",1,1)
            goto ReadyActual
            RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"ReadyActual")
            RW00 A 0 A_JumpIfInventory("RTCW_StG44Magazine",1,"ReadyActual")
            goto ReloadCheck

		Deselect:
			RW00 V 0 A_PlaySound("RTCW/Change",CHAN_7)
			RW00 G 2 A_TakeInventory("RTCW_ThompsonSpread")
			RW00 HIJK 2
			Goto Super::Deselect

		Ready: // our "Select" state
			RW00 A 0 A_TakeInventory("RTCW_WeaponToken",256)
			RW00 A 0 A_GiveInventory("RTCW_WeaponToken",8)
			RW00 L 2 A_TakeInventory("RTCW_ThompsonSpread")
			RW00 MNO 2
            goto ReadyActual

		Altfire:
			RW00 A 0 A_GiveInventory("RTCW_StG44Alt",1)
		Fire:
            RW00 B 0 A_JumpIfInventory("SamsaraReloadMode",1,"FireReloadable")
        FireNonReloadable:
            RW00 B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart")
            RW00 B 0 A_JumpIfInventory("Clip",1,"FireStart")
            goto FireDry

        FireReloadable:
            RW00 B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireStart")
            RW00 B 0 A_JumpIfInventory("RTCW_StG44Magazine",1,"FireStart")
        FireReloadableDryCheck:
            RW00 B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReloadStart")
            RW00 B 0 A_JumpIfInventory("Clip",1,"ReloadStart")
            goto FireDry

        FireDry:
            RW00 A 17 A_PlaySound("RTCW/NoAmmo",CHAN_5)
			TNT1 A 0 A_SelectWeapon("RTCW_Knife")
			Goto Deselect

        FireUnderwater:
            RW00 A 17 A_PlaySound("RTCW/UnderwaterFire",CHAN_5)
            goto ReadyActual
			
		FireStart:
			RW00 A 0 A_JumpIfInventory("RTCW_StG44Alt",1,"FireStartAlt")
            RW00 A 0 A_JumpIfInventory("SamsaraCurrentWaterLevel",0,"FireUnderwater")
			RW00 A 0 A_GiveInventory("RTCW_FiringToken",1)
			RW00 A 0 A_GiveInventory("RTCW_BlazkoWeaponAlert",1)
			RW00 A 0 A_FireCustomMissile("RTCW_Casing",-random(85,105),0,8,4,0,-4)
			RW00 A 0 A_GunFlash("FireFlash")
			RW00 A 1 A_GiveInventory("SamsaraRTCWStG44AttackHandler")
			RW00 B 1 
			RW00 C 0 A_Jump(37,"FireContinue1")
			goto FireContinue2

        FireContinue1:
			RW00 C 2
            goto FireFinish

        FireContinue2:
			RW00 C 1
            goto FireFinish

        FireFinish:
			RW00 D 1 A_JumpIfInventory("RTCW_StG44Alt",1,"FireFinishAlt")
			RW00 A 0 A_ReFire
			goto ReadyActual
			
		FireStartAlt:
			RW00 A 0 A_JumpIfInventory("SamsaraCurrentWaterLevel",0,"FireUnderwater")
			RW00 A 0 A_GiveInventory("RTCW_FiringToken",1)
			RW00 A 0 A_GiveInventory("RTCW_BlazkoWeaponAlert",1)
			RW00 A 0 A_FireCustomMissile("RTCW_Casing",-random(85,105),0,8,4,0,-4)
			RW00 A 0 A_GunFlash("FireFlash")
			RW00 A 3 A_GiveInventory("SamsaraRTCWStG44AttackHandler")
			RW00 B 2 
			RW00 C 0 A_Jump(37,"FireContinue1")
			goto FireContinue2

        FireFinishAlt:
			RW00 D 1 A_TakeInventory("RTCW_StG44Alt",1)
			RW00 A 0 A_ReFire("Altfire")
			goto ReadyActual
			
		FireFlash:
			RW03 B 0 A_Jump(128,"FireFlash2")
			RW03 B 2 Bright
			Stop
		FireFlash2:
			RW04 B 2 Bright
			Stop

        Reload:
        ReloadCheck:
            RW00 A 0 A_JumpIfInventory("RTCW_StG44Magazine",0,"ReadyActual")
            RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
            RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReloadStart")
            RW00 A 0 A_JumpIfInventory("Clip",1,"ReloadStart")
            goto ReadyActual

		ReloadStart:
			TNT1 A 0 A_PlaySound("RTCW/MP40/Reload",CHAN_6)
			//TNT1 A 0 A_JumpIfInventory("RTCW_StG44Magazine",1,1)
			goto ReloadStartNoAmmo
			RW00 "QRSTUVWXYZ[\]" 2
			RW01 ABCDEFGHIJKLMNOPQRSTU 2
			RW02 EFG 2
			RW00 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReloadAmmo")
            goto ReloadDone

		ReloadStartNoAmmo:
			RW00 "QRSTUVWXYZ[\]" 2
			RW01 ABCDEFGHIJKLMNOPQR 2 
			RW01 STU 1
			RW01 V 1 A_PlaySound("RTCW/MP40/Reload2",CHAN_6)
			RW01 "WXYZ[\]" 1
			RW02 ABCDEFG 2
			RW00 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReloadAmmo")
            goto ReloadDone

        ReloadDone:
            RW00 A 0
			goto ReadyActual

        ReloadAmmo:
            TNT1 A 0 A_JumpIfInventory("RTCW_StG44Magazine",0,"ReloadDone")
            TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
            TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
            TNT1 A 0 A_JumpIfInventory("Clip",1,1)
            goto ReloadDone
            TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
            TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
            TNT1 A 0 A_TakeInventory("Clip",1)
            TNT1 A 0 A_GiveInventory("RTCW_StG44Magazine")
            loop

		Kicking:
			TNT1 A 0 A_GunFlash("KickingFoot")
		KickingDelay:
			RW00 A 18
			RW00 A 0 A_TakeInventory("RTCW_Kicking",1)
			goto ReadyActual
	}
}

actor SamsaraRTCWStG44AttackHandler : Trigger
{
    States
    {
      // Ammo check stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"PickupCheckAmmoReloadable")
      PickupCheckAmmoNonReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupTakeAmmoNonReloadable")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"PickupTakeAmmoNonReloadable")
        stop

      PickupCheckAmmoReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("RTCW_StG44Magazine",1,"PickupTakeAmmoReloadable")
        stop

      // Ammo taking stuff
      PickupTakeAmmoNonReloadable:
        TNT1 A 0 A_TakeInventory("Clip",1,TIF_NOTAKEINFINITE)
        goto PickupAttack

      PickupTakeAmmoReloadable:
        TNT1 A 0 A_TakeInventory("RTCW_StG44Magazine",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("RTCW/StG44/Fire",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("RTCW_StG44Alt",1,"PickupAttack2")
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("RTCW_StG44Tracer",CallACS("RTCW_Decorate",1,1)*frandom(-1.0,1.0),false,0,4,0,CallACS("RTCW_Decorate",1,1)*frandom(-1.0,1.0))
        goto PickupFinish

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("RTCW_StG44TracerCoop",CallACS("RTCW_Decorate",1,1)*frandom(-1.0,1.0),false,0,4,0,CallACS("RTCW_Decorate",1,1)*frandom(-1.0,1.0))
        goto PickupFinish
		
	  PickupAttack2:
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttack2Coop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttack2Coop")
		goto PickupFinish
		
	  PickupAttack2DM:
        TNT1 A 0 A_FireCustomMissile("RTCW_StG44Tracer",CallACS("RTCW_Decorate",1,1)*frandom(-0.1,0.1),false,0,4,0,CallACS("RTCW_Decorate",1,1)*frandom(-0.1,0.1))
        goto PickupFinish

      PickupAttack2Coop:
        TNT1 A 0 A_FireCustomMissile("RTCW_StG44TracerCoop",CallACS("RTCW_Decorate",1,1)*frandom(-0.1,0.1),false,0,4,0,CallACS("RTCW_Decorate",1,1)*frandom(-0.1,0.1))
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 A_GiveInventory("RTCW_FG42Spread")
        stop
    }
}

actor RTCW_StG44Magazine : Ammo
{
    Inventory.MaxAmount 30
    +IGNORESKILL
}

//Was 19 damage
Actor RTCW_StG44Tracer : RTCW_Tracer
{ 
	States
	{
		Crash:
		Death:
			TNT1 A 0 A_Explode(CallACS("RTCW_BulletDamage",25),8,!XF_HURTSOURCE,0,8)
			Goto Super::Death
		XDeath:
			TNT1 A 0 A_Explode(CallACS("RTCW_BulletDamage",25),8,!XF_HURTSOURCE,0,8)
			Goto Super::XDeath
	}
}

Actor RTCW_StG44TracerCoop : RTCW_StG44Tracer { +THRUSPECIES Species "Player" }
