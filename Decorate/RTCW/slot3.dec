Actor RTCW_Mauser : RTCW_Weapon
{
	Weapon.SlotNumber 3
	Weapon.SlotPriority 0
	Weapon.SelectionOrder 1400
	Weapon.AmmoType1 "RTCW_MauserMagazine"
	Weapon.AmmoUse1 1
	Weapon.AmmoType2 "Shell"
	Weapon.AmmoGive2 5
	Obituary "%o was killed (mauser) by %k."
	Inventory.Pickupmessage "Mauser"
	Inventory.PickupSound "RTCW/WeaponPickup"
	Tag "Sniper Rifle"
	+WEAPON.NOALERT
	+WEAPON.AMMO_OPTIONAL
	States
	{
		Spawn:
			RW98 A -1
			Stop

        ReadyActual:
            RW00 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReadyActualReloadable")
        ReadyActualNonReloadable:
			RW00 A 0 A_JumpIfInventory("RTCW_Scoped",1,3)
			RW00 A 0 A_JumpIfInventory("RTCW_Kicking",1,"Kicking")
			RW00 A 1 A_WeaponReady
			goto ReadyActualAmmoCheck
			TNT1 A 0 A_JumpIf(CallACS("RTCW_Decorate", 3) == 0, 2)
			TNT1 A 1 A_ZoomFactor(90.0/CallACS("RTCW_Decorate", 2)*1.0)
			TNT1 A 0 A_JumpIfInventory("RTCW_Kicking",1,"Kicking")
			TNT1 A 1 A_WeaponReady
			goto ReadyActualAmmoCheck

        ReadyActualReloadable:
			RW00 A 0 A_JumpIfInventory("RTCW_Scoped",1,3)
			RW00 A 0 A_JumpIfInventory("RTCW_Kicking",1,"Kicking")
			RW00 A 1 A_WeaponReady(WRF_ALLOWRELOAD)
			goto ReadyActualAmmoCheck
			TNT1 A 0 A_JumpIf(CallACS("RTCW_Decorate", 3) == 0, 2)
			TNT1 A 1 A_ZoomFactor(90.0/CallACS("RTCW_Decorate", 2)*1.0)
			TNT1 A 0 A_JumpIfInventory("RTCW_Kicking",1,"Kicking")
			TNT1 A 1 A_WeaponReady(WRF_ALLOWRELOAD)
			goto ReadyActualAmmoCheck

        ReadyActualAmmoCheck:
            RW00 A 0 A_JumpIfInventory("SamsaraReloadMode",1,1)
            goto ReadyActual
            RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"ReadyActual")
            RW00 A 0 A_JumpIfInventory("RTCW_MauserMagazine",1,"ReadyActual")
            goto ReloadCheck

		Deselect:
			RW00 "]" 0 A_PlaySound("RTCW/Change",CHAN_7)
			RW00 "]" 0 A_TakeInventory("RTCW_ZoomFactor",90)
			RW00 "]" 0 A_TakeInventory("RTCW_Scoped",1)
			RW00 "]" 2 A_ZoomFactor(1.0)
			RW01 ABCD 2
			Goto Super::Deselect

		Ready: // our "Select" state
			RW00 A 0 A_TakeInventory("RTCW_WeaponToken",256)
			RW01 E 2 A_GiveInventory("RTCW_WeaponToken",10)
			RW01 FGHIJK 2
            goto ReadyActual

		Fire:
            RW00 B 0 A_JumpIfInventory("SamsaraReloadMode",1,"FireReloadable")
        FireNonReloadable:
            RW00 B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart")
            RW00 B 0 A_JumpIfInventory("Shell",1,"FireStart")
            goto FireDry

        FireReloadable:
            RW00 B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireStart")
            RW00 B 0 A_JumpIfInventory("RTCW_MauserMagazine",1,"FireStart")
        FireReloadableDryCheck:
            RW00 B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReloadScopeCheck")
            RW00 B 0 A_JumpIfInventory("Shell",1,"ReloadScopeCheck")
            goto FireDry

        FireDry:
            RW00 A 0 A_PlaySound("RTCW/NoAmmo",CHAN_5)
			RW00 A 17 A_JumpIfInventory("RTCW_Scoped",1,1)
            goto FireDryFinish
			TNT1 A 17
			goto FireDryFinish

        FireDryFinish:
			TNT1 A 0 A_SelectWeapon("RTCW_Knife")
			Goto Deselect

        FireUnderwater:
            RW00 A 0 A_PlaySound("RTCW/UnderwaterFire",CHAN_5)
			RW00 A 17 A_JumpIfInventory("RTCW_Scoped",1,1)
            goto ReadyActual
			TNT1 A 17
			goto ReadyActual

		FireStart:
            RW00 B 0 A_JumpIfInventory("SamsaraCurrentWaterLevel",0,"FireUnderwater")
			RW00 B 0 A_GiveInventory("RTCW_FiringToken",1)
			RW00 B 0 A_GiveInventory("RTCW_BlazkoWeaponAlert",1)
			RW00 B 0 A_FireCustomMissile("RTCW_Casing",-random(85,105),0,8,4,0,-4)
			RW00 B 0 A_JumpIfInventory("RTCW_Scoped",1,"FireScoped")
			RW00 B 0 A_GunFlash("FireFlash")
			RW00 B 2 A_GiveInventory("SamsaraRTCWMauserAttackHandler")
			RW00 CDEFGHIJKLM 2
			RW00 A 15
			goto ReadyActual

		FireScoped:
			TNT1 B 6 A_GiveInventory("SamsaraRTCWMauserAttackHandler")
			TNT1 BCDEFGHIJKLM 2
			TNT1 A 15
			goto ReadyActual

		FireFlash:
			RW03 B 2 Bright
			Stop

		FireFlashLast:
			RW00 P 2 Bright
			Stop

		AltFire:
			RW00 A 0 A_PlaySound("RTCW/Change",CHAN_7)
			RW00 A 0 A_JumpIfInventory("RTCW_Scoped",1,"AltFireUnscope")
		AltFireScope:
			TNT1 A 0 A_GiveInventory("RTCW_ZoomFactor",12)
			TNT1 A 0 A_GiveInventory("RTCW_Scoped",1)
			TNT1 A 0 ACS_NamedExecuteWithResult("RTCW_Scope")
			TNT1 A 10 A_ZoomFactor(90.0/12.0)
			goto ReadyActual

		AltFireUnscope:
			RW00 A 0 A_TakeInventory("RTCW_ZoomFactor",90)
			RW00 A 0 A_TakeInventory("RTCW_Scoped",1)
			RW00 A 0 A_ZoomFactor(1.0)
			goto Ready

        Reload:
        ReloadCheck:
            RW00 A 0 A_JumpIfInventory("RTCW_MauserMagazine",0,"ReadyActual")
            RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
            RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReloadScopeCheck")
            RW00 A 0 A_JumpIfInventory("Shell",1,"ReloadScopeCheck")
            goto ReadyActual

        ReloadScopeCheck:
			RW00 A 0 A_JumpIfInventory("RTCW_Scoped",1,1)
            goto ReloadStart
			RW00 A 0 A_PlaySound("RTCW/Change",CHAN_7)
			RW00 A 0 A_TakeInventory("RTCW_ZoomFactor",90)
			RW00 A 0 A_TakeInventory("RTCW_Scoped",1)
			RW01 E 2 A_ZoomFactor(1.0)
			RW01 FGHIJK 2
            goto ReloadCheck

		ReloadStart:
			RW01 L 2 A_PlaySound("RTCW/Mauser/Reload",CHAN_6)
			RW01 "MNOPQRSTUVWXYZ[\]" 2
			RW02 ABCDEF 2
			TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReloadAmmo")
            goto ReloadDone

        ReloadDone:
            TNT1 A 0 // had no frame, but seems fine
			goto ReadyActual

        ReloadAmmo:
            TNT1 A 0 A_JumpIfInventory("RTCW_MauserMagazine",0,"ReloadDone")
            TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
            TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
            TNT1 A 0 A_JumpIfInventory("Shell",1,1)
            goto ReloadDone
            TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
            TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
            TNT1 A 0 A_TakeInventory("Shell",1)
            TNT1 A 0 A_GiveInventory("RTCW_MauserMagazine")
            loop

		Kicking:
			TNT1 A 0 A_GunFlash("KickingFoot")
		KickingDelay:
			RW00 A 18 A_JumpIfInventory("RTCW_Scoped",1,1)
			goto KickingDelayFinish
			TNT1 A 18 // not really supposed to see anything, even the kick
			goto KickingDelayFinish

        KickingDelayFinish:
			RW00 A 0 A_TakeInventory("RTCW_Kicking",1)
			goto ReadyActual
	}
}

actor SamsaraRTCWMauserAttackHandler : Trigger
{
    States
    {
      // Ammo check stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"PickupCheckAmmoReloadable")
      PickupCheckAmmoNonReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupTakeAmmoNonReloadable")
        TNT1 A 0 A_JumpIfInventory("Shell",1,"PickupTakeAmmoNonReloadable")
        stop

      PickupCheckAmmoReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"PickupPlaySound")
        TNT1 A 0 A_JumpIfInventory("RTCW_MauserMagazine",1,"PickupTakeAmmoReloadable")
        stop

      // Ammo taking stuff
      PickupTakeAmmoNonReloadable:
        TNT1 A 0 A_TakeInventory("Shell",1,TIF_NOTAKEINFINITE)
        goto PickupPlaySound

      PickupTakeAmmoReloadable:
        TNT1 A 0 A_TakeInventory("RTCW_MauserMagazine",1)
        goto PickupPlaySound

      // Sound stuff
      PickupPlaySound:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"PickupPlaySoundReloadable")
      PickupPlaySoundNonReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupPlaySoundNormal")
        TNT1 A 0 A_JumpIfInventory("Shell",1,"PickupPlaySoundNormal")
        goto PickupPlaySoundLast

      PickupPlaySoundReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"PickupPlaySoundNormal")
        TNT1 A 0 A_JumpIfInventory("RTCW_MauserMagazine",1,"PickupPlaySoundNormal")
        goto PickupPlaySoundLast

      PickupPlaySoundNormal:
        TNT1 A 0 A_PlaySound("RTCW/Mauser/Fire",CHAN_WEAPON)
        goto PickupAttack

      PickupPlaySoundLast:
        TNT1 A 0 A_PlaySound("RTCW/Mauser/FireLast",CHAN_WEAPON)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("RTCW_Scoped",1,"PickupAttackScoped")
      // Unscoped
      PickupAttackUnscoped:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackUnscopedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackUnscopedCoop")
      PickupAttackUnscopedDM:
        TNT1 A 0 A_FireCustomMissile("RTCW_SnooperTracer",frandom(-2.5,2.5),false,4,4,0,frandom(-2.5,2.5))
        goto PickupFinish

      PickupAttackUnscopedCoop:
        TNT1 A 0 A_FireCustomMissile("RTCW_SnooperTracerCoop",frandom(-2.5,2.5),false,4,4,0,frandom(-2.5,2.5))
        goto PickupFinish

      // Scoped
      PickupAttackScoped:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackScopedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackScopedCoop")
      PickupAttackScopedDM:
        TNT1 A 0 A_FireCustomMissile("RTCW_MauserTracer",0,false,0,8)
        goto PickupFinish

      PickupAttackScopedCoop:
        TNT1 A 0 A_FireCustomMissile("RTCW_MauserTracerCoop",0,false,0,8)
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 A_JumpIfInventory("RTCW_Scoped",1,"PickupFinishScoped")
      PickupFinishUnscoped:
        TNT1 A 0 ACS_NamedExecuteWithResult("RTCW_Recoil", 2)
        stop

      PickupFinishScoped:
        TNT1 A 0 A_GiveInventory("RTCW_AimSpread",16711680)
        TNT1 A 0 A_SetAngle(angle+frandom(-2.0,2.0))
        TNT1 A 0 ACS_NamedExecuteWithResult("RTCW_Recoil", 3)
        stop
    }
}

actor RTCW_MauserMagazine : Ammo
{
    Inventory.MaxAmount 10
    +IGNORESKILL
}

//Dunno what it was, but I think this is a fair value
Actor RTCW_MauserTracer : RTCW_Tracer
{ 
	States
	{
		Crash:
		Death:
			TNT1 A 0 A_Explode(CallACS("RTCW_BulletDamage",200),8,!XF_HURTSOURCE,0,8)
			Goto Super::Death
		XDeath:
			TNT1 A 0 A_Explode(CallACS("RTCW_BulletDamage",200),8,!XF_HURTSOURCE,0,8)
			Goto Super::XDeath
	}
}

Actor RTCW_MauserTracerCoop : RTCW_MauserTracer { +THRUSPECIES Species "Player" }

Actor RTCW_MauserTracerDM : RTCW_Tracer
{ 
	States
	{
		Crash:
		Death:
			TNT1 A 0 A_Explode(CallACS("RTCW_BulletDamage",140),8,!XF_HURTSOURCE,0,8) 
			Goto Super::Death
		XDeath:
			TNT1 A 0 A_Explode(CallACS("RTCW_BulletDamage",140),8,!XF_HURTSOURCE,0,8)
			Goto Super::XDeath
	}
}
