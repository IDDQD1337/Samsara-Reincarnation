Actor RTCW_Mauser : RTCW_Weapon
{
	Weapon.SlotNumber 3
	Weapon.SlotPriority 0
	Weapon.SelectionOrder 1400
	Weapon.AmmoType1 "RTCW_MauserMagazine"
	Weapon.AmmoUse1 1
	Weapon.AmmoType2 "Shell"
	Weapon.AmmoGive2 5
	Obituary "%o was killed (mauser) by %k."
	Inventory.PickupMessage "Mauser Rifle"
	Tag "Sniper Rifle"
	States
	{
		Spawn:
			RW98 A -1
			stop

        ReadyActual:
            RW00 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReadyActualReloadable")
        ReadyActualNonReloadable:
			RW00 A 0 A_JumpIfInventory("RTCW_Scoped",1,3)
			RW00 A 0 A_JumpIfInventory("RTCW_Kicking",1,"Kicking")
			RW00 A 1 A_WeaponReady(WRF_ALLOWZOOM)
			goto ReadyActualAmmoCheck
			TNT1 A 0 A_JumpIf(CallACS("RTCW_Decorate", 3) == 0, 2)
			TNT1 A 1 A_ZoomFactor(90.0/CallACS("RTCW_Decorate", 2)*1.0,ZOOM_INSTANT)
			TNT1 A 0 A_JumpIfInventory("RTCW_Kicking",1,"Kicking")
			TNT1 A 1 A_WeaponReady(WRF_ALLOWZOOM)
			goto ReadyActualAmmoCheck

        ReadyActualReloadable:
			RW00 A 0 A_JumpIfInventory("RTCW_Scoped",1,3)
			RW00 A 0 A_JumpIfInventory("RTCW_Kicking",1,"Kicking")
			RW00 A 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
			goto ReadyActualAmmoCheck
			TNT1 A 0 A_JumpIf(CallACS("RTCW_Decorate", 3) == 0, 2)
			TNT1 A 1 A_ZoomFactor(90.0/CallACS("RTCW_Decorate", 2)*1.0,ZOOM_INSTANT)
			TNT1 A 0 A_JumpIfInventory("RTCW_Kicking",1,"Kicking")
			TNT1 A 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
			goto ReadyActualAmmoCheck

        ReadyActualAmmoCheck:
            RW00 A 0 A_JumpIfInventory("SamsaraReloadMode",1,1)
            goto ReadyActual
            RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"ReadyActual")
            RW00 A 0 A_JumpIfInventory("RTCW_MauserMagazine",1,"ReadyActual")
            goto ReloadCheck

		Deselect:
			RW01 A 0 A_JumpIfInventory("RTCW_Scoped",1,1)
            goto DeselectStart
            RW01 A 0 A_TakeInventory("RTCW_ZoomFactor")
            RW01 A 0 A_TakeInventory("RTCW_Scoped")
            RW01 A 0 A_ZoomFactor(1,ZOOM_INSTANT)
            goto DeselectStart

        DeselectStart:
            RW01 A 0 A_JumpIfHealthLower(1,"DeselectInstant")
			RW01 A 2 A_PlaySound("RTCW/Change",CHAN_7)
			RW01 BCD 2
			goto DeselectInstant

		Ready: // our "Select" state
			RW00 A 0 A_TakeInventory("RTCW_WeaponToken",256)
			RW00 A 0 A_GiveInventory("RTCW_WeaponToken",10)
            RW01 E 0 A_JumpIfInventory("RTCW_Scoped",1,"ReadyZoomOut1") // map change hack
        ReadyStart1:
			RW01 E 1 A_JumpIfInventory("SamsaraHasDoubleFiringSpeed",1,1)
            RW01 E 0 A_JumpIfInventory("RTCW_Scoped",1,"ReadyZoomOut2") // map change hack
        ReadyStart2:
			RW01 E 1
			RW01 FGHIJK 2
            goto ReadyActual

        ReadyZoomOut1:
            RW01 E 0 A_TakeInventory("RTCW_ZoomFactor")
            RW01 E 0 A_TakeInventory("RTCW_Scoped")
            RW01 E 0 A_ZoomFactor(1,ZOOM_INSTANT)
            goto ReadyStart1

        ReadyZoomOut2:
            RW01 E 0 A_TakeInventory("RTCW_ZoomFactor")
            RW01 E 0 A_TakeInventory("RTCW_Scoped")
            RW01 E 0 A_ZoomFactor(1,ZOOM_INSTANT)
            goto ReadyStart2

		Fire:
            RW00 B 0 A_JumpIfInventory("SamsaraReloadMode",1,"FireReloadable")
        FireNonReloadable:
            RW00 B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart")
            RW00 B 0 A_JumpIfInventory("Shell",1,"FireStart")
            goto FireDry

        FireReloadable:
            RW00 B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireStart")
            RW00 B 0 A_JumpIfInventory("RTCW_MauserMagazine",1,"FireStart")
        FireReloadableDryCheck:
            RW00 B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReloadScopeCheck")
            RW00 B 0 A_JumpIfInventory("Shell",1,"ReloadScopeCheck")
            goto FireDry

        FireDry:
            RW00 A 0 A_ClearReFire
            RW00 A 0 A_PlaySound("RTCW/NoAmmo",CHAN_5)
			RW00 A 0 A_JumpIfInventory("RTCW_Scoped",1,2)
			RW00 A 17 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
            goto FireDryFinish
			TNT1 A 17 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto FireDryFinish

        FireDryFinish:
			RW00 A 0 A_SelectWeapon("RTCW_Luger")
			goto Deselect

        FireUnderwater:
            RW00 A 0 A_ClearReFire
            RW00 A 0 A_PlaySound("RTCW/UnderwaterFire",CHAN_5)
			RW00 A 0 A_JumpIfInventory("RTCW_Scoped",1,2)
			RW00 A 17 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
            goto ReadyActual
			TNT1 A 17 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto ReadyActual

		FireStart:
            RW00 B 0 A_JumpIfInventory("SamsaraCurrentWaterLevel",0,"FireUnderwater")
			RW00 B 0 A_GiveInventory("RTCW_FiringToken",1)
			RW00 B 0 A_GiveInventory("RTCW_BlazkoWeaponAlert",1)
			RW00 B 0 A_SpawnItemEx("RTCW_Casing",4,-6*cos(-pitch),43+(6*sin(-pitch)),1,0,2*cos(-pitch),-random(85,95),SXF_TRANSFERPITCH)
			RW00 B 0 A_JumpIfInventory("RTCW_Scoped",1,"FireScoped")
			RW00 B 0 A_GunFlash("FireFlash")
			RW00 B 0 A_GiveInventory("SamsaraRTCWMauserAttackHandler")
			RW00 BCDEFGHIJKLM 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW00 A 15 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto ReadyActual

		FireScoped:
			TNT1 A 0 A_GiveInventory("SamsaraRTCWMauserAttackHandler")
            TNT1 A 6 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			TNT1 AAAAAAAAAAAA 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			TNT1 A 15 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto ReadyActual

		FireFlash:
			TNT1 A 0 A_Jump(256,1,2)
			RW03 B 2 Bright
			stop
			RW04 B 2 Bright
			stop

		FireFlashLast:
			TNT1 A 0 A_Jump(256,1,2)
			RW03 P 2 Bright
			stop
			RW04 P 2 Bright
			stop

		AltFire:
        Zoom:
			RW00 A 0 A_PlaySound("RTCW/Change",CHAN_7)
			RW00 A 0 A_JumpIfInventory("RTCW_Scoped",1,"ZoomUnscope")
		ZoomScope:
			TNT1 A 0 A_GiveInventory("RTCW_ZoomFactor",12)
			TNT1 A 0 A_GiveInventory("RTCW_Scoped",1)
			TNT1 A 0 ACS_NamedExecuteWithResult("RTCW_Scope")
			TNT1 A 0 A_ZoomFactor(90.0/12.0,ZOOM_INSTANT)
            TNT1 A 10 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto ReadyActual

		ZoomUnscope:
			RW00 A 0 A_TakeInventory("RTCW_ZoomFactor",90)
			RW00 A 0 A_TakeInventory("RTCW_Scoped",1)
			RW00 A 0 A_ZoomFactor(1,ZOOM_INSTANT)
            RW01 E 0 A_JumpIfInventory("SamsaraHasDoubleFiringSpeed",1,2)
            RW01 EE 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
            RW01 FGHIJK 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto ReadyActual

        Reload:
        ReloadCheck:
            RW00 A 0 A_JumpIfInventory("RTCW_MauserMagazine",0,"ReadyActual")
            RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
            RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReloadScopeCheck")
            RW00 A 0 A_JumpIfInventory("Shell",1,"ReloadScopeCheck")
            goto ReadyActual

        ReloadScopeCheck:
			RW00 A 0 A_JumpIfInventory("RTCW_Scoped",1,1)
            goto ReloadStart
			RW00 A 0 A_PlaySound("RTCW/Change",CHAN_7)
			RW00 A 0 A_TakeInventory("RTCW_ZoomFactor",90)
			RW00 A 0 A_TakeInventory("RTCW_Scoped",1)
			RW00 A 0 A_ZoomFactor(1,ZOOM_INSTANT)
            RW01 E 0 A_JumpIfInventory("SamsaraHasDoubleFiringSpeed",1,2)
            RW01 EE 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
            RW01 FGHIJK 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
            goto ReloadCheck

		ReloadStart:
			RW01 L 0 A_PlaySound("RTCW/Mauser/Reload",CHAN_6)
			RW01 "LMNOPQRSTUVWXYZ[\]" 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW02 ABCDEF 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW00 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReloadAmmo")
        ReloadFinish:
            RW00 A 0
			goto ReadyActual

        ReloadAmmo:
            RW00 A 0 A_JumpIfInventory("RTCW_MauserMagazine",0,"ReloadFinish")
            RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
            RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
            RW00 A 0 A_JumpIfInventory("Shell",1,1)
            goto ReloadFinish
            RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
            RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
            RW00 A 0 A_TakeInventory("Shell",1)
            RW00 A 0 A_GiveInventory("RTCW_MauserMagazine")
            loop

		Kicking:
			RW00 A 0 A_GunFlash("KickingFoot")
		KickingDelay:
			RW00 A 0 A_JumpIfInventory("RTCW_Scoped",1,2)
			RW00 A 18 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto KickingDelayFinish
			TNT1 A 18 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE) // not really supposed to see anything, even the kick
			goto KickingDelayFinish

        KickingDelayFinish:
			RW00 A 0 A_TakeInventory("RTCW_Kicking",1)
			goto ReadyActual
	}
}

actor SamsaraRTCWMauserAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"PickupReloadable")
      PickupNonReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupPlaySound")
        TNT1 A 0 A_JumpIfInventory("Shell",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Shell",1)
        goto PickupPlaySound

      PickupReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"PickupPlaySound")
        TNT1 A 0 A_JumpIfInventory("RTCW_MauserMagazine",1,1)
        stop
        TNT1 A 0 A_TakeInventory("RTCW_MauserMagazine",1)
        goto PickupPlaySound

      // Sound stuff
      PickupPlaySound:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"PickupPlaySoundReloadable")
      PickupPlaySoundNonReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupPlaySoundNormal")
        TNT1 A 0 A_JumpIfInventory("Shell",1,"PickupPlaySoundNormal")
        goto PickupPlaySoundLast

      PickupPlaySoundReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"PickupPlaySoundNormal")
        TNT1 A 0 A_JumpIfInventory("RTCW_MauserMagazine",1,"PickupPlaySoundNormal")
        goto PickupPlaySoundLast

      PickupPlaySoundNormal:
        TNT1 A 0 A_PlaySound("RTCW/Mauser/Fire",CHAN_WEAPON)
        goto PickupAttack

      PickupPlaySoundLast:
        TNT1 A 0 A_PlaySound("RTCW/Mauser/FireLast",CHAN_WEAPON)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("RTCW_Scoped",1,"PickupAttackScoped")
      // Unscoped
      PickupAttackUnscoped:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackUnscopedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackUnscopedCoop")
      PickupAttackUnscopedDM:
        TNT1 A 0 A_FireCustomMissile("RTCW_SnooperTracer",frandom(-2.5,2.5),false,4,9,0,frandom(-2.5,2.5))
        goto PickupFinishUnscoped

      PickupAttackUnscopedCoop:
        TNT1 A 0 A_FireCustomMissile("RTCW_SnooperTracerCoop",frandom(-2.5,2.5),false,4,9,0,frandom(-2.5,2.5))
        goto PickupFinishUnscoped

      // Scoped
      PickupAttackScoped:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackScopedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackScopedCoop")
      PickupAttackScopedDM:
        TNT1 A 0 A_FireCustomMissile("RTCW_MauserTracer",0,false,0,8)
        goto PickupFinishScoped

      PickupAttackScopedCoop:
        TNT1 A 0 A_FireCustomMissile("RTCW_MauserTracerCoop",0,false,0,8)
        goto PickupFinishScoped

      // Finishing stuff
      // Unscoped
      PickupFinishUnscoped:
        TNT1 A 0 ACS_NamedExecuteWithResult("RTCW_Recoil", 2)
        stop

      // Scoped
      PickupFinishScoped:
        TNT1 A 0 A_GiveInventory("RTCW_AimSpread",16711680)
        TNT1 A 0 A_SetAngle(angle+frandom(-2.0,2.0))
        TNT1 A 0 ACS_NamedExecuteWithResult("RTCW_Recoil", 3)
        stop
    }
}

actor RTCW_MauserMagazine : Ammo
{
    Inventory.MaxAmount 10
    Tag "Sniper Rifle Magazine"
    +IGNORESKILL
}

//Dunno what it was, but I think this is a fair value
Actor RTCW_MauserTracer : RTCW_Tracer
{ 
	States
	{
		Crash:
		Death:
			TNT1 A 0 A_Explode(CallACS("RTCW_BulletDamage",200),8,!XF_HURTSOURCE,0,8)
			Goto Super::Death
		XDeath:
			TNT1 A 0 A_Explode(CallACS("RTCW_BulletDamage",200),8,!XF_HURTSOURCE,0,8)
			Goto Super::XDeath
	}
}

Actor RTCW_MauserTracerCoop : RTCW_MauserTracer { +THRUSPECIES Species "Player" }

Actor RTCW_MauserTracerDM : RTCW_Tracer
{ 
	States
	{
		Crash:
		Death:
			TNT1 A 0 A_Explode(CallACS("RTCW_BulletDamage",140),8,!XF_HURTSOURCE,0,8) 
			Goto Super::Death
		XDeath:
			TNT1 A 0 A_Explode(CallACS("RTCW_BulletDamage",140),8,!XF_HURTSOURCE,0,8)
			Goto Super::XDeath
	}
}
