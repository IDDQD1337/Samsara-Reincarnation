Actor RTCW_Panzerfaust : RTCW_Weapon
{
	Weapon.SlotNumber 5
    Weapon.SlotPriority 0
	Weapon.SelectionOrder 1500
	Weapon.AmmoType2 "RocketAmmo"
	Weapon.AmmoType1 "RTCW_PanzerfaustMagazine"
	Weapon.AmmoUse1 1
	Weapon.AmmoGive2 3
	Obituary "%o was killed (rl) by %k."
	Inventory.Pickupmessage "Panzerfaust"
	Inventory.PickupSound "RTCW/WeaponPickup"
	Tag "Panzerfaust"
	+WEAPON.AMMO_OPTIONAL
	+WEAPON.EXPLOSIVE
    +NOALERT
	States
	{
		Spawn:
			RW98 A -1
			Stop

        ReadyActual:
            RW02 C 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReadyActualReloadable")
        ReadyActualNonReloadable:
            RW02 C 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReadyActualNonReloadableAmmo")
            RW02 C 0 A_JumpIfInventory("RocketAmmo",1,"ReadyActualNonReloadableAmmo")
            goto ReadyActualNonReloadableNoAmmo

		ReadyActualNonReloadableNoAmmo:
			TNT1 A 0 A_JumpIfInventory("RTCW_Kicking",1,"KickingNoAmmo") // [tv50] I'm lazy
			TNT1 A 1 A_WeaponReady
			goto ReadyActualNoAmmoCheck

		ReadyActualNonReloadableAmmo:
			RW02 C 0 A_JumpIfInventory("RTCW_Kicking",1,"Kicking")
			RW02 C 0 A_GunFlash("ReadyActualFlash")
			RW02 C 1 A_WeaponReady
			goto ReadyActualAmmoCheck

        ReadyActualReloadable:
            RW02 C 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"ReadyActualReloadableAmmo")
            RW02 C 0 A_JumpIfInventory("RTCW_PanzerfaustMagazine",1,"ReadyActualReloadableAmmo")
            goto ReadyActualReloadableNoAmmo

		ReadyActualReloadableNoAmmo:
			TNT1 A 0 A_JumpIfInventory("RTCW_Kicking",1,"KickingNoAmmo") // [tv50] I'm lazy
			TNT1 A 1 A_WeaponReady(WRF_ALLOWRELOAD)
			goto ReadyActualAmmoCheck

		ReadyActualReloadableAmmo:
			RW02 C 0 A_JumpIfInventory("RTCW_Kicking",1,"Kicking")
			RW02 C 0 A_GunFlash("ReadyActualFlash")
			RW02 C 1 A_WeaponReady(WRF_ALLOWRELOAD)
			goto ReadyActualAmmoCheck

		ReadyActualFlash:
			RW07 C 2
			Stop

        ReadyActualNoAmmoCheck:
            TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReadyActualAmmoCheck")
            TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReloadStart")
            TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,"ReloadStart")
            goto ReadyActual

        ReadyActualAmmoCheck:
            RW02 C 0 A_JumpIfInventory("SamsaraReloadMode",1,1)
            goto ReadyActual
            RW02 C 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"ReadyActual")
            RW02 C 0 A_JumpIfInventory("RTCW_PanzerfaustMagazine",1,"ReadyActual")
            goto ReloadCheck

		Deselect:
			RW00 V 0 A_PlaySound("RTCW/Change",CHAN_7)
            RW00 V 0 A_JumpIfInventory("SamsaraReloadMode",1,"DeselectReloadable")
        DeselectNonReloadable:
            RW00 V 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"DeselectAmmo")
            RW00 V 0 A_JumpIfInventory("RocketAmmo",1,"DeselectAmmo")
            goto DeselectNoAmmo

        DeselectReloadable:
            RW00 V 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"DeselectAmmo")
            RW00 V 0 A_JumpIfInventory("RTCW_PanzerfaustMagazine",1,"DeselectAmmo")
            goto DeselectNoAmmo

        DeselectNoAmmo:
			TNT1 A 2
			TNT1 AAAAAAA 2
			TNT1 AAAAAAAAAAAA 2
            goto Super::Deselect

        DeselectAmmo:
			RW00 V 2 A_GunFlash("DeselectFlash")
			RW00 "WXYZ[\]" 2
			RW01 ABCDEFGHIJKL 2
			Goto Super::Deselect

		DeselectFlash:
			RW05 "VWXYZ[\]" 2
			RW06 ABCDEFGHIJKL 2
			Stop

		Ready: // our "Select" state
			RW00 A 0 A_TakeInventory("RTCW_WeaponToken",256)
			RW00 A 0 A_GiveInventory("RTCW_WeaponToken",13)
            RW00 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReadyReloadable")
        ReadyNonReloadable:
            RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReadyAmmo")
            RW00 A 0 A_JumpIfInventory("RocketAmmo",1,"ReadyAmmo")
            goto ReadyNoAmmo

        ReadyReloadable:
            RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"ReadyAmmo")
            RW00 A 0 A_JumpIfInventory("RTCW_PanzerfaustMagazine",1,"ReadyAmmo")
            goto ReadyNoAmmo

        ReadyNoAmmo:
            TNT1 A 2
            TNT1 AAAAAAAA 2
            TNT1 AA 2
            goto ReadyActual

        ReadyAmmo:
			RW01 M 2 A_GunFlash("ReadyFlash")
			RW01 NOPQRSTU 2
			RW02 AB 2
            goto ReadyActual

		ReadyFlash:
			RW06 MNOPQRSTU 2
			RW07 AB 2
			Stop

		Fire:
            RW00 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"FireReloadable")
        FireNonReloadable:
            RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart")
            RW00 A 0 A_JumpIfInventory("RocketAmmo",1,"FireStart")
            goto FireDry

        FireReloadable:
            RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireStart")
            RW00 A 0 A_JumpIfInventory("RTCW_PanzerfaustMagazine",1,"FireStart")
        FireReloadableDryCheck:
            RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReloadStart")
            RW00 A 0 A_JumpIfInventory("RocketAmmo",1,"ReloadStart")
            goto FireDry

        FireDry:
			TNT1 A 17 A_PlaySound("RTCW/NoAmmo",CHAN_5)
			TNT1 A 0 A_SelectWeapon("RTCW_Knife")
			Goto Deselect

        FireUnderwater:
            RW09 A 17 A_PlaySound("RTCW/UnderwaterFire",CHAN_5)
            goto ReadyActual

        FireStart:
            RW00 A 0 A_JumpIfInventory("SamsaraCurrentWaterLevel",0,"FireUnderwater")
			RW00 A 0 A_GiveInventory("RTCW_FiringToken",1)
			RW00 A 0 A_GiveInventory("RTCW_BlazkoWeaponAlert",1)
			RW00 A 0 A_FireCustomMissile("RTCW_PanzerfaustShell",-random(85,105),false,8,4,0,-4)
        FireLastShotCalc:
            RW00 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"FireLastShotCalcReloadable")
        FireLastShotCalcNonReloadable:
            RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireContinueLast")
            RW00 A 0 A_JumpIfInventory("RocketAmmo",2,"FireContinueLast")
            goto FireContinueLastNoAmmo

        FireLastShotCalcReloadable:
            RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireContinueNormal")
            RW00 A 0 A_JumpIfInventory("RTCW_PanzerfaustMagazine",2,"FireContinueNormal")
            RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireContinueLast")
            RW00 A 0 A_JumpIfInventory("RocketAmmo",1,"FireContinueLast")
            goto FireContinueLastNoAmmo

        FireContinueNormal:
			RW02 C 0 A_GunFlash("FireFlash")
			RW02 C 2 A_GiveInventory("SamsaraRTCWPanzerfaustAttackHandler")
			RW02 DEFGCCCCCCCCCCC 2 // precautionary parity
            RW02 C 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReadyActual")
            goto ReadyActualNoAmmoCheck

        FireContinueLast:
			RW00 A 0 A_GunFlash("FireFlashLast")
			RW00 A 2 A_GiveInventory("SamsaraRTCWPanzerfaustAttackHandler")
			RW00 BCDEFGHIJKLMNOP 2
            RW00 P 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReadyActual")
            goto ReadyActualNoAmmoCheck

		FireContinueLastNoAmmo:
			RW02 C 0 A_GunFlash("FireFlashLastNoAmmo")
			RW02 C 0 A_PlaySound("RTCW/NoAmmo",CHAN_5)
			RW02 C 0 A_PlaySound("RTCW/Change",CHAN_7)
			RW02 C 2 A_GiveInventory("SamsaraRTCWPanzerfaustAttackHandler")
			RW02 DEFGHIJKL 2
			TNT1 "MNOPQRSTUVWXYZ[\]" 2
			TNT1 ABCDEFGHIJKLM 2
			TNT1 A 0 A_SelectWeapon("RTCW_Knife")
			goto Super::Deselect

		FireFlash:
		FireFlashLastNoAmmo:
			RW04 B 2 Bright
			stop

		FireFlashLast:
			RW04 A 2 Bright
			stop

        Reload:
        ReloadCheck:
            TNT1 A 0 A_JumpIfInventory("RTCW_PanzerfaustMagazine",0,"ReadyActual")
            TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
            TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReloadStart")
            TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,"ReloadStart")
            goto ReadyActual

        ReloadStart:
            RW00 P 0
			RW01 G 2 A_GunFlash("ReloadFlash")
			RW01 "HIJKLMNOPQRSTUVWXYZ[\]" 2
			RW02 AB 2
            RW02 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReloadAmmo")
        ReloadDone:
            RW02 A 0
			goto ReadyActual

		ReloadFlash:
			RW06 "GHIJKLMNOPQRSTUVWXYZ[\]" 2
			RW07 AB 2
			Stop

        ReloadAmmo:
            TNT1 A 0 A_JumpIfInventory("RTCW_PanzerfaustMagazine",0,"ReloadDone")
            TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
            TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
            TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,1)
            goto ReloadDone
            TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
            TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
            TNT1 A 0 A_TakeInventory("RocketAmmo",1)
            TNT1 A 0 A_GiveInventory("RTCW_PanzerfaustMagazine")
            loop

		KickingNoAmmo: // jumps to "with ammo" version for the time being
		Kicking:
			TNT1 A 0 A_GunFlash("KickingFoot")
		KickingDelay:
			RW09 A 18
			RW09 A 0 A_TakeInventory("RTCW_Kicking",1)
			goto ReadyActual
	}
}

actor SamsaraRTCWPanzerfaustAttackHandler : Trigger
{
    States
    {
      // Ammo check stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"PickupCheckAmmoReloadable")
      PickupCheckAmmoNonReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupTakeAmmoNonReloadable")
        TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,"PickupTakeAmmoNonReloadable")
        stop

      PickupCheckAmmoReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("RTCW_PanzerfaustMagazine",1,"PickupTakeAmmoReloadable")
        stop

      // Ammo taking stuff
      PickupTakeAmmoNonReloadable:
        TNT1 A 0 A_TakeInventory("RocketAmmo",1,TIF_NOTAKEINFINITE)
        goto PickupAttack

      PickupTakeAmmoReloadable:
        TNT1 A 0 A_TakeInventory("RTCW_PanzerfaustMagazine",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("RTCW/Panzerfaust/Fire",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("RTCW_PanzerfaustRocket",0,false,8)
        goto PickupFinish

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("RTCW_PanzerfaustRocketCoop",0,false,8)
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 A_Quake(3,15,0,128,"")
        stop
    }
}

actor RTCW_PanzerfaustMagazine : Ammo
{
    Inventory.MaxAmount 1
    +IGNORESKILL
}

//Was 100 impact 120 splash, now 240 splash, 200 impact
Actor RTCW_PanzerfaustRocket
{
	Height 10
	Radius 5
	Damage (250)
	Speed 32
	Projectile
	+SKYEXPLODE
	DamageType "RTCWExplosive"
	States
	{
		Spawn:
			RW00 A 0 NoDelay A_PlaySound("RTCW/Panzerfaust/Fly",CHAN_5,0.5,1)
			RW00 A 0 ACS_NamedExecuteWithResult("RTCW_SpawnRocketFlare")
		SpawnLoop:
			RW00 A 1 Bright
			Loop
		Crash:
		Death:
		XDeath:
			RW00 AAAA 0 A_SpawnItemEx("RTCW_PanzerfaustSmokeExplosion",0,0,0,frandom(-0.5,0.5),frandom(-0.5,0.5),1)
			RW00 AAAA 0 A_SpawnItemEx("RTCW_ExplosionSmokeDebris")
			RW00 A 0 A_Quake(3,15,0,128,"")
			RW00 A 0 A_GiveInventory("RTCW_BlazkoWeaponAlert",1,AAPTR_Target)
			RW00 A 0 A_Explode(300,120,XF_HURTSOURCE,1,0)
			RW00 A 0 A_SpawnItemEx("RTCW_PanzerfaustRocketExplosion")
			TNT1 A 15
			Stop
	}
}

Actor RTCW_PanzerfaustRocketCoop : RTCW_PanzerfaustRocket { +THRUSPECIES Species "Player" }

Actor RTCW_PanzerfaustShell : RTCW_Casing
{
	Scale 2.0
	BounceType "None"
	States
	{
		Spawn:
			RW00 A 0 NoDelay A_ChangeVelocity(frandom(-0.75,0.75),frandom(-0.75,0.75),0,CVF_RELATIVE)
			RW00 A 0 ThrustThingZ(0,random(8,12),0,1)
		SpawnLoop:
			RW00 A 5 A_SpawnItemEx("RTCW_SmokePuff",0,-16,0,0,0,frandom(0.25,0.5))
			Loop
		Death:
			RW00 AAAAAAAAAAAAAAAAAAAA 5 A_SpawnItemEx("RTCW_SmokePuff",0,-16,0,0,0,frandom(0.25,0.5))
			RW00 AAAAAAAA 1 A_Fadeout(0.1)
			Stop
	}
}

Actor RTCW_PanzerfaustRocketFlare
{
	Height 10
	Radius 5
	Renderstyle Add
	+NOINTERACTION
	States
	{
		Spawn:
			RW00 A 5 Bright
		SpawnLoop:
			RW00 A 0 A_SpawnItemEx("RTCW_PanzerfaustSmoke",-16*cos(velz/24),0,-16*sin(velz/24))
			RW00 A 0 A_SpawnItemEx("RTCW_PanzerfaustSmoke",-8*cos(velz/24),0,-8*sin(velz/24))
			RW00 A 1 Bright A_SpawnItemEx("RTCW_PanzerfaustSmoke")
			Loop
	}
}

Actor RTCW_ExplosionSmokeDebris
{
	Height 4
	Radius 2
	Gravity 0.33
	Damage (0)
	Projectile
	Scale 0.1
	BounceType "Doom"
	BounceCount 3
	-NOGRAVITY
	+THRUACTORS
	States
	{
		Spawn:
			TNT1 A 0 NoDelay A_ChangeVelocity(((2*random(0,1))-1)*frandom(1.0,2.0),((2*random(0,1))-1)*frandom(1.0,2.0),frandom(3.0,5.0),CVF_REPLACE)
		SpawnLoop:
			TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("RTCW_PanzerfaustSmoke",0,0,0,0,0,0,0,SXF_TRANSFERSCALE)
			Stop	
	}
}

Actor RTCW_PanzerfaustRocketExplosion
{
	Height 4
	Radius 2
	Scale 2.0
	+NOINTERACTION
	States
	{
		Spawn:
			//We have to duplicate the sprites because of a zandronum bug
			RW06 A 0 NoDelay A_Quake(3,15,0,256,"")
			RW06 A 0 A_PlaySound("RTCW/Panzerfaust/Explode",CHAN_5)
			RW06 ABCDEFGHIJKLMNOPQRSTUVWX 2 Bright
			Stop
	}
}

Actor RTCW_PanzerfaustSmoke
{
	Radius 1
	Height 1
	Alpha 0.2
	Scale 0.33
	RenderStyle Shaded
	+NOINTERACTION
	+CLIENTSIDEONLY
	+FORCEXYBILLBOARD
	States
	{
		Spawn:
			RW05 ABCDEFGHIJKLMNOPQRSTUVWX 2
			Stop
	}
}

Actor RTCW_PanzerfaustSmokeExplosion : RTCW_PanzerfaustSmoke { Scale 2.0 Alpha 0.33 }
