actor RTCW_ThompsonSpread : Counter { Inventory.MaxAmount 6 }

Actor RTCW_Thompson : RTCW_Weapon
{
	Weapon.SlotNumber 2
	Weapon.SlotPriority 1
	Weapon.SelectionOrder 750
	Weapon.AmmoType1 "RTCW_ThompsonMagazine"
	Weapon.AmmoUse1 1
	Weapon.AmmoType2 "RTCW_AlliedAmmo1"
	Weapon.AmmoGive2 15
	Obituary "%o was killed (thompson) by %k."
	Inventory.Pickupmessage "Thompson"
	Inventory.PickupSound "RTCW/WeaponPickup"
	Tag "Thompson M1A1"
	+WEAPON.NOALERT
	+WEAPON.AMMO_OPTIONAL
	States
	{
		Spawn:
			RW98 A -1
			Stop

        ReadyActual:
			RW00 A 0 A_TakeInventory("RTCW_ThompsonSpread")
			RW00 A 0 A_JumpIfInventory("RTCW_Kicking",1,"Kicking")
            RW00 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReadyActualReloadable")
        ReadyActualNonReloadable:
			RW00 A 1 A_WeaponReady
			goto ReadyActualAmmoCheck

        ReadyActualReloadable:
			RW00 A 1 A_WeaponReady(WRF_ALLOWRELOAD)
			goto ReadyActualAmmoCheck

        ReadyActualAmmoCheck:
            RW00 A 0 A_JumpIfInventory("SamsaraReloadMode",1,1)
            goto ReadyActual
            RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"ReadyActual")
            RW00 A 0 A_JumpIfInventory("RTCW_ThompsonMagazine",1,"ReadyActual")
            goto ReloadCheck

		Deselect:
			RW00 V 0 A_PlaySound("RTCW/Change",CHAN_7)
			RW00 G 2 A_TakeInventory("RTCW_ThompsonSpread")
			RW00 HIJK 2
			Goto Super::Deselect

		Ready: // our "Select" state
			RW00 A 0 A_TakeInventory("RTCW_WeaponToken",256)
			RW00 A 0 A_GiveInventory("RTCW_WeaponToken",9)
			RW00 L 2 A_TakeInventory("RTCW_ThompsonSpread")
			RW00 MNO 2
            goto ReadyActual

		Fire:
            RW00 B 0 A_JumpIfInventory("SamsaraReloadMode",1,"FireReloadable")
        FireNonReloadable:
            RW00 B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart")
            RW00 B 0 A_JumpIfInventory("RTCW_AlliedAmmo1",1,"FireStart")
            goto FireDry

        FireReloadable:
            RW00 B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireStart")
            RW00 B 0 A_JumpIfInventory("RTCW_ThompsonMagazine",1,"FireStart")
        FireReloadableDryCheck:
            RW00 B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReloadStart")
            RW00 B 0 A_JumpIfInventory("RTCW_AlliedAmmo1",1,"ReloadStart")
            goto FireDry

        FireDry:
			RW00 A 0 A_PlaySound("RTCW/NoAmmo",CHAN_5)
            RW00 A 17 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			TNT1 A 0 A_SelectWeapon("RTCW_Luger")
			Goto Deselect

        FireUnderwater:
			RW00 A 0 A_PlaySound("RTCW/UnderwaterFire",CHAN_5)
            RW00 A 17 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
            goto ReadyActual

		FireStart:
            RW00 B 0 A_JumpIfInventory("SamsaraCurrentWaterLevel",0,"FireUnderwater")
			RW00 B 0 A_GiveInventory("RTCW_FiringToken",1)
			RW00 B 0 A_GiveInventory("RTCW_BlazkoWeaponAlert",1)
			RW00 B 0 A_SpawnItemEx("RTCW_Casing",4,-10*cos(-pitch),43+(10*sin(-pitch)),1,0,2*cos(-pitch),-random(85,95),SXF_TRANSFERPITCH)
			RW00 B 0 A_GunFlash("FireFlash")
			RW00 B 0 A_GiveInventory("SamsaraRTCWThompsonAttackHandler")
            RW00 B 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW00 C 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW00 D 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW00 B 0 A_ReFire
			goto ReadyActual

		FireFlash:
			TNT1 A 0 A_Jump(256,1,2)
			RW03 B 2 Bright
			Stop
			RW04 B 2 Bright
			Stop

        Reload:
        ReloadCheck:
            RW00 A 0 A_JumpIfInventory("RTCW_ThompsonMagazine",0,"ReadyActual")
            RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
            RW00 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReloadStart")
            RW00 A 0 A_JumpIfInventory("RTCW_AlliedAmmo1",1,"ReloadStart")
            goto ReadyActual

		ReloadStart:
			TNT1 A 0 A_PlaySound("RTCW/Thompson/Reload",CHAN_6)
			//TNT1 A 0 A_JumpIfInventory("RTCW_ThompsonMagazine",1,1)
			goto ReloadStartAmmoless
			RW00 "QRSTUVWXYZ[\]" 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW01 ABCDEFGHIJKLMNOPQR 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW02 FG 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW00 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReloadAmmo")
            goto ReloadDone

		ReloadStartAmmoless:
			RW00 "QRSTUVWXYZ[\]" 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW01 ABCDEFGHIJKLMNOPQR 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW01 STUV 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW01 W 0 A_PlaySound("RTCW/Thompson/Reload2",CHAN_6)
			RW01 "WXYZ[\]" 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW02 ABCDEFG 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW00 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReloadAmmo")
            goto ReloadDone

        ReloadDone:
            RW00 A 0
			goto ReadyActual

        ReloadAmmo:
            TNT1 A 0 A_JumpIfInventory("RTCW_ThompsonMagazine",0,"ReloadDone")
            TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
            TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
            TNT1 A 0 A_JumpIfInventory("RTCW_AlliedAmmo1",1,1)
            goto ReloadDone
            TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
            TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
            TNT1 A 0 A_TakeInventory("RTCW_AlliedAmmo1",1)
            TNT1 A 0 A_GiveInventory("RTCW_ThompsonMagazine")
            loop

		Kicking:
			TNT1 A 0 A_GunFlash("KickingFoot")
		KickingDelay:
			RW00 A 18 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			RW00 A 0 A_TakeInventory("RTCW_Kicking",1)
			goto ReadyActual
	}
}

actor SamsaraRTCWThompsonAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"PickupReloadable")
      PickupNonReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("RTCW_AlliedAmmo1",1,1)
        stop
        TNT1 A 0 A_TakeInventory("RTCW_AlliedAmmo1",1)
        goto PickupAttack

      PickupReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("RTCW_ThompsonMagazine",1,1)
        stop
        TNT1 A 0 A_TakeInventory("RTCW_ThompsonMagazine",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("RTCW/Thompson/Fire",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("RTCW_ColtTracer",CallACS("RTCW_Decorate",1,2)*frandom(-1.0,1.0),false,4,9,0,CallACS("RTCW_Decorate",1,2)*frandom(-1.0,1.0))
        goto PickupFinish

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("RTCW_ColtTracerCoop",CallACS("RTCW_Decorate",1,2)*frandom(-1.0,1.0),false,4,9,0,CallACS("RTCW_Decorate",1,2)*frandom(-1.0,1.0))
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 A_GiveInventory("RTCW_ThompsonSpread")
        stop
    }
}

actor RTCW_ThompsonMagazine : Ammo
{
    Inventory.MaxAmount 30
    +IGNORESKILL
}
