Actor RTCW_Sten : RTCW_Weapon
{
	Weapon.SlotNumber 8
	Weapon.SelectionOrder 600
	Weapon.AmmoUse1 1
	Weapon.AmmoUse2 1
	Weapon.AmmoType1 "RTCW_StenMagazine"
	Weapon.AmmoType2 "Clip"
	Weapon.AmmoGive2 16
	Obituary "%o was killed (sten) by %k."
	Inventory.Pickupmessage "Sten"
	Inventory.PickupSound "RTCW/WeaponPickup"
	Tag "Silenced Sten"
	+WEAPON.NOALERT
	+WEAPON.AMMO_OPTIONAL
	States
	{
		Spawn:
			RW98 A -1
			Stop
		Deselect:
			RW00 V 0 A_PlaySound("RTCW/Change",CHAN_7)
			RW00 HIJKL 2
			Goto Super::Deselect
		Ready:
			RW00 A 0 A_TakeInventory("RTCW_WeaponToken",256)
			RW00 A 0 A_GiveInventory("RTCW_WeaponToken",17)
			RW00 MNOP 2
		ReadyLoop:
			RW00 A 0 A_JumpIfInventory("RTCW_Kicking",1,"Kicking")
			RW00 A 1 A_WeaponReady(WRF_ALLOWRELOAD)
			Loop
		NoAmmo:
			RW00 A 0 A_PlaySound("RTCW/NoAmmo",CHAN_5)
			RW00 A 17
			TNT1 A 0 A_SelectWeapon("RTCW_Knife")
			Goto Deselect
		Fire:
			RW00 B 0 A_JumpIfInventory("RTCW_StenMagazine",1,1)
			Goto Reload
			RW00 B 0 A_JumpIfInventory("RTCW_StenHeat",0,"CoolDown")
			RW00 B 0 A_FireCustomMissile("RTCW_Casing",-random(85,105),0,8,4,0,-4)
			RW00 B 0 A_SpawnItemEx("RTCW_VenomSmoke",cos(pitch)*20,4,35-(sin(pitch)*20),(cos(pitch)*frandom(-0.125,0.125))+(sin(pitch)*0.33),frandom(-0.125,0.125),(cos(pitch)*0.33)-sin(pitch)*frandom(-0.125,0.125))
			RW00 B 1 A_GiveInventory("SamsaraRTCWStenAttackHandler")
			RW00 C 2
			RW00 D 1
			RW00 B 0 A_Refire("Fire")
			RW00 B 0 A_TakeInventory("RTCW_StenSpread",4)
			Goto ReadyLoop
		CoolDown:
			RW00 A 0 A_PlaySound("RTCW/Sten/Overheat",CHAN_5)
			RW00 A 0 A_GiveInventory("RTCW_StenCooling",1)
			RW00 AAAAAAAAAAAAAAA 4 A_SpawnItemEx("RTCW_VenomSmoke",cos(pitch)*20,4,35-(sin(pitch)*20),(cos(pitch)*frandom(-0.125,0.125))+(sin(pitch)*0.33),frandom(-0.125,0.125),(cos(pitch)*0.33)-sin(pitch)*frandom(-0.125,0.125))
			RW00 A 0 A_TakeInventory("RTCW_StenCooling",1)
			RW00 AAA 4
			Goto ReadyLoop
		Reload:
			TNT1 A 0 A_JumpIfInventory("RTCW_StenMagazine",0,"ReadyLoop")
			TNT1 A 0 A_JumpIfInventory("Clip",1,"ReloadSuccess")
			TNT1 A 0 A_JumpIfInventory("RTCW_StenMagazine",1,1)
			goto NoAmmo
			TNT1 A 0
			goto ReadyLoop
		ReloadSuccess:
			TNT1 A 0 //A_JumpIfInventory("RTCW_StenMagazine",1,1)
			goto ReloadNoAmmo		
			RW00 A 0 A_PlaySound("RTCW/Sten/Reload",CHAN_6)			
			RW00 "RSTUVWXYZ[\]" 2
			RW01 ABCDEFGHIJKLMNOPQRSTU 2
			RW02 MNOPQ 2
			RW00 A 0
			goto ReloadAmmo
		ReloadNoAmmo:
			RW00 A 0 A_PlaySound("RTCW/Sten/Reload",CHAN_6)			
			RW00 "RSTUVWXYZ[\]" 2
			RW01 ABCDEFGHIJKLMNOPQ 2
			RW01 "RSTUVWXYZ[\]" 1
			RW02 A 0 A_PlaySound("RTCW/Sten/Reload2",CHAN_6)		
			RW02 ABCDEFGHIJLMNOPQ 2
			RW00 A 0
			goto ReloadAmmo		
		ReloadAmmo:
			TNT1 A 0 A_JumpIfInventory("RTCW_StenMagazine",0,"ReadyLoop")
			TNT1 A 0 A_JumpIfInventory("Clip",1,1)
			goto ReadyLoop
			TNT1 A 0 A_GiveInventory("RTCW_StenMagazine",1)
			TNT1 A 0 A_TakeInventory("Clip",1,TIF_NOTAKEINFINITE)
			Loop
		Kicking:
			TNT1 A 0 A_Gunflash("KickingFoot")
		KickingDelay:
			RW00 A 18 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			RW00 A 0 A_TakeInventory("RTCW_Kicking",1)
			Goto ReadyLoop
	}
}

Actor RTCW_StenCooling : Inventory { +INVENTORY.UNDROPPABLE Inventory.MaxAmount 1 Inventory.InterHubAmount 0 }
Actor RTCW_StenHeat : Inventory { +INVENTORY.UNDROPPABLE Inventory.MaxAmount 10 Inventory.InterHubAmount 0 }
Actor RTCW_StenSpread : Inventory { +INVENTORY.UNDROPPABLE Inventory.MaxAmount 2 Inventory.InterHubAmount 0  }

Actor RTCW_StenMagazine : Ammo
{
	Inventory.MaxAmount 32
	Ammo.BackpackMaxAmount 32
}

//Was 10 damage, now 20
Actor RTCW_StenTracer : RTCW_Tracer
{ 
	States
	{
		Crash:
		Death:
			TNT1 A 0 A_Explode(CallACS("RTCW_BulletDamage",25),8,!XF_HURTSOURCE,0,8)
			Goto Super::Death
		XDeath:
			TNT1 A 0 A_Explode(CallACS("RTCW_BulletDamage",25),8,!XF_HURTSOURCE,0,8)
			Goto Super::XDeath
	}
}

Actor RTCW_StenTracerCoop : RTCW_StenTracer { +THRUSPECIES Species "Player" }

actor SamsaraRTCWStenAttackHandler : Trigger
{
	States
	{
		// Ammo check stuff
		Pickup:
			TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"PickupCheckAmmoReloadable")
		PickupCheckAmmoNonReloadable:
			TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupTakeAmmoNonReloadable")
			TNT1 A 0 A_JumpIfInventory("Clip",1,"PickupTakeAmmoNonReloadable")
			Stop
		PickupCheckAmmoReloadable:
			TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"PickupAttack")
			TNT1 A 0 A_JumpIfInventory("RTCW_StenMagazine",1,"PickupTakeAmmoReloadable")
			Stop

		// Attack stuff
		PickupAttack:
			TNT1 A 0 A_GiveInventory("RTCW_StenHeat",1)
			TNT1 A 0 A_GiveInventory("RTCW_StenSpread",1)
			TNT1 A 0 A_PlaySound("RTCW/Sten/Fire",CHAN_Weapon)
			TNT1 A 0 ACS_NamedExecuteWithResult("RTCW_Recoil",1)
			TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
			TNT1 A 0 A_FireCustomMissile("RTCW_StenTracer",CallACS("RTCW_Decorate",1,3)*frandom(-1.0,1.0),0,0,4,0,CallACS("RTCW_Decorate",1,3)*frandom(-1.0,1.0))
			Stop
		PickupAttackCoop:
			TNT1 A 0 A_FireCustomMissile("RTCW_StenTracerCoop",CallACS("RTCW_Decorate",1,3)*frandom(-1.0,1.0),0,0,4,0,CallACS("RTCW_Decorate",1,3)*frandom(-1.0,1.0))
			Stop

		// Ammo taking stuff
		PickupTakeAmmoNonReloadable:
			TNT1 A 0 A_TakeInventory("Clip",1,TIF_NOTAKEINFINITE)
			Goto PickupAttack
		PickupTakeAmmoReloadable:
			TNT1 A 0 A_TakeInventory("RTCW_StenMagazine",1)
			Goto PickupAttack
	}
}
