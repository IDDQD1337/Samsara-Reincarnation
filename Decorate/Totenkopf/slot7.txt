actor Totenkopf_Flakgun : InstantSwitchWeapon
{
    Weapon.SelectionOrder 500
    Weapon.SlotNumber 7
    Inventory.PickUpSound "Totenkopf/getweapon"
    Weapon.AmmoUse 3
    Weapon.AmmoGive 60
    Weapon.AmmoType "Cell"
    +THRUGHOST
    +DONTBOB
    +INVENTORY.UNDROPPABLE
    Inventory.PickupMessage "Flakgun"
    Obituary ""
    Weapon.YAdjust 16
    States
    {
      Spawn:
		TK09 F 4
        Loop

      Ready:
        TK09 A 1 A_WeaponReady
        loop

      Fire:
        TK09 B 2
		Goto Hold

      Hold:
		TK09 C 0 A_JumpIfInventory("CoopModeOn", 1, "CoopHold")
        TK09 C 0 A_PlaySound("Totenkopf/atkflakgun",CHAN_WEAPON)
        TK09 C 3 Bright A_FireCustomMissile("Totenkopf_Flakgun_Projectile",random(-3,3))
		TK09 D 2 Bright
		TK09 D 0 A_Refire
        TK09 E 3
        Goto Ready
	  CoopHold:
        TK09 C 0 A_PlaySound("Totenkopf/atkflakgun",CHAN_WEAPON)
        TK09 C 3 Bright A_FireCustomMissile("Totenkopf_Flakgun_ProjectileCoop",random(-3,3))
		TK09 D 2 Bright
		TK09 D 0 A_Refire
        TK09 E 3
        Goto Ready	  
    }
} 

Actor Totenkopf_Flakgun_Projectile : Totenkopf_Tracer
{
	Damage (100)
	Projectile
	DamageType "TotenkopfFlak"
	+BLOODLESSIMPACT
	+HITTRACER
	-NOEXTREMEDEATH
	+EXTREMEDEATH
	+NODAMAGETHRUST
	+FORCERADIUSDMG
	States
	{
		Spawn:
			TNT1 A 1
			Loop
		Crash:
		Death:
			TNT1 A 0 A_Explode(100,33)
			TNT1 A 1
			TNT1 A 0 A_Playsound("Totenkopf/explosion", CHAN_BODY)
			TK54 ABC 2 Bright
			Stop
		XDeath:
			TNT1 A 0 A_Explode(100,33)
			TNT1 A 0 A_JumpIf(CallACS("Totenkopf_BloodGenerator_Offsets")==1,1)
			TNT1 A 1 Bright
			TNT1 A 0 A_GiveInventory("Totenkopf_BloodGenerator",1,AAPTR_TRACER)
			TK54 ABC 2 Bright
			Stop
	}
}
Actor Totenkopf_Flakgun_ProjectileCoop : Totenkopf_Flakgun_Projectile { Species "Player" +THRUSPECIES +DONTHARMCLASS +DONTHARMSPECIES +MTHRUSPECIES }