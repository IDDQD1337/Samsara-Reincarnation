actor Totenkopf_BlankPuff
{
    +CLIENTSIDEONLY
	+NOEXTREMEDEATH
	-BLOODSPLATTER
	+BLOODLESSIMPACT
	+NODAMAGETHRUST
    States
    {
      Spawn:
        TNT1 A 1
        Stop
    }
}

Actor Totenkopf_BloodGenerator : CustomInventory
{
	-COUNTITEM
	+INVENTORY.ALWAYSPICKUP
	Inventory.PickupMessage ""
	Inventory.PickupSound ""
	States
	{
		Pickup:
			TNT1 A 0
			TNT1 A 0 A_SpawnItemEx("Totenkopf_Blood",radius*cos(CallACS("Totenkopf_BloodGenerator_Return",1)),radius*sin(CallACS("Totenkopf_BloodGenerator_Return",1)),CallACS("Totenkopf_BloodGenerator_Return",2),0,0,0,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR|SXF_ABSOLUTEPOSITION)
			TNT1 A 0
			stop
	}
}

Actor Totenkopf_Blood
{
	+NOINTERACTION
	+NOGRAVITY
	+CLIENTSIDEONLY
	States
	{
		Spawn:
			TNT1 A 0
			TK50 ABC 4
			Stop
	}
}

Actor Totenkopf_Blood_Mutant : Totenkopf_Blood
{
	States
	{
		Spawn:
			TNT1 A 0
			TK56 ABC 4
			Stop
	}
}

Actor Totenkopf_BloodGeneratorAngle : Inventory { Inventory.Maxamount 131072 }
Actor Totenkopf_BloodGeneratorZ : Inventory { Inventory.Maxamount 131072 }

Actor Totenkopf_Tracer : FastProjectile
{
	Height 4
	Radius 2
	Speed 512
	Projectile
	DamageType "TotenkopfBullet"
	+BLOODLESSIMPACT
	+HITTRACER
	+NOEXTREMEDEATH
	+NODAMAGETHRUST
	States
	{
		Spawn:
			TNT1 A 1
			Loop
		Crash:
		Death:
			TNT1 A 1
			TNT1 A 0 A_Playsound("Totenkopf/Ricochet", CHAN_BODY)
			TK51 BCDE 2 Bright
			Stop
		XDeath:
			TNT1 A 0 A_JumpIf(CallACS("Totenkopf_BloodGenerator_Offsets")==1,1)
			TNT1 A 1 Bright
			TNT1 A 0 A_GiveInventory("Totenkopf_BloodGenerator",1,AAPTR_TRACER)
			TNT1 A 1
			Stop
	}
}

Actor Totenkopf_Tracer_Trail
{
	+NOINTERACTION
	+CLIENTSIDEONLY
	States
	{
		Spawn:
			TK51 AA 1 Bright
			Stop
	}
}

Actor BackpackOriginal : Backpack {}

Actor Totenkopf_BackpackPickup
{
	States
	{
		Spawn:
			TNT1 AA 0 A_SpawnItemEx("BackpackOriginal",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
			TNT1 A 0 A_SpawnItemEx("Totenkopf_PistolPickup",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
			TNT1 A 1
			Stop
	}
}

Actor Totenkopf_PistolPickup : CustomInventory
{
	+INVENTORY.AUTOACTIVATE
	Inventory.PickUpSound "Totenkopf/getweapon"
	States
	{
		Spawn:
			TK02 M 4
			Loop
		Pickup:
			TNT1 A 0 A_JumpIfInventory("Totenkopf_PistolDual",1,1)
			Goto Pickup+3
			TNT1 A 0 A_JumpIfInventory("Totenkopf_Pistol",1,1)
			Goto Pickup+3
			TNT1 A 0 A_JumpIfInventory("Clip",0,"PickupFailure")
			Goto Pickup+3
			TNT1 A 0 A_JumpIfInventory("Totenkopf_Pistol",1,"PickupDual")
			TNT1 A 0 A_Log("Walther P38 Pistol")
			TNT1 A 0 A_GiveInventory("Totenkopf_Pistol",1)
			Stop
		PickupDual:
			TNT1 A 0 A_Log("Walther P38 Pistol")
			TNT1 A 0 A_GiveInventory("Totenkopf_PistolDual",1)
			Stop
		PickupFailure:
			TNT1 A 0 A_Railwait
			fail
	}
}

Actor Totenkopf_BurningGuard
{
	+ISMONSTER
	+NODAMAGE
	-COUNTKILL
	-SHOOTABLE
	-SOLID
	Height 52
	Radius 16
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_JumpIf(CallACS("Totenkopf_BurningVoice")==1,1)
			TK13 ABCDABCDABCDABCD 5 Bright
			TK13 EFG 5 Bright
			TK13 H -1
			Stop
	}
}

Actor TotenkopfPowerArmor : CustomInventory
{
	Radius 20
	Height 16
	Inventory.PickupMessage "Power Armor: your armor now regenerates!"
	Inventory.PickupSound "Totenkopf/Bonus"
	Inventory.Icon "TK55A0"
	+INVENTORY.ALWAYSPICKUP
	States
	{
		Spawn:
			TK55 A 6 Bright
			Loop
		Pickup:
			TNT1 A 0 A_GiveInventory("TotenkopfPowerArmorPickup",1)
			TNT1 A 0 A_GiveInventory("TotenkopfHasPowerArmor",1)
			stop
	}
}

Actor TotenkopfPowerArmorPickup : BasicArmorPickup
{
	Radius 20
	Height 16
	Inventory.PickupSound "Totenkopf/Bonus"
	Inventory.PickupMessage "Power Armor: your armor now regenerates!"
	Inventory.Icon "TK55A0"
	Armor.SavePercent 50
	Armor.SaveAmount 200
	+INVENTORY.KEEPDEPLETED
	States
	{
		Spawn:
			TK55 A 6 Bright
			Loop
	}
}

Actor TotenkopfPowerArmorKeeper : BasicArmorBonus
{
	Radius 20
	Height 16
	Inventory.Icon "TK55A0"
	Armor.SavePercent 50
	Armor.SaveAmount 1
	Armor.MaxSaveAmount 200
	+INVENTORY.ALWAYSPICKUP
	States
	{
		Spawn:
			TNT1 A 35
			Loop
	}
}

Actor TotenkopfHealingOrb : CustomInventory
{
	Radius 20
	Height 16
	Inventory.PickupMessage "Healing Orb: your health now regenerates!"
	Inventory.PickupSound "Totenkopf/Bonus"
	Inventory.Icon "TK55B0"
	+INVENTORY.ALWAYSPICKUP
	States
	{
		Spawn:
			TK55 A 6 Bright
			Loop
		Pickup:
			TNT1 A 0 A_GiveInventory("TotenkopfHealingOrbHealer",200)
			TNT1 A 0 A_GiveInventory("TotenkopfHasHealingOrb",1)
			stop
	}
}

ACTOR TotenkopfHealingOrbHealer : Health
{
	+COUNTITEM
	+INVENTORY.ALWAYSPICKUP
	Inventory.Amount 1
	Inventory.MaxAmount 200
	Inventory.PickupMessage ""
	States
	{
		Spawn:
			TNT1 A 1
			Loop
	}
}

Actor TotenkopfHasPowerArmor : Inventory { Inventory.Maxamount 1 }
Actor TotenkopfHasHealingOrb : Inventory { Inventory.Maxamount 1 }