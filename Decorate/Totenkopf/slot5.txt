actor Totenkopf_Panzerschreck : InstantSwitchWeapon
{
    Tag "Panzerschreck"
    Weapon.SelectionOrder 975
    Weapon.SlotNumber 5
    Inventory.PickUpSound "Totenkopf/getweapon"
    Weapon.AmmoUse 1
    Weapon.AmmoGive 2
    Weapon.AmmoType "RocketAmmo"
    +THRUGHOST
    +DONTBOB
    +INVENTORY.UNDROPPABLE
    Inventory.PickupMessage "Panzerschreck"
    Obituary ""
    Weapon.YAdjust 16
    States
    {
      Spawn:
		TK07 H 4
        Loop

      Ready:
        TK07 A 1 A_WeaponReady
        loop

      Fire:
		TK07 B 0 A_JumpIfInventory("CoopModeOn", 1, "CoopFire")
        TK07 B 3
        TK07 C 0 A_PlaySound("Totenkopf/missilefire",CHAN_WEAPON)
        TK07 C 3 Bright A_FireCustomMissile("Totenkopf_Panzerschreck_Projectile",0,1,0,8)
		TK07 DE 3
		TK07 F 32
		TK07 A 3
        Goto Ready
	  CoopFire:
        TK07 B 3
        TK07 C 0 A_PlaySound("Totenkopf/missilefire",CHAN_WEAPON)
        TK07 C 3 Bright A_FireCustomMissile("Totenkopf_Panzerschreck_ProjectileCoop",0,1,0,8)
		TK07 DE 3
		TK07 F 32
		TK07 A 3
        Goto Ready	  
    }
}

Actor Totenkopf_Panzerschreck_Projectile
{
	Speed 16
	Damage (0)
	Height 16
	Radius 8
	Projectile
	DamageType "TotenkopfRocket"
	+BLOODLESSIMPACT
	+NODAMAGETHRUST
	//+EXTREMEDEATH
	+FORCERADIUSDMG
	States
	{
		Spawn:
			TK53 A 1 Bright
			TK53 A 0 A_SpawnItemEx("Totenkopf_Panzerschreck_Smoke",-16,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
			Loop
		Death:
			TNT1 A 0 A_Explode(400,66)
			TNT1 A 0 A_Playsound("Totenkopf/missilehit", CHAN_6)
			TK53 FGH 6 Bright
			Stop
	}
} 

Actor Totenkopf_Panzerschreck_ProjectileCoop : Totenkopf_Panzerschreck_Projectile { Species "Player" +THRUSPECIES +DONTHARMCLASS +DONTHARMSPECIES +MTHRUSPECIES }

Actor Totenkopf_Panzerschreck_Smoke
{
	+NOINTERACTION
	+CLIENTSIDEONLY
	States
	{
		Spawn:
			TK53 A 0
			TK53 B 6
			TK53 CDE 3
			Stop
	}
}
