Actor Q3BFG10K : Q3Weapon
{
	Weapon.AmmoUse 2
	Weapon.AmmoGive 50
	Weapon.SlotNumber 7
	Weapon.AmmoType "Cell"
	Inventory.Pickupmessage "BFG10K"
	Obituary "%o was blasted by %k's BFG"
	Tag "BFG-10K"
	+WEAPON.BFG
	+WEAPON.EXPLOSIVE
	+FLOATBOB
	States
	{
		Spawn:
			Q399 ABCDEFGH 2 Bright
			Loop
		Deselect:
			Q301 A 0 A_StopSound(CHAN_5)
			Q301 A 0 A_GunFlash("DeselectGlow",GFF_NOEXTCHANGE)
			Q301 A 0 A_PlaySound("Quake3/Weapons/Change",CHAN_5)
			Q301 ABCDE 1
			Goto DeselectLoop
		DeselectGlow:
			Q306 ABCDE 1 Bright
			Stop
		Ready:
			Q302 A 0 A_PlaySound("Quake3/BFG/Idle",CHAN_5,0.75,true)
			Q302 A 0 A_GunFlash("SelectGlow",GFF_NOEXTCHANGE)
			Q302 ABCD 1
		ReadyLoop:
			Q300 A 0 A_GunFlash("ReadyGlow",GFF_NOEXTCHANGE)
			Q300 AAAAAAAAAAAAAAAAAAAAAAAA 1 A_WeaponReady
			Loop
		SelectGlow:
			Q307 ABCD 1 Bright
			Stop
		ReadyGlow:
			Q305 ABCDEFGH 3 Bright
			Stop
		Fire:
			Q310 A 0 A_GunFlash
			Q310 A 0 A_PlaySound("Quake3/BFG/Fire",CHAN_WEAPON)
			Q310 A 0 A_JumpIfInventory("CoopModeOn",1,2)
			Q310 A 0 A_FireCustomMissile("Q3BFGProjectile",0,1,6)
			Goto FireContinue
			Q310 A 0 A_FireCustomMissile("Q3BFGProjectileCoop",0,1,6)
			Goto FireContinue
		FireContinue:
			Q310 ACF 2
			Q310 A 0 A_Refire("Fire")
			Goto FireFinish
		FireFinish:
			Q303 A 0 A_GunFlash("FinishGlow",GFF_NOEXTCHANGE)
			Q303 "ADGJMPSVY\" 1
			Q304 "BEH" 2 A_WeaponReady
			Q303 "DGJMPSVY\" 2 A_WeaponReady
			Q304 "BEHK" 3 A_WeaponReady
			Goto ReadyLoop
		FinishGlow:
			Q305 ABCDEFGH 3 Bright
			Loop
		Flash:
			Q309 A 0 A_GiveInventory("Q3QuadDamageSound",1)
			Q309 AB 2 Bright
			Q309 C 2 Bright
			Stop	
	}
}

Actor Q3BFGProjectile
{
	Height 12
	Radius 6
	Damage (100)
	Speed 32
	Scale 0.33
	Renderstyle Add
	DamageType "Q3BFG"
	Decal "Q3ExplosiveImpact"
	Projectile
	+FORCEXYBILLBOARD
	+FORCERADIUSDMG
	+HITTRACER
	States
	{
		Spawn:
			Q307 A 0 NoDelay A_Playsound("Quake3/RocketLauncher/Fly",CHAN_5,1.0,true)
		SpawnLoop:
			Q307 A 2 Bright A_SpawnItemEx("Q3BFGRing")
			Loop
		XDeath:
			TNT1 A 0 A_StopSound(CHAN_5)
			TNT1 A 0 ACS_NamedExecuteWithResult("Q3_BloodGenerator_Offsets")
			TNT1 A 0 A_GiveInventory("Q3_BloodGenerator",1,AAPTR_TRACER)
		Death:
			TNT1 A 0 A_PlaySound("Quake3/RocketLauncher/Explode",CHAN_BODY)
			TNT1 A 0 A_Explode(100,100)
			TNT1 A 0 A_StopSound(CHAN_5)
			TNT1 A 0 A_SpawnItemEx("Q3BFGBoom")
			TNT1 A 0 A_JumpIf(abs(z - floorz) <= 4,"HitFloor")
			TNT1 A 0 A_CheckCeiling("HitCeiling")
			TNT1 A 1
			Stop
		HitFloor:
			TNT1 A 0 ACS_NamedExecuteWithResult("Q3SpawnDecal",1)
			TNT1 A 0 A_SetPitch(90)
			TNT1 A 0 A_JumpIfCloser(32,"Thrust")
			TNT1 A 1
			Stop
		HitCeiling:
			TNT1 A 0 ACS_NamedExecuteWithResult("Q3SpawnDecal",1,1)
			TNT1 A 0 A_SetPitch(-90)
			TNT1 A 1
			Stop
		Thrust:
			TNT1 A 1 A_GiveToTarget("Q3BFGJumpModifier",1)
			Stop
	}
}

Actor Q3BFGProjectileCoop : Q3BFGProjectile { +THRUSPECIES Species "Player" }

Actor Q3BFGBoom
{
	RenderStyle Add
	Height 4
	Radius 2
	+NOGRAVITY
	+NOINTERACTION
	+CLIENTSIDEONLY
	+FORCEXYBILLBOARD
	States
	{
		Spawn:
			Q307 A 0 NoDelay ACS_NamedExecuteWithResult("Q3EffectLifeCycle",0,50,0,9)
			Q307 BCD 3 Bright
			Q307 D 100 Bright
			Stop
	}
}

Actor Q3BFGRing
{
	RenderStyle Add
	Height 4
	Radius 2
	Scale 0.125
	+NOGRAVITY
	+NOINTERACTION
	+CLIENTSIDEONLY
	+FORCEXYBILLBOARD
	States
	{
		Spawn:
			Q307 E 0 NoDelay ACS_NamedExecuteWithResult("Q3EffectLifeCycle",0,50,-75,6)
			Q307 E 100 Bright
			Stop
	}
}

Actor Q3BFGJumpModifier : CustomInventory
{
	Inventory.MaxAmount 0
	+AUTOACTIVATE
	States
	{
		Pickup:
			TNT1 A 0 ThrustThingZ(0,28,0,1)
			Stop
	}
}
