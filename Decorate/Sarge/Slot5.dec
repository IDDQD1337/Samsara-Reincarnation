Actor Q3RocketLauncher : Q3Weapon
{
	Weapon.AmmoUse 1
	Weapon.AmmoGive 5
	Weapon.SlotNumber 5
	Weapon.AmmoType "RocketAmmo"
	Inventory.Pickupmessage "Rocket Launcher"
	Obituary "%o ate %k's rocket"
	Tag "Rocket Launcher"
	+FLOATBOB
	States
	{
		Spawn:
			Q399 A 1
			Loop
		Deselect:
			Q301 A 0 A_PlaySound("Quake3/Weapons/Change",CHAN_5)
			Q301 ABCDE 1
			Goto DeselectLoop
		Ready:
			Q302 ABCD 1
		ReadyLoop:
			Q300 A 1 A_WeaponReady
			Loop
		Fire:
			Q303 A 0 A_GunFlash
			Q303 A 0 A_JumpIfInventory("CoopModeOn",1,2)
			Q303 A 0 A_FireCustomMissile("Q3Rocket",0,1,4,8)
			Goto FireContinue
			Q303 A 0 A_FireCustomMissile("Q3RocketCoop",0,1,4,8)
			Goto FireContinue
		FireContinue:
			Q303 A 0 A_PlaySound("Quake3/RocketLauncher/Fire",CHAN_WEAPON)
			Q303 ABCDEF 3
		FireFinish:
			Q303 F 10 A_WeaponReady(WRF_NOFIRE)
			Goto ReadyLoop
		Flash:
			Q304 A 0 A_GiveInventory("Q3QuadDamageSound",1)
			Q304 A 3 Bright
			Stop
	}
}

Actor Q3Rocket
{
	Height 12
	Radius 6
	Speed 24
	Damage (100)
	Projectile
	Decal "Q3ExplosiveImpact"
	DamageType "Q3Explosive"
	+FORCERADIUSDMG
	States
	{
		Spawn:
			Q300 A 0 NoDelay ACS_NamedExecuteWithResult("Q3SpawnRocketFlare")
			Q300 A 0 A_Playsound("Quake3/RocketLauncher/Fly",CHAN_5,1.0,true)
		SpawnLoop:
			Q300 ABCDEFGHIJKLMNOPQRSTUVWX 2 A_SpawnItemEx("Q3GrenadeSmoke")
			Loop
		Death:
			TNT1 A 0 A_StopSound(CHAN_5)
			TNT1 A 0 A_PlaySound("Quake3/RocketLauncher/Explode",CHAN_BODY)
			TNT1 A 0 A_Explode(100,100)
			TNT1 A 0 A_SpawnItemEx("Q3RocketBoom")
			TNT1 A 0 A_JumpIf(abs(z - floorz) <= 4,"HitFloor")
			TNT1 A 0 A_CheckCeiling("HitCeiling")
			Stop
		HitFloor:
			TNT1 A 0 ACS_NamedExecuteWithResult("Q3SpawnDecal",1)
			TNT1 A 0 A_SetPitch(90)
			TNT1 A 0 A_JumpIfCloser(32,"Thrust")
			TNT1 A 1
			Stop
		Thrust:
			TNT1 A 1 A_GiveToTarget("Q3RocketJumpModifier",1)
			Stop
		HitCeiling:
			TNT1 A 0 ACS_NamedExecuteWithResult("Q3SpawnDecal",1,1)
			TNT1 A 0 A_SetPitch(-90)
			TNT1 A 1
			Stop
	}
}

Actor Q3RocketCoop : Q3Rocket { +THRUSPECIES Species "Player" }

Actor Q3RocketJumpModifier : CustomInventory
{
	Inventory.MaxAmount 0
    +AUTOACTIVATE
	States
    {
		Pickup:
			TNT1 A 0 ThrustThingZ(0,20,0,1)
			Stop
    }
}

Actor Q3RocketFlare
{
	Height 10
	Radius 5
	Renderstyle Add
	+NOINTERACTION
	States
	{
		Spawn:
			Q300 ABCDEFGHIJKLMNOPQRSTUVWX 2 Bright
			Loop
	}
}

Actor Q3RocketBoom
{
	RenderStyle Add
	Height 4
	Radius 2
	+NOGRAVITY
	+NOINTERACTION
	+CLIENTSIDEONLY
	+FORCEXYBILLBOARD
	States
	{
		Spawn:
			Q304 A 0 NoDelay ACS_NamedExecuteWithResult("Q3EffectLifeCycle",0,50,0,16)
			Q304 ABCDEFGH 2 Bright
			Q304 H 100 Bright
			Stop
	}
}