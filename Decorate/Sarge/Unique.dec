Actor Q3Railgun : Q3Weapon
{
	Weapon.AmmoUse 1
	Weapon.AmmoGive 5
	Weapon.SlotNumber 9
	Weapon.AmmoType "Q3RailgunAmmo"
	Inventory.Pickupmessage "Railgun"
	Obituary "%o was railed by %k"
	Tag "Rail Gun"
	+FLOATBOB
	States
	{
		Spawn:
			Q399 A 1
			Loop
		Deselect:
			Q301 A 0 A_StopSound(CHAN_5)
			Q301 A 0 A_GunFlash("DeselectGlow",GFF_NOEXTCHANGE)
			Q301 A 0 A_PlaySound("Quake3/Weapons/Change",CHAN_5)
			Q301 ABCDE 1
			Goto DeselectLoop
		DeselectGlow:
			Q306 ABCDE 1 Bright
			Stop
		Ready:
			Q302 A 0 A_PlaySound("Quake3/Railgun/Idle",CHAN_5,0.5,true)
			Q302 A 0 A_GunFlash("SelectGlow",GFF_NOEXTCHANGE)
			Q302 ABCD 1
		ReadyLoop:
			Q300 A 0 A_GunFlash("ReadyGlow",GFF_NOEXTCHANGE)
			Q300 A 1 A_WeaponReady
			Loop
		SelectGlow:
			Q307 ABCD 1 Bright
			Stop
		ReadyGlow:
			Q305 A 1 Bright
			Stop
		Fire:
			Q303 A 0 A_GunFlash
			Q303 A 0 A_PlaySound("Quake3/Railgun/Fire",CHAN_WEAPON)
			Q303 A 0 A_JumpIfInventory("CoopModeOn",1,"FireCoop")
			Q303 A 0 A_FireCustomMissile("Q3RailGunProjectile",0,0,6)
			Q303 A 0 A_RailAttack(100,6,1,None,None,RGF_SILENT,0,"Q3RailGunPuff",0,0,8192)
			Goto FireContinue
		FireCoop:
			Q303 A 0 A_FireCustomMissile("Q3RailGunProjectileCoop",0,0,6)
			Q303 A 0 A_RailAttack(100,6,1,None,None,RGF_SILENT,0,"Q3RailGunPuffCoop",0,0,8192)
			Goto FireContinue
		FireContinue:
			Q303 A 6 Bright
			Q303 BCDEF 6
			Q300 A 16
			Goto ReadyLoop
		Flash:
			Q304 A 0 A_GiveInventory("Q3QuadDamageSound",1)
			Q304 A 3 Bright
			Q308 A 3 Bright
			Q308 BCDEF 6 Bright
			Q305 A 16 Bright
			Stop		
	}
}

Actor Q3RailGunPuff
{
	Height 8
	Radius 4
	DamageType "Q3Energy"
	+BLOODLESSIMPACT
	States
	{
		Spawn:
			TNT1 A 1
			Stop
	}
}

Actor Q3RailGunPuffCoop : Q3RailGunPuff { Species "Player" +THRUSPECIES +MTHRUSPECIES }

Actor Q3RailGunProjectile : FastProjectile
{
	Height 2
	Radius 1
	Damage (0)
	Speed 2048
	Decal "Q3LightningImpact"
	+SKYEXPLODE
	+THRUACTORS
	+BLOODLESSIMPACT
	+HITTRACER
	States
	{
		Spawn:
			TNT1 A 1
			Loop
		Death:
			TNT1 A 0
			TNT1 A 0 A_JumpIf(abs(z - floorz) <= 4,"HitFloor")
			TNT1 A 0 A_CheckCeiling("HitCeiling")
			Goto DrawBeam
		XDeath:
			TNT1 A 0 ACS_NamedExecuteWithResult("Q3_BloodGenerator_Offsets")
			TNT1 A 0 A_GiveInventory("Q3_BloodGenerator",1,AAPTR_TRACER)
			Goto DrawBeam
		HitFloor:
			TNT1 A 0 ACS_NamedExecuteWithResult("Q3SpawnDecal",2)
			TNT1 A 0 A_SetPitch(90)
			Goto DrawBeam
		HitCeiling:
			TNT1 A 0 ACS_NamedExecuteWithResult("Q3SpawnDecal",2,1)
			TNT1 A 0 A_SetPitch(-90)
			Goto DrawBeam
		DrawBeam:
			TNT1 A 0 A_FaceTarget
			TNT1 A 0 A_SetAngle(angle+180)
			TNT1 A 0 A_SpawnItemEx("Q3RailGunImpactShock",-4)
			TNT1 A 0 ACS_NamedExecuteWithResult("Q3DrawLaser",1)
			TNT1 A 1
			Stop
	}
}

Actor Q3RailGunProjectileCoop : Q3RailGunProjectile { +THRUSPECIES Species "Player" }

Actor Q3RailGunSegment
{
	+NOINTERACTION
	+NOGRAVITY
	Height 4
	Radius 2
	Renderstyle Add
	States
	{
		Spawn:
			Q300 A 3 Bright
			Stop
	}
}

Actor Q3RailGunImpactShock
{
	+NOINTERACTION
	+NOGRAVITY
	+FORCEXYBILLBOARD
	Height 4
	Radius 2
	Scale 2.0
	States
	{
		Spawn:
			Q305 ABCD 5
			Stop
	}
}

ACTOR Q3RailgunAmmo : Ammo
{
	Inventory.Amount 1
	Inventory.MaxAmount 50
	Ammo.BackpackAmount 1
	Ammo.BackpackMaxAmount 100
	Inventory.Icon "AMSMP0"
}

Actor Q3ProximityLauncher : Q3Weapon
{
	Weapon.AmmoUse 1
	Weapon.AmmoGive 10
	Weapon.SlotNumber 9
	Weapon.AmmoType "Q3GrenadeLauncherAmmo"
	Inventory.Pickupmessage "Proximity Launcher"
	Obituary "%o was too close to %k's Prox Mine"
	Tag "Prox Launcher"
	+WEAPON.NOALERT
	+FLOATBOB
	States
	{
		Spawn:
			Q399 A 1
			Loop
		Deselect:
			Q301 A 0 A_PlaySound("Quake3/Weapons/Change",CHAN_5)
			Q301 ABCDE 1
			Goto DeselectLoop
		Ready:
			Q302 ABCD 1
		ReadyLoop:
			Q300 ABCDEFGHIJKLMNOP 3 A_WeaponReady
			Loop
		Fire:
			Q303 A 0 A_GunFlash
			Q303 A 0 A_JumpIfInventory("CoopModeOn",1,2)
			Q303 A 0 A_FireCustomMissile("Q3ProximityMine",0,1,4,8)
			Goto FireContinue
			Q303 A 0 A_FireCustomMissile("Q3ProximityMineCoop",0,1,4,8)
			Goto FireContinue
		FireContinue:
			Q303 A 0 A_PlaySound("Quake3/ProximityLauncher/Fire",CHAN_WEAPON)
			Q303 ABCDEF 3
		FireFinish:
			Q303 GHI 3 A_WeaponReady(WRF_NOFIRE)
			Goto ReadyLoop
		Flash:
			Q304 A 0 A_GiveInventory("Q3QuadDamageSound",1)
			Q304 A 3 Bright
			Stop
	}
}

Actor Q3ProximityMine
{
	Height 12
	Radius 6
	Speed 24
	Damage (100)
	Projectile
	DamageType "Q3Bullet"
	-NOGRAVITY
	+HITTRACER
	+FORCERADIUSDMG
	var int user_startingAngle;
	States
	{
		Spawn:
			Q300 A 0
			Q300 A 0 A_SetUserVar("user_startingAngle",angle)
			Q300 A 0 A_SetPitch(random(0,359))
			Q300 A 0 ThrustThingZ(0,16,0,1)
		SpawnLoop:
			Q300 A 0 A_SpawnItemEx("Q3GrenadeSmoke")
			Q300 A 0 A_SetPitch(pitch+frandom(5.0,10.0))
			Q300 A 2 A_SetAngle(angle+frandom(5.0,10.0))
			Loop
		XDeath:
			TNT1 A 0 A_PlaySound("Quake3/ProximityLauncher/HitFlesh",CHAN_5)
			TNT1 A 0 A_PlaySound("Quake3/ProximityLauncher/Activate",CHAN_BODY)
			Goto Remove
		Death:
			TNT1 A 0 A_PlaySound("Quake3/ProximityLauncher/HitWall",CHAN_5)
			TNT1 A 0 A_JumpIf(abs(z - floorz) <= 4,"HitFloor")
			TNT1 A 0 A_CheckCeiling("HitCeiling")
			TNT1 A 0 A_NoGravity
			TNT1 A 0 A_SetPitch(90)
			TNT1 A 0 A_SetAngle(user_startingAngle)
			TNT1 A 0 ACS_NamedExecuteWithResult("Q3ProximityMineGlow")
			Q300 C 0
			Goto MineWait
		Crash:
			TNT1 A 0 A_PlaySound("Quake3/ProximityLauncher/HitMetal",CHAN_5)
			Goto Death+1
		HitFloor:
			TNT1 A 0 A_SetPitch(0)
			TNT1 A 0 ACS_NamedExecuteWithResult("Q3ProximityMineGlow",1)
			TNT1 A 0 A_Gravity
			Q300 A 0
			Goto MineWait
		HitCeiling:
			TNT1 A 0 A_NoGravity
			TNT1 A 0 ThrustThingZ(0,128,0,1)
			TNT1 A 0 A_SetPitch(180)
			TNT1 A 0 ACS_NamedExecuteWithResult("Q3ProximityMineGlow",2)
			Q300 B 0
			Goto MineWait
		MineWait:
			Q300 "#" 0 A_ChangeFlag("MOVEWITHSECTOR",1)
			Q300 "#" 0 A_JumpIfInventory("Q3MineRemoval",1,"Remove")
			Q300 "#" 1
			Loop
		Remove:
			TNT1 A 0 A_SetDamageType("Q3Explosive")
			TNT1 A 0 A_PlaySound("Quake3/RocketLauncher/Explode",CHAN_BODY)
			TNT1 A 0 A_Explode(150,150,0,1)
			TNT1 A 0 A_SpawnItemEx("Q3GrenadeBoom")
			TNT1 A 1
			Stop
	}
}

Actor Q3ProximityMineCoop : Q3ProximityMine { +THRUSPECIES Species "Player" }

Actor Q3ProximityMineGlow
{
	+ISMONSTER
	-COUNTKILL
	+FRIENDLY
	+NOGRAVITY
	+LOOKALLAROUND
	ReactionTime 525
	Renderstyle Add
	Height 4
	Radius 2
	States
	{
		Spawn:
			Q300 A 0 NoDelay A_TransferPointer(AAPTR_Tracer,AAPTR_Default,AAPTR_Target,AAPTR_Master)
			Q300 A 0 A_CopyFriendliness(AAPTR_Master)
		SpawnLoop:
			Q300 A 1 Bright A_LookEx(LOF_NOSOUNDCHECK,0,64)
			Q300 A 0 A_CountDown
			Q300 A 0 A_JumpIfInventory("Q3MineTicker",0,2)
			Q300 A 0 A_GiveInventory("Q3MineTicker",1)
			Loop
			Q300 A 0 A_PlaySound("Quake3/ProximityLauncher/Tick",CHAN_6)
			Q300 A 0 A_TakeInventory("Q3MineTicker",35)
			Loop
		See:
		Death:
			TNT1 A 0 A_PlaySound("Quake3/ProximityLauncher/Activate",CHAN_BODY)
			Q300 A 17 Bright
			TNT1 A 0 A_GiveInventory("Q3MineRemoval",1,AAPTR_TRACER)
			TNT1 A 1
			Stop
	}
}

Actor Q3ProximityMineGlowFloor : Q3ProximityMineGlow {}
Actor Q3ProximityMineGlowCeiling : Q3ProximityMineGlow {}

Actor Q3MineRemoval : Q3Boolean {}
Actor Q3MineTicker : Q3Counter { Inventory.MaxAmount 35 }

Actor Q3Chaingun : Q3Weapon
{
	Weapon.AmmoUse 1
	Weapon.AmmoGive 60
	Weapon.SlotNumber 8
	Weapon.AmmoType "Q3ChaingunAmmo"
	Inventory.Pickupmessage "Chaingun"
	Obituary "%o got lead poisoning from %k's Chaingun"
	Tag "Chaingun"
	+FLOATBOB
	States
	{
		Spawn:
			Q399 A 1
			Loop
		Deselect:
			Q301 A 0 A_StopSound(CHAN_5)
			Q301 A 0 A_PlaySound("Quake3/Weapons/Change",CHAN_5)
			Q301 ABCDE 1
			Goto DeselectLoop
		Ready:
			Q302 ABCD 1
		ReadyLoop:
			Q300 A 0 A_TakeInventory("Q3ChainGunConsumeAmmo",1)
			Q300 A 0 A_StopSound(CHAN_5)
			Q300 A 1 A_WeaponReady
			Loop
		Fire:
			Q303 A 0 A_PlaySound("Quake3/Chaingun/Loop",CHAN_5,0.75,true)
			Q303 A 1 A_GunFlash		Q303 D 0 A_Refire(1)	Goto FireFinish
			Q303 D 1 A_GunFlash		Q303 G 0 A_Refire(1)	Goto FireFinish
			Q303 G 1 A_GunFlash		Q303 J 0 A_Refire(1)	Goto FireFinish
			Q303 J 1 A_GunFlash		Q303 M 0 A_Refire(1)	Goto FireFinish
			Q303 M 1 A_GunFlash		Q303 P 0 A_Refire(1)	Goto FireFinish
			Q303 P 1 A_GunFlash		Q303 S 0 A_Refire(1)	Goto FireFinish
			Q303 S 1 A_GunFlash		Q303 V 0 A_Refire(1)	Goto FireFinish
			Q303 V 1 A_GunFlash		Q303 Y 0 A_Refire(1)	Goto FireFinish
			Q303 Y 1 A_GunFlash		Q303 "\" 0 A_Refire(1)	Goto FireFinish
			Q303 "\" 1 A_GunFlash	Q304 B 0 A_Refire(1)	Goto FireFinish
			Q304 B 1 A_GunFlash		Q304 E 0 A_Refire(1)	Goto FireFinish
			Q304 E 1 A_GunFlash		Q304 H 0 A_Refire(1)	Goto FireFinish
			Q304 H 1 A_GunFlash		Q303 A 0 A_Refire("Fire")
			Goto FireFinish
		FireFinish:
			Q303 A 0 A_PlaySound("Quake3/Chaingun/End",CHAN_5,0.75)
			Q303 "ADGJMPSVY\" 1
			Q304 "BEH" 2 A_WeaponReady
			Q303 "DGJMPSVY\" 2 A_WeaponReady
			Q304 "BEHK" 3 A_WeaponReady
			Goto ReadyLoop
		Flash:
			Q305 A 0 A_GiveInventory("Q3QuadDamageSound",1)
			Q305 A 0 A_SpawnItemEx("Q3MachineGunBrass",8,4,49,0,0.75+frandom(0.0,0.5),frandom(1.0,2.0),frandom(-2.0,2.0))
			Q305 A 0 A_JumpIfInventory("CoopModeOn",1,2)
			Q305 A 0 A_FireBullets(3.0,3.0,-1,8,"Q3BulletPuff",FBF_NORANDOM|FBF_NORANDOMPUFFZ)
			Goto FlashContinue
			Q305 A 0 A_FireBullets(3.0,3.0,-1,8,"Q3BulletPuffCoop",FBF_NORANDOM|FBF_NORANDOMPUFFZ)
			Goto FlashContinue
		FlashContinue:
			Q305 A 0 A_JumpIfInventory("Q3ChainGunPlaySound",0,"PlaySound")
			Q305 A 0 A_GiveInventory("Q3ChainGunPlaySound",1)
			Q305 A 0 A_JumpIfInventory("Q3ChainGunConsumeAmmo",1,1)
			Goto Consume
			Q305 A 0 A_TakeInventory("Q3ChainGunConsumeAmmo",1)
			Stop
		Consume:
			Q305 A 0 A_GiveInventory("Q3ChainGunConsumeAmmo",1)
			Q305 A 0 A_TakeInventory("Q3ChaingunAmmo",1,TIF_NOTAKEINFINITE)
			Goto FinishFlash
		PlaySound:
			Q305 A 0 A_TakeInventory("Q3ChainGunPlaySound",256)
			Q305 A 0 A_PlaySound("Quake3/Chaingun/Fire",CHAN_WEAPON)
			Goto FlashContinue
		FinishFlash:
			Q305 A 1 Bright
			Stop 
	}
}

Actor Q3ChainGunConsumeAmmo : Q3Boolean {}
Actor Q3ChainGunPlaySound : Q3Counter { Inventory.MaxAmount 2 }

ACTOR Q3ChaingunAmmo : Ammo
{
	Inventory.Amount 1
	Inventory.MaxAmount 300
	Ammo.BackpackAmount 10
	Ammo.BackpackMaxAmount 600
	Inventory.Icon "AMSMN0"
}