Actor Q3Shotgun : Q3Weapon
{
	Weapon.AmmoUse 1
	Weapon.AmmoGive 5
	Weapon.SlotNumber 2
	Weapon.AmmoType "Shell"
	Inventory.Pickupmessage "Shotgun"
	Obituary "%o was gunned down by %k"
	Tag "Shotgun"
	+FLOATBOB
	States
	{
		Spawn:
			Q399 A 1
			Loop
		Deselect:
			Q301 A 0 A_PlaySound("Quake3/Weapons/Change",CHAN_5)
			Q301 ABCDE 1
			Goto DeselectLoop
		Ready:
			Q302 ABCD 1
		ReadyLoop:
			Q300 A 1 A_WeaponReady
			Loop
		Fire:
			Q303 A 0 A_GunFlash
			Q303 A 0 A_SpawnItemEx("Q3ShotGunSmoke",40,-16,20,0,0,frandom(0.125,0.25))
			Q303 A 0 A_SpawnItemEx("Q3ShotGunSmoke",40,16,20,0,0,frandom(0.125,0.25))
			Q303 A 0 A_SpawnItemEx("Q3ShotgunShell",8,4,49,0,1.5+frandom(0.0,1.2),1.0+frandom(0.0,0.8),frandom(-2.0,2.0))
			Q303 A 0 A_SpawnItemEx("Q3ShotgunShell",8,2,49,0,-(1.5+frandom(0.0,1.2)),1.0+frandom(0.0,0.8),frandom(-2.0,2.0))
			Q303 A 0 A_FireBullets(8.0,8.0,11,10,"Q3ShotgunPuff",FBF_NORANDOM|FBF_NORANDOMPUFFZ|FBF_USEAMMO)
			Q303 A 0 A_PlaySound("Quake3/ShotGun/Fire",CHAN_WEAPON)
			Q303 ABCDEF 6
			Goto ReadyLoop
		Flash:
			Q304 A 0 A_GiveInventory("Q3QuadDamageSound",1)
			Q304 A 3 Bright
			Stop
	}
}

Actor Q3ShotgunPuff : Q3BulletPuff
{
	States
	{
		Spawn:
			Q300 A 0
			Q300 A 0 A_JumpIf(abs(z - floorz) <= 4,"HitFloor")
			Q300 A 0 A_CheckCeiling("HitCeiling")
			Q300 A 0 A_JumpIf(waterlevel == 0, 2)
			Q300 A 0 ACS_NamedExecuteWithResult("Q3DrawLaser",2)
			Q300 A 0 A_FaceTarget
			Q300 ABCDEF 1 Bright
			Stop
		HitFloor:
			TNT1 A 0 ACS_NamedExecuteWithResult("Q3SpawnDecal")
			TNT1 A 0 A_SetPitch(90)
			Goto Spawn+3
		HitCeiling:
			TNT1 A 0 ACS_NamedExecuteWithResult("Q3SpawnDecal",0,1)
			TNT1 A 0 A_SetPitch(-90)
			Goto Spawn+3
	}
}

Actor Q3ShotgunSmoke
{
	+NOINTERACTION
	+NOGRAVITY
	Alpha 0.5
	Height 4
	Radius 2
	States
	{
		Spawn:
			Q302 A 0
			Q302 A 4 A_Fadeout(0.05)
			Wait
	}
}

Actor Q3ShotgunShell : Q3MachineGunBrass {}