Actor Q3GrenadeLauncher : Q3Weapon
{
	Weapon.AmmoUse 1
	Weapon.AmmoGive 5
	Weapon.SlotNumber 1
	Weapon.AmmoType "Q3GrenadeLauncherAmmo"
	Inventory.Pickupmessage "Grenade Launcher"
	Obituary "%o ate %k's grenade"
	Tag "Grenade Launcher"
	+WEAPON.EXPLOSIVE
	+WEAPON.NOALERT
	+FLOATBOB
	States
	{
		Spawn:
			Q399 A 1
			Loop
		Deselect:
			Q301 A 0 A_PlaySound("Quake3/Weapons/Change",CHAN_5)
			Q301 ABCDE 1
			Goto DeselectLoop
		Ready:
			Q302 ABCD 1
		ReadyLoop:
			Q300 A 1 A_WeaponReady
			Loop
		Fire:
			Q303 A 0 A_GunFlash
			Q303 A 0 A_JumpIfInventory("CoopModeOn",1,2)
			Q303 A 0 A_FireCustomMissile("Q3Grenade",0,1,4,8)
			Goto FireContinue
			Q303 A 0 A_FireCustomMissile("Q3GrenadeCoop",0,1,4,8)
			Goto FireContinue
		FireContinue:
			Q303 A 0 A_PlaySound("Quake3/GrenadeLauncher/Fire",CHAN_WEAPON)
			Q303 ABCDEF 3
		FireFinish:
			Q303 F 10 A_WeaponReady(WRF_NOFIRE)
			Goto ReadyLoop
		Flash:
			Q304 A 0 A_GiveInventory("Q3QuadDamageSound",1)
			Q304 A 3 Bright
			Stop
	}
}

Actor Q3Grenade
{
	Height 12
	Radius 6
	Speed 24
	Damage (100)
	Projectile
	BounceType "Doom"
	BounceSound "Quake3/GrenadeLauncher/Bounce"
	Decal "Q3ExplosiveImpact"
	DamageType "Q3Explosive"
	-NOGRAVITY
	+FORCERADIUSDMG
	States
	{
		Spawn:
			Q300 A 0
			Q300 A 0 A_SetAngle(random(-30,30))
			Q300 A 0 A_SetPitch(random(0,359))
			Q300 A 0 ThrustThingZ(0,16,0,1)
		SpawnLoop:
			Q300 A 0 A_SpawnItemEx("Q3GrenadeSmoke")
			Q300 A 0 A_SetPitch(pitch+frandom(5.0,10.0))
			Q300 A 2 A_SetAngle(angle+frandom(5.0,10.0))
			Loop
		Death:
			TNT1 A 0 A_SetDamageType("Q3Explosive")
			TNT1 A 0 A_PlaySound("Quake3/RocketLauncher/Explode",CHAN_BODY)
			TNT1 A 0 A_Explode(100,100,XF_HURTSOURCE,1)
			TNT1 A 0 A_SpawnItemEx("Q3GrenadeBoom")
			TNT1 A 0 A_JumpIf(abs(z - floorz) <= 4,"HitFloor")
			TNT1 A 0 A_CheckCeiling("HitCeiling")
			Stop
		HitFloor:
			TNT1 A 0 ACS_NamedExecuteWithResult("Q3SpawnDecal",1)
			TNT1 A 0 A_SetPitch(90)
			TNT1 A 1
			Stop
		HitCeiling:
			TNT1 A 0 ACS_NamedExecuteWithResult("Q3SpawnDecal",1,1)
			TNT1 A 0 A_SetPitch(-90)
			TNT1 A 1
			Stop
	}
}

Actor Q3GrenadeCoop : Q3Grenade { +THRUSPECIES Species "Player" }

Actor Q3GrenadeBoom
{
	RenderStyle Add
	Height 4
	Radius 2
	Scale 1.0
	+NOGRAVITY
	+NOINTERACTION
	+CLIENTSIDEONLY
	+FORCEXYBILLBOARD
	States
	{
		Spawn:
			Q303 A 0 NoDelay ACS_NamedExecuteWithResult("Q3EffectLifeCycle",0,50,0,18)
			Q303 ABC 6 Bright
			Q303 C 100 Bright
			Stop
	}
}

ACTOR Q3GrenadeLauncherAmmo : Ammo
{
	Inventory.Amount 1
	Inventory.MaxAmount 75
	Ammo.BackpackAmount 2
	Ammo.BackpackMaxAmount 150
	Inventory.Icon "AMSMS0"
}
