Actor Q3GrenadeLauncher : Q3Weapon
{
	Weapon.SlotNumber 1
	Weapon.AmmoUse 1
	Weapon.AmmoGive 5
	Weapon.AmmoType "Q3GrenadeLauncherAmmo"
	Inventory.PickupMessage "Grenade Launcher"
	Obituary "%o ate %k's grenade."
	Tag "Grenade Launcher"
    +AMMO_OPTIONAL // to be obsoleted by q3core
	+EXPLOSIVE
	+FLOATBOB
    +NOALERT // to be obsoleted by q3core
	States
	{
		Spawn:
			Q399 A -1
			stop

		ReadyActual:
			Q300 A 1 A_WeaponReady
			loop

		Deselect:
            Q301 A 0 A_JumpIfHealthLower(1,"DeselectInstant")
			Q301 A 1 A_PlaySound("Quake3/Weapons/Change",CHAN_5)
			Q301 BCDE 1
			goto DeselectInstant

		Ready: // our "Select" state
			Q302 ABCD 1
            goto ReadyActual

		Fire:
            Q303 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart")
            Q303 A 0 A_JumpIfInventory("Q3GrenadeLauncherAmmo",1,"FireStart")
            goto FireDry

        FireDry:
			Q300 A 0 A_PlaySound("Quake3/Weapons/NoAmmo",CHAN_5)
            Q300 A 17 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			Q300 A 0 A_SelectWeapon("Q3Machinegun")
			goto Deselect

        FireStart:
            Q303 A 0 A_AlertMonsters
			Q303 A 0 A_GunFlash
			Q303 A 0 A_GiveInventory("SamsaraQuake3GrenadeLauncherAttackHandler")
			Q303 ABCDE 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			Q303 F 13 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			goto ReadyActual

		Flash:
			Q304 A 3 Bright
			stop
	}
}

actor SamsaraQuake3GrenadeLauncherAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Q3GrenadeLauncherAmmo",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Q3GrenadeLauncherAmmo",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("Quake3/GrenadeLauncher/Fire",CHAN_WEAPON)
        TNT1 A 0 A_GiveInventory("Q3QuadDamageSound")
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("Q3Grenade",0,false,4,8)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("Q3GrenadeCoop",0,false,4,8)
        stop
    }
}

Actor Q3Grenade
{
	Height 12
	Radius 6
	Speed 24
	Damage (100)
	Projectile
	BounceType "Doom"
	BounceSound "Quake3/GrenadeLauncher/Bounce"
	Decal "Q3ExplosiveImpact"
	DamageType "Q3Explosive"
	-NOGRAVITY
	+FORCERADIUSDMG
	States
	{
		Spawn:
			Q300 A 0
			Q300 A 0 A_SetAngle(random(-30,30))
			Q300 A 0 A_SetPitch(random(0,359))
			Q300 A 0 ThrustThingZ(0,16,0,1)
		SpawnLoop:
			Q300 A 0 A_SpawnItemEx("Q3GrenadeSmoke")
			Q300 A 0 A_SetPitch(pitch+frandom(5.0,10.0))
			Q300 A 2 A_SetAngle(angle+frandom(5.0,10.0))
			loop

		Death:
			TNT1 A 0 A_SetDamageType("Q3Explosive")
			TNT1 A 0 A_PlaySound("Quake3/RocketLauncher/Explode",CHAN_VOICE)
			TNT1 A 0 A_Explode(100,100)
			TNT1 A 0 A_SpawnItemEx("Q3GrenadeBoom")
			TNT1 A 0 A_JumpIf(abs(z - floorz) <= 4, "HitFloor")
			TNT1 A 1 A_CheckCeiling("HitCeiling")
			stop

		HitFloor:
			TNT1 A 0 ACS_NamedExecuteWithResult("Q3SpawnDecal", 1)
			TNT1 A 0 A_SetPitch(90)
			TNT1 A 1
			stop

		HitCeiling:
			TNT1 A 0 ACS_NamedExecuteWithResult("Q3SpawnDecal", 1, 1)
			TNT1 A 0 A_SetPitch(-90)
			TNT1 A 1
			stop
	}
}

Actor Q3GrenadeCoop : Q3Grenade { +THRUSPECIES Species "Player" }

Actor Q3GrenadeBoom
{
	RenderStyle Add
	Height 4
	Radius 2
	Scale 1.0
	+NOGRAVITY
	+NOINTERACTION
	+CLIENTSIDEONLY
	+FORCEXYBILLBOARD
	States
	{
		Spawn:
			Q303 A 0 NoDelay ACS_NamedExecuteWithResult("Q3EffectLifeCycle", 0, 50, 0, 18)
			Q303 ABC 6 Bright
			Q303 C 100 Bright
			Stop
	}
}

actor Q3GrenadeLauncherAmmo : Ammo
{
    Inventory.MaxAmount 75
    Ammo.BackpackAmount 2
    Ammo.BackpackMaxAmount 150
    Inventory.Icon "AMSMS0"
    Tag "Grenades"
}
