Actor Q3Railgun : Q3Weapon
{
	Weapon.AmmoUse 1
	Weapon.AmmoGive 10
	Weapon.SlotNumber 9
	Weapon.AmmoType "Shell"
	Inventory.Pickupmessage "Railgun"
	Obituary "%o was railed by %k"
	Tag "Railgun"
	+FLOATBOB
	States
	{
		Spawn:
			Q399 A 1
			Loop
		Deselect:
			Q301 A 0 A_StopSound(CHAN_5)
			Q301 A 0 A_GunFlash("DeselectGlow",GFF_NOEXTCHANGE)
			Q301 A 0 A_PlaySound("Quake3/Weapons/Change",CHAN_5)
			Q301 ABCDE 1
			Goto DeselectLoop
		DeselectGlow:
			Q306 ABCDE 1 Bright
			Stop
		Ready:
			Q302 A 0 A_PlaySound("Quake3/Railgun/Idle",CHAN_5,0.5,true)
			Q302 A 0 A_GunFlash("SelectGlow",GFF_NOEXTCHANGE)
			Q302 ABCD 1
		ReadyLoop:
			Q300 A 0 A_GunFlash("ReadyGlow",GFF_NOEXTCHANGE)
			Q300 A 1 A_WeaponReady
			Loop
		SelectGlow:
			Q307 ABCD 1 Bright
			Stop
		ReadyGlow:
			Q305 A 1 Bright
			Stop
		Fire:
			Q303 A 0 A_GunFlash
			Q303 A 0 A_PlaySound("Quake3/Railgun/Fire",CHAN_WEAPON)
			Q303 A 0 A_FireCustomMissile("Q3RailGunProjectile",0,0,6)
			Q303 A 0 A_RailAttack(100,6,1,"","",RGF_SILENT,0,"Q3RailGunPuff",0,0,8192,0,0,0,"Q3NullRail")
			Q303 A 6 Bright
			Q303 BCDEF 6
			Q300 A 16
			Goto ReadyLoop
		Flash:
			Q304 A 0 A_GiveInventory("Q3QuadDamageSound",1)
			Q304 A 3 Bright
			Q308 A 3 Bright
			Q308 BCDEF 6 Bright
			Q305 A 16 Bright
			Stop		
	}
}

Actor Q3RailGunPuff
{
	Height 8
	Radius 4
	DamageType "Q3Energy"
	+BLOODLESSIMPACT
	States
	{
		Spawn:
			TNT1 A 1
			Stop
	}
}

Actor Q3RailGunProjectile : FastProjectile
{
	Height 2
	Radius 1
	Damage (0)
	Speed 2048
	Decal "Q3LightningImpact"
	+SKYEXPLODE
	+THRUACTORS
	+BLOODLESSIMPACT
	States
	{
		Spawn:
			TNT1 A 1
			Loop
		Death:
			TNT1 A 0
			TNT1 A 0 A_JumpIf(abs(z - floorz) <= 4,"HitFloor")
			TNT1 A 0 A_CheckCeiling("HitCeiling")
			Goto DrawBeam
		XDeath:
			TNT1 A 0 A_SpawnItemEx("Q3BloodSpray",0,0,0,0,0,0,0,SXF_TRANSFERSCALE)
			Goto DrawBeam
		HitFloor:
			TNT1 A 0 ACS_NamedExecuteWithResult("Q3SpawnDecal",2)
			TNT1 A 0 A_SetPitch(90)
			Goto DrawBeam
		HitCeiling:
			TNT1 A 0 ACS_NamedExecuteWithResult("Q3SpawnDecal",2,1)
			TNT1 A 0 A_SetPitch(-90)
			Goto DrawBeam
		DrawBeam:
			TNT1 A 0 A_FaceTarget
			TNT1 A 0 A_SetAngle(angle+180)
			TNT1 A 0 A_SpawnItemEx("Q3RailGunImpactShock",-4)
			TNT1 A 0 ACS_NamedExecuteWithResult("Q3DrawLaser",1)
			TNT1 A 1
			Stop
	}
}

Actor Q3RailGunSegment
{
	+NOINTERACTION
	+NOGRAVITY
	Height 4
	Radius 2
	Renderstyle Add
	States
	{
		Spawn:
			Q300 A 3 Bright
			Stop
	}
}

Actor Q3RailGunImpactShock
{
	+NOINTERACTION
	+NOGRAVITY
	+FORCEXYBILLBOARD
	Height 4
	Radius 2
	Scale 2.0
	States
	{
		Spawn:
			Q305 ABCD 5
			Stop
	}
}