Actor Q3Gauntlet : Q3Weapon
{
	Weapon.SlotNumber 0
	Inventory.Pickupmessage "Gauntlet"
	Obituary "%o was pummeled by %k"
	Tag "Gauntlet"
	+WEAPON.MELEEWEAPON
	+WEAPON.WIMPY_WEAPON
	+WEAPON.NOALERT
	+FLOATBOB
	States
	{
		Spawn:
			Q399 A 1
			Loop
		Deselect:
			Q301 A 0 A_StopSound(CHAN_5)
			Q301 A 0 A_PlaySound("Quake3/Weapons/Change",CHAN_5)
			Q301 ABCDE 1
			Goto DeselectLoop
		Ready:
			Q300 A 0 A_StopSound(CHAN_5)
			Q302 ABCD 1
		ReadyLoop:
			Q300 A 1 A_WeaponReady
			Loop
		Fire:
			Q303 A 0 A_PlaySound("Quake3/Gauntlet/Loop",CHAN_5,1.0,true)
			Q303 A 0 A_GunFlash
			Q303 A 0 A_Saw("","Quake3/Gauntlet/Hit",20,"Q3GauntletPuff",SF_NORANDOM,48)
			Q303 ACEG 1
			Q303 J 0 A_Refire("Fire")
			Goto FireFinish
		FireFinish:
			Q303 A 0 A_StopSound(CHAN_5)
			Q303 "ADGJMPSVY\" 1
			Q304 "BEH" 2 A_WeaponReady
			Q303 "DGJMPSVY\" 2 A_WeaponReady
			Q304 "BEHK" 3 A_WeaponReady
			Goto ReadyLoop
		Flash:
			Q305 A 0 A_GiveInventory("Q3QuadDamageSound",1)
			Q305 AB 2 Bright
			Stop
	}
}

Actor Q3GauntletPuff
{
	+NOINTERACTION
	+NOGRAVITY
	+PUFFONACTORS
	+BLOODLESSIMPACT
	DamageType "Q3Bullet"
	Height 4
	Radius 2
	States
	{
		Spawn:
			TNT1 A 1
			Stop
		XDeath:
			TNT1 A 0 A_SpawnItemEx("Q3BloodSpray",0,0,0,0,0,0,0,SXF_TRANSFERSCALE)
			TNT1 A 1
			Stop
	}
}