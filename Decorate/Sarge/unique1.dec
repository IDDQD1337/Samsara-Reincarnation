Actor Q3Chaingun : Q3Weapon
{
	Weapon.AmmoUse 1
	Weapon.AmmoGive 60
	Weapon.SlotNumber 8
	Weapon.AmmoType "Q3ChaingunAmmo"
	Inventory.Pickupmessage "Chaingun"
	Obituary "%o got lead poisoning from %k's Chaingun"
	Tag "Chaingun"
	+FLOATBOB
	States
	{
		Spawn:
			Q399 A 1
			Loop
		Deselect:
			Q301 A 0 A_StopSound(CHAN_5)
			Q301 A 0 A_PlaySound("Quake3/Weapons/Change",CHAN_5)
			Q301 ABCDE 1
			goto DeselectInstant
		Ready:
			Q302 ABCD 1
		ReadyLoop:
			Q300 A 0 A_TakeInventory("Q3ChainGunConsumeAmmo",1)
			Q300 A 0 A_StopSound(CHAN_5)
			Q300 A 1 A_WeaponReady
			Loop
		Fire:
			Q303 A 0 A_PlaySound("Quake3/Chaingun/Loop",CHAN_5,0.75,true)
			Q303 A 1 A_GunFlash		Q303 D 0 A_Refire(1)	Goto FireFinish
			Q303 D 1 A_GunFlash		Q303 G 0 A_Refire(1)	Goto FireFinish
			Q303 G 1 A_GunFlash		Q303 J 0 A_Refire(1)	Goto FireFinish
			Q303 J 1 A_GunFlash		Q303 M 0 A_Refire(1)	Goto FireFinish
			Q303 M 1 A_GunFlash		Q303 P 0 A_Refire(1)	Goto FireFinish
			Q303 P 1 A_GunFlash		Q303 S 0 A_Refire(1)	Goto FireFinish
			Q303 S 1 A_GunFlash		Q303 V 0 A_Refire(1)	Goto FireFinish
			Q303 V 1 A_GunFlash		Q303 Y 0 A_Refire(1)	Goto FireFinish
			Q303 Y 1 A_GunFlash		Q303 "\" 0 A_Refire(1)	Goto FireFinish
			Q303 "\" 1 A_GunFlash	Q304 B 0 A_Refire(1)	Goto FireFinish
			Q304 B 1 A_GunFlash		Q304 E 0 A_Refire(1)	Goto FireFinish
			Q304 E 1 A_GunFlash		Q304 H 0 A_Refire(1)	Goto FireFinish
			Q304 H 1 A_GunFlash		Q303 A 0 A_Refire("Fire")
			Goto FireFinish
		FireFinish:
			Q303 A 0 A_PlaySound("Quake3/Chaingun/End",CHAN_5,0.75)
			Q303 "ADGJMPSVY\" 1
			Q304 "BEH" 2 A_WeaponReady
			Q303 "DGJMPSVY\" 2 A_WeaponReady
			Q304 "BEHK" 3 A_WeaponReady
			Goto ReadyLoop
		Flash:
			Q305 A 0 A_GiveInventory("Q3QuadDamageSound",1)
			Q305 A 0 A_SpawnItemEx("Q3MachineGunBrass",8,4,49,0,0.75+frandom(0.0,0.5),frandom(1.0,2.0),frandom(-2.0,2.0))
			Q305 A 0 A_JumpIfInventory("CoopModeOn",1,2)
			Q305 A 0 A_FireBullets(3.0,3.0,-1,8,"Q3BulletPuff",FBF_NORANDOM|FBF_NORANDOMPUFFZ)
			Goto FlashContinue
			Q305 A 0 A_FireBullets(3.0,3.0,-1,8,"Q3BulletPuffCoop",FBF_NORANDOM|FBF_NORANDOMPUFFZ)
			Goto FlashContinue
		FlashContinue:
			Q305 A 0 A_JumpIfInventory("Q3ChaingunPlaySound",0,"PlaySound")
			Q305 A 0 A_GiveInventory("Q3ChaingunPlaySound",1)
			Q305 A 0 A_JumpIfInventory("Q3ChaingunConsumeAmmo",1,1)
			Goto Consume
			Q305 A 0 A_TakeInventory("Q3ChaingunConsumeAmmo",1)
			Stop
		Consume:
			Q305 A 0 A_GiveInventory("Q3ChaingunConsumeAmmo",1)
			Q305 A 0 A_TakeInventory("Q3ChaingunAmmo",1,TIF_NOTAKEINFINITE)
			Goto FinishFlash
		PlaySound:
			Q305 A 0 A_TakeInventory("Q3ChaingunPlaySound",256)
			Q305 A 0 A_PlaySound("Quake3/Chaingun/Fire",CHAN_WEAPON)
			Goto FlashContinue
		FinishFlash:
			Q305 A 1 Bright
			Stop 
	}
}

Actor Q3ChaingunConsumeAmmo : Q3Boolean {}
Actor Q3ChaingunPlaySound : Q3Counter { Inventory.MaxAmount 2 }

ACTOR Q3ChaingunAmmo : Ammo
{
	Inventory.Amount 1
	Inventory.MaxAmount 300
	Ammo.BackpackAmount 10
	Ammo.BackpackMaxAmount 600
	Inventory.Icon "AMSMN0"
}
