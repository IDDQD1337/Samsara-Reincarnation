Actor Q3Nailgun : Q3Weapon
{
	Weapon.AmmoUse 5
	Weapon.AmmoGive 50
	Weapon.SlotNumber 3
	Weapon.AmmoType "Clip"
	Inventory.Pickupmessage "Nailgun"
	Obituary "%o was nailed by %k"
	Tag "Nailgun"
	+FLOATBOB
	States
	{
		Spawn:
			Q399 A 1
			Loop
		Deselect:
			Q301 A 0 A_PlaySound("Quake3/Weapons/Change",CHAN_5)
			Q301 ABCDE 1
			goto DeselectInstant
		Ready:
			Q302 ABCD 1
		ReadyLoop:
			Q300 A 1 A_WeaponReady
			Loop
		Fire:
			Q303 A 0 A_GunFlash
			Q303 A 0 A_TakeInventory("Clip",5,TIF_NOTAKEINFINITE)
			Q303 A 0 A_JumpIfInventory("CoopModeOn",1,"FireCoop")
			Q303 AAAAAAAAAA 0 A_FireCustomMissile("Q3NailProjectile",frandom(-5.0,5.0),0,6,0,0,frandom(-5.0,5.0))
			Goto FireContinue
		FireCoop:
			Q303 AAAAAAAAAA 0 A_FireCustomMissile("Q3NailProjectileCoop",frandom(-5.0,5.0),0,6,0,0,frandom(-5.0,5.0))
			Goto FireContinue
		FireContinue:
			Q303 A 0 A_PlaySound("Quake3/Nailgun/Fire",CHAN_WEAPON)
			Q303 ABCDEF 6
			Goto ReadyLoop
		Flash:
			Q304 A 0 A_GiveInventory("Q3QuadDamageSound",1)
			Q304 A 3 Bright
			Stop
	}
}

Actor Q3NailProjectile
{
	Height 4
	Radius 2
	Speed 24
	Damage (20)
	Projectile
	Decal "Q3BulletImpact"
	+BLOODLESSIMPACT
	+HITTRACER
	States
	{
		Spawn:
			Q300 A 0 A_PlaySound("Quake3/Nailgun/Fly",CHAN_5,1.0,true)
			Q300 A 1 A_SpawnItemEx("Q3NailSmoke")
			Loop
		Death:
			TNT1 A 0 A_StopSound(CHAN_5)
			TNT1 A 0 A_Playsound("Quake3/Nailgun/HitWall",CHAN_6)
			TNT1 A 0 A_JumpIf(abs(z - floorz) <= 4,"HitFloor")
			TNT1 A 0 A_CheckCeiling("HitCeiling")
			TNT1 A 1
			Stop
		Crash:
			TNT1 A 0 A_StopSound(CHAN_5)
			TNT1 A 0 A_Playsound("Quake3/Nailgun/HitMetal",CHAN_6)
			TNT1 A 0 A_JumpIf(abs(z - floorz) <= 4,"HitFloor")
			TNT1 A 0 A_CheckCeiling("HitCeiling")
			TNT1 A 1
			Stop
		HitFloor:
			TNT1 A 0 ACS_NamedExecuteWithResult("Q3SpawnDecal")
			TNT1 A 0 A_SetPitch(90)
			Goto Death+4
		HitCeiling:
			TNT1 A 0 ACS_NamedExecuteWithResult("Q3SpawnDecal",0,1)
			TNT1 A 0 A_SetPitch(-90)
			Goto Death+4
		XDeath:
			TNT1 A 0 A_Playsound("Quake3/Nailgun/HitFlesh",CHAN_6)
			TNT1 A 0 ACS_NamedExecuteWithResult("Q3_BloodGenerator_Offsets")
			TNT1 A 0 A_GiveInventory("Q3_BloodGenerator",1,AAPTR_TRACER)
			TNT1 A 1
			Stop
	}
}

Actor Q3NailProjectileCoop : Q3NailProjectile { +THRUSPECIES Species "Player" }

Actor Q3NailSmoke
{
	+NOINTERACTION
	+NOGRAVITY
	+FORCEXYBILLBOARD
	Renderstyle Add
	Alpha 1.0
	Scale 0.1
	Height 4
	Radius 2
	States
	{
		Spawn:
			TNT1 A 0 NoDelay A_JumpIf(waterlevel > 0, "Bubble")
			Q308 A 0 ACS_NamedExecuteWithResult("Q3EffectLifeCycle",0,50,-32,17)
			Q308 A 100
			Stop
		Bubble:
			TNT1 A 0 A_SpawnItemEx("Q3Bubble")
			TNT1 A 1
			Stop
	}
}