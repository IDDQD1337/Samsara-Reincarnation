actor SamsaraUniquePickup2 : CustomInventory
{
	+QUIET
	+DONTGIB
	States
	{
	Spawn:
		UNIQ B 0 Bright
		TNT1 A 0 A_JumpIf(CallACS("SamsaraDecorate",27,0,0) == 1, "Nope")
		TNT1 A 0 A_JumpIf(CallACS("SamsaraDecorate",29,0,0) == 1, "Nope")
		UNIQ B 1 Bright A_JumpIf(CallACS("SamsaraDecorate", 19), "Nope")
		Goto SpawnLoop

	Nope:
		UNIQ B 0 Bright
		Stop

	SpawnLoop:
		"----" A 35 bright ACS_NamedExecuteAlways("SamsaraClientClass", 0, -2)
		Loop

		NoGuy:        UNIQ B 35 bright A_ChangeFlag("FLOATBOB", 0) Goto SpawnLoop
		NoGuyClassic: UNI2 A 35 bright A_ChangeFlag("FLOATBOB", 0) Goto SpawnLoop

		Doomguy:      DGBZ A 35 A_ChangeFlag("FLOATBOB", 0) Goto SpawnLoop
		DoomguyStr:
			DGBZ A 0 A_ChangeFlag("FLOATBOB", 0)
			DGBZ A 35 A_SpawnItemEx("SamsaraDoomguyStrRepeaterDummy",0.1,0.1)
			Goto SpawnLoop
		Chexguy:      SHOC C 35 A_ChangeFlag("FLOATBOB", 0) Goto SpawnLoop
		Heretic:      EGGC ABCB 6 A_ChangeFlag("FLOATBOB", 1) Goto SpawnLoop
		Wolfguy:      WFLF A 35 A_ChangeFlag("FLOATBOB", 0) Goto SpawnLoop
        Hexen:
        HexenClassic:
        HexenMage:
		HexenFighter: H124 ABCDEFG 4 bright A_ChangeFlag("FLOATBOB", 1) Goto SpawnLoop
		Dukeguy:      DKSR R 35 A_ChangeFlag("FLOATBOB", 0) Goto SpawnLoop
		Marathon:     AWPN B 35 A_ChangeFlag("FLOATBOB", 0) Goto SpawnLoop
		Quake:        QLSC Z 35 A_ChangeFlag("FLOATBOB", 0) Goto SpawnLoop
		Rott:         PSPL A 35 A_ChangeFlag("FLOATBOB", 0) Goto SpawnLoop
		Blake:
		AOG:          BAMR A 35 bright A_ChangeFlag("FLOATBOB", 0) Goto SpawnLoop
		Caleb:        AKIM B 35 A_ChangeFlag("FLOATBOB", 0) Goto SpawnLoop
		Strifeguy:    UPGR FEDCBAGHIJKJIHGABCDE 2 bright A_ChangeFlag("FLOATBOB", 0) Goto SpawnLoop
		Doom64:       64BZ A 35 A_ChangeFlag("FLOATBOB", 0) Goto SpawnLoop
		Erad:         D7X_ ABCDEFG 3 A_ChangeFlag("FLOATBOB", 1) Goto SpawnLoop
		C7:           7MN2 A 35 A_ChangeFlag("FLOATBOB", 0) Goto SpawnLoop
		RMR:          RM8Z H 35 A_ChangeFlag("FLOATBOB", 0) Goto SpawnLoop
		Katarn:       PDFU B 35 A_ChangeFlag("FLOATBOB", 0) Goto SpawnLoop
		POGreed:      PPGU C 35 A_ChangeFlag("FLOATBOB", 0) Goto SpawnLoop
		Disruptor:    SQ72 X 35 A_ChangeFlag("FLOATBOB", 0) Goto SpawnLoop
		Witchaven:    WIN0 G 35 A_ChangeFlag("FLOATBOB", 0) Goto SpawnLoop
		SW:           SW10 K 35 A_ChangeFlag("FLOATBOB", 0) Goto SpawnLoop
		CM:           CMUQ A 35 A_ChangeFlag("FLOATBOB", 0) Goto SpawnLoop
		HalfLife:
		Shephard:     HWU0 A 35 A_ChangeFlag("FLOATBOB", 0) Goto SpawnLoop
		HalfLifeLoadout1:
		ShephardLoadout1: OWU0 A 35 A_ChangeFlag("FLOATBOB", 0) Goto SpawnLoop
		Jon:          WROR A 35 bright A_ChangeFlag("FLOATBOB", 1) Goto SpawnLoop
		RR:           RR00 FGHIHGF 5 A_ChangeFlag("FLOATBOB", 0) Goto SpawnLoop
		Bitterman:    Q2WP A 35 A_ChangeFlag("FLOATBOB", 0) Goto SpawnLoop
		LostWolfguy:  WFLF B 35 A_ChangeFlag("FLOATBOB", 0) Goto SpawnLoop
		BeachDukeguy: DKSR W 35 A_ChangeFlag("FLOATBOB", 0) Goto SpawnLoop
		Demoness:     HX00 A 35 A_ChangeFlag("FLOATBOB", 0) Goto SpawnLoop
		Bond:         GE00 Z 35 A_ChangeFlag("FLOATBOB", 0) Goto SpawnLoop
		Totenkopf:    TK11 F 35 A_ChangeFlag("FLOATBOB", 0) Goto SpawnLoop
		Catacomb:
			CT01 NO 4 bright A_ChangeFlag("FLOATBOB", 0)
			CTAA ABGHMN 4 bright A_ChangeFlag("FLOATBOB", 0)
			Goto SpawnLoop
		Painkiller:   PK99 A 35 bright A_ChangeFlag("FLOATBOB", 1) Goto SpawnLoop
		Unreal:       UN00 A 35 A_ChangeFlag("FLOATBOB", 0) Goto SpawnLoop
		RTCW:         RW98 A 35 A_ChangeFlag("FLOATBOB", 0) Goto SpawnLoop
		Sarge:        Q399 A 35 A_ChangeFlag("FLOATBOB", 1) Goto SpawnLoop
		Descent:      DS55 ABCDEFGHIJKLMNOP 2 A_ChangeFlag("FLOATBOB", 1) Goto SpawnLoop
		DeusEx:       DX03 A 35 A_ChangeFlag("FLOATBOB", 0) Goto SpawnLoop
		Duke64guy:    PDWP I 35 A_ChangeFlag("FLOATBOB", 0) Goto SpawnLoop

		DoomSpecial:         UNIQ D 35 bright A_ChangeFlag("FLOATBOB", 0) Goto SpawnLoop
		DoomStrSpecial:      WREP B 35 bright A_ChangeFlag("FLOATBOB", 0) Goto SpawnLoop
		ChexSpecial:         UNI2 Z 35 bright A_ChangeFlag("FLOATBOB", 0) Goto SpawnLoop
		HereticSpecial:      UNIQ F 35 bright A_ChangeFlag("FLOATBOB", 0) Goto SpawnLoop
		WolfSpecial:         UNIQ H 35 bright A_ChangeFlag("FLOATBOB", 0) Goto SpawnLoop
		HexenSpecial:
		HexenClassicSpecial:
		HexenMageSpecial:
		HexenFighterSpecial: UNIQ G 35 bright A_ChangeFlag("FLOATBOB", 0) Goto SpawnLoop // NEEDS NEW SPRITE
		DukeSpecial:         DKSR Y 35 bright A_ChangeFlag("FLOATBOB", 0) Goto SpawnLoop
		MarathonSpecial:     UNIQ I 35 bright A_ChangeFlag("FLOATBOB", 0) Goto SpawnLoop
		QuakeSpecial:        UNIQ T 35 bright A_ChangeFlag("FLOATBOB", 0) Goto SpawnLoop
		RottSpecial:         PRBT A 35 bright A_ChangeFlag("FLOATBOB", 0) Goto SpawnLoop
		BlakeSpecial:
		AOGSpecial:          UNQB B 35 bright A_ChangeFlag("FLOATBOB", 0) Goto SpawnLoop
		CalebSpecial:        UNQC C 35 bright A_ChangeFlag("FLOATBOB", 0) Goto SpawnLoop
		StrifeSpecial:       UNI2 B 35 bright A_ChangeFlag("FLOATBOB", 0) Goto SpawnLoop
		Doom64Special:       UNIQ U 35 bright A_ChangeFlag("FLOATBOB", 0) Goto SpawnLoop
		EradSpecial:         ERPS H 35 bright A_ChangeFlag("FLOATBOB", 0) Goto SpawnLoop
		C7Special:           7MN2 B 35 bright A_ChangeFlag("FLOATBOB", 0) Goto SpawnLoop
		RMRSpecial:          RMZ6 A 35 bright A_ChangeFlag("FLOATBOB", 0) Goto SpawnLoop
		KatarnSpecial:       PDFU A 35 bright A_ChangeFlag("FLOATBOB", 0) Goto SpawnLoop
		POGreedSpecial:      GN7Q Z 35 bright A_ChangeFlag("FLOATBOB", 0) Goto SpawnLoop
		DisruptorSpecial:    SQ73 H 35 bright A_ChangeFlag("FLOATBOB", 0) Goto SpawnLoop
		WitchavenSpecial:    UNIQ Y 35 bright A_ChangeFlag("FLOATBOB", 0) Goto SpawnLoop
		HalfLifeSpecial:
		ShephardSpecial:     UNIQ X 35 bright A_ChangeFlag("FLOATBOB", 0) Goto SpawnLoop
		HalfLifeSpecialLoadout1:
		ShephardSpecialLoadout1: UNI2 X 35 bright A_ChangeFlag("FLOATBOB", 0) Goto SpawnLoop
		SWSpecial:           PSW0 A 35 bright A_ChangeFlag("FLOATBOB", 0) Goto SpawnLoop
		CMSpecial:           CMPK P 35 bright A_ChangeFlag("FLOATBOB", 0) Goto SpawnLoop
		JonSpecial:          PSPK A 35 bright A_ChangeFlag("FLOATBOB", 0) Goto SpawnLoop
		RRSpecial:           RRPK O 35 bright A_ChangeFlag("FLOATBOB", 0) Goto SpawnLoop
		BittermanSpecial:    Q2WP B 35 bright A_ChangeFlag("FLOATBOB", 0) Goto SpawnLoop
		LostWolfSpecial:     UNI2 H 35 bright A_ChangeFlag("FLOATBOB", 0) Goto SpawnLoop
		BeachDukeSpecial:    DKSR X 35 bright A_ChangeFlag("FLOATBOB", 0) Goto SpawnLoop
		DemonessSpecial:     HX00 B 35 bright A_ChangeFlag("FLOATBOB", 0) Goto SpawnLoop
		BondSpecial:         GE00 Y 35 bright A_ChangeFlag("FLOATBOB", 0) Goto SpawnLoop
		TotenkopfSpecial:    TK11 G 35 bright A_ChangeFlag("FLOATBOB", 0) Goto SpawnLoop
		CatacombSpecial:     CTUN A 35 bright A_ChangeFlag("FLOATBOB", 0) Goto SpawnLoop
		PainkillerSpecial:   PK99 B 35 bright A_ChangeFlag("FLOATBOB", 0) Goto SpawnLoop
		UnrealSpecial:       UN00 B 35 bright A_ChangeFlag("FLOATBOB", 0) Goto SpawnLoop
		RTCWSpecial:         RW98 B 35 bright A_ChangeFlag("FLOATBOB", 0) Goto SpawnLoop
		SargeSpecial:        Q399 B 35 bright A_ChangeFlag("FLOATBOB", 0) Goto SpawnLoop
		DescentSpecial:      GXUN B 35 bright A_ChangeFlag("FLOATBOB", 0) Goto SpawnLoop
		DeusExSpecial:       DX03 B 35 bright A_ChangeFlag("FLOATBOB", 0) Goto SpawnLoop
		Duke64Special:       PDJP A 35 bright A_ChangeFlag("FLOATBOB", 0) Goto SpawnLoop

	Pickup:
		TNT1 A 0 A_JumpIfInventory("DoomguyClass", 1, "PickupAlt")
		TNT1 A 0 A_JumpIfInventory("ChexClass", 1, "PickupAlt")
		TNT1 A 0 A_JumpIfInventory("CorvusClass", 1, "PickupAlt")
		TNT1 A 0 A_JumpIfInventory("HexenClass", 1, "PickupAlt")
		TNT1 A 0 A_JumpIfInventory("DukeClass", 1, "PickupAlt")
		TNT1 A 0 A_JumpIfInventory("MarathonClass", 1, "PickupAlt")
		TNT1 A 0 A_JumpIfInventory("RRClass", 1, "PickupAlt")
		TNT1 A 0 A_JumpIfInventory("BittermanClass", 1, "PickupAlt")
		TNT1 A 0 A_JumpIfInventory("CalebClass", 1, "PickupAlt")
		TNT1 A 0 A_JumpIfInventory("HalfLifeClass", 1, "PickupAltHL")
		TNT1 A 0 A_JumpIfInventory("SWClass", 1, "PickupAlt")
		TNT1 A 0 A_JumpIfInventory("RottClass", 1, "PickupAlt")
		TNT1 A 0 A_JumpIfInventory("Hexen2Class", 1, "PickupAlt")
		TNT1 A 0 A_JumpIfInventory("BondClass", 1, "PickupAlt")
		TNT1 A 0 A_JumpIfInventory("WolfenClass", 1, "PickupAlt")
		TNT1 A 0 A_JumpIfInventory("DisruptorClass", 1, "PickupAlt")
		TNT1 A 0 A_JumpIfInventory("UnrealClass", 1, "PickupAlt")
		TNT1 A 0 A_JumpIfInventory("RTCWClass", 1, "PickupAlt")
		TNT1 A 0 A_JumpIfInventory("Q3Class", 1, "PickupAlt")
		TNT1 A 0 A_JumpIfInventory("DescentClass", 1, "PickupAlt")
		TNT1 A 0 A_JumpIfInventory("DeusExClass", 1, "PickupAlt")
		TNT1 A 0 A_JumpIf(CallACS("SamsaraGiveUnique"), "PickupSuccess")
		Goto PickupFailure
	PickupAltHL:
		TNT1 A 0 A_JumpIfInventory("HL Crossbow", 1, "HLPickupAmmo")
		TNT1 A 0 A_JumpIf(CallACS("SamsaraGiveUnique", 1), 1)
		Goto PickupFailure
		TNT1 A 0 ACS_NamedExecuteAlways("HL2685",0,18)
		Goto PickupSuccess
	HLPickupAmmo:
		TNT1 A 0 A_JumpIfInventory("Shell",0,"PickupFailure")
		TNT1 A 0 ACS_NamedExecuteAlways("HL2685",0,18)
		TNT1 A 0 A_GiveInventory("Shell", 10)
		TNT1 A 0 ACS_NamedExecuteAlways("SamsaraClientUniquePickup", 0, 1)
		Stop
	PickupAlt:
		TNT1 A 0 A_JumpIfInventory("DoomguyClass", 1, "PickupDoom")
		TNT1 A 0 A_JumpIfInventory("DukeClass", 1, "PickupDuke")
		TNT1 A 0 A_JumpIfInventory("BondClass", 1, "CheckBond")
		TNT1 A 0 A_JumpIfInventory("BittermanClass", 1, "PickupBitterman")
		TNT1 A 0 A_JumpIfInventory("DisruptorClass", 1, "CheckDisruptor")
		TNT1 A 0 A_JumpIfInventory("WolfenClass", 1, "PickupWolfen")
		TNT1 A 0 A_JumpIf(CallACS("SamsaraGiveUnique", 1), "PickupSuccess")
		Goto PickupFailure
	PickupSuccess:
		TNT1 A 0 A_ChangeFlag("FLOATBOB", 0)
		Stop
	PickupFailure:
		TNT1 A 0 A_ChangeFlag("FLOATBOB", 0)
		Fail
	PickupDoom:
		TNT1 A 0 A_GiveInventory("DGStrHasPlasmaRepeater")
		TNT1 A 0 A_GiveInventory("Samsara_ModeWeaponChange")
		TNT1 A 0 A_JumpIfInventory("Cell",0,2)
		TNT1 A 0 A_GiveInventory("Cell",20)
		TNT1 A 0 A_JumpIfInventory("DoomClassMode", 2, 2)
		TNT1 A 0 A_JumpIf(CallACS("SamsaraGiveUnique"), "PickupSuccess")
		Goto PickupFailure
		TNT1 A 0 A_JumpIf(CallACS("SamsaraGiveUnique", 1), "PickupSuccess")
		Goto PickupFailure
	PickupDuke:
		TNT1 A 0 A_JumpIfInventory("DukeClassMode", 2, "PickupDuke64")
		TNT1 A 0 A_JumpIfInventory("Shrinker",1,"PickupDukeAmmo")
		TNT1 A 0 A_JumpIf(CallACS("SamsaraGiveUnique", 1), 1)
		Goto PickupFailure
		TNT1 A 0 A_GiveInventory("DukeHasShrinker")
		TNT1 A 0 A_GiveInventory("Samsara_ModeWeaponChange")
		TNT1 A 0 A_JumpIfInventory("ShrinkerAmmo",0,2)
		TNT1 A 0 A_GiveInventory("ShrinkerAmmo",3)
        TNT1 A 0 ACS_NamedExecuteWithResult("SamsaraWeaponTaunt")
		Goto PickupSuccess
	PickupDukeAmmo:
		TNT1 A 0 A_JumpIfInventory("ShrinkerAmmo",0,"PickupFailure")
		TNT1 A 0 A_GiveInventory("Samsara_ModeWeaponChange")
		TNT1 A 0 A_GiveInventory("ShrinkerAmmo",3)
		TNT1 A 0 ACS_NamedExecuteAlways("SamsaraClientUniquePickup", 0, 1)
        TNT1 A 0 ACS_NamedExecuteWithResult("SamsaraWeaponTaunt")
		Stop
	PickupDuke64:
		TNT1 A 0 A_JumpIf(CallACS("SamsaraGiveUnique"), "PickupSuccess")
		Goto PickupFailure
	PickupBitterman:
		TNT1 A 0 A_JumpIf(CallACS("SamsaraGiveUnique", 1), "PickupSuccessBitterman")
		Goto PickupFailure
	PickupSuccessBitterman:
		TNT1 A 0 A_GiveInventory("Q2DoubleDamage", 1)
		TNT1 A 0 A_ChangeFlag("FLOATBOB", 0)
		Stop
	CheckDisruptor:
		TNT1 A 0 A_JumpIfInventory("DisruptorPSIEnergy", 0, "PickupDisrup2")
		TNT1 A 0 A_JumpIfInventory("DisruptorTeraBall", 1, 4)
		TNT1 A 0 A_GiveInventory("DisruptorPSIEnergy", 50)
		TNT1 A 0 A_JumpIfInventory("DisruptorEnergyBall", 1, 2)
		TNT1 A 0 A_JumpIf(CallACS("SamsaraGiveUnique"), "PickupSuccess")
		Goto PickupFailure
		TNT1 A 0 A_JumpIfInventory("DisruptorPSIEnergy", 0, "PickupFailure")
		TNT1 A 0 A_GiveInventory("DisruptorPSIEnergy", 50)
		TNT1 A 0 ACS_NamedExecuteAlways("SamsaraClientUniquePickup", 0, 0)
		Goto PickupSuccess
	PickupDisrup2:
		TNT1 A 0 A_JumpIf(CallACS("SamsaraGiveUnique"), "PickupSuccess")
		Goto PickupFailure
	CheckBond:
		TNT1 A 0 A_JumpIfInventory("Clip", 0, "CheckBond2")
		TNT1 A 0 A_GiveInventory("Clip", 10)
		TNT1 A 0 A_JumpIfInventory("Goldeneye_SilencedD5K",1,2)
		TNT1 A 0 A_JumpIf(CallACS("SamsaraGiveUnique", 1),"PickupSuccess")
		Goto PickupSuccess
		TNT1 A 0 A_GiveInventory("Goldeneye_D5KDualToken", 1)
		TNT1 A 0 A_GiveInventory("Clip", 10)
		TNT1 A 0 ACS_NamedExecuteAlways("SamsaraClientUniquePickup", 0, 1)
		Goto PickupSuccess
	CheckBond2:
		TNT1 A 0 A_JumpIfInventory("Goldeneye_D5KDualToken", 1, "PickupFailure")
		Goto CheckBond+1
	PickupWolfen:
		TNT1 A 0 A_GiveInventory("Totenkopf_IHaveMG42", 1)
		TNT1 A 0 A_GiveInventory("Wolfen_IHaveExtraLife", 1)
		TNT1 A 0 A_GiveInventory("Samsara_ModeWeaponChange", 1)
		TNT1 A 0 A_JumpIfInventory("Cell", 0, 2)
		TNT1 A 0 A_GiveInventory("Cell", 80)
		TNT1 A 0 A_JumpIfInventory("WolfenClassMode", 2, 2)
		TNT1 A 0 A_JumpIf(CallACS("SamsaraGiveUnique"), "PickupSuccess")
		Goto PickupFailure
		TNT1 A 0 A_JumpIf(CallACS("SamsaraGiveUnique", 1), "PickupSuccess")
		Goto PickupFailure
	}
}

actor SamsaraDoomguyStrRepeaterDummy : CustomInventory
{
	+QUIET
	+AUTOACTIVATE
	+FLOORHUGGER
	Inventory.PickupMessage ""
	Inventory.MaxAmount 0
	States
	{
	Spawn:
		WREP A 38
		Stop
	Pickup:
		TNT1 A 0
		Stop
	PickupFail:
		TNT1 A 0
		Fail
	}
}
