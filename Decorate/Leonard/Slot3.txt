Actor " Scattered Gun " : Weapon
{
	Inventory.PickupMessage "Yer Scattered Gun!"
    Inventory.PickupSound "RR/Wpickup"
	Weapon.SelectionOrder 2800
    Weapon.BobRangeX 0.75
    Weapon.BobRangeY 0.95
    Weapon.BobSpeed 1.1	
	Weapon.SlotNumber 3
	Weapon.SlotPriority 3
	Weapon.AmmoType "Shell"
	Weapon.AmmoUse 0
	Weapon.AmmoGive 16
	Obituary "%o learned things by %k's way."
	+Weapon.NoAlert
	Inventory.RestrictedTo "Leonard"	
	States
	{
		Spawn:
			RR_S A -1 Bright
			Stop
		Select:
			TNT1 A 0 A_Raise
			RR_S B 1 A_Raise
			Loop
		Deselect:
			TNT1 A 0 A_Lower
			RR_S B 1 A_Lower
			Loop
		Ready:
			TNT1 A 0 A_JumpIfInventory("RRDrunkToken", 1, "SetDrunkView")
			Goto SetSoberView
			TNT1 A 0 A_JumpIfInventory("LeonardWaiting", 15, "WaitingTaunt")
			TNT1 A 0 A_JumpIfInventory("LeonardReady",1,"IntroTaunt")		
			RR_S B 1 A_WeaponReady
			Loop
		Fire:
			TNT1 A 0 A_JumpIfInventory("RRDrunkToken", 1, "SetDrunkViewFire")
			Goto SetSoberViewFire
			TNT1 A 0 A_JumpIfNoAmmo("Ready")		
			TNT1 A 0 A_JumpIfInventory("RRSGFireT",2,"Reload")
			TNT1 A 0 A_JumpIfInventory("RRSGFireT",1,"Fire2")
			RR_S C 1 Bright A_PlaySound("RR/SGFire",CHAN_WEAPON)
			TNT1 A 0 A_AlertMonsters
			TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,2)
			RR_S C 2 Bright A_FireBullets(frandom(6,7.5),0.80,6,15,"RRPuff",FBF_NORANDOM)
			Goto A
			TNT1 AAAAA 0 A_FireCustomMissile("RRBullet2",frandom(frandom(7.50,-7.50),frandom(6.00,-6.00)),0,0,0,0,FRandom(1.25,-1.25))
			RR_S C 2 Bright A_FireCustomMissile("RRBullet2",frandom(frandom(7.50,-7.50),frandom(6.00,-6.00)),0,0,0,0,FRandom(1.25,-1.25))
		A:	
			TNT1 A 0 A_TakeInventory("Shell",1,TIF_NOTAKEINFINITE)	
			TNT1 A 0 A_Refire			
			RR_S C 1 Bright A_GiveInventory("RRSGFireT",1)
			RR_S DDGGGGHHHIIIJJJJ 1 Bright
			Goto Ready
		Fire2:
			TNT1 A 0 A_JumpIfNoAmmo("Ready")
			RR_S E 1 Bright A_PlaySound("RR/SGFire",CHAN_WEAPON)
			TNT1 A 0 A_AlertMonsters			
			TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,2)
			RR_S E 2 Bright A_FireBullets(frandom(6,7.5),0.80,6,15,"RRPuff",FBF_NORANDOM)
			Goto B
			TNT1 AAAAA 0 A_FireCustomMissile("RRBullet2",frandom(frandom(7.50,-7.50),frandom(6.00,-6.00)),0,0,0,0,FRandom(1.25,-1.25))
			RR_S E 2 Bright A_FireCustomMissile("RRBullet2",frandom(frandom(7.50,-7.50),frandom(6.00,-6.00)),0,0,0,0,FRandom(1.25,-1.25))
		B:	
			TNT1 A 0 A_TakeInventory("Shell",1,TIF_NOTAKEINFINITE)	
			RR_S E 1 Bright A_GiveInventory("RRSGFireT",1)
			RR_S FFGGGGHHHIIIJJJJ 1 Bright
		Reload:
			TNT1 A 0 A_JumpIfInventory("Shell",1,1)
			Goto Ready
			TNT1 A 0 A_PlaySound("RR/SGReload",CHAN_BODY)
			TNT1 A 0 A_TakeInventory("RRSGFireT",2)
			RR_S KKK 1 Bright A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
			TNT1 A 0 A_GunFlash
			RR_S L 1 Bright A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
			RR_S LL 1 Offset(-6,43) Bright A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
			RR_S L 1 Offset(-1,30) Bright A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
			RR_S L 1 Bright A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
			RR_S KKK 1 Bright A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
			RR_S BBB 1 Offset(1,34) Bright A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
			TNT1 A 0 A_ClearReFire
			Goto Ready
		Flash:
			RR_S M 1 Bright
			RR_S MM 1 Offset(-6,43) Bright 
			RR_S M 1 offset(-1,30) Bright
			RR_S M 1 Bright
			RR_S M 1 Bright			
			Goto LightDone
		IntroTaunt:
			TNT1 A 0 A_JumpIfInventory("LeonardBallgag",1,2)
			TNT1 A 0 A_PlaySoundEx("Leonard/Enter", "SoundSlot7", 0, 0)
			TNT1 A 0 A_TakeInventory("LeonardReady", 1)
			TNT1 A 0 A_GiveInventory("LeonardTauntCooldown",5)
			TNT1 A 0 ACS_ExecuteAlways(706, 0)
			Goto Ready
		WaitingTaunt:
			TNT1 A 0 A_JumpIfInventory("LeonardBallgag",1,2)
			TNT1 A 0 A_PlaySoundEx("Leonard/Bored", "SoundSlot7", 0, 0)
			TNT1 A 0 A_TakeInventory("LeonardWaiting", 15)
			TNT1 A 0 A_GiveInventory("LeonardTauntCooldown",5)
			TNT1 A 0 ACS_ExecuteAlways(706, 0)
			Goto Ready
		Hold:
			TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopHold")		
			RR_S C 0 A_GiveInventory("RRSGFireT",1)
			RR_S D 1 Bright
			TNT1 A 0 A_JumpIfNoAmmo("RefreshFire")
			RR_S E 1 Bright A_PlaySound("RR/SGFire",CHAN_WEAPON)
			TNT1 A 0 A_AlertMonsters			
			RR_S E 1 Bright A_FireBullets(frandom(6,7.5),0.80,6,15,"RRPuff",FBF_NORANDOM)
			TNT1 A 0 A_TakeInventory("Shell",1,TIF_NOTAKEINFINITE)			
			RR_S E 0 Bright A_GiveInventory("RRSGFireT",1)
			RR_S FFGGGGGGGHHHHIIIJJJJ 1 Bright
			TNT1 A 0 A_ClearRefire
			Goto Reload
		CoopHold:	
			RR_S C 0 A_GiveInventory("RRSGFireT",1)
			RR_S D 1 Bright
			TNT1 A 0 A_JumpIfNoAmmo("RefreshFire")
			RR_S E 1 Bright A_PlaySound("RR/SGFire",CHAN_WEAPON)
			TNT1 A 0 A_AlertMonsters			
			TNT1 AAAAA 0 A_FireCustomMissile("RRBullet2",frandom(frandom(7.50,-7.50),frandom(6.00,-6.00)),0,0,0,0,FRandom(1.25,-1.25))
			RR_S E 1 Bright A_FireCustomMissile("RRBullet2",frandom(frandom(7.50,-7.50),frandom(6.00,-6.00)),0,0,0,0,FRandom(1.25,-1.25))
			TNT1 A 0 A_TakeInventory("Shell",1,TIF_NOTAKEINFINITE)			
			RR_S E 0 Bright A_GiveInventory("RRSGFireT",1)
			RR_S FFGGGGGGGHHHHIIIJJJJ 1 Bright
			TNT1 A 0 A_ClearRefire			
			Goto Reload		
		RefreshFire:
			TNT1 A 0 A_ClearRefire
			Goto Ready
		SetDrunkView:
			TNT1 A 0 A_ZoomFactor(0.55)
			Goto Ready+1
	    SetDrunkViewFire:
			TNT1 A 0 A_ZoomFactor(0.55)
			Goto Fire+1
		SetSoberView:
			TNT1 A 0 A_ZoomFactor(1.0)
			Goto Ready+1
	    SetSoberViewFire:
			TNT1 A 0 A_ZoomFactor(1.0)
			Goto Fire+1
	}
}

Actor RRBullet2 : RRBullet1 { Damage (15) }
Actor RRSGFireT : Inventory { Inventory.Amount 1 Inventory.MaxAmount 2 }