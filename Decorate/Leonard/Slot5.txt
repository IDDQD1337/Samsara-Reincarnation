Actor " Chicken Crossbow " : Weapon
{
	Inventory.PickupMessage "Chicken Crossbow"
    Inventory.PickupSound "RR/Wpickup"
	Weapon.SelectionOrder 2500
    Weapon.BobRangeX 0.75
    Weapon.BobRangeY 0.95
    Weapon.BobSpeed 1.1	
	Weapon.SlotNumber 5
	Weapon.SlotPriority 5
	Weapon.AmmoType "RocketAmmo"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 7
	Obituary "%o didnt expect %k's cock... :^)"
	Inventory.RestrictedTo "Leonard"	
	States
	{
		Spawn:
			CH_7 M -1 Bright
			Stop
		Select:
			TNT1 A 0 A_JumpIfnoAmmo("SelectAmmoLess")  
		SelectLoop:			
			TNT1 A 0 A_Raise
			CH_R A 1 A_Raise
			Loop
		SelectAmmoLess:
			TNT1 A 0 A_Raise
			CH_R C 1 A_Raise
			Loop
		Deselect:
			TNT1 A 0 A_JumpIfnoAmmo("DeselectAmmoLess")
		DeselectLoop:			
			TNT1 A 0 A_Lower
			CH_R A 1 A_Lower
			Loop
		DeselectAmmoLess:
			TNT1 A 0 A_Lower
			CH_R C 1 A_Lower
			Loop
		Ready:
			TNT1 A 0 A_JumpIfInventory("RRDrunkToken", 1, "SetDrunkView")
			Goto SetSoberView
			TNT1 A 0 A_JumpIfInventory("LeonardWaiting", 15, "WaitingTaunt")
			TNT1 A 0 A_JumpIfInventory("LeonardReady",1,"IntroTaunt")		
			TNT1 A 0 A_JumpIfNoAmmo("ReadyAmmoless")
			TNT1 A 0 A_Jump(4,"B")
			TNT1 A 0 A_Jump(3,"B")
			TNT1 A 0 A_Jump(2,"B")
			TNT1 A 0 A_Jump(1,"Ready2")
		B:	
			CH_R A 1 A_WeaponReady
			Loop
		Ready2:	
			TNT1 A 0 A_PlaySound("RR/XBOWCOCKCRY",CHAN_WEAPON)
			CH_R H 8 A_WeaponReady
			Goto Ready
		ReadyAmmoless:
			TNT1 A 0 A_JumpIfInventory("LeonardReady",1,"IntroTaunt")		
			TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,"Reload")	
			CH_R C 1 A_WeaponReady
			Loop
		Fire:
			TNT1 A 0 A_JumpIfInventory("RRDrunkToken", 1, "SetDrunkViewFire")
			Goto SetSoberViewFire
			CH_R B 2 Bright A_PlaySound("RR/XBOWFire",CHAN_WEAPON)
			TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,2)
			TNT1 A 0 A_FireCustomMissile("RRCHICKENBOMB",0,1,8,0)
			Goto A
			TNT1 A 0 A_FireCustomMissile("RRCHICKENBOMB_2",0,1,8,0)
		A:	
			CH_R CCC 1 Bright
			CH_R DDDDDDDD 1 Bright
		Reload:
			TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,1)
			Goto ReadyAmmoless
			TNT1 A 0 A_PlaySound("RR/XBOWReload",CHAN_BODY)
			CH_R EEEE 1 Bright A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
			CH_R FFFF 1 Bright A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
			CH_R GGGG 1 Bright A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
			CH_R HHHHH 1 Bright A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
			CH_R AAAAAA 1 Bright A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
			Goto Ready
		IntroTaunt:
			TNT1 A 0 A_JumpIfInventory("LeonardBallgag",1,2)
			TNT1 A 0 A_PlaySoundEx("Leonard/Enter", "SoundSlot7", 0, 0)
			TNT1 A 0 A_TakeInventory("LeonardReady", 1)
			TNT1 A 0 A_GiveInventory("LeonardTauntCooldown",5)
			TNT1 A 0 ACS_ExecuteAlways(706, 0)
			Goto Ready
		WaitingTaunt:
			TNT1 A 0 A_JumpIfInventory("LeonardBallgag",1,2)
			TNT1 A 0 A_PlaySoundEx("Leonard/Bored", "SoundSlot7", 0, 0)
			TNT1 A 0 A_TakeInventory("LeonardWaiting", 15)
			TNT1 A 0 A_GiveInventory("LeonardTauntCooldown",5)
			TNT1 A 0 ACS_ExecuteAlways(706, 0)
			Goto Ready
		SetDrunkView:
			TNT1 A 0 A_ZoomFactor(0.55)
			Goto Ready+1
	    SetDrunkViewFire:
			TNT1 A 0 A_ZoomFactor(0.55)
			Goto Fire+1
		SetSoberView:
			TNT1 A 0 A_ZoomFactor(1.0)
			Goto Ready+1
	    SetSoberViewFire:
			TNT1 A 0 A_ZoomFactor(1.0)
			Goto Fire+1
	}
}

Actor RRCHICKENBOMB
{
	Height 4
	Radius 8
	Decal Sorch
    Speed 50
	Scale 0.35
	Damage (140)
	Projectile
	+NOBLOOD
	+BLOODLESSIMPACT
	+THRUGHOST	
	+SEEKERMISSILE
	+SCREENSEEKER
	DamageType "LeonardDamage2"	
    States
    {
      Spawn:
		TNT1 A 0
	  SpawnLoop:
		TNT1 A 0 A_SpawnItemEx("RRCOCKFEATHER",0,0,0,random(-60,60)/10.00,random(-60,60)/10.00,random(5,20)/10.00,SXF_CLIENTSIDE)
		CH_R M 1 Bright A_SpawnItemEx("RRLEONARDSMOKET",0,0,0,0,0,4,0,0,SXF_CLIENTSIDE)
        CH_R MM 1 Bright A_SeekerMissile(90,90)
		Loop
      Death:
		CH_R M 10 Bright A_PlaySound("RR/COCKEXP")
		TNT1 A 0 A_Explode(140,140)
		TNT1 A 5 A_SpawnItemEx("RRFAKEEXPLOSION",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
        Stop	   		
     XDeath:
	  Crash:
		TNT1 A 0 A_Explode(140,140)
		TNT1 A 0 A_SpawnItemEx("RRFAKEEXPLOSION",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
		TNT1 A 5 A_PlaySound("RR/COCKBOOM")
        Stop	   
    }
}

Actor RRCHICKENBOMB_2 : RRCHICKENBOMB
{
	Species "Player"
	+THRUSPECIES
	+DONTHARMCLASS
	+DONTHARMSPECIES
	+MTHRUSPECIES
	Damage (100)	
	States
	{
      Death:
		CH_R M 10 Bright A_PlaySound("RR/COCKEXP")
		TNT1 A 0 A_Explode(140,142)
		TNT1 A 5 A_SpawnItemEx("RRFAKEEXPLOSION",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
        Stop	   		
     XDeath:
	  Crash:
		TNT1 A 0 A_Explode(140,142)
		TNT1 A 0 A_SpawnItemEx("RRFAKEEXPLOSION",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
		TNT1 A 5 A_PlaySound("RR/COCKBOOM")
        Stop	   
	}
}	
		
Actor RRFAKEEXPLOSION
{
  +NOINTERACTION
  +CLIENTSIDEONLY
  states
  {
  Spawn:
		CH_R R 2 Bright
		CH_R SSTTUUVVWWXXYYZZ 1 Bright
		CH_7 AABBCCDDEEFFGGHHIIJJKKL 1 Bright
		CH_7 LL 1 Bright A_FadeOut
    Stop
  }
}