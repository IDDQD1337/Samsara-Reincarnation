actor WolfenClassMode : Counter { Inventory.MaxAmount 2}
actor Wolfen_IHaveExtraLife : Boolean {}
actor Totenkopf_IHaveDualPistols : Boolean {}
actor Totenkopf_IHaveDualMP40s : Boolean {}
actor Totenkopf_IHaveMauser : Boolean {}
actor Totenkopf_IHaveDualMausers : Boolean {}
actor Totenkopf_IHaveSTG44 : Boolean {}
actor Totenkopf_IHaveFlamethrower : Boolean {}
actor Totenkopf_IHaveMG42 : Boolean {}
actor Totenkopf_IHaveScope : Boolean {}
actor Totenkopf_IHavePowerArmor : Boolean {}
actor Totenkopf_IAmWolverine : Boolean {}
actor Totenkopf_GimmeDat : Boolean {}
actor WolfenPlayer : PlayerPawn
{
    Player.DisplayName "Blazkowicz"
    Player.StartItem "Luger", 1
    Player.StartItem "Knife", 1
    Player.StartItem "WolfenClass", 1
    Player.StartItem "CanWolfMovement", 1
    Player.StartItem "IsSamsaraClass", 1
	Player.StartItem "TauntButton", 1
    Player.StartItem "Clip", 50
    Player.Face "BJF"
    Player.JumpZ 9//Default is 8
    BloodColor "Red"
    Player.Soundclass "WolfenPlayer"
	DamageFactor "RebelMissile", 0
    DamageFactor "PoisonCloud", 1.75
    //DamageFactor "Wraithverge", 0.75
	DamageFactor "DynamiteCoop", 0.0	
    DamageFactor "CorvusCrossbow", 1.1
    DamageFactor "MightyBoot", 1.5
    DamageFactor "DragonClaw", 1.1
    DamageFactor "WMCBeam", 0.75
    DamageFactor "WMCNade", 0.75
	DamageFactor "EnemyInfightDamage", 0.0
	DamageFactor "ElectricBolt", 1.0
	DamageFactor "PhosFireCoop", 0.0
	DamageFactor "EradCoopExplosive2", 0.0
    DamageFactor "C7MineBoom", 0.0	
    DamageFactor "EradCoopGreenFire", 0.0	
	DamageFactor "BCannonProj", 0.75	
	BloodType "SamsaraBloodyHellBlood"
    DamageFactor "RMR-BigPlasma", 0.85		
	DamageFactor "SWNukeCoop", 0.0		
	DamageFactor "SWRailgunCoop", 0.0
	DamageFactor "SWFlameCoop", 0.0
	DamageFactor "SWFlame2Coop", 0.0
	DamageFactor "SWStickyCoop", 0.0	
	DamageFactor "CMDMGSHIT", 0.0	
    +NOSKIN
    -DONTBLAST
    -THRUSPECIES
    Species "Player"
    Player.ColorRange 112, 127
    States
    {
      Spawn:
        TNT1 A 0 A_TakeInventory("WolfCheckMoving")
        BJBL A 1 A_JumpIfInventory("WolfMoving", 1, "See")
        Loop
      See:
        BJBL AAAABBBBCCCCDDDD 1 A_GiveInventory("WolfCheckMoving")
        Loop
      Missile:
		BJBL E 6
        BJBL F 6 Bright	  
        Goto Spawn
      Melee:
        BJBL F 3 BRIGHT		
        Goto Missile
      Pain:
        TNT1 A 0 A_JumpIfInventory("AlreadyDiedOnceBJ",1,"ChanceToFail")
        TNT1 A 0 A_JumpIfInventory("WolfExtraLife",1,"GoingToSalvate")
        BJBL G 4 
        BJBL G 4 A_Pain
        Goto Spawn
	  ChanceToFail:
		TNT1 A 0 A_Jump(128,"SalvationFail")
        BJBL G 4 A_JumpIfHealthLower(20, "GoingTo")
        BJBL G 4 A_Pain
        Goto Spawn
	  SalvationFail:
        BJBL G 4 
        BJBL G 4 A_Pain
        Goto Spawn
      GoingToSalvate:
        BJBL G 4 A_JumpIfHealthLower(20, "GoingTo")
        BJBL G 4 A_Pain
        Goto Spawn
      GoingTo: // Deathmatch Version
	    TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"GoingToUnNerfed")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"GoingToUnNerfedCoop")
		TNT1 A 0 A_GiveInventory("Medikit")
        TNT1 A 0 A_GiveInventory("Medikit")
        TNT1 A 0 A_GiveInventory("Medikit")
        TNT1 A 0 A_GiveInventory("Medikit")
		TNT1 A 0 A_TakeInventory("Clip",999)
		TNT1 A 0 A_TakeInventory("RocketAmmo",999)
		TNT1 A 0 A_TakeInventory("Cell",999)
		TNT1 A 0 A_TakeInventory("Armor",999)
		TNT1 A 0 A_TakeInventory("BJSuperKnife",1)
		TNT1 A 0 A_TakeInventory("Machine Gun",1)
		TNT1 A 0 A_TakeInventory("  Chaingun  ",1)
		TNT1 A 0 A_TakeInventory(" Rocket Launcher ",1)
		TNT1 A 0 A_TakeInventory(" Flamethrower ",1)
		TNT1 A 0 A_TakeInventory("Spear of Destiny",1)
		TNT1 A 0 A_GiveInventory("Clip",50)
		TNT1 A 0 A_GiveInventory("Salvate")
		TNT1 A 0 A_TakeInventory("Wolfen_IHaveExtraLife")
        //TNT1 A 0 A_GiveInventory("WolfLifeProtection")
        TNT1 A 0 A_PlayerScream
        BJBL G 4 A_TakeInventory("WolfExtraLife",1)
        BJBL G 4
        Goto Spawn
	  GoingToUnNerfed: // SP Version
        TNT1 A 0 A_GiveInventory("Medikit")
        TNT1 A 0 A_GiveInventory("Medikit")
        TNT1 A 0 A_GiveInventory("Medikit")
        TNT1 A 0 A_GiveInventory("Medikit")
        TNT1 A 0 A_GiveInventory("Clip",50)
        TNT1 A 0 A_GiveInventory("BlueArmor")
        TNT1 A 0 A_GiveInventory("Salvate")
        TNT1 A 0 A_GiveInventory("WolfLifeProtection")
		TNT1 A 0 A_TakeInventory("Wolfen_IHaveExtraLife")
        TNT1 A 0 A_PlayerScream
        BJBL G 4 A_TakeInventory("WolfExtraLife",1)
        BJBL G 4
        Goto Spawn
	  GoingToUnNerfedCoop: // CO-OP Version
		TNT1 A 0 A_GiveInventory("AlreadyDiedOnceBJ",1) //Prevents BJ beign immortal on survival
        TNT1 A 0 A_GiveInventory("Medikit")
        TNT1 A 0 A_GiveInventory("Medikit")
        TNT1 A 0 A_GiveInventory("Medikit")
        TNT1 A 0 A_GiveInventory("Medikit")
        TNT1 A 0 A_GiveInventory("Clip",50)
        TNT1 A 0 A_GiveInventory("BlueArmor")
        TNT1 A 0 A_GiveInventory("Salvate")
        TNT1 A 0 A_GiveInventory("WolfLifeProtection")	
		TNT1 A 0 A_TakeInventory("Wolfen_IHaveExtraLife")
        TNT1 A 0 A_PlayerScream
        BJBL G 4 A_TakeInventory("WolfExtraLife",1)
        BJBL G 4
        Goto Spawn
	  Death:
	    TNT1 A 0 A_SpawnItem("UniversalBloodyHellGoreDeathEffect")
        BJBL G 8 A_PlayerScream
        BJBL H 8 A_NoBlocking
        BJBL I 8
        BJBL JKL 8
        BJBL M -1
        Stop
	  XDeath:	
	    TNT1 A 0 A_SpawnItem("UniversalBloodyHellGoreXDeathEffect")
        BJGB A 5 A_PlayerScream
        BJGB B 5 A_NoBlocking
        BJGB C 5
        BJGB DEFGHI 4
        BJGB J -1
        Stop
      Death.CorvusGrimBallista:
        BJBL G 8 A_PlaySoundEx("*falling",CHAN_VOICE,0,0)
        BJBL H 8 A_NoBlocking
        BJBL I 8
        BJBL JKL 8
        BJBL M -1
        Stop
      Death.ZorchPistol:
      Death.Zorch:
      Death.ZorchKamikaze:
      Death.PhasingZorch:
      Death.ZorchSpectral:
        BJBL G 0 A_Stop
        TNT1 A 0 A_PlaySoundEx("chex/zorch","soundslot5",0,0)
        BJBL G 3 A_SpawnItem("ZorchEffectSmall")
        BJBL G 3 A_SetTranslucent(0.9,0)
        BJBL G 0 A_PlayerScream
        BJBL G 3 A_SetTranslucent(0.8,0)
        BJBL G 0 A_NoBlocking
        BJBL G 3 A_SetTranslucent(0.7,0)
        BJBL G 3 A_SetTranslucent(0.6,0)
        BJBL G 3 A_SetTranslucent(0.5,0)
        BJBL G 3 A_SetTranslucent(0.4,0)
        BJBL G 3 A_SetTranslucent(0.3,0)
        BJBL G 3 A_SetTranslucent(0.2,0)
        BJBL G 3 A_SetTranslucent(0.1,0)
        ACLO E 1 A_SetTranslucent(1,0)
        ACLO E -1
        stop
      CoopModeOn:
        TNT1 A 0
        TNT1 A 0 A_ChangeFlag("THRUSPECIES",1)
        TNT1 A 0 A_ChangeFlag("DONTBLAST",1)
        BJBL A 1
        goto Spawn
      Death.DukeDamage:
      Death.DukePistol:
        TNT1 A 0 A_Jump(218,"Death")
        TNT1 A 0 A_GiveToTarget("DukeDMTaunt1",1)
        goto Death
      Death.DukeExplosive:
      Death.DukeSpectral:
        TNT1 A 0 A_JumpIfHealthLower(-5,"DukeGibStart")
        goto Death.DukeDamage
      DukeGibStart:
	    BJBL G 1
        TNT1 A 0 A_UnsetSolid
        TNT1 A 0 A_PlaySound("duke/enemygib")
        TNT1 A 0 A_SpawnDebris("DukeGibs1")
        TNT1 A 0 A_SpawnDebris("DukeGibs2")
        TNT1 A 0 A_SpawnDebris("DukeGibs3")
        TNT1 A 0 A_SpawnDebris("DukeGibs4")
        TNT1 A 0 A_SpawnDebris("DukeGibs5")
        TNT1 A 0 A_SpawnDebris("DukeGibs6")
        TNT1 A 0 A_Jump(96,"XDeath")
        TNT1 A 0 A_GiveToTarget("DukeDMTaunt1",1)
        Goto XDeath
      DukeXDeath:
	    TNT1 A 0 A_SpawnItem("UniversalBloodyHellGoreXDeathEffect")
        BJGB A 5 A_PlayerScream
        BJGB B 5 A_NoBlocking
        BJGB C 5
        BJGB DEFGHI 4
        BJGB J -1
        Stop
      Death.DukeIce:
        TNT1 A 0 A_Jump(218,"Ice")
        TNT1 A 0 A_GiveToTarget("DukeDMTaunt1",1)
        goto Ice
      Ice:
      Death.HexenIce:
      Death.Ice:
        TNT1 A 0 A_PlayWeaponSound("misc/freeze")
        BJBL Z 70 A_NoBlocking
        BJBL Z 0 A_SetTranslucent(0,0)
        TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("IceChunk2",random(-1,1),random(-1,1),random(8,48),random(-2,2),random(-2,2),random(1,6))
        BJBL Z 1 A_PlayWeaponSound("misc/icebreak")
        BJBL Z -1
        stop		
	  Death.PhosFireCoop:
      Death.HexGreenFire:
      Death.QuakeFire:
	  Death.FireExplosive:
      Burn:
        BJBR A 4 A_PlayerScream
        BJBR B 4 A_NoBlocking
        BJBR C 4
        BJBR DEFGH 4
        BJBR I -1
        Stop
      Pain.MightyBoot:
	    BJBL G 1 ACS_ExecuteAlways(312, 0, 25, 5, 24)
        BJBL G 1 A_PlaySoundEx("duke/kickhit","body",0,0)
        TNT1 A 0 A_Recoil(12)
        BJBL G 6 A_Pain
        Goto See
	  Pain.MarathonStun:
        TNT1 A 0 A_Stop
	    BJBL G 1 ACS_ExecuteAlways(312, 0, 25, 5, 100)
		TNT1 A 0 A_GiveInventory("NothingSpeed")
		TNT1 A 0 A_SetBlend("White",0.9,105)
        BJBL G 1 A_PlaySoundEx("marathon/tech50/stun","body",0,0)
        BJBL G 6 A_Pain
        Goto See
	  Pain.LifeLeech:
		TNT1 A 0 A_GiveToTarget("SerpentHealth", random(1,2))
		TNT1 A 0 A_GiveToTarget("HealthBonus",1)
		Goto Pain
	  Death.Dynamite:
	  Death.LifeLeech:
	  Death.CalebBullet:
	  Death.CalebNapalm:
	  Death.Tesla:
	  Death.Vodoo:
		TNT1 A 0 A_Jump(218, "Death")
		TNT1 A 0 A_GiveToTarget("CalebDMTaunt1", 1)
		Goto Death 
	  Death.CalebFire:
		TNT1 A 0 A_Jump(218, "Burn")
		TNT1 A 0 A_GiveToTarget("CalebDMTaunt1", 1)
		Goto Burn	
	  Death.RottExplosive:
		TNT1 A 0 A_SpawnItemEx("RottGibbedMan",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE)
		BLAN A 1
		TNT1 A 0 A_Jump(128,2)
		TNT1 A 0 A_GiveToTarget("LudicrousGibsItem", 1)
		TNT1 AA 0
		BLAN A 5 A_NoBlocking
		Stop
	  Death.RottFire:
	    TNT1 A 0 A_Scream
		ske7 A 0 A_playsound("gen/burned") 
		ske7 ABCDEFGHIJKLMNOPQRSTUVWXYZ 2 bright
		skex ABCDEFGHIJKL 2 bright
		skex M 0 A_playsound("gen/skel") 
		skex MNOPQRSTUV 3 bright
		skex V -1 A_Fall    
	    Stop		 
	  Death.RottDisintegrate:
		TNT1 A 0 A_Scream
		TNT1 A 0 A_UnsetSolid		
		ACLO E 5 A_SpawnItem("RGodModeDeath")
		BLAN A -1 A_Fall
		Stop	
      Death.EradDisc:
	  Death.EradShock:
	  Death.EradPiercingDart:
	  Death.EradExplosive:
	  Death.EradNova:
	  Death.EradExplosive2:	  
		TNT1 A 0 A_Jump(218, "Death")
		TNT1 A 0 A_GiveToTarget("EleenaDMTaunt1", 1)
		Goto Death
	  Death.EradGreenFire:
	  Death.EradCoopGreenFire:	  
		TNT1 A 0 A_Jump(218, "Burn")
		TNT1 A 0 A_GiveToTarget("EleenaDMTaunt1", 1)
		Goto Burn	
      Disintegrate:
        TNT1 A 0 A_GiveInventory("EnemyZorched",1)
        TNT1 A 0 A_NoBlocking
        TNT1 A 0 A_ChangeFlag("DONTGIB",1)
        TNT1 A 0 A_SpawnItemEx("BlazkoDisintegrate",0,0,5,0,0,0,0,32)
        ACLO E 5 A_PlayerScream
        ACLO E -1
        stop		
	  Death.Nuke:
		ACLO E 0 A_UnsetSolid
		ACLO E 0 A_PlayerScream
		ACLO E 0 A_NoBlocking
		ACLO E 5 A_SpawnItemEx("BlazkoNuke")
		ACLO E -1
		stop
	}
}

Actor BlazkoNuke
{
	-SOLID
	+NOBLOCKMAP
	+CLIENTSIDEONLY
	states
	{
	Spawn:
		NK24 ABCDEFG 5 bright
		stop
	}
}

actor BlazkoDisintegrate : ZombiemanDisintegrate 
{
   States
   {
      Spawn:
        TNT1 A 0
        BJBL G 3 A_SpawnItem("DisintEffectSmall",0,10)
        BJBL G 3 A_SetTranslucent(0.9,0)
        BJBL G 3 A_SetTranslucent(0.8,0)
        BJBL G 3 A_SetTranslucent(0.7,0)
        BJBL H 3 A_SetTranslucent(0.6,0)
        BJBL H 3 A_SetTranslucent(0.5,0)
        BJBL H 2 A_SetTranslucent(0.4,0)
        BJBL H 2 A_SetTranslucent(0.3,0)
        BJBL I 2 A_SetTranslucent(0.2,0)
        BJBL I 2 A_SetTranslucent(0.1,0)
        Stop
   }
}

actor WolfenAmmoCrate : CustomInventory
{
    +INVENTORY.INVBAR
    +INVENTORY.UNDROPPABLE
    Inventory.maxamount 1
    Inventory.Icon "BJAMMCRT"
    Inventory.UseSound "wolfen/ammocrate"
    States
    {
      Spawn:
        ACLO E 5
        Stop

      Use:
        TNT1 A 0 A_SetBlend("white",0.1,1)
        TNT1 A 0 A_GiveInventory("Clip", 400)
        Stop
    }
}

actor WolfMoving: Boolean {}
actor CanWolfMovement : Boolean {}
actor WolfenMovement : Boolean {}
actor WolfenClass : Boolean {}

actor WolfCheckMoving: CustomInventory
{
    States
    {
      Pickup:
        TNT1 A 0 A_JumpIfInventory("WolfMoving",1,"DoNothing")
        TNT1 A 0 A_JumpIfInventory("WolfenMovement",1,"DoStuff")

      DoNothing:
        TNT1 A 0
        stop

      DoStuff:
        TNT1 A 0 ACS_ExecuteAlways(SAMSARA_DECORATE, 0, 2)
        stop
    }
}

actor WolfenBullet : FastProjectile
{
    Damage (Random(0,256)/4)
    Radius 1
    Height 1
    Speed 320
    Projectile
    Renderstyle "Normal"
    Species "Player"
    +BLOODSPLATTER
	+NOEXTREMEDEATH
	+NODAMAGETHRUST
    +THRUGHOST
    +THRUSPECIES
    +NOTIMEFREEZE
    Alpha 1.0
    States
    {
      Spawn:
        TNT1 A 1
        loop

      Death:
        TNT1 A 1
        stop
    }
}

actor WolfenBullet2 : WolfenBullet { Damage (Random(0,256)/8) }
Actor WolfenBullet3 : WolfenBullet { Damage (Random(0,256)/16) }

actor BlankPuff
{
    +CLIENTSIDEONLY
	+NOEXTREMEDEATH
	+NODAMAGETHRUST
    States
    {
      Spawn:
        TNT1 A 1
        Stop
    }
}

actor BlankPuffBloodless : BlankPuff { -BLOODSPLATTER +BLOODLESSIMPACT }
Actor AlreadyDiedOnceBJ : Inventory { Inventory.Amount 1 Inventory.MaxAmount 1 }

actor Totenkopf_BlankPuff
{
    +CLIENTSIDEONLY
	+NOEXTREMEDEATH
	-BLOODSPLATTER
	+BLOODLESSIMPACT
	+NODAMAGETHRUST
    States
    {
      Spawn:
        TNT1 A 1
        Stop
    }
}

Actor Totenkopf_BloodGenerator : CustomInventory
{
	-COUNTITEM
	+INVENTORY.ALWAYSPICKUP
	Inventory.PickupMessage ""
	Inventory.PickupSound ""
	States
	{
		Pickup:
			TNT1 A 0
			TNT1 A 0 A_SpawnItemEx("Totenkopf_Blood",radius*cos(CallACS("Totenkopf_BloodGenerator_Return",1)),radius*sin(CallACS("Totenkopf_BloodGenerator_Return",1)),CallACS("Totenkopf_BloodGenerator_Return",2),0,0,0,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR|SXF_ABSOLUTEPOSITION)
			TNT1 A 0
			stop
	}
}

Actor Totenkopf_Blood
{
	+NOINTERACTION
	+NOGRAVITY
	//+CLIENTSIDEONLY
	States
	{
		Spawn:
			TNT1 A 0
			TK50 ABC 4 A_Stop
			Stop
	}
}

Actor Totenkopf_Blood_Mutant : Totenkopf_Blood
{
	States
	{
		Spawn:
			TNT1 A 0
			TK56 ABC 4 A_Stop
			Stop
	}
}

Actor Totenkopf_BloodGeneratorAngle : Inventory { Inventory.Maxamount 131072 }
Actor Totenkopf_BloodGeneratorZ : Inventory { Inventory.Maxamount 131072 }

Actor Totenkopf_Tracer : FastProjectile
{
	Height 4
	Radius 2
	Speed 512
	Projectile
	DamageType "TotenkopfBullet"
	+BLOODLESSIMPACT
	+HITTRACER
	+NOEXTREMEDEATH
	+NODAMAGETHRUST
	States
	{
		Spawn:
			TNT1 A 1
			Loop
		Crash:
		Death:
			TNT1 A 1
			TNT1 A 0 A_Playsound("Totenkopf/Ricochet", CHAN_BODY)
			TNT1 A 0 A_SpawnItemEx("Totenkopf_Tracer_Spark",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
			Stop
		XDeath:
			TNT1 A 0 A_JumpIf(CallACS("Totenkopf_BloodGenerator_Offsets")==1,1)
			TNT1 A 1 Bright
			TNT1 A 0 A_GiveInventory("Totenkopf_BloodGenerator",1,AAPTR_TRACER)
			TNT1 A 1
			Stop
	}
}

Actor Totenkopf_Tracer_Trail
{
	+NOINTERACTION
	+CLIENTSIDEONLY
	States
	{
		Spawn:
			TK51 AA 1 Bright
			Stop
	}
}

Actor Totenkopf_Tracer_Spark
{
	+NOINTERACTION
	+CLIENTSIDEONLY
	States
	{
		Spawn:
			TK51 B 0
			TK51 BCDE 2 Bright
			Stop
	}
}

Actor Totenkopf_BurningGuard
{
	+ISMONSTER
	+NODAMAGE
	-COUNTKILL
	-SHOOTABLE
	-SOLID
	Height 52
	Radius 16
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_JumpIf(CallACS("Totenkopf_BurningVoice")==1,1)
			TK13 ABCDABCDABCDABCD 5 Bright
			TK13 EFG 5 Bright
			TK13 H -1
			Stop
	}
}

Actor TotenkopfPowerArmor : CustomInventory
{
	Radius 20
	Height 16
	Inventory.PickupMessage "Power Armor: your armor now regenerates!"
	Inventory.PickupSound "Totenkopf/Bonus"
	Inventory.Icon "TK55A0"
	+INVENTORY.ALWAYSPICKUP
	States
	{
		Spawn:
			TK55 A 6 Bright
			Loop
		Pickup:
			TNT1 A 0 A_GiveInventory("TotenkopfPowerArmorPickup",1)
			TNT1 A 0 A_GiveInventory("TotenkopfHasPowerArmor",1)
			stop
	}
}

Actor TotenkopfPowerArmorPickup : BasicArmorPickup
{
	Radius 20
	Height 16
	Inventory.PickupSound "Totenkopf/Bonus"
	Inventory.PickupMessage "Power Armor: your armor now regenerates!"
	Inventory.Icon "TK55A0"
	Armor.SavePercent 50
	Armor.SaveAmount 200
	+INVENTORY.KEEPDEPLETED
	States
	{
		Spawn:
			TK55 A 6 Bright
			Loop
	}
}

Actor TotenkopfPowerArmorKeeper : BasicArmorBonus
{
	Radius 20
	Height 16
	Inventory.Icon "TK55A0"
	Armor.SavePercent 50
	Armor.SaveAmount 1
	Armor.MaxSaveAmount 200
	+INVENTORY.ALWAYSPICKUP
	States
	{
		Spawn:
			TNT1 A 35
			Loop
	}
}

Actor TotenkopfHealingOrb : CustomInventory
{
	Radius 20
	Height 16
	Inventory.PickupMessage "Healing Orb: your health now regenerates!"
	Inventory.PickupSound "Totenkopf/Bonus"
	Inventory.Icon "TK55B0"
	+INVENTORY.ALWAYSPICKUP
	States
	{
		Spawn:
			TK55 A 6 Bright
			Loop
		Pickup:
			TNT1 A 0 A_GiveInventory("TotenkopfHealingOrbHealer",200)
			TNT1 A 0 A_GiveInventory("TotenkopfHasHealingOrb",1)
			stop
	}
}

ACTOR TotenkopfHealingOrbHealer : Health
{
	+COUNTITEM
	+INVENTORY.ALWAYSPICKUP
	Inventory.Amount 1
	Inventory.MaxAmount 200
	Inventory.PickupMessage ""
	States
	{
		Spawn:
			TNT1 A 1
			Loop
	}
}

Actor TotenkopfHasPowerArmor : Inventory { Inventory.Maxamount 1 }
Actor TotenkopfHasHealingOrb : Inventory { Inventory.Maxamount 1 }
Actor TotenkopfSecretCounter : Inventory { Inventory.Maxamount 30 }

Actor WolfLostMonsterSet : Inventory { Inventory.MaxAmount 1 Inventory.InterHubAmount 0 }
Actor WolfLostMapDummy
{
	Projectile
	Damage 0
	+NOINTERACTION
	States
	{
		Spawn:
		Death:
		XDeath:
			TNT1 A 0
			TNT1 A 35 
			TNT1 A 0 A_JumpIfInTargetInventory("WolfenClassMode",2,"Erase")
			TNT1 A 0 A_JumpIfInTargetInventory("WolfenClassMode",1,"Spawn")
			Goto Erase
		Erase:
			TNT1 A 1
			stop
	}
}

Actor TotenkopfMonsterSet : Inventory { Inventory.MaxAmount 1 Inventory.InterHubAmount 0 }
Actor TotenkopfMapDummy
{
	Projectile
	Damage 0
	+NOINTERACTION
	States
	{
		Spawn:
		Death:
		XDeath:
			TNT1 A 0
			TNT1 A 35
			TNT1 A 0 A_JumpIfInTargetInventory("WolfenClassMode",2,"Spawn")
			Goto Erase
		Erase:
			TNT1 A 1
			stop
	}
}