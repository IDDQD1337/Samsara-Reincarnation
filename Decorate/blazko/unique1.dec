actor SamsaraTotenkopfFlamethrowerAttackSilentSound : Boolean {}

actor Totenkopf_Flamethrower : InstantSwitchWeapon
{
    Tag "Flammenwerfer" // "Flamethrower" already exists and would be confusing with Original/Lost Missions flamethrower
	Weapon.SelectionOrder 400
    Weapon.SlotNumber 8
    Inventory.PickUpSound "Totenkopf/getweapon"
    Weapon.AmmoUse 1
    Weapon.AmmoGive 0
    Weapon.AmmoType "Totenkopf_FlamethrowerFuel"
    +DONTBOB
    +INVENTORY.UNDROPPABLE
    Inventory.PickupMessage "Flamethrower"
	Inventory.RestrictedTo "WolfenPlayer"		
    Weapon.YAdjust 16
    States
    {
      Spawn:
        TK06 F -1
        stop

      Ready:
        TK06 A 1 A_WeaponReady
        loop

      Fire:
        TK06 B 2
      Hold:
        TNT1 A 0 A_TakeInventory("SamsaraTotenkopfFlamethrowerAttackSilentSound")
        TK06 C 2 Bright A_GiveInventory("SamsaraTotenkopfFlamethrowerAttackHandler")
        TNT1 A 0 A_GiveInventory("SamsaraTotenkopfFlamethrowerAttackSilentSound")
        TK06 DCDC 2 Bright A_GiveInventory("SamsaraTotenkopfFlamethrowerAttackHandler")
        TK06 D 4 Bright A_GiveInventory("SamsaraTotenkopfFlamethrowerAttackHandler")
        TK06 E 3 A_ReFire
        goto Ready
    }
}

actor SamsaraTotenkopfFlamethrowerAttackHandler : Trigger
{
    States
    {
      // Ammo check stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupTakeAmmo")
        TNT1 A 0 A_JumpIfInventory("Totenkopf_FlamethrowerFuel",1,"PickupTakeAmmo")
        stop

      // Ammo taking stuff
      PickupTakeAmmo:
        TNT1 A 0 A_TakeInventory("Totenkopf_FlamethrowerFuel",1,TIF_NOTAKEINFINITE)
        goto PickupPlaySound

      // Sound stuff
      PickupPlaySound:
        TNT1 A 0 A_JumpIfInventory("SamsaraTotenkopfFlamethrowerAttackSilentSound",1,"PickupAttack")
        TNT1 A 0 A_PlaySound("Totenkopf/flamethrower",CHAN_WEAPON)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("Totenkopf_Flamethrower_Projectile",0,false,0,8)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("Totenkopf_Flamethrower_ProjectileCoop",0,false,0,8)
        stop
    }
}

Actor Totenkopf_Flamethrower_Projectile
{
	Speed 24
	Damage (random(0,256)>>3)
	Height 16
	Radius 8
	Projectile
	DamageType "TotenkopfFire"
	+BLOODLESSIMPACT
	+NOEXTREMEDEATH
	+NODAMAGETHRUST
	States
	{
		Spawn:
			TK52 ABABCDCD 3 Bright
		Death:
			TK52 A 0 A_Stop
			TK52 EFG 2 Bright
			Stop
	}
} 
Actor Totenkopf_Flamethrower_ProjectileCoop : Totenkopf_Flamethrower_Projectile { Species "Player" +THRUSPECIES }

Actor Totenkopf_FlameThrowerFuel : Ammo 
{
	Inventory.MaxAmount 200
	Ammo.BackPackMaxAmount 400
	Ammo.BackPackAmount 40
	Inventory.Icon "AMSMN0"
}	
