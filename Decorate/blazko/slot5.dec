actor " Rocket Launcher ": InstantSwitchWeapon
{
    Weapon.SelectionOrder 2500
    Weapon.SlotNumber 5
    Inventory.PickUpSound "wolfen/rocketget"
    Weapon.AmmoUse 1
    Weapon.AmmoGive 2
    Weapon.AmmoType "RocketAmmo"
    //+AMMO_OPTIONAL
    +DONTBOB
    +INVENTORY.UNDROPPABLE
    Inventory.PickupMessage "You got the Rocket Launcher!"
    Weapon.YAdjust 16
	Inventory.RestrictedTo "WolfenPlayer"
    States
    {
      Spawn:
        WRLP A -1
        Stop

      Ready:
		TNT1 A 0 A_JumpIfInventory("WolfenLostMode",1,2)
		WRCK A 1 A_WeaponReady
        loop
        WRCK E 1 A_WeaponReady
        loop

      Fire:
        TNT1 A 0 A_JumpIfInventory("WolfenLostMode",1,"FireLost")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopFire")
        TNT1 A 0 A_JumpIfInventory("RJingModeOn",1,"RJFire")
        WRCK B 4 BRIGHT A_FireCustomMissile("WolfRocket")
        WRCK CDCA 4
        WRCK A 0 A_Refire
        Goto Ready
        
      RJFire:
        WRCK B 4 BRIGHT A_FireCustomMissile("WolfRocketRJ")
        WRCK CDCA 4
        WRCK A 0 A_Refire
        Goto Ready

      CoopFire:
        TNT1 A 0 A_JumpIfInventory("RJingModeOn",1,"RJFireCoop")
        WRCK B 4 BRIGHT A_FireCustomMissile("WolfRocket2")
        WRCK CDCA 4
        WRCK A 0 A_Refire
        Goto Ready
        
      RJFireCoop:
        WRCK B 4 BRIGHT A_FireCustomMissile("WolfRocket2RJ")
        WRCK CDCA 4
        WRCK A 0 A_Refire
        Goto Ready
		
	  FireLost:
	    TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopFireLost")
        TNT1 A 0 A_JumpIfInventory("RJingModeOn",1,"RJFireLost")
        WRCK F 4 BRIGHT A_FireCustomMissile("LostRocket")
        WRCK GHGE 4
        WRCK E 0 A_Refire
        Goto Ready
		
	  RJFireLost:
        WRCK F 4 BRIGHT A_FireCustomMissile("LostRocketRJ")
        WRCK GHGE 4
        WRCK E 0 A_Refire
        Goto Ready

      CoopFireLost:
        TNT1 A 0 A_JumpIfInventory("RJingModeOn",1,"RJFireCoopLost")
        WRCK F 4 BRIGHT A_FireCustomMissile("LostRocket2")
        WRCK GHGE 4
        WRCK E 0 A_Refire
        Goto Ready
        
      RJFireCoopLost:
        WRCK F 4 BRIGHT A_FireCustomMissile("LostRocket2RJ")
        WRCK GHGE 4
        WRCK E 0 A_Refire
        Goto Ready
    }
}

actor WolfRocket
{
    Scale 0.75
    Radius 8
    Height 6
    Speed 30
    Damage 15
    Projectile
    +RIPPER
    +FORCERADIUSDMG
    +NOEXTREMEDEATH
    +NOBOSSRIP
    SeeSound "wolfen/missilefire"
    DeathSound "wolfen/missilexplod"
    Obituary "$SAMSARA_WOLFEN_OB_SLOT5"
    States
    {
      Spawn:
        WMSL A 3 A_SpawnItem("WolfRocketSmoke")
        Loop

      Death:
        WFLB C 8 Bright A_Explode(128,32,0)
        WFLB D 6 Bright
        WFLB E 4 Bright
        Stop
    }
}

actor WolfRocketRJ : WolfRocket
{
    States
    {
      Death:
        WFLB C 8 Bright A_Explode(128,64)
        WFLB D 6 Bright
        WFLB E 4 Bright
        Stop
    }
}

actor WolfRocket2: WolfRocket { +THRUSPECIES Species "Player" }
actor WolfRocket2RJ: WolfRocketRJ { +THRUSPECIES Species "Player" }

actor WolfRocketSmoke
{
    Radius 1
    Height 1
    Speed 0
    Renderstyle Translucent
    Alpha 0.5
    +NOGRAVITY
    -SOLID
    +NOCLIP
    +CLIENTSIDEONLY
    States
    {
      Spawn:
        TNT1 A 2
        RTRL ABCD 4
        stop
    }
}

actor LostRocket : WolfRocket
{
	States
	{
	  Spawn:
        WRK2 A 3 A_SpawnItem("SODRocketSmoke")
        Loop

      Death:
        WEX2 A 8 Bright A_Explode(128,32,0)
        WEX2 B 6 Bright
        WEX2 C 4 Bright
        Stop
	}
}

actor LostRocketRJ : LostRocket
{
    States
    {
      Death:
        WEX2 A 8 Bright A_Explode(128,64)
        WEX2 B 6 Bright
        WEX2 C 4 Bright
        Stop
    }
}

actor LostRocket2: LostRocket { +THRUSPECIES Species "Player" }
actor LostRocket2RJ: LostRocketRJ { +THRUSPECIES Species "Player" }

ACTOR SODRocketSmoke : WolfRocketSmoke
{
	States
	{
		Spawn:
			TNT1 A 2
			WSM2 ABCD 4
			Stop
	}
}
