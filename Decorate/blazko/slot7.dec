actor "Spear of Destiny": InstantSwitchWeapon
{
    Weapon.SelectionOrder 2900
    Inventory.PickUpSound "wolfen/spearget"
    Weapon.AmmoUse 40
    Weapon.AmmoGive 120
    Weapon.AmmoType "Cell"
    +DONTBOB
    +INVENTORY.UNDROPPABLE
    +WEAPON.NOAUTOFIRE
	+WEAPON.AMMO_OPTIONAL
    +WEAPON.BFG
    +NOALERT
	Inventory.RestrictedTo "WolfenPlayer"
    Obituary "$SAMSARA_WOLFEN_OB_SLOT7" 
    Inventory.PickupMessage "You got the Spear of Destiny!"
	Weapon.YAdjust 16
    Tag "Spear of Destiny"
    States
    {
      Spawn:
        WSPR E -1
        stop

      // Original
      Ready:
        TNT1 A 0 A_JumpIfInventory("WolfenClassMode",2,"ReadyCTK")
        TNT1 A 0 A_JumpIfInventory("WolfenClassMode",1,"ReadyLost")
        WSPR A 1 A_WeaponReady
        loop

      Fire:
        TNT1 A 0 A_JumpIfInventory("WolfenClassMode",2,"FireCTK")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart")
        TNT1 A 0 A_JumpIfInventory("Cell",40,"FireStart")
      FireNoAmmo:
        TNT1 A 0 A_ClearReFire
        TNT1 A 0 A_SelectWeapon("Luger")
        goto Deselect

      FireStart:
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_PlaySound("wolfen/spearready",CHAN_WEAPON)
        WSPR B 20 A_JumpIfInventory("WolfenClassMode",1,"FireLost")
        WSPR C 15 Bright
        WSPR D 15 Bright A_GiveInventory("SamsaraWolfenSpearOfDestinyAttackHandler")
        WSPR C 15 Bright
        WSPR B 15
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Cell",40,1)
        goto FireNoAmmo
        WSPR A 5 A_ReFire
        goto Ready

      Hold:
        TNT1 A 0 A_JumpIfInventory("WolfenClassMode",2,"HoldCTK")	  
        goto Fire

      // Lost Missions
      ReadyLost:
        WSPR J 1 A_WeaponReady
        goto Ready

      FireLost:
        WSPR K 20
        WSPR L 15 Bright
        WSPR M 15 Bright A_GiveInventory("SamsaraWolfenSpearOfDestinyAttackHandler")
        WSPR L 15 Bright
        WSPR K 15
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Cell",40,1)
        goto FireNoAmmo
        WSPR J 5 A_ReFire
        goto Ready

      // Castle Totenkopf SDL (Flakgun)
      ReadyCTK:
        TK09 A 1 A_WeaponReady
        goto Ready

      FireCTK:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStartCTK")
        TNT1 A 0 A_JumpIfInventory("Cell",3,"FireStartCTK")
        goto FireNoAmmo

      FireStartCTK:
        TK09 B 3 A_AlertMonsters
        goto HoldStartCTK

      HoldCTK:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Cell",3,1)
        goto FireNoAmmo
        TNT1 A 0 A_AlertMonsters
      HoldStartCTK:
        TK09 C 3 Bright A_GiveInventory("SamsaraTotenkopfFlakgunAttackHandler")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Cell",3,1)
        goto FireNoAmmo
        TK09 D 4 Bright A_GiveInventory("SamsaraTotenkopfFlakgunAttackHandler")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Cell",3,1)
        goto FireNoAmmo
        TK09 E 3 A_ReFire
        goto Ready
    }
}

// Original/Lost Missions

actor SamsaraWolfenSpearOfDestinyAttackHandler : Trigger
{
    States
    {
      // Ammo check stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupTakeAmmo")
        TNT1 A 0 A_JumpIfInventory("Cell",40,"PickupTakeAmmo")
        stop

      // Ammo taking stuff
      PickupTakeAmmo:
        TNT1 A 0 A_TakeInventory("Cell",40,TIF_NOTAKEINFINITE)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("wolfen/spearfire",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("SpearBoltDM",0,false)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("SpearBolt2",0,false)
        stop
    }
}

actor SpearBolt
{
    Speed 30 // Normal BFG Ball speed is 25
    Height 2
    Radius 4
    Mass 9
    Damage 55
    SeeSound ""
    DeathSound "wolfen/spearexplode1"
    Obituary "$SAMSARA_WOLFEN_OB_SLOT7" 
    PROJECTILE
    +DONTREFLECT
    DamageType "BJSpear"
    Scale 0.80
    RENDERSTYLE ADD
    States
    {
      Spawn:
        SPB1 ABABABAB 3 BRIGHT A_SpawnItemEx("SpearShotTrail", 0, 0, 0)
        goto Death

      Death:
        SPX1 A 0 A_Stop
        SPX1 AAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("SpearShotMini", 0, 0, random(-180, 180), 2, random(-25, 25))
        SPX1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("SpearShotMini2", 0, 0, random(-180, 180), 2, random(-25, 25))
        SPX1 A 0 A_Scream
        SPX1 A 4 BRIGHT A_Explode(120, 240, 0)
        SPX1 BCDEF 4 BRIGHT
        Stop
    }
}

actor SpearBoltDM: SpearBolt
{
    DamageType "BJSpear"
    Obituary "$SAMSARA_WOLFEN_OB_SLOT7" 
    States
    {
      Death:
        SPX1 A 0 A_Stop
        SPX1 AAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("SpearShotMiniDM", 0, 0, random(-180, 180), 2, random(-25, 25))
        SPX1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("SpearShotMini2DM", 0, 0, random(-180, 180), 2, random(-25, 25))
        SPX1 A 0 A_Scream
        SPX1 A 4 BRIGHT A_Explode(120, 198, 0)
        SPX1 BCDEF 4 BRIGHT
        Stop
    }
}

actor SpearBolt2: SpearBolt
{
    +THRUSPECIES
    Species "Player"
    States
    {
      Spawn:
        SPB1 ABABABAB 3 BRIGHT A_SpawnItemEx("SpearShotTrail", 0, 0, 0)
        goto Death

      Death:
        SPX1 A 0 A_Stop
        SPX1 AAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("SpearShotMiniCoop", 0, 0, random(-180, 180), 2, random(-25, 25))
        SPX1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("SpearShotMini2Coop", 0, 0, random(-180, 180), 2, random(-25, 25))
        SPX1 A 0 A_Scream
        SPX1 A 4 BRIGHT A_Explode(120, 240, 0)
        SPX1 BCDEF 4 BRIGHT
        Stop
    }
}

actor SpearShotMini
{
    Radius 10
    Height 16
    Speed 10
    Damage 4
    Scale 0.75
    Obituary "$SAMSARA_WOLFEN_OB_SLOT7" 
    PROJECTILE
    +RIPPER
    +NOBOSSRIP
    +DOOMBOUNCE
    +NODAMAGETHRUST
    +SKYEXPLODE
    +CANBOUNCEWATER
    +DONTREFLECT
    DamageType "BJSpear"
    SeeSound "wolfen/spearbounce"
    DeathSound "wolfen/spearexplode2"
    States
    {
      Spawn:
        SPB3 ABABABABAB 10 BRIGHT A_SpawnItemEx("SpearShotTrail2", 0, 0, 1)
        TNT1 A 0 A_Jump(128, "Spawn")
        goto Death

      Death:
        TNT1 A 0 A_Scream
        SPX3 C 1 A_Explode(64, 64, 0)
        SPX3 C 1
        SPX3 C 4 BRIGHT
        SPX3 D 6 BRIGHT
        Stop
    }
}

actor SpearShotMiniDM: SpearShotMini
{
    DamageType "BJSpear"
    Damage 3
    +STRIFEDAMAGE
    States
    {
      Spawn:
        SPB3 ABABAB 10 BRIGHT A_SpawnItemEx("SpearShotTrail2", 0, 0, 1)
        TNT1 A 0 A_Jump(96, "Spawn")
        goto Death

      Death:
        TNT1 A 0 A_Scream
        SPX3 C 1 A_Explode(32, 64, 0)
        SPX3 C 1
        SPX3 C 4 BRIGHT
        SPX3 D 6 BRIGHT
        Stop
    }
}

actor SpearShotMiniCoop: SpearShotMini { Species "Player" +THRUSPECIES }

actor SpearShotMini2
{
    Radius 3
    Height 4
    Speed 10
    Damage 4
    Obituary "$SAMSARA_WOLFEN_OB_SLOT7" 
    PROJECTILE
    +RIPPER
    +NOBOSSRIP
    +STRIFEDAMAGE
    +DOOMBOUNCE
    +NODAMAGETHRUST
    +SKYEXPLODE
    +CANBOUNCEWATER
    +DONTREFLECT
    DamageType "BJSpear"
    SeeSound "wolfen/spearbounce"
    DeathSound "wolfen/spearexplode2"
    States
    {
      Spawn:
        SPB2 ABABABABAB 10 BRIGHT A_SpawnItemEx("SpearShotTrail2", 0, 0, 1)
        TNT1 A 0 A_Jump(128, "Spawn")
        goto Death

      Death:
        TNT1 A 0 A_Scream
        SPX2 AA 2 A_Explode(64, 64, 0)
        SPX2 BCDE 4 BRIGHT
        Stop
    }
}

actor SpearShotMini2DM: SpearShotMini2
{
    DamageType "BJSpear"
    Damage 3
    +STRIFEDAMAGE
    States
    {
      Spawn:
        SPB2 ABABAB 10 BRIGHT A_SpawnItemEx("SpearShotTrail2", 0, 0, 1)
        TNT1 A 0 A_Jump(96, "Spawn")
        goto Death

      Death:
        TNT1 A 0 A_Scream
        SPX2 AA 2 A_Explode(32, 64, 0)
        SPX2 BCDE 4 BRIGHT
        Stop
    }
}

actor SpearShotMini2Coop: SpearShotMini2 { Species "Player" +THRUSPECIES }

actor SpearShotTrail 
{
    Speed 1
    Scale 0.8
    Damage 0
    PROJECTILE
    -SOLID
    +NOCLIP
    +NOINTERACTION
    +CLIENTSIDEONLY
    States
    {
      Spawn:
        TNT1 A 2
        SPTR ABCD 2 Bright
        Stop   
    }
}

actor SpearShotTrail2: SpearShotTrail
{
    Scale 0.3
    +CLIENTSIDEONLY
}

// Castle Totenkopf SDL

actor SamsaraTotenkopfFlakgunAttackHandler : Trigger
{
    States
    {
      // Ammo check stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupTakeAmmo")
        TNT1 A 0 A_JumpIfInventory("Cell",3,"PickupTakeAmmo")
        stop

      // Ammo taking stuff
      PickupTakeAmmo:
        TNT1 A 0 A_TakeInventory("Cell",3,TIF_NOTAKEINFINITE)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("Totenkopf/atkflakgun",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("Totenkopf_Flakgun_ProjectileDM",random(-6,6),false)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("Totenkopf_Flakgun_ProjectileCoop",random(-6,6),false)
        stop
    }
}

Actor Totenkopf_Flakgun_Projectile : Totenkopf_Tracer
{
	Damage (0)
	Projectile
	DamageType "TotenkopfFlak"
	+BLOODLESSIMPACT
	+HITTRACER
	+NODAMAGETHRUST
	+FORCERADIUSDMG
	States
	{
		Spawn:
			TNT1 A 1
			Loop
		Crash:
		Death:
			TNT1 A 1
			TNT1 A 0 A_Jump(102,3)
			TNT1 A 0 A_ChangeFlag("NOEXTREMEDEATH",false)
			TNT1 A 0 A_ChangeFlag("EXTREMEDEATH",true)
			TNT1 A 0 A_Explode(100*CallACS("Totenkopf_ExplosiveFactor"),64,XF_HURTSOURCE,0,16)
			TNT1 A 0 A_Playsound("Totenkopf/explosion", CHAN_BODY)
			TK54 A 2 Bright A_ChangeFlag("NOTIMEFREEZE",false)
			TK54 BC 2 Bright
			Stop
		XDeath:
			TNT1 A 1
			TNT1 A 0 A_Jump(102,3)
			TNT1 A 0 A_ChangeFlag("NOEXTREMEDEATH",false)
			TNT1 A 0 A_ChangeFlag("EXTREMEDEATH",true)
			TNT1 A 0 A_Explode(100*CallACS("Totenkopf_ExplosiveFactor"),64,XF_HURTSOURCE,0,16)
			TNT1 A 0 A_JumpIf(CallACS("Totenkopf_BloodGenerator_Offsets")==1,1)
			TNT1 A 0 A_GiveInventory("Totenkopf_BloodGenerator",1,AAPTR_TRACER)
			TK54 A 2 Bright A_ChangeFlag("NOTIMEFREEZE",false)
			TK54 BC 2 Bright
			Stop
	}
}
Actor Totenkopf_Flakgun_ProjectileDM : Totenkopf_Flakgun_Projectile
{
	States
	{
		Crash:
		Death:
			TNT1 A 1
			TNT1 A 0 A_Jump(102,3)
			TNT1 A 0 A_ChangeFlag("NOEXTREMEDEATH",false)
			TNT1 A 0 A_ChangeFlag("EXTREMEDEATH",true)
			TNT1 A 0 A_Explode(50*CallACS("Totenkopf_ExplosiveFactor"),64,XF_HURTSOURCE,0,16)
			TNT1 A 0 A_Playsound("Totenkopf/explosion", CHAN_BODY)
			TK54 A 2 Bright A_ChangeFlag("NOTIMEFREEZE",false)
			TK54 BC 2 Bright
			Stop
		XDeath:
			TNT1 A 1
			TNT1 A 0 A_Jump(102,3)
			TNT1 A 0 A_ChangeFlag("NOEXTREMEDEATH",false)
			TNT1 A 0 A_ChangeFlag("EXTREMEDEATH",true)
			TNT1 A 0 A_Explode(50*CallACS("Totenkopf_ExplosiveFactor"),64,XF_HURTSOURCE,0,16)
			TNT1 A 0 A_JumpIf(CallACS("Totenkopf_BloodGenerator_Offsets")==1,1)
			TNT1 A 0 A_GiveInventory("Totenkopf_BloodGenerator",1,AAPTR_TRACER)
			TK54 A 2 Bright A_ChangeFlag("NOTIMEFREEZE",false)
			TK54 BC 2 Bright
			Stop
	}
}	
Actor Totenkopf_Flakgun_ProjectileCoop : Totenkopf_Flakgun_Projectile { Species "Player" +THRUSPECIES +DONTHARMCLASS +DONTHARMSPECIES +MTHRUSPECIES }
