actor "Machine Gun": InstantSwitchWeapon
{
    Weapon.SelectionOrder 1300
    Weapon.SlotNumber 2
    Inventory.PickUpSound "wolfen/mgunget"
    Weapon.AmmoUse1 1
    Weapon.AmmoGive1 0
    Weapon.AmmoType1 "Clip"
	Weapon.AmmoType2 "Shell"
	Weapon.AmmoUse2 0
	Weapon.AmmoGive2 0
    +DONTBOB
    +INVENTORY.UNDROPPABLE
	+WEAPON.AMMO_OPTIONAL
	+WEAPON.NOALERT
    Inventory.PickupMessage "You got the Machine Gun!"
    Obituary "$SAMSARA_WOLFEN_OB_SLOT2"
    Weapon.YAdjust 16
	Inventory.RestrictedTo "WolfenPlayer"
    Tag "Machine Gun"
    States
    {
      Spawn:
        WMGP A -1
        stop

      // Original
      Ready:
        TNT1 A 0 A_JumpIfInventory("WolfenClassMode",2,"ReadyCTK")
        TNT1 A 0 A_JumpIfInventory("WolfenClassMode",1,"ReadyLost")
        WMGN A 1 A_WeaponReady(WRF_NOSECONDARY)
        loop

      Fire:
        TNT1 A 0 A_JumpIfInventory("WolfenClassMode",2,"FireCTK")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"FireStart")
        TNT1 A 0 A_SelectWeapon("Luger")
        goto Deselect

      FireStart:
        TNT1 A 0 A_AlertMonsters
        WMGN A 2 A_JumpIfInventory("WolfenClassMode",1,"FireLost")
        WMGN B 2
      FireStartAmmoCheck:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"FireAttack")
        goto HoldStart

      FireAttack:
        TNT1 A 0 A_GunFlash
        goto HoldStart

      Hold:
        TNT1 A 0 A_JumpIfInventory("WolfenClassMode",2,"FireCTK")	  
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,3)
        TNT1 A 0 A_JumpIfInventory("Clip",1,2)
        TNT1 A 0 A_SelectWeapon("Luger")
        goto Deselect
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_GunFlash
        TNT1 A 0 A_JumpIfInventory("WolfenClassMode",1,"HoldLost")
      HoldStart:
        WMGN C 3 Bright A_GiveInventory("SamsaraWolfenMachineGunAttackHandler")
        WMGN D 3
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        goto HoldNoAmmo
        WMGN E 3 A_ReFire
        goto Ready

      HoldNoAmmo:
        TNT1 A 0 A_ClearReFire
        TNT1 A 0 A_SelectWeapon("Luger")
        goto Deselect

      // Lost Missions
      ReadyLost:
        WMGN F 1 A_WeaponReady(WRF_NOSECONDARY)
        goto Ready

      FireLost:
        WMGN FG 2
      FireStartLostAmmoCheck:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireLostAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"FireLostAttack")
        goto HoldLost

      FireLostAttack:
        TNT1 A 0 A_GunFlash
        goto HoldLost

      HoldLost:
        WMGN H 3 Bright A_GiveInventory("SamsaraWolfenLostMachineGunAttackHandler")
        WMGN I 3
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        goto HoldNoAmmo
        WMGN J 3 A_ReFire
        goto Ready

      // Castle Totenkopf SDL (Kar98k Rifle)
      ReadyCTK:
        TNT1 A 0 A_JumpIfInventory("Totenkopf_Sniper",1,2)
        TK03 A 1 A_WeaponReady(WRF_NOSECONDARY)
        goto Ready
        TK12 A 1 A_WeaponReady
        goto Ready

      FireCTK:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStartCTK")
        TNT1 A 0 A_JumpIfInventory("Shell",1,"FireStartCTK")
        TNT1 A 0 A_SelectWeapon("Luger")
        goto Deselect

      FireStartCTK:
        TNT1 A 0 A_AlertMonsters
        TK03 A 0 A_JumpIfInventory("Totenkopf_Sniper",1,"FireStartCTKScope")
        goto FireContinueCTK

      FireStartCTKScope:
        TK12 A 0
        goto FireContinueCTK

      FireContinueCTK:
        "####" B 3
        "####" C 3 Bright A_GiveInventory("SamsaraTotenkopfKar98kAttackHandler")
        "####" D 5
        "####" A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        "####" A 0 A_JumpIfInventory("Shell",1,1)
        goto Ready
        "####" E 5 A_PlaySound("Totenkopf/kar98bolt")
        "####" F 5
        "####" GHI 2
        "####" JK 5
        "####" L 3
        goto Ready

	  AltFire:
        TK12 A 8
        TNT1 A 0 A_SelectWeapon("Totenkopf_Sniper")
        goto Deselect
    }
}

// Original

actor SamsaraWolfenMachineGunAttackHandler : Trigger
{
    States
    {
      // Ammo check stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupTakeAmmo")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"PickupTakeAmmo")
        stop

      // Ammo taking stuff
      PickupTakeAmmo:
        TNT1 A 0 A_TakeInventory("Clip",1,TIF_NOTAKEINFINITE)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("wolfen/machinegun",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireBullets(0,0,1,7,"BlankPuffThruGhost",FBF_NOFLASH)
        TNT1 A 0 A_FireBullets(0,0,1,2,"BlankPuffThruGhostBloodless",FBF_NOFLASH,120)
        TNT1 A 0 A_FireBullets(0,0,1,1,"BlankPuffThruGhostBloodless",FBF_NOFLASH,64)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireBullets(0,0,1,7,"BlankPuffThruGhostCoop",FBF_NOFLASH)
        TNT1 A 0 A_FireBullets(0,0,1,2,"BlankPuffThruGhostBloodlessCoop",FBF_NOFLASH,120)
        TNT1 A 0 A_FireBullets(0,0,1,1,"BlankPuffThruGhostBloodlessCoop",FBF_NOFLASH,64)
        stop
    }
}

// Lost Missions

actor SamsaraWolfenLostMachineGunAttackHandler : Trigger
{
    States
    {
      // Ammo check stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupTakeAmmo")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"PickupTakeAmmo")
        stop

      // Ammo taking stuff
      PickupTakeAmmo:
        TNT1 A 0 A_TakeInventory("Clip",1,TIF_NOTAKEINFINITE)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("wolfen/machinegun2",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireBullets(0,0,1,7,"BlankPuffThruGhost",FBF_NOFLASH)
        TNT1 A 0 A_FireBullets(0,0,1,2,"BlankPuffThruGhostBloodless",FBF_NOFLASH,120)
        TNT1 A 0 A_FireBullets(0,0,1,1,"BlankPuffThruGhostBloodless",FBF_NOFLASH,64)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireBullets(0,0,1,7,"BlankPuffThruGhostCoop",FBF_NOFLASH)
        TNT1 A 0 A_FireBullets(0,0,1,2,"BlankPuffThruGhostBloodlessCoop",FBF_NOFLASH,120)
        TNT1 A 0 A_FireBullets(0,0,1,1,"BlankPuffThruGhostBloodlessCoop",FBF_NOFLASH,64)
        stop
    }
}

// Castle Totenkopf SDL

actor SamsaraTotenkopfKar98kAttackHandler : Trigger
{
    States
    {
      // Ammo check stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupTakeAmmo")
        TNT1 A 0 A_JumpIfInventory("Shell",1,"PickupTakeAmmo")
        stop

      // Ammo taking stuff
      PickupTakeAmmo:
        TNT1 A 0 A_TakeInventory("Shell",1,TIF_NOTAKEINFINITE)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("Totenkopf/atkkar98",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("Totenkopf_Tracer_Kar98k",0,false)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("Totenkopf_Tracer_Kar98kCoop",0,false)
        stop
    }
}

actor Totenkopf_Tracer_Kar98k : Totenkopf_Tracer
{
    Damage ((75+random(0,256)%10))
    +THRUGHOST
}

actor Totenkopf_Tracer_Kar98kCoop : Totenkopf_Tracer_Kar98k { Species "Player" +THRUSPECIES }
