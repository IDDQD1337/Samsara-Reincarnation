actor WraithvergeGhostCount : Counter { Inventory.MaxAmount 26 }

actor Wraithverge : CWeapWraithverge
{
    Weapon.SelectionOrder 3000
    +WEAPON.PRIMARY_USES_BOTH
	+WEAPON.AMMO_OPTIONAL
	+WEAPON.ALT_AMMO_OPTIONAL
    +INVENTORY.UNDROPPABLE
    Weapon.AmmoUse1 20
    Weapon.AmmoUse2 20
    Weapon.AmmoGive1 25
    Weapon.AmmoGive2 25
    Weapon.KickBack 150
    Weapon.AmmoType1 "Clip"
    Weapon.AmmoType2 "Cell"
	Inventory.RestrictedTo "HexenPlayer"
    Weapon.SisterWeapon "Spectral Wraithverge"
    Obituary "$SAMSARA_HEXEN_OB_SLOT7"
    Inventory.PickupMessage "You picked up the Wraithverge!"
    Inventory.PickupSound "hexen/weaponget"
    States
    {
      Spawn:
        WWRT A -1
        Stop
        
      Ready:
		TNT1 A 0 A_JumpIfInventory("DaedolonMode", 1, "ReadyMage")
        CHLY A 1 A_WeaponReady
        Loop
		
    ReadyMage:
        MSTF AAAAAABBBBBBCCCCCCDDDDDDEEEEEEFFFFF 1 A_WeaponReady
        Goto Ready		
        
      Select:
		TNT1 A 0 A_JumpIfInventory("DaedolonMode", 1, "SelectMage")
        CHLY A 1 A_Raise
        Loop
        
      Deselect:
		TNT1 A 0 A_JumpIfInventory("DaedolonMode", 1, "DeselectMage")
        CHLY A 1 A_Lower
        Loop
		
    SelectMage:
        MSTF A 1 A_Raise
        Loop
		
    DeselectMage:
        MSTF A 1 A_Lower
        Loop		
        
      Fire:
		TNT1 A 0 A_JumpIfInventory("DaedolonMode", 1, "FireMage")
        CHLY AB 1 bright offset(0, 40)
        CHLY CD 2 bright offset(0, 43)
        CHLY E 2 bright offset(0, 45)
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"CoopFire")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopFire")
		TNT1 A 0 A_SetBlend("4F 4F 4F",0.75,1)
		TNT1 A 0 A_PlayWeaponSound("HolySymbolFire")
		TNT1 A 0 A_FireCustomMissile("HolySpiritDM")
		TNT1 A 0 A_FireCustomMissile("HolySpiritDM2",0,0)
		TNT1 A 0 A_FireCustomMissile("HolySpiritDM3",0,0)
		TNT1 A 0 A_FireCustomMissile("HolySpiritDM4",0,0)
        CHLY FFFFFF 1 bright offset(0, 48) A_SetBlend("4F 4F 4F",0.6,1)
        CHLY GG 2 bright offset(0, 40) A_SetBlend("4F 4F 4F",0.55,6) //A_CHolyPalette
        CHLY G 2 offset(0, 36) //A_CHolyPalette
        goto FireEnd
		
    FireMage:
		MSTF G 0 A_JumpIfInventory("Clip", 15, 1)
		Goto Ready
		MSTF G 0 A_JumpIfInventory("Cell", 15, 1)
		Goto Ready
        MSTF G 4 Offset(0, 40)
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopFireMage")
        TNT1 A 0 A_FireCustomMissile("MageStaffFX3",-3,0,0,8)
        TNT1 A 0 A_FireCustomMissile("MageStaffFX3",0,0,0,8)
        TNT1 A 0 A_FireCustomMissile("MageStaffFX3",3,0,0,8)
        TNT1 A 0 A_PlayWeaponSound("MageStaffFire")
		TNT1 A 0 A_TakeInventory("Clip", 15, TIF_NOTAKEINFINITE)
		TNT1 A 0 A_TakeInventory("Cell", 15, TIF_NOTAKEINFINITE)
        TNT1 A 0 A_SetBlend("Gold",0.3,10)
        MSTF H 4 Bright Offset(0, 48)
        MSTF H 2 Bright Offset(0, 48)
        MSTF II 2 Offset(0, 48)
        MSTF I 1 Offset(0, 40)
        MSTF J 5 Offset(0, 36)
        Goto Ready				
		
	  CoopFire:
		TNT1 A 0 A_SetBlend("4F 4F 4F",0.75,1)
		TNT1 A 0 A_PlayWeaponSound("HolySymbolFire")
        TNT1 A 0 A_CHolyAttack
		TNT1 AAA 0 A_CHolyPalette
        CHLY FFFFFF 1 bright offset(0, 48) A_SetBlend("4F 4F 4F",0.6,1)
        CHLY GG 2 bright offset(0, 40) A_SetBlend("4F 4F 4F",0.55,6)
        CHLY G 2 offset(0, 36)
        goto FireEnd
		
      CoopFireMage:  
		TNT1 A 0 A_FireCustomMissile("MageStaffFX3Coop",-3,0,0,8)
        TNT1 A 0 A_FireCustomMissile("MageStaffFX3Coop",0,0,0,8)
        TNT1 A 0 A_FireCustomMissile("MageStaffFX3Coop",3,0,0,8)
        TNT1 A 0 A_PlayWeaponSound("MageStaffFire")
		TNT1 A 0 A_TakeInventory("Clip", 15, TIF_NOTAKEINFINITE)
		TNT1 A 0 A_TakeInventory("Cell", 15, TIF_NOTAKEINFINITE)
        TNT1 A 0 A_SetBlend("Gold",0.3,10)
        MSTF H 4 Bright Offset(0, 48)
        MSTF H 2 Bright Offset(0, 48)
        MSTF II 2 Offset(0, 48)
        MSTF I 1 Offset(0, 40)
        MSTF J 5 Offset(0, 36)
        Goto Ready		
        
      FireEnd:
        CHLY A 20
        CHLY A 5 A_Refire
        Goto Ready
    }
}

actor "Spectral Wraithverge": Wraithverge
{
    Weapon.AmmoType1 ""
    Weapon.AmmoType2 ""
    Weapon.AmmoUse1  0
    Weapon.AmmoUse2  0
    Weapon.AmmoGive1 0
    Weapon.AmmoGive2 0

    Weapon.SisterWeapon "Wraithverge"
    +POWERED_UP

    States
    {
      Fire:
        CHLY AB 1 bright offset(0, 40)
        CHLY CD 2 bright offset(0, 43)
        CHLY E 2 bright offset(0, 45)
        CHLY F 0 A_SetBlend("80 80 FF", 0.4, 0.33)
        CHLY F 0 ACS_ExecuteAlways(317, 0, 8)
        CHLY F 0 A_PlaySound("weapons/sigil", 6)
        CHLY F 0 A_PlaySound("HolySymbolFire", CHAN_WEAPON)
        CHLY F 6 bright offset(0, 48) A_FireCustomMissile("SpectralHolyMissile")
        CHLY G 4 bright offset(0, 40)
        CHLY G 2 offset(0, 36)
        goto FireEnd
    }
}

actor HolySpirit2 : HolySpirit replaces HolySpirit
{
    Speed 15 // Previously 12
    Alpha 0.5
	PROJECTILE
    +BRIGHT
	+FORCERADIUSDMG
	+THRUSPECIES
	+DONTHARMSPECIES
	+MTHRUSPECIES
	+DONTHARMCLASS
	Species "Player"
    DamageType "Wraithverge"
	States
	{
    Spawn:
      SPIR A 2 A_CHolySeek
	  TNT1 A 0 A_ChangeFlag("PAINLESS", 1)
      TNT1 A 0 A_Explode(Random(1,8), 64, 0)
	  TNT1 A 0 A_SpawnItemEx("HolyMissilePuff2",0,0,-6,0,0,0,0,128) //ThrustThing(angle*256/360, 4, 0, 0)
	  SPIR A 2 A_CHolySeek
	  TNT1 A 0 A_ChangeFlag("PAINLESS", 1)
      TNT1 A 0 A_Explode(random(1,8), 64, 0)
	  TNT1 A 0 A_SpawnItemEx("HolyMissilePuff2",0,0,-6,0,0,0,0,128) //ThrustThing(angle*256/360, 4, 0, 0)
	  SPIR B 2 A_CHolySeek
	  TNT1 A 0 A_ChangeFlag("PAINLESS", 1)
      TNT1 A 0 A_Explode(random(1,8), 64, 0)
	  TNT1 A 0 A_SpawnItemEx("HolyMissilePuff2",0,0,-6,0,0,0,0,128) //ThrustThing(angle*256/360, 4, 0, 0)
      SPIR B 2 A_CHolyCheckScream
	  TNT1 A 0 A_ChangeFlag("PAINLESS", 0)
      TNT1 A 0 A_Explode(random(1,8), 64, 0)
	  TNT1 A 0 A_SpawnItemEx("HolyMissilePuff2",0,0,-6,0,0,0,0,128) //ThrustThing(angle*256/360, 4, 0, 0)
      goto Spawn
    Death:
      SPIR D 4
      SPIR E 4 A_Scream
      SPIR FGHI 4
      Stop
    }
}

actor HolySpiritDM : MageStaffFX2//HolySpirit
{
    Obituary "$SAMSARA_HEXEN_OB_SLOT7"
    //Renderstyle Translucent
    Speed 20 // Previously 12
    //Alpha 0.5
    Radius 10
    Height 6
	Damage 5
	PROJECTILE
    +RIPPER
    +SEEKERMISSILE
    +FOILINVUL
    +SKYEXPLODE
    +NOEXPLODEFLOOR
    +CANBLAST
    +EXTREMEDEATH
    +BRIGHT
	//+SKULLFLY
	+HEXENBOUNCE
	+LOOKALLAROUND
	BounceFactor 0.9
	WallBounceFactor 0.9
    DeathSound "SpiritDie"
	//+MISSILE
    DamageType "Wraithverge"
	States
	{
    Spawn:
      SPIR PPPP 3 bright A_SpawnItemEx("HolyMissilePuff")
	  TNT1 A 0 A_Explode(128,128,0)
    Spawn2:
	  TNT1 A 0 A_SetTranslucent(0.5,0)
      SPIR A 1 A_SetAngle(-67.5)
	  TNT1 A 0 ThrustThing(angle*256/360, 6, 0, 0)
    Spawn3:
	  TNT1 A 0 A_Jump(20,"Scream")
	Idle:
	  TNT1 A 0 A_JumpIfInventory("WraithvergeGhostCount",26,"Death")
	  TNT1 A 0 A_GiveInventory("WraithvergeGhostCount",1)
	  TNT1 A 0 A_CStaffMissileSlither
      SPIR A 2 A_MStaffTrack//A_CHolySeek
	  TNT1 A 0 A_ChangeFlag("PAINLESS", 1)
      TNT1 A 0 A_Explode(random(1,8), 32, 0)
	  TNT1 A 0 A_SpawnItemEx("HolyMissilePuff2",0,0,-6,0,0,0,0,128) //ThrustThing(angle*256/360, 4, 0, 0)
	  TNT1 A 0 A_CStaffMissileSlither
	  SPIR A 2 A_MStaffTrack//A_CHolySeek
	  TNT1 A 0 A_ChangeFlag("PAINLESS", 1)
      TNT1 A 0 A_Explode(random(1,8), 32, 0)
	  TNT1 A 0 A_SpawnItemEx("HolyMissilePuff2",0,0,-6,0,0,0,0,128) //ThrustThing(angle*256/360, 4, 0, 0)
	  TNT1 A 0 A_CStaffMissileSlither
	  SPIR B 2 A_MStaffTrack//A_CHolySeek
	  TNT1 A 0 A_ChangeFlag("PAINLESS", 1)
      TNT1 A 0 A_Explode(random(1,8)*random(1,2), 32, 0)
	  TNT1 A 0 A_SpawnItemEx("HolyMissilePuff2",0,0,-6,0,0,0,0,128) //ThrustThing(angle*256/360, 4, 0, 0)
	  TNT1 A 0 A_CStaffMissileSlither
      SPIR B 2 A_MStaffTrack//A_CHolyCheckScream
	  TNT1 A 0 A_ChangeFlag("PAINLESS", 0)
      TNT1 A 0 A_Explode(random(1,8), 32, 0)
	  TNT1 A 0 A_SpawnItemEx("HolyMissilePuff2",0,0,-6,0,0,0,0,128) //ThrustThing(angle*256/360, 4, 0, 0)
	  TNT1 A 0 A_JumpIfTargetInLos("SetNoclip",180)
	  TNT1 A 0 A_ChangeFlag("NOCLIP",0)
      goto Spawn3
    Death:
      SPIR D 4 A_Stop
      SPIR E 4 A_Scream
      SPIR FGHI 4
      Stop
	Scream:
	  TNT1 A 0 A_PlaySoundEx("SpiritActive",Voice,0,0)
	  goto Idle
	SetNoclip:
	  TNT1 A 0 A_ChangeFlag("NOCLIP",1)
	  goto Idle
    }
}

actor HolySpiritDM2 : HolySpiritDM
{
    States
	{
    Spawn:
      TNT1 A 12 //Bright A_SpawnItemEx("HolyMissilePuff")
    Spawn2:
	  TNT1 A 0 A_SetTranslucent(0.5,0)
      SPIR A 1 A_SetAngle(67.5)
	  TNT1 A 0 ThrustThing(angle*256/360, 6, 0, 0)
	  goto Spawn3
	}
}

actor HolySpiritDM3 : HolySpiritDM
{
    States
	{
    Spawn:
      TNT1 A 12 //Bright A_SpawnItemEx("HolyMissilePuff")
    Spawn2:
	  TNT1 A 0 A_SetTranslucent(0.5,0)
      SPIR A 1 A_SetAngle(-135)
	  TNT1 A 0 ThrustThing(angle*256/360, 6, 0, 0)
	  goto Spawn3
	}
}

actor HolySpiritDM4 : HolySpiritDM
{
    States
	{
    Spawn:
      TNT1 A 12 //Bright A_SpawnItemEx("HolyMissilePuff")
    Spawn2:
	  TNT1 A 0 A_SetTranslucent(0.5,0)
      SPIR A 1 A_SetAngle(135)
	  TNT1 A 0 ThrustThing(angle*256/360, 6, 0, 0)
	  goto Spawn3
	}
}

actor HolyMissilePuff2: HolyMissilePuff replaces HolyMissilePuff
{
    -NOBLOCKMAP -NOGRAVITY -DROPOFF -NOTELEPORT
    +NOINTERACTION +CLIENTSIDEONLY
}

actor SpectralHolyMissile: HolyMissile
{
    DamageType "Spectral"
    
    States
    {
      Spawn:
        SPIR PPPP 3 bright A_SpawnItemEx("HolyMissilePuff2")
        goto Death
        
      Death:
        SPIR P 0 bright A_CustomMissile("SpectralSpirit", 0,0, -67.5, 6)
        SPIR P 0 bright A_CustomMissile("SpectralSpirit", 0,0, -22.5, 6)
        SPIR P 0 bright A_CustomMissile("SpectralSpirit", 0,0,  22.5, 6)
        SPIR P 1 bright A_CustomMissile("SpectralSpirit", 0,0,  67.5, 6)
        stop
    }
}

actor SpectralSpirit: HolySpirit2
{
    Damage 0
    DamageType "Spectral"
    +FORCERADIUSDMG
    
    States
    {
      Spawn:
		TNT1 A 0 A_ChangeFlag("PAINLESS", 1)
        SPIR A 0 A_Explode(random(1,8)*3, 16, 0)
        SPIR A 2 A_CHolySeek
		TNT1 A 0 A_ChangeFlag("PAINLESS", 1)
        SPIR A 0 A_Explode(random(1,8)*3, 16, 0)
        SPIR A 2 A_CHolySeek
		TNT1 A 0 A_ChangeFlag("PAINLESS", 1)
        SPIR A 0 A_Explode(random(1,8)*3, 16, 0)
        SPIR B 2 A_CHolySeek
		TNT1 A 0 A_ChangeFlag("PAINLESS", 0)
        SPIR A 0 A_Explode(random(1,8)*3, 16, 0)
        SPIR B 2 A_CHolyCheckScream
        loop

      Death:
		TNT1 A 0 A_ChangeFlag("PAINLESS", 1)
        SPIR A 0 A_Explode(random(1,8)*3, 16, 0)
        SPIR D 4
        SPIR E 4 A_Scream
        SPIR FGHI 4
        Stop
    }
}

Actor HolyMissile_Coop : HolyMissile replaces HolyMissile { Species "Player" +THRUSPECIES +DONTHARMCLASS +DONTHARMSPECIES +MTHRUSPECIES }


actor MageStaffFX3 
{
    Height 2
    Radius 2
    Damage 10 // was 4. buffed because BloodScourge is a little underwhelming
    Speed 17
	+SEEKERMISSILE
	+SCREENSEEKER
    +EXTREMEDEATH
    Decal Scorch
    DeathSound "MageStaffExplode"
    DamageType "Fire"
    Obituary "$SAMSARA_HEXEN_OB_INSTAGIB"
	PROJECTILE
    States
    {
    Spawn:
        MSP2 ABCD 2 Bright A_SeekerMissile(2, 10, SMF_LOOK | SMF_PRECISE, 51)
        Loop
    Death:
        MSP2 E 4 Bright A_SetTranslucent(1, 1)
        MSP2 F 5 Bright A_Explode(80, 192, 0)
        MSP2 GH 5 Bright
        MSP2 I 4 Bright
        Stop
    }
}

actor MageStaffFX3Coop : MageStaffFX3 { Species "Player" +THRUSPECIES +DONTHARMCLASS +DONTHARMSPECIES +MTHRUSPECIES }