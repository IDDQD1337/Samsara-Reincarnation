actor "Serpent Staff" : CWeapStaff
{
    Weapon.SelectionOrder 1600
    Weapon.SlotNumber 4
    Weapon.AmmoUse1 1
    Weapon.AmmoGive1 25
    Weapon.KickBack 150
    Weapon.YAdjust 10
    Weapon.AmmoType1 "Clip"
    Inventory.PickupMessage "You got the Serpent Staff!"
    Inventory.PickupSound "hexen/weaponget"
    Obituary "$SAMSARA_HEXEN_OB_SLOT4_1"
	Inventory.RestrictedTo "HexenPlayer"
    +INVENTORY.UNDROPPABLE
    States
    {
      Spawn:
        WCSS A -1
        Stop

      Select:
		TNT1 A 0 A_JumpIfInventory("DaedolonMode", 1, "SelectMage")
        CSSF C 1 A_Raise
        Loop

      Deselect:
		TNT1 A 0 A_JumpIfInventory("DaedolonMode", 1, "DeselectMage")
        CSSF B 3
        CSSF C 4
        CSSF C 1 A_Lower
        Wait	
		
	 DeselectMage:
		CSIH A 1 A_Lower
		Loop		
		
	 SelectMage:
		CSIH A 1 A_Raise
		Loop		
		

      Ready:
		TNT1 A 0 A_JumpIfInventory("DaedolonMode", 1, "ReadyMage")
        CSSF C 4
        CSSF B 3 A_CStaffInitBlink
        CSSF AAAAAAA 1 A_WeaponReady
        CSSF A 1 A_CStaffCheckBlink
        Goto Ready + 2
		
	/* ReadyMage:
		CONF AAAABBBBCCCC 1 A_WeaponReady
		Goto Ready	*/
		
	 ReadyMage:
	    CSIH A 1 A_WeaponReady
		Goto Ready			

      Fire:
		TNT1 A 0 A_JumpIfInventory("DaedolonMode", 1, "FireMage")
        CSSF A 0 A_TakeInventory("DrainCounter")
        goto Hold

      Hold:
        CSSF A 1 BRIGHT Offset (0, 45) A_JumpIfCloser(128, "Drain")
        CSSF A 0 A_TakeInventory("DrainCounter", 1)
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopFire")
        CSSF J 1 BRIGHT Offset (0, 50) A_CStaffAttack
        CSSF J 2 BRIGHT Offset (0, 50)
        CSSF J 2 Offset (0, 45)
        CSSF A 2 Offset (0, 40)
        CSSF A 2 Offset (0, 36)
        Goto Ready + 2
	
	  // Fire Blast. Has better accuracy and faster fire rate than frost shards, but about equal damage
	  //Mage has no real automatics. That's right, He's all about burst damage.
	
	  FireMage:
		TNT1 A 0 A_JumpIfInventory("Clip", 3, 1)
		Goto Ready
		TNT1 A 0 A_playsound("hexen/cult/fire")
		CSIH A 1
		TNT1 A 0 A_JumpIfInventory("CoopModeOn", 1, "FireMageCoop")
		TNT1 AAAA 0 A_FireCustomMissile("CultShot1",frandom(-12,12),0,0,0,1,frandom(-4,4))
		CSIH B 3 Bright A_FireCustomMissile("CultShot1",0,0,0,0)//,frandom(-12,12),1,0,0,1,frandom(-4,4))
		CSIH B 0 Bright A_TakeInventory("Clip", 3, TIF_NOTAKEINFINITE)
		CSIH CDE 3 Bright
		CSIH FGHAA 3 Bright
		TNT1 A 0 A_Refire("FireMage")
		Goto Ready		
	  

      Blink:
        CSSF BBBCCCCCBBB 1 A_WeaponReady
        Goto Ready + 2

      Drain:
        CSSF K 0 A_JumpIfInventory("DrainCounter", 1, "DrainSound0")
        CSSF K 0 A_GiveInventory("DrainCounter", 3)
        CSSF K 0 A_PlaySound("ClericCStaffHitThing", 6)
        goto Drain2

      DrainSound0:
        CSSF K 0 A_TakeInventory("DrainCounter", 1)
        CSSF K 0 A_JumpIfInventory("DrainCounter", 2, 1)
        goto Drain2
        CSSF K 0 A_PlaySound("ClericCStaffHitThing", 5)
        goto Drain2

      Drain2:
        CSSF K 0 A_CustomPunch(random(20,35), 1, 1, "StaffDrainPuff", 98)
        CSSF A 0 A_JumpIf(ACS_ExecuteWithResult(SAMSARA_DECORATE, 22), "InfiniteDrain")
        CSSF K 0 A_GiveInventory("SerpentHealth", random(2,4))
        CSSF K 0 A_GiveInventory("HealthBonus",1)
        goto Drain3

      InfiniteDrain:
        CSSF K 0 A_GiveInventory("InfiniteHealth", random(3,5))
        goto Drain3

      Drain3:
        CSSF K 8 BRIGHT Offset (0, 36)
        CSSF K 0 A_Refire
        Goto Ready + 2

      CoopFire:
        TNT1 A 0 A_PlayWeaponSound("ClericCStaffFire")
        TNT1 A 0 A_FireCustomMissile("CStaffMissileCoop2",-3,0)
        CSSF J 1 BRIGHT Offset (0, 50) A_FireCustomMissile("CStaffMissileCoop",3,1)//A_CStaffAttack
        CSSF J 2 BRIGHT Offset (0, 50)
        CSSF J 2 Offset (0, 45)
        CSSF A 2 Offset (0, 40)
        CSSF A 2 Offset (0, 36)
        CSSF K 0 A_Refire
        Goto Ready + 2
		
	  FireMageCoop:
		TNT1 AAAA 0 A_FireCustomMissile("CultShot1Coop",frandom(-12,12),0,0,0,1,frandom(-4,4))
		CSIH B 4 Bright A_FireCustomMissile("CultShot1Coop")//,frandom(-12,12),1,0,0,1,frandom(-4,4))
		CSIH CDE 3 Bright
		CSIH FGHAA 3 Bright
		TNT1 A 0 A_Refire
		Goto Ready			
    }
}

actor DrainCounter: Counter {}

actor SerpentHealth: Health
{
    Inventory.Amount 1
}

actor CStaffMissile2 : CStaffMissile replaces CStaffMissile
{
    Decal DoomImpScorch
    Damage (random(5,25))//5
    Obituary "$SAMSARA_HEXEN_OB_SLOT4_2"
    +BLOODSPLATTER
}

actor CStaffMissileCoop : CStaffMissile2
{
    Species "Player"
    +THRUSPECIES
}

actor CStaffMissileCoop2 : CStaffMissile2
{
    Species "Player"
    +THRUSPECIES
    States
    {
      Spawn:
        /*CSSF D 1 Bright A_SetAngle(4 + angle)
        CSSF D 1 Bright //ThrustThing(angle*256/360+64, 1, 0, 0)
        CSSF E 1 Bright A_SetAngle(2 + angle)//ThrustThing(angle*256/360+192, 1, 0, 0)
        CSSF ED 1 Bright
        CSSF D 1 Bright A_SetAngle(2 - angle)
        CSSF E 1 Bright //ThrustThing(angle*256/360+192, 1, 0, 0)
        CSSF E 1 Bright A_SetAngle(4 - angle)//ThrustThing(angle*256/360+192, 2, 0, 0) //ThrustThing(angle*256/360+64, 1, 0, 0)
        CSSF DD 1 Bright*/
        TNT1 AAAAAAAAAAAA 0 A_CStaffMissileSlither

      Spawn2:
        CSSF EEDD 1 Bright A_CStaffMissileSlither
        Loop
    }
}

actor StaffDrainPuff: HammerPuff
{
    SeeSound ""
    AttackSound ""
    ActiveSound ""
}

ACTOR CultShot1
{
	Radius 6
	Height 16
	Speed 35
	Damage (Random(2,12)) // was 1,6
	Projectile
	Bouncecount 6
	Wallbouncefactor 2
	+RANDOMIZE
	+RIPPER
	+THRUACTORS
	+HEXENBOUNCE
	+CANBOUNCEWATER
	+FORCERADIUSDMG
	RenderStyle Add
	scale .75
	SeeSound ""
	DeathSound "hexen/cult/hit"
	Decal "Scorch"
	States
	{
		Spawn:
			SCBL A 1 BRIGHT A_SpawnItemEX("CultTrail1") TNT1 A 0 A_Explode(Random(2,12), 8, !XF_HURTSOURCE, false, 8)
			TNT1 AAA 0 A_Cstaffmissileslither
			SCBL A 1 BRIGHT A_SpawnItemEX("CultTrail1") TNT1 A 0 A_Explode(Random(2,12), 8, !XF_HURTSOURCE, false, 8)
			TNT1 AAA 0 A_Cstaffmissileslither
			SCBL A 1 BRIGHT A_SpawnItemEX("CultTrail1") TNT1 A 0 A_Explode(Random(2,12), 8, !XF_HURTSOURCE, false, 8)
			TNT1 AAA 0 A_Cstaffmissileslither
			SCBL B 1 BRIGHT A_SpawnItemEX("CultTrail1") TNT1 A 0 A_Explode(Random(2,12), 8, !XF_HURTSOURCE, false, 8)
			TNT1 AAA 0 A_Cstaffmissileslither
			SCBL B 1 BRIGHT A_SpawnItemEX("CultTrail1") TNT1 A 0 A_Explode(Random(2,12), 8, !XF_HURTSOURCE, false, 8)
			TNT1 AAA 0 A_Cstaffmissileslither
			SCBL B 1 BRIGHT A_SpawnItemEX("CultTrail1") TNT1 A 0 A_Explode(Random(2,12), 8, !XF_HURTSOURCE, false, 8)
			TNT1 AAA 0 A_Cstaffmissileslither
			SCBL C 1 BRIGHT A_SpawnItemEX("CultTrail1") TNT1 A 0 A_Explode(Random(2,12), 8, !XF_HURTSOURCE, false, 8)
			TNT1 AAA 0 A_Cstaffmissileslither
			SCBL C 1 BRIGHT A_SpawnItemEX("CultTrail1") TNT1 A 0 A_Explode(Random(2,12), 8, !XF_HURTSOURCE, false, 8)
			TNT1 AAA 0 A_Cstaffmissileslither
			SCBL C 1 BRIGHT A_SpawnItemEX("CultTrail1") TNT1 A 0 A_Explode(Random(2,12), 8, !XF_HURTSOURCE, false, 8)
			TNT1 AAA 0 A_Cstaffmissileslither
			SCBL D 1 BRIGHT A_SpawnItemEX("CultTrail1") TNT1 A 0 A_Explode(Random(2,12), 8, !XF_HURTSOURCE, false, 8)
			TNT1 AAA 0 A_Cstaffmissileslither
			SCBL D 1 BRIGHT A_SpawnItemEX("CultTrail1") TNT1 A 0 A_Explode(Random(2,12), 8, !XF_HURTSOURCE, false, 8)
			TNT1 AAA 0 A_Cstaffmissileslither
			SCBL D 1 BRIGHT A_SpawnItemEX("CultTrail1") TNT1 A 0 A_Explode(Random(2,12), 8, !XF_HURTSOURCE, false, 8)
			TNT1 AAA 0 A_Cstaffmissileslither
			Loop
		Death:
			SCBL EEEEFFFGGH 2 BRIGHT
			Stop
	}
}


ACTOR CultTrail1
{
	+NOCLIP
	+NOINTERACTION
	+NOGRAVITY
	renderstyle ADD
	alpha 0.5
	scale .5
	States
	{
		Spawn:
			TNT1 A 0 A_Jump(256,"A")
		A:
			CSFT AABBCCDDEE 1 BRIGHT //A_fadeout(0.05)
			Stop
	}
}

Actor CultShot1Coop : CultShot1 { +DONTHURTSPECIES Species "Player" +DONTHARMCLASS }