actor SamsaraHexenSerpentStaffBlinkCounter : Counter {}
actor SamsaraHexenSerpentStaffDrained : Boolean {}
actor SamsaraHexenJotunnsFlailMeleePuffSpawned : Boolean {}
actor SamsaraHexenJotunnsFlailMeleeAmmoTaken : Boolean {} // for spread rune

actor "Serpent Staff" : SamsaraHexenWeapon
{
    Inventory.PickupMessage "Serpent Staff"
    Weapon.SlotNumber 4
    Weapon.SelectionOrder 1600
    Weapon.YAdjust 10
    Weapon.AmmoType "Clip"
    Weapon.AmmoGive 25
    Weapon.AmmoUse 1
    Obituary "$SAMSARA_HEXEN_OB_SLOT4_1"
    Tag "Serpent Staff"
    +AMMO_OPTIONAL
    +NOALERT
    States
    {
      Spawn:
        WCSS A -1
        stop

      // Parias
      Ready:
        TNT1 A 0 A_TakeInventory("SamsaraHexenSerpentStaffBlinkCounter") // reset
        TNT1 A 0 A_GiveInventory("SamsaraHexenSerpentStaffBlinkCounter",random(20,147))
        TNT1 A 0 A_JumpIfInventory("HexenClassMode",2,"ReadyLoopFighter")
        TNT1 A 0 A_JumpIfInventory("HexenClassMode",1,"ReadyLoopMage")
      ReadyCleric:
        CSSF C 4
        CSSF B 3
      ReadyLoop:
        TNT1 A 0 A_JumpIfInventory("HexenClassMode",2,"ReadyLoopFighter")
        TNT1 A 0 A_JumpIfInventory("HexenClassMode",1,"ReadyLoopMage")
      ReadyLoopCleric:
        CSSF AAAAAAA 1 A_WeaponReady
        TNT1 A 0 A_TakeInventory("SamsaraHexenSerpentStaffBlinkCounter",1)
        TNT1 A 0 A_JumpIfInventory("SamsaraHexenSerpentStaffBlinkCounter",1,1)
        goto ReadyLoopClericBlink
        CSSF A 1 A_WeaponReady
        goto ReadyLoop

      ReadyLoopClericBlink:
        TNT1 A 0 A_GiveInventory("SamsaraHexenSerpentStaffBlinkCounter",random(12,76))
        CSSF BBBCCCCCBBB 1 A_WeaponReady
        goto ReadyLoop

      Deselect:
        TNT1 A 0 A_JumpIfInventory("HexenClassMode",2,"DeselectLoopFighter")
        TNT1 A 0 A_JumpIfInventory("HexenClassMode",1,"DeselectLoopMage")
      DeselectCleric:
        CSSF B 3
        CSSF C 4
      DeselectLoop:
        TNT1 A 0 A_JumpIfInventory("HexenClassMode",2,"DeselectLoopFighter")
        TNT1 A 0 A_JumpIfInventory("HexenClassMode",1,"DeselectLoopMage")
      DeselectLoopCleric:
        CSSF C 1 A_Lower
        goto DeselectLoop

      Select:
        TNT1 A 0 A_JumpIfInventory("HexenClassMode",2,"SelectFighter")
        TNT1 A 0 A_JumpIfInventory("HexenClassMode",1,"SelectMage")
      SelectCleric:
        CSSF C 1 A_Raise
        goto Select

      Fire:
        TNT1 A 0 A_JumpIfInventory("HexenClassMode",2,"FireFighter")
        TNT1 A 0 A_JumpIfInventory("HexenClassMode",1,"FireMage")
      FireCleric:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        goto NoAmmoSwitch
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_JumpIfCloser(128,"FireClericMelee")
        goto FireClericRanged

      FireClericMelee:
        TNT1 A 0 A_GiveInventory("SamsaraHexenSerpentStaffMeleeAttackHandler")
        TNT1 A 0 A_JumpIfInventory("SamsaraHexenSerpentStaffDrained",1,1)
        goto FireClericRanged
        CSSF K 8 Bright Offset(0,36)
        goto FireClericFinish

      FireClericRanged:
        CSSF A 1 Bright Offset(0,45)
        CSSF J 3 Bright Offset(0,50) A_GiveInventory("SamsaraHexenSerpentStaffRangedAttackHandler")
        CSSF J 2 Offset(0,45)
        CSSF A 2 Offset(0,40)
        CSSF A 2 Offset(0,36)
        goto FireClericFinish

      FireClericFinish:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        goto NoAmmoSwitch
        TNT1 A 0 A_ReFire
        goto ReadyLoop

      // Daedolon (Blight Shock)
      ReadyLoopMage:
        H110 A 1 A_WeaponReady
        goto ReadyLoop

      DeselectLoopMage:
        H110 A 1 A_Lower
        goto DeselectLoop

      SelectMage:
        H110 A 1 A_Raise
        goto Select

      FireMage:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Clip",5,1)
        goto NoAmmoSwitch
        H110 A 1 A_AlertMonsters
        H110 B 4 Bright A_GiveInventory("SamsaraHexenBlightShockAttackHandler")
        H110 CDEFGH 3 Bright
        H110 A 6
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Clip",5,1)
        goto NoAmmoSwitch
        TNT1 A 0 A_ReFire
        goto ReadyLoop

      // Baratus (Jotunn's Flail)
      ReadyLoopFighter:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReadyLoopFighterAmmo")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"ReadyLoopFighterAmmo")
      ReadyLoopFighterNoAmmo:
        H122 A 1 A_WeaponReady
        goto ReadyLoop

      ReadyLoopFighterAmmo:
        H122 H 1 Bright A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        goto ReadyLoop
        H122 H 1 Bright A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        goto ReadyLoop
        H122 H 1 Bright A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        goto ReadyLoop
        H122 I 1 Bright A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        goto ReadyLoop
        H122 I 1 Bright A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        goto ReadyLoop
        H122 I 1 Bright A_WeaponReady
        goto ReadyLoop

      DeselectLoopFighter:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"DeselectLoopFighterAmmo")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"DeselectLoopFighterAmmo")
      DeselectLoopFighterNoAmmo:
        H122 A 1 A_Lower
        goto DeselectLoop

      DeselectLoopFighterAmmo:
        H122 H 1 Bright A_Lower
        goto DeselectLoop

      SelectFighter:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"SelectFighterAmmo")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"SelectFighterAmmo")
      SelectFighterNoAmmo:
        H122 A 1 A_Raise
        goto Select

      SelectFighterAmmo:
        H122 H 1 Bright A_Raise
        goto Select

      FireFighter:
        TNT1 A 0 A_TakeInventory("SamsaraHexenJotunnsFlailMeleePuffSpawned")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireFighterAmmo")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"FireFighterAmmo")
      FireFighterNoAmmo:
        H122 B 4
        H122 C 4 A_PlaySound("FighterHammerMiss",CHAN_WEAPON)
        goto FireFighterContinue1

      FireFighterAmmo:
        H122 J 4 Bright
        H122 K 4 Bright A_PlaySound("FighterHammerMiss",CHAN_WEAPON)
        goto FireFighterContinue1

      FireFighterContinue1:
        TNT1 A 0 A_GiveInventory("SamsaraHexenJotunnsFlailMeleeAttackHandler1")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireFighterContinue1Ammo")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"FireFighterContinue1Ammo")
      FireFighterContinue1NoAmmo:
        H122 D 4
        goto FireFighterContinue2

      FireFighterContinue1Ammo:
        H122 L 4 Bright
        goto FireFighterContinue2

      FireFighterContinue2:
        TNT1 A 0 A_GiveInventory("SamsaraHexenJotunnsFlailMeleeAttackHandler2")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireFighterContinue2Ammo")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"FireFighterContinue2Ammo")
      FireFighterContinue2NoAmmo:
        H122 E 4
        goto FireFighterContinue3

      FireFighterContinue2Ammo:
        H122 M 4 Bright
        goto FireFighterContinue3

      FireFighterContinue3:
        TNT1 A 0 A_JumpIfCloser(128,"FireFighterContinue3MeleeOnly") // hack for non-bleeding actors
        TNT1 A 0 A_GiveInventory("SamsaraHexenJotunnsFlailMeleeAttackHandler3")
        TNT1 A 0 A_GiveInventory("SamsaraHexenJotunnsFlailRangedAttackHandler")
        goto FireFighterFinish

      FireFighterContinue3MeleeOnly:
        TNT1 A 0 A_GiveInventory("SamsaraHexenJotunnsFlailMeleeAttackHandler3")
        goto FireFighterFinish

      FireFighterFinish:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireFighterFinishAmmo")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"FireFighterFinishAmmo")
      FireFighterFinishNoAmmo:
        H122 FG 4
        goto ReadyLoop

      FireFighterFinishAmmo:
        H122 NO 4 Bright
        goto ReadyLoop

      // "No ammo" switch
      NoAmmoSwitch:
        TNT1 A 0 A_ClearReFire
        TNT1 A 0 ACS_NamedExecuteWithResult("Hexen_SwitchPriority")
        goto Deselect
    }
}

// Parias

// Melee

actor SamsaraHexenSerpentStaffMeleeAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"PickupAttack")
        stop

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_TakeInventory("SamsaraHexenSerpentStaffDrained")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttackInfinite")
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupAttackDMSpread")
        TNT1 A 0 A_CustomPunch(random(20,35),true,0,"SamsaraHexenCStaffPuff",128)
        stop

      PickupAttackDMSpread:
        TNT1 A 0 A_CustomPunch(random(20,35)*3,true,0,"SamsaraHexenCStaffPuff",128)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupAttackCoopSpread")
        TNT1 A 0 A_CustomPunch(random(20,35),true,0,"SamsaraHexenCStaffPuffCoop",128)
        stop

      PickupAttackCoopSpread:
        TNT1 A 0 A_CustomPunch(random(20,35)*3,true,0,"SamsaraHexenCStaffPuffCoop",128)
        stop

      PickupAttackInfinite:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackInfiniteCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackInfiniteCoop")
      PickupAttackInfiniteDM:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupAttackInfiniteDMSpread")
        TNT1 A 0 A_CustomPunch(random(20,35),true,0,"SamsaraHexenCStaffPuffInfinite",128)
        stop

      PickupAttackInfiniteDMSpread:
        TNT1 A 0 A_CustomPunch(random(20,35)*3,true,0,"SamsaraHexenCStaffPuffInfinite",128)
        stop

      PickupAttackInfiniteCoop:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupAttackInfiniteCoopSpread")
        TNT1 A 0 A_CustomPunch(random(20,35),true,0,"SamsaraHexenCStaffPuffInfiniteCoop",128)
        stop

      PickupAttackInfiniteCoopSpread:
        TNT1 A 0 A_CustomPunch(random(20,35)*3,true,0,"SamsaraHexenCStaffPuffInfiniteCoop",128)
        stop
    }
}

actor SamsaraHexenCStaffPuff : CStaffPuff
{
    +HITTRACER
    +PUFFGETSOWNER
    States
    {
      Spawn:
      Crash:
        goto Super::Spawn

      Melee:
        TNT1 A 0 A_TakeInventory("Clip",1,0,AAPTR_TARGET)
      MeleeDrain:
        TNT1 A 0 A_JumpIf(CallACS("SamsaraHexenSerpentStaffMeleeHealCheck") == 1, 1)
        goto Super::Spawn
        TNT1 A 0 A_GiveInventory("SamsaraHexenSerpentStaffDrained",1,AAPTR_TARGET)
        TNT1 A 0 A_JumpIf(CallACS("SamsaraDecorate", 22), "MeleeDrainInfinite")
        TNT1 A 0 A_GiveInventory("FiniteHealth",random(2,4),AAPTR_TARGET)
        TNT1 A 0 A_GiveInventory("FiniteHealth200",1,AAPTR_TARGET)
        goto Super::Spawn

      MeleeDrainInfinite:
        TNT1 A 0 A_GiveInventory("InfiniteHealth",random(3,5),AAPTR_TARGET)
        goto Super::Spawn
    }
}

actor SamsaraHexenCStaffPuffCoop : SamsaraHexenCStaffPuff { +MTHRUSPECIES }

actor SamsaraHexenCStaffPuffInfinite : SamsaraHexenCStaffPuff
{
    States
    {
      Melee:
        goto MeleeDrain
    }
}

actor SamsaraHexenCStaffPuffInfiniteCoop : SamsaraHexenCStaffPuffInfinite { +MTHRUSPECIES }

actor SerpentHealth : Health { Inventory.Amount 1 } // should be obsoleted in favor of FiniteHealth

// Ranged

actor SamsaraHexenSerpentStaffRangedAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("ClericCStaffFire",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("SamsaraHexenCStaffMissile",-3,false)
        TNT1 A 0 A_FireCustomMissile("SamsaraHexenCStaffMissile2",3,false)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("SamsaraHexenCStaffMissileCoop",-3,false)
        TNT1 A 0 A_FireCustomMissile("SamsaraHexenCStaffMissile2Coop",3,false)
        stop
    }
}

actor SamsaraHexenCStaffMissile : CStaffMissile
{
    Damage (random(1,5)*5) // 5
    WeaveIndexXY 32
    Decal "DoomImpScorch"
    Obituary "$SAMSARA_HEXEN_OB_SLOT4_2"
    +BLOODSPLATTER
}

actor SamsaraHexenCStaffMissile2 : SamsaraHexenCStaffMissile { WeaveIndexXY 0 }

actor SamsaraHexenCStaffMissileCoop : SamsaraHexenCStaffMissile
{
    Species "Player"
    +THRUSPECIES
}

actor SamsaraHexenCStaffMissile2Coop : SamsaraHexenCStaffMissile2
{
    Species "Player"
    +THRUSPECIES
}

// Daedolon

actor SamsaraHexenBlightShockAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",5,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",5)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("hexen/cult/fire",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 AAAA 0 A_FireCustomMissile("BlightShockShot1",frandom(-12,12),false,0,0,1,frandom(-4,4))
        TNT1 A 0 A_FireCustomMissile("BlightShockShot1",0,false)
        stop

      PickupAttackCoop:
        TNT1 AAAA 0 A_FireCustomMissile("BlightShockShot1Coop",frandom(-12,12),false,0,0,1,frandom(-4,4))
        TNT1 A 0 A_FireCustomMissile("BlightShockShot1Coop",0,false)
        stop
    }
}

actor BlightShockShot1
{
    Projectile
    Radius 6
    Speed 35
    Damage 1 // was 1,6
    DamageType "HexenPoison"
    PoisonDamage 2, 4
    PoisonDamageType "HexenPoison"
    BounceType "Hexen"
    BounceCount 2
    WallBounceFactor 2
    RenderStyle "Add"
    Scale 0.75
    DeathSound "hexen/cult/hit"
    Decal "DoomImpScorch"
    //+ADDITIVEPOISONDAMAGE
    +BLOODSPLATTER
    +CANBOUNCEWATER
    +RANDOMIZE
    +RIPPER
    States
    {
      Spawn:
        H111 A 1 Bright A_SpawnItemEx("BlightShockTrail1") TNT1 AAA 0 A_CStaffMissileSlither
        H111 A 1 Bright A_SpawnItemEx("BlightShockTrail1") TNT1 AAA 0 A_CStaffMissileSlither
        H111 A 1 Bright A_SpawnItemEx("BlightShockTrail1") TNT1 AAA 0 A_CStaffMissileSlither
        H111 B 1 Bright A_SpawnItemEx("BlightShockTrail1") TNT1 AAA 0 A_CStaffMissileSlither
        H111 B 1 Bright A_SpawnItemEx("BlightShockTrail1") TNT1 AAA 0 A_CStaffMissileSlither
        H111 B 1 Bright A_SpawnItemEx("BlightShockTrail1") TNT1 AAA 0 A_CStaffMissileSlither
        H111 C 1 Bright A_SpawnItemEx("BlightShockTrail1") TNT1 AAA 0 A_CStaffMissileSlither
        H111 C 1 Bright A_SpawnItemEx("BlightShockTrail1") TNT1 AAA 0 A_CStaffMissileSlither
        H111 C 1 Bright A_SpawnItemEx("BlightShockTrail1") TNT1 AAA 0 A_CStaffMissileSlither
        H111 D 1 Bright A_SpawnItemEx("BlightShockTrail1") TNT1 AAA 0 A_CStaffMissileSlither
        H111 D 1 Bright A_SpawnItemEx("BlightShockTrail1") TNT1 AAA 0 A_CStaffMissileSlither
        H111 D 1 Bright A_SpawnItemEx("BlightShockTrail1") TNT1 AAA 0 A_CStaffMissileSlither
        loop

      Death:
        H111 E 8 Bright
        H111 F 6 Bright
        H111 G 4 Bright
        H111 H 2 Bright
        stop
    }
}

actor BlightShockTrail1
{
    RenderStyle "Add"
    Alpha 0.5
    Scale 0.5
    +NOCLIP
    +NOGRAVITY
    +NOINTERACTION
    States
    {
      Spawn:
        H112 A 0 Bright
        H112 AABBCCDDEE 1 Bright //A_FadeOut(0.05)
        stop
    }
}

actor BlightShockShot1Coop : BlightShockShot1
{
    Species "Player"
    +THRUSPECIES
}

// Baratus

// Melee

actor SamsaraHexenJotunnsFlailMeleeAttackHandler1 : Trigger
{
    States
    {
      // Initializing stuff
      Pickup:
        TNT1 A 0 A_TakeInventory("SamsaraHexenJotunnsFlailMeleeAmmoTaken")
        goto PickupAmmoCheck

      // Ammo checking stuff
      PickupAmmoCheck:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttackStrong")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"PickupAttackStrong")
        goto PickupAttackWeak

      // Attack stuff
      // Weak
      PickupAttackWeak:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackWeakCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackWeakCoop")
      PickupAttackWeakDM:
        TNT1 A 0 A_FireBullets(-10,-5,-1,random(10,20),"FlailPuff",FBF_NORANDOM|FBF_EXPLICITANGLE|FBF_NOFLASH|FBF_NORANDOMPUFFZ,128)
        stop

      PickupAttackWeakCoop:
        TNT1 A 0 A_FireBullets(-10,-5,-1,random(10,20),"FlailPuffCoop",FBF_NORANDOM|FBF_EXPLICITANGLE|FBF_NOFLASH|FBF_NORANDOMPUFFZ,128)
        stop

      // Strong
      PickupAttackStrong:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttackStrongInfinite")
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackStrongCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackStrongCoop")
      PickupAttackStrongDM:
        TNT1 A 0 A_FireBullets(-10,-5,-1,random(20,40),"FlailPuffGlow",FBF_NORANDOM|FBF_EXPLICITANGLE|FBF_NOFLASH|FBF_NORANDOMPUFFZ,128)
        stop

      PickupAttackStrongCoop:
        TNT1 A 0 A_FireBullets(-10,-5,-1,random(20,40),"FlailPuffGlowCoop",FBF_NORANDOM|FBF_EXPLICITANGLE|FBF_NOFLASH|FBF_NORANDOMPUFFZ,128)
        stop

      PickupAttackStrongInfinite:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackStrongInfiniteCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackStrongInfiniteCoop")
      PickupAttackStrongInfiniteDM:
        TNT1 A 0 A_FireBullets(-10,-5,-1,random(20,40),"FlailPuffGlowInfinite",FBF_NORANDOM|FBF_EXPLICITANGLE|FBF_NOFLASH|FBF_NORANDOMPUFFZ,128)
        stop

      PickupAttackStrongInfiniteCoop:
        TNT1 A 0 A_FireBullets(-10,-5,-1,random(20,40),"FlailPuffGlowInfiniteCoop",FBF_NORANDOM|FBF_EXPLICITANGLE|FBF_NOFLASH|FBF_NORANDOMPUFFZ,128)
        stop
    }
}

actor SamsaraHexenJotunnsFlailMeleeAttackHandler2 : Trigger
{
    States
    {
      // Initializing stuff
      Pickup:
        TNT1 A 0 A_TakeInventory("SamsaraHexenJotunnsFlailMeleeAmmoTaken")
        goto PickupAmmoCheck

      // Ammo checking stuff
      PickupAmmoCheck:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttackStrong")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"PickupAttackStrong")
        goto PickupAttackWeak

      // Attack stuff
      // Weak
      PickupAttackWeak:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackWeakCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackWeakCoop")
      PickupAttackWeakDM:
        TNT1 A 0 A_FireBullets(0,0,-1,random(10,20),"FlailPuff",FBF_NORANDOM|FBF_EXPLICITANGLE|FBF_NOFLASH|FBF_NORANDOMPUFFZ,128)
        stop

      PickupAttackWeakCoop:
        TNT1 A 0 A_FireBullets(0,0,-1,random(10,20),"FlailPuffCoop",FBF_NORANDOM|FBF_EXPLICITANGLE|FBF_NOFLASH|FBF_NORANDOMPUFFZ,128)
        stop

      // Strong
      PickupAttackStrong:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttackStrongInfinite")
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackStrongCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackStrongCoop")
      PickupAttackStrongDM:
        TNT1 A 0 A_FireBullets(0,0,-1,random(20,40),"FlailPuffGlow",FBF_NORANDOM|FBF_EXPLICITANGLE|FBF_NOFLASH|FBF_NORANDOMPUFFZ,128)
        stop

      PickupAttackStrongCoop:
        TNT1 A 0 A_FireBullets(0,0,-1,random(20,40),"FlailPuffGlowCoop",FBF_NORANDOM|FBF_EXPLICITANGLE|FBF_NOFLASH|FBF_NORANDOMPUFFZ,128)
        stop

      PickupAttackStrongInfinite:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackStrongInfiniteCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackStrongInfiniteCoop")
      PickupAttackStrongInfiniteDM:
        TNT1 A 0 A_FireBullets(0,0,-1,random(20,40),"FlailPuffGlowInfinite",FBF_NORANDOM|FBF_EXPLICITANGLE|FBF_NOFLASH|FBF_NORANDOMPUFFZ,128)
        stop

      PickupAttackStrongInfiniteCoop:
        TNT1 A 0 A_FireBullets(0,0,-1,random(20,40),"FlailPuffGlowInfiniteCoop",FBF_NORANDOM|FBF_EXPLICITANGLE|FBF_NOFLASH|FBF_NORANDOMPUFFZ,128)
        stop
    }
}

actor SamsaraHexenJotunnsFlailMeleeAttackHandler3 : Trigger
{
    States
    {
      // Initializing stuff
      Pickup:
        TNT1 A 0 A_TakeInventory("SamsaraHexenJotunnsFlailMeleeAmmoTaken")
        goto PickupAmmoCheck

      // Ammo checking stuff
      PickupAmmoCheck:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttackStrong")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"PickupAttackStrong")
        goto PickupAttackWeak

      // Attack stuff
      // Weak
      PickupAttackWeak:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackWeakCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackWeakCoop")
      PickupAttackWeakDM:
        TNT1 A 0 A_FireBullets(10,5,-1,random(10,20),"FlailPuff",FBF_NORANDOM|FBF_EXPLICITANGLE|FBF_NOFLASH|FBF_NORANDOMPUFFZ,128)
        stop

      PickupAttackWeakCoop:
        TNT1 A 0 A_FireBullets(10,5,-1,random(10,20),"FlailPuffCoop",FBF_NORANDOM|FBF_EXPLICITANGLE|FBF_NOFLASH|FBF_NORANDOMPUFFZ,128)
        stop

      // Strong
      PickupAttackStrong:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttackStrongInfinite")
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackStrongCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackStrongCoop")
      PickupAttackStrongDM:
        TNT1 A 0 A_FireBullets(10,5,-1,random(20,40),"FlailPuffGlow",FBF_NORANDOM|FBF_EXPLICITANGLE|FBF_NOFLASH|FBF_NORANDOMPUFFZ,128)
        stop

      PickupAttackStrongCoop:
        TNT1 A 0 A_FireBullets(10,5,-1,random(20,40),"FlailPuffGlowCoop",FBF_NORANDOM|FBF_EXPLICITANGLE|FBF_NOFLASH|FBF_NORANDOMPUFFZ,128)
        stop

      PickupAttackStrongInfinite:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackStrongInfiniteCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackStrongInfiniteCoop")
      PickupAttackStrongInfiniteDM:
        TNT1 A 0 A_FireBullets(10,5,-1,random(20,40),"FlailPuffGlowInfinite",FBF_NORANDOM|FBF_EXPLICITANGLE|FBF_NOFLASH|FBF_NORANDOMPUFFZ,128)
        stop

      PickupAttackStrongInfiniteCoop:
        TNT1 A 0 A_FireBullets(10,5,-1,random(20,40),"FlailPuffGlowInfiniteCoop",FBF_NORANDOM|FBF_EXPLICITANGLE|FBF_NOFLASH|FBF_NORANDOMPUFFZ,128)
        stop
    }
}

// Weak

actor FlailPuff : AxePuff
{
    SeeSound ""
    ActiveSound ""
    AttackSound ""
    +NOTIMEFREEZE
    +PUFFGETSOWNER
    States
    {
      Spawn:
        FHFX S 0 NoDelay A_PlaySound("FighterAxeHitThing")
        goto PuffSpawned

      XDeath:
        TNT1 A 0 A_PlaySound("FighterAxeHitThing")
        goto PuffSpawned

      Crash:
        TNT1 A 0 A_PlaySound("FighterHammerHitWall")
        goto PuffSpawned

      PuffSpawned:
        TNT1 A 0 A_GiveInventory("SamsaraHexenJotunnsFlailMeleePuffSpawned",1,AAPTR_TARGET)
        TNT1 A 0 A_ChangeFlag("NOTIMEFREEZE",false)
        goto Super::Spawn
    }
}

actor FlailPuffCoop : FlailPuff { +MTHRUSPECIES }

// Strong

actor FlailPuffGlow : AxePuffGlow
{
    DamageType "HexenIce"
    RenderStyle "Normal"
    SeeSound ""
    ActiveSound ""
    AttackSound ""
    +NOTIMEFREEZE
    +PUFFGETSOWNER
    States
    {
      Spawn:
        SHEX A 0 Bright NoDelay A_JumpIfInventory("SamsaraHexenJotunnsFlailMeleeAmmoTaken",1,3,AAPTR_TARGET)
        TNT1 A 0 A_TakeInventory("Clip",1,0,AAPTR_TARGET)
        TNT1 A 0 A_GiveInventory("SamsaraHexenJotunnsFlailMeleeAmmoTaken",1,AAPTR_TARGET)
        TNT1 A 0 A_PlaySound("FighterAxeHitThing")
        goto PuffSpawned

      XDeath:
        TNT1 A 0 A_JumpIfInventory("SamsaraHexenJotunnsFlailMeleeAmmoTaken",1,3,AAPTR_TARGET)
        TNT1 A 0 A_TakeInventory("Clip",1,0,AAPTR_TARGET)
        TNT1 A 0 A_GiveInventory("SamsaraHexenJotunnsFlailMeleeAmmoTaken",1,AAPTR_TARGET)
        TNT1 A 0 A_PlaySound("FighterAxeHitThing")
        goto PuffSpawned

      Crash:
        TNT1 A 0 A_PlaySound("FighterHammerHitWall")
        goto PuffSpawned

      PuffSpawned:
        TNT1 A 0 A_GiveInventory("SamsaraHexenJotunnsFlailMeleePuffSpawned",1,AAPTR_TARGET)
        SHEX A 4 Bright A_ChangeFlag("NOTIMEFREEZE",false)
        SHEX BCDE 4 Bright
        stop
    }
}

actor FlailPuffGlowCoop : FlailPuffGlow { +MTHRUSPECIES }

actor FlailPuffGlowInfinite : FlailPuffGlow
{
    States
    {
      Spawn:
        SHEX A 0 Bright NoDelay A_PlaySound("FighterAxeHitThing")
        goto PuffSpawned

      XDeath:
        TNT1 A 0 A_PlaySound("FighterAxeHitThing")
        goto PuffSpawned
    }
}

actor FlailPuffGlowInfiniteCoop : FlailPuffGlowInfinite { +MTHRUSPECIES }

// Ranged

actor SamsaraHexenJotunnsFlailRangedAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHexenJotunnsFlailMeleePuffSpawned",1,"PickupStop")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",3,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",3)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("FlailIceSpikeBall",0,false)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("FlailIceSpikeBall2",0,false)
        stop
    }
}

actor FlailIceSpikeBall
{
    Projectile
    Radius 4
    Height 8
    Speed 16
    Damage 8
    DamageType "HexenIce"
    DeathSound "MageShardsExplode"
    +BLOODSPLATTER
    States
    {
      Spawn:
        H122 PQ 2 Bright A_CustomMissile("FlailIceSpikeMissile",0,0,random(0,359),CMF_AIMDIRECTION|CMF_TRACKOWNER|CMF_SAVEPITCH)
        loop

      Death:
        SHEX A 4 Bright A_Explode(32,16,0)
        SHEX BCDE 4 Bright
        stop
    }
}

actor FlailIceSpikeBall2 : FlailIceSpikeBall
{
    Species "Player"
    +THRUSPECIES
    States
    {
      Spawn:
        H122 PQ 2 Bright A_CustomMissile("FlailIceSpikeMissile2",0,0,random(0,359),CMF_AIMDIRECTION|CMF_TRACKOWNER|CMF_SAVEPITCH)
        loop
    }
}

actor FlailIceSpikeMissile : FrostMissileDM { Damage (random(8,16)) }

actor FlailIceSpikeMissile2 : FrostMissile2 { Damage (random(8,16)) }
