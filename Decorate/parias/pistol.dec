actor "Sapphire Wand" : MWeapWand
{
    Weapon.SelectionOrder 3600
    Weapon.SlotNumber 1
    +INVENTORY.UNDROPPABLE
    Weapon.KickBack 0
    Weapon.YAdjust 9
	Inventory.RestrictedTo "HexenPlayer"	
    States
    {
      Select:
		TNT1 A 0 A_JumpIfInventory("HexenClassMode", 2, "SelectFighter")
        MWND A 1 A_Raise
        Loop
	  SelectFighter:
		H100 E 1 A_Raise
		Loop
      Deselect:
		TNT1 A 0 A_JumpIfInventory("HexenClassMode", 2, "DeselectFighter")
        MWND A 1 A_Lower
        Loop
	  DeselectFighter:
		H100 E 1 A_Lower
		Loop
      Ready:
		TNT1 A 0 A_JumpIfInventory("HexenClassMode", 2, "ReadyFighter")
        MWND A 1 A_WeaponReady
        Loop
	  ReadyFighter:
		H100 E 0 A_JumpIfNoAmmo("FakeReadyFighter")
		H100 EF 5
	  RealReadyFighter:
		TNT1 A 0 A_JumpIfInventory("HexenClassMode", 2, 1)
		Goto Ready
		H100 A 1 A_WeaponReady
		Loop
	  FakeReadyFighter:
		TNT1 A 0 A_JumpIfInventory("HexenClassMode", 2, 1)
		Goto Ready
		H100 E 1 A_WeaponReady
		Loop
      Fire:
		TNT1 A 0 A_JumpIfInventory("HexenClassMode", 2, "FireFighter")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopFire")
		MWND A 2
		MWND J 2
		MWND K 2 Bright
		MWND B 4 Bright Offset (0, 48) A_FireCustomMissile ("MageWandMissile2")
		MWND J 3 Offset (0, 40)
		MWND A 2 Offset (0, 36)
		MWND A 3 Offset (0, 32) A_ReFire
		Goto Ready
      CoopFire:
		MWND A 2
		MWND J 2
		MWND K 2 Bright
		MWND B 4 Bright Offset (0, 48) A_FireCustomMissile ("MageWandMissile3")
		MWND J 3 Offset (0, 40)
		MWND A 2 Offset (0, 36)
		MWND A 3 Offset (0, 32) A_ReFire
		Goto Ready
	  FireFighter:
		H100 B 6 A_FireCustomMissile("HexenImpalerMissile",0,1)
		H100 B 0 A_Recoil(1)
		H100 C 8 A_PlaySound("Hexen/Weapon/XBowLoad", CHAN_WEAPON)
		H100 D 5
		H100 E 5 A_CheckReload
		H100 F 5
		H100 A 12
		Goto RealReadyFighter
    }
}

actor MageWandMissile2 : MageWandMissile replaces MageWandMissile
{
    Radius 3
    Height 2
    Obituary "$SAMSARA_HEXEN_OB_PISTOL"
    Decal RailScorchLower
    Damage 1
    +STRIFEDAMAGE
    +SPAWNSOUNDSOURCE
    +NOBOSSRIP
    DamageType "Pistol"
}

actor MageWandMissile3 : MageWandMissile2 { Species "Player" +THRUSPECIES }

ACTOR HexenImpalerMissile : FastProjectile
{
	Projectile
	+STRIFEDAMAGE
	+BLOODSPLATTER
	+NOEXTREMEDEATH
	Radius 4
	Height 8
	Damage 25
	Speed 80
	SeeSound "Hexen/Weapon/XBowFire"
	States
	{
		Spawn:
			H100 H 1
			Loop
		Death:
		Crash:
			H100 I 3 A_PlaySound("Hexen/Weapon/XBowHit")
			H100 JKLMN 3
			Stop
		XDeath:
			TNT1 A 10 A_PlaySound("Hexen/Weapon/XBowSplatter")
			Stop
	}
}