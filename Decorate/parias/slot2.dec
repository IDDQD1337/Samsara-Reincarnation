actor "Frost Shards" : SamsaraHexenWeapon
{
    Inventory.PickupMessage "Crusader's Longbow"
    Weapon.SlotNumber 2
    Weapon.SelectionOrder 1700
    Weapon.YAdjust 20
    Weapon.AmmoType "Clip"
    Weapon.AmmoGive 25
    Weapon.AmmoUse 3
    Obituary "$SAMSARA_HEXEN_OB_SLOT2"
    Tag "Crusader's Longbow"
    +AMMO_OPTIONAL
    +BLOODSPLATTER
    +NOALERT
    States
    {
      Spawn:
        H114 A -1
        stop

      // Parias
      Ready:
        TNT1 A 0 A_JumpIfInventory("HexenClassMode",2,"ReadyFighter")
        TNT1 A 0 A_JumpIfInventory("HexenClassMode",1,"ReadyMage")
      ReadyCleric:
        H114 BBBFFF 1 A_WeaponReady
        goto Ready

      Deselect:
        TNT1 A 0 A_JumpIfInventory("HexenClassMode",2,"DeselectFighter")
        TNT1 A 0 A_JumpIfInventory("HexenClassMode",1,"DeselectMage")
      DeselectCleric:
        H114 B 1 A_Lower
        goto Deselect

      Select:
        TNT1 A 0 A_JumpIfInventory("HexenClassMode",2,"SelectFighter")
        TNT1 A 0 A_JumpIfInventory("HexenClassMode",1,"SelectMage")
      SelectCleric:
        H114 B 1 A_Raise
        goto Select

      Fire:
        TNT1 A 0 A_JumpIfInventory("HexenClassMode",2,"FireFighter")
        TNT1 A 0 A_JumpIfInventory("HexenClassMode",1,"FireMage")
      FireCleric:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Clip",3,1)
        goto NoAmmoSwitch
        H114 C 3
        H114 D 3 A_PlaySound("Hexen/BowString",CHAN_WEAPON)
        goto HoldStartCleric

      Hold:
        TNT1 A 0 A_JumpIfInventory("HexenClassMode",2,"FireFighter")
        TNT1 A 0 A_JumpIfInventory("HexenClassMode",1,"HoldMage")
      HoldCleric:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"HoldStartCleric")
        TNT1 A 0 A_JumpIfInventory("Clip",3,"HoldStartCleric")
        goto NoAmmoSwitch

      HoldStartCleric:
        H114 EG 3
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Clip",3,1)
        goto NoAmmoSwitch
        TNT1 A 0 A_ReFire
        H114 H 3 A_GiveInventory("SamsaraHexenCrusadersLongbowAttackHandler")
        H114 ICB 3
        goto Ready

      // Daedolon (Frost Shards)
      ReadyMage:
        CONE H 1 A_WeaponReady
        goto Ready

      DeselectMage:
        CONE H 1 A_Lower
        goto Deselect

      SelectMage:
        CONE H 1 A_Raise
        goto Select

      FireMage:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Clip",2,1)
        goto NoAmmoSwitch
        CONE I 3 A_AlertMonsters
        CONE J 4
        goto HoldStartMage

      HoldMage:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Clip",2,1)
        goto NoAmmoSwitch
        TNT1 A 0 A_AlertMonsters
      HoldStartMage:
        CONE K 3
        CONE L 5 Bright
        CONE M 4 Bright A_GiveInventory("SamsaraHexenFrostShardsAttackHandler")
        CONE N 4
        CONE H 5
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Clip",2,1)
        goto NoAmmoSwitch
        CONE H 7 A_ReFire
        goto Ready

      // Baratus (Spear of Zeal)
      ReadyFighter:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReadyFighterAmmo")
        TNT1 A 0 A_JumpIfInventory("Clip",3,"ReadyFighterAmmo")
      ReadyFighterNoAmmo:
        H101 A 1 A_WeaponReady
        goto Ready

      ReadyFighterAmmo:
        H101 BCDC 5 Bright A_WeaponReady
        goto Ready

      DeselectFighter:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"DeselectFighterAmmo")
        TNT1 A 0 A_JumpIfInventory("Clip",3,"DeselectFighterAmmo")
      DeselectFighterNoAmmo:
        H101 A 1 A_Lower
        goto Deselect

      DeselectFighterAmmo:
        H101 B 1 Bright A_Lower
        goto Deselect

      SelectFighter:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"SelectFighterAmmo")
        TNT1 A 0 A_JumpIfInventory("Clip",3,"SelectFighterAmmo")
      SelectFighterNoAmmo:
        H101 A 1 A_Raise
        goto Select

      SelectFighterAmmo:
        H101 B 1 Bright A_Raise
        goto Select

      FireFighter:
        TNT1 A 0 A_ClearReFire
        TNT1 A 0 A_JumpIfCloser(128,"FireFighterMelee")
        goto FireFighterRanged

      FireFighterMelee:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireFighterMeleeAmmo")
        TNT1 A 0 A_JumpIfInventory("Clip",3,"FireFighterMeleeAmmo")
      FireFighterMeleeNoAmmo:
        H101 A 2 Offset(-32,34)
        H101 A 2 Offset(-64,38)
        goto FireFighterMeleeContinue

      FireFighterMeleeAmmo:
        H101 B 2 Bright Offset(-32,34)
        H101 B 2 Bright Offset(-64,38)
        goto FireFighterMeleeContinue

      FireFighterMeleeContinue:
        TNT1 A 0 A_GiveInventory("SamsaraHexenSpearOfZealMeleeAttackHandler")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireFighterMeleeFinishAmmo")
        TNT1 A 0 A_JumpIfInventory("Clip",3,"FireFighterMeleeFinishAmmo")
      FireFighterMeleeFinishNoAmmo:
        H101 A 2 Offset(-96,42)
        H101 A 2 Offset(-64,38)
        H101 A 2 Offset(-32,34)
        H101 "[" 8 Offset(-32,32) // offset hack via TEXTURES since 0 in either position (especially X) implies the default
        goto Ready

      FireFighterMeleeFinishAmmo:
        H101 B 2 Bright Offset(-96,42)
        H101 B 2 Bright Offset(-64,38)
        H101 B 2 Bright Offset(-32,34)
        H101 "\" 8 Bright Offset(-32,32) // offset hack via TEXTURES since 0 in either position (especially X) implies the default
        goto Ready

      FireFighterRanged:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireFighterRangedAmmo")
        TNT1 A 0 A_JumpIfInventory("Clip",3,"FireFighterRangedAmmo")
      FireFighterRangedNoAmmo:
        H101 EF 4
        H101 G 3
        H101 H 2 A_PlaySound("FighterHammerMiss",CHAN_WEAPON)
        goto FireFighterRangedContinue

      FireFighterRangedAmmo:
        H101 NO 4 Bright
        H101 P 3 Bright
        H101 Q 2 Bright A_PlaySound("FighterHammerMiss",CHAN_WEAPON)
        goto FireFighterRangedContinue

      FireFighterRangedContinue:
        H101 I 2 A_GiveInventory("SamsaraHexenSpearOfZealRangedAttackHandler")
        H101 JKLM 2
        TNT1 A 16
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireFighterRangedFinishAmmo")
        TNT1 A 0 A_JumpIfInventory("Clip",3,"FireFighterRangedFinishAmmo")
      FireFighterRangedFinishNoAmmo:
        H101 A 3 Offset(0,92)
        H101 A 3 Offset(0,72)
        H101 A 3 Offset(0,52)
        goto Ready

      FireFighterRangedFinishAmmo:
        H101 B 3 Offset(0,92) Bright
        H101 B 3 Offset(0,72) Bright
        H101 B 3 Offset(0,52) Bright
        goto Ready

      // "No ammo" switch
      NoAmmoSwitch:
        TNT1 A 0 A_ClearReFire
        TNT1 A 0 ACS_NamedExecuteWithResult("Hexen_SwitchPriority")
        goto Deselect
    }
}

// Parias

actor SamsaraHexenCrusadersLongbowAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",3,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",3)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("Hexen/ElectricArrow",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("HexenShockArrow",0,false,5,0)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("HexenShockArrow2",0,false,5,0)
        stop
    }
}

actor HexenShockArrow
{
    Projectile
    Radius 5
    Height 5
    Speed 50
    Damage 21
    DamageType "Electric"
    RenderStyle "Add"
    DeathSound "Hexen/ArrowHit"
    Obituary "%o was shocked by one of %k's thunder arrows."
    +BLOODSPLATTER
    +DONTREFLECT
    States
    {
      Spawn:
        H115 A 1 Bright A_SpawnItemEx("HexenElectroTrail")
        loop

      Death:
        FAXE R 4 Bright A_AlertMonsters
        FAXE STUVWX 4 Bright
        stop
    }
}

actor HexenShockArrow2 : HexenShockArrow
{
    Species "Player"
    +THRUSPECIES
}

actor HexenElectroTrail
{
    RenderStyle "Add"
    Scale 0.2
    +CLIENTSIDEONLY
    +NOGRAVITY
    States
    {
      Spawn:
        FAXE UVWX 2 Bright
        stop
    }
}

// Daedolon

actor SamsaraHexenFrostShardsAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",2,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",2)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("MageShardsFire",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfCloser(92,"PickupAttackMelee")
        goto PickupAttackRanged

      // Melee
      PickupAttackMelee:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackMeleeCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackMeleeCoop")
      PickupAttackMeleeDM:
        TNT1 A 0 A_FireCustomMissile("FrostMissileMelee",0,false)
        stop

      PickupAttackMeleeCoop:
        TNT1 A 0 A_FireCustomMissile("FrostMissileMeleeCoop",0,false)
        stop

      // Ranged
      // THIS HERE AREA WAS A DICK
      PickupAttackRanged:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackRangedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackRangedCoop")
      PickupAttackRangedDM:
      PickupAttackRangedDMPattern1:
        TNT1 A 0 A_FireCustomMissile("FrostMissileDM",14,false,6,1)
        TNT1 A 0 A_FireCustomMissile("FrostMissileDM",9,false,4,1)
        TNT1 A 0 A_FireCustomMissile("FrostMissileDM",5,false,2,1)
        TNT1 A 0 A_FireCustomMissile("FrostMissileDM",-14,false,-6,1)
        TNT1 A 0 A_FireCustomMissile("FrostMissileDM",-9,false,-4,1)
        TNT1 A 0 A_FireCustomMissile("FrostMissileDM",-5,false,-2,1)
      PickupAttackRangedDMPattern2:
        TNT1 A 0 A_FireCustomMissile("FrostMissileDM2",0,false,0,8)
        TNT1 A 0 A_FireCustomMissile("FrostMissileDM3",0,false,0,16)
        TNT1 A 0 A_FireCustomMissile("FrostMissileDM4",0,false,0,24)
        TNT1 A 0 A_FireCustomMissile("FrostMissileDM2",0,false,0,-2)
        TNT1 A 0 A_FireCustomMissile("FrostMissileDM3",0,false,0,-3)
        TNT1 A 0 A_FireCustomMissile("FrostMissileDM4",0,false,0,-4)
      PickupAttackRangedDMPattern3:
        TNT1 A 0 A_FireCustomMissile("FrostMissileDM2",4,false,1,16)
        TNT1 A 0 A_FireCustomMissile("FrostMissileDM2",4,false,1,-2)
        TNT1 A 0 A_FireCustomMissile("FrostMissileDM2",-4,false,-1,16)
        TNT1 A 0 A_FireCustomMissile("FrostMissileDM2",-4,false,-1,-2)
      PickupAttackRangedDMPattern4:
        TNT1 A 0 A_FireCustomMissile("FrostMissileDM",0,false)
        stop

      PickupAttackRangedCoop:
      PickupAttackRangedCoopPattern1:
        TNT1 A 0 A_FireCustomMissile("FrostMissile2",14,false,6,1)
        TNT1 A 0 A_FireCustomMissile("FrostMissile2",9,false,4,1)
        TNT1 A 0 A_FireCustomMissile("FrostMissile2",5,false,2,1)
        TNT1 A 0 A_FireCustomMissile("FrostMissile2",-14,false,-6,1)
        TNT1 A 0 A_FireCustomMissile("FrostMissile2",-9,false,-4,1)
        TNT1 A 0 A_FireCustomMissile("FrostMissile2",-5,false,-2,1)
      PickupAttackRangedCoopPattern2:
        TNT1 A 0 A_FireCustomMissile("FrostMissile2_2",0,false,0,8)
        TNT1 A 0 A_FireCustomMissile("FrostMissile2_3",0,false,0,16)
        TNT1 A 0 A_FireCustomMissile("FrostMissile2_4",0,false,0,24)
        TNT1 A 0 A_FireCustomMissile("FrostMissile2_2",0,false,0,-2)
        TNT1 A 0 A_FireCustomMissile("FrostMissile2_3",0,false,0,-3)
        TNT1 A 0 A_FireCustomMissile("FrostMissile2_4",0,false,0,-4)
      PickupAttackRangedCoopPattern3:
        TNT1 A 0 A_FireCustomMissile("FrostMissile2_2",4,false,1,16)
        TNT1 A 0 A_FireCustomMissile("FrostMissile2_2",4,false,1,-2)
        TNT1 A 0 A_FireCustomMissile("FrostMissile2_2",-4,false,-1,16)
        TNT1 A 0 A_FireCustomMissile("FrostMissile2_2",-4,false,-1,-2)
      PickupAttackRangedCoopPattern4:
        TNT1 A 0 A_FireCustomMissile("FrostMissile2",0,false)
        stop
      // Y'ALL MOTHERFUCKERS BETTER APPRECIATE IT
    }
}

// Melee

actor FrostMissileMelee : FastProjectile
{
    Radius 6
    Height 6
    Speed 20
    Damage (random(70,95)) // Previously 90-115
    DamageType "HexenIce"
    +BLOODLESSIMPACT
    States
    {
      Spawn:
        TNT1 AA 1
        stop

      Death:
        TNT1 A 1
        stop
    }
}

actor FrostMissileMeleeCoop : FrostMissileMelee
{
    Species "Player"
    +THRUSPECIES
}

// Ranged

actor FrostMissileDM : FrostMissile
{
    Damage (random(1,4))
    DamageType "HexenIce"
    Obituary "$SAMSARA_HEXEN_OB_SLOT2"
    +BLOODSPLATTER
    +THRUGHOST
}

actor FrostMissileDM2 : FrostMissileDM { Speed 23 }
actor FrostMissileDM3 : FrostMissileDM { Speed 21 }
actor FrostMissileDM4 : FrostMissileDM { Speed 19 }

actor FrostMissile2 : FrostMissile replaces FrostMissile
{
    DamageType "HexenIce"
    Obituary "$SAMSARA_HEXEN_OB_SLOT2"
    Species "Player"
    +BLOODSPLATTER
    +THRUGHOST
    +THRUSPECIES
}

actor FrostMissileEnemy : FrostMissile // dark side version
{
    DamageType "HexenIce"
    Obituary "$SAMSARA_HEXEN_OB_SLOT2"
    +BLOODSPLATTER
    +THRUGHOST
}

actor FrostMissile2_1 : FrostMissileDM
{
    Species "Player"
    +THRUSPECIES
}

actor FrostMissile2_2 : FrostMissileDM2
{
    Species "Player"
    +THRUSPECIES
}

actor FrostMissile2_3 : FrostMissileDM3
{
    Species "Player"
    +THRUSPECIES
}

actor FrostMissile2_4 : FrostMissileDM4
{
    Species "Player"
    +THRUSPECIES
}

// Baratus

// Melee

actor SamsaraHexenSpearOfZealMeleeAttackHandler : Trigger
{
    States
    {
      // Ammo checking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttackStrong")
        TNT1 A 0 A_JumpIfInventory("Clip",3,"PickupAttackStrong")
        goto PickupAttackWeak

      // Attack stuff
      // Weak
      PickupAttackWeak:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackWeakCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackWeakCoop")
      PickupAttackWeakDM:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupAttackWeakDMSpread")
        TNT1 A 0 A_CustomPunch(random(24,48),true,0,"SpearPuff",128)
        stop

      PickupAttackWeakDMSpread:
        TNT1 A 0 A_CustomPunch(random(24,48)*3,true,0,"SpearPuff",128)
        stop

      PickupAttackWeakCoop:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupAttackWeakCoopSpread")
        TNT1 A 0 A_CustomPunch(random(24,48),true,0,"SpearPuffCoop",128)
        stop

      PickupAttackWeakCoopSpread:
        TNT1 A 0 A_CustomPunch(random(24,48)*3,true,0,"SpearPuffCoop",128)
        stop

      // Strong
      PickupAttackStrong:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttackStrongInfinite")
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackStrongCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackStrongCoop")
      PickupAttackStrongDM:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupAttackStrongDMSpread")
        TNT1 A 0 A_CustomPunch(random(48,96),true,0,"SpearPuffGlow",128)
        stop

      PickupAttackStrongDMSpread:
        TNT1 A 0 A_CustomPunch(random(48,96)*3,true,0,"SpearPuffGlow",128)
        stop

      PickupAttackStrongCoop:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupAttackStrongCoopSpread")
        TNT1 A 0 A_CustomPunch(random(48,96),true,0,"SpearPuffGlowCoop",128)
        stop

      PickupAttackStrongCoopSpread:
        TNT1 A 0 A_CustomPunch(random(48,96)*3,true,0,"SpearPuffGlowCoop",128)
        stop

      PickupAttackStrongInfinite:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackStrongInfiniteCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackStrongInfiniteCoop")
      PickupAttackStrongInfiniteDM:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupAttackStrongInfiniteDMSpread")
        TNT1 A 0 A_CustomPunch(random(48,96),true,0,"SpearPuffGlowInfinite",128)
        stop

      PickupAttackStrongInfiniteDMSpread:
        TNT1 A 0 A_CustomPunch(random(48,96)*3,true,0,"SpearPuffGlowInfinite",128)
        stop

      PickupAttackStrongInfiniteCoop:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupAttackStrongInfiniteCoopSpread")
        TNT1 A 0 A_CustomPunch(random(48,96),true,0,"SpearPuffGlowInfiniteCoop",128)
        stop

      PickupAttackStrongInfiniteCoopSpread:
        TNT1 A 0 A_CustomPunch(random(48,96)*3,true,0,"SpearPuffGlowInfiniteCoop",128)
        stop
    }
}

// Weak

actor SpearPuff : AxePuff {}
actor SpearPuffCoop : SpearPuff { +MTHRUSPECIES }

// Strong

actor SpearPuffGlow : AxePuffGlow
{
    DamageType "TimonsAxe"
    +PUFFGETSOWNER
    States
    {
      Spawn:
      Crash:
        H101 TUVWXYZ 4 Bright
        stop

      Melee:
        TNT1 A 0 A_TakeInventory("Clip",3,0,AAPTR_TARGET)
        goto Spawn
    }
}

actor SpearPuffGlowCoop : SpearPuffGlow { +MTHRUSPECIES }

actor SpearPuffGlowInfinite : SpearPuffGlow
{
    States
    {
      Melee:
        goto Spawn
    }
}

actor SpearPuffGlowInfiniteCoop : SpearPuffGlowInfinite { +MTHRUSPECIES }

// Ranged

actor SamsaraHexenSpearOfZealRangedAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttackStrong")
        TNT1 A 0 A_JumpIfInventory("Clip",3,1)
        goto PickupAttackWeak
        TNT1 A 0 A_TakeInventory("Clip",3)
        goto PickupAttackStrong

      // Attack stuff
      // Weak
      PickupAttackWeak:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackWeakCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackWeakCoop")
      PickupAttackWeakDM:
        TNT1 A 0 A_FireCustomMissile("Hexen_FSpearFX1",0,false,8,16)
        stop

      PickupAttackWeakCoop:
        TNT1 A 0 A_FireCustomMissile("Hexen_FSpearFX1Coop",0,false,8,16)
        stop

      // Strong
      PickupAttackStrong:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackStrongCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackStrongCoop")
      PickupAttackStrongDM:
        TNT1 A 0 A_FireCustomMissile("Hexen_FSpearFX2",0,false,8,16)
        stop

      PickupAttackStrongCoop:
        TNT1 A 0 A_FireCustomMissile("Hexen_FSpearFX2Coop",0,false,8,16)
        stop
    }
}

// Weak

actor Hexen_FSpearFX1
{
    Projectile
    Radius 6
    Height 6
    Speed 50
    Damage 10
    Gravity 0.33
    DeathSound "Hexen/weapons/spearhit"
    Obituary "%o was impaled by %k's throwing spears."
    +BLOODSPLATTER
    -NOGRAVITY
    States
    {
      Spawn:
        H101 R 1 A_SpawnItemEx("Hexen_FSpearFX3",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
        loop

      Death:
        TNT1 A 1 A_AlertMonsters
        stop
    }
}

actor Hexen_FSpearFX1Coop : Hexen_FSpearFX1
{
    Species "Player"
    +THRUSPECIES
}

actor Hexen_FSpearFX3
{
    Radius 2
    Height 2
    +CLIENTSIDEONLY
    +NOINTERACTION
    States
    {
      Spawn:
        H101 R 2
        H101 RRRR 2 A_FadeOut(0.2)
        stop
    }
}

// Strong

actor Hexen_FSpearFX2 : Hexen_FSpearFX1
{
    Radius 4
    Height 6
    Speed 90
    Damage 20
    States
    {
      Spawn:
        H101 S 1 Bright A_SpawnItemEx("Hexen_FSpearFX4",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
        loop

      Death:
        TNT1 A 0 A_AlertMonsters
        TNT1 A 1 A_SpawnItemEx("Hexen_FSpearFX5Spawner",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
        stop
    }
}

actor Hexen_FSpearFX2Coop : Hexen_FSpearFX2
{
    Species "Player"
    +THRUSPECIES
}

actor Hexen_FSpearFX4 : Hexen_FSpearFX3
{
    States
    {
      Spawn:
        H101 S 2 Bright
        H101 SSSS 2 Bright A_FadeOut(0.2)
        stop
    }
}

actor Hexen_FSpearFX5Spawner
{
    +CLIENTSIDEONLY
    +NOINTERACTION
    States
    {
      Spawn:
        TNT1 A 0
        TNT1 AAAAAAAAA 0 A_SpawnItemEx("Hexen_FSpearFX5",0,0,2,frandom(-4,4),frandom(-4,4),random(-2,6),0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
        TNT1 A 1
        stop
    }
}

actor Hexen_FSpearFX5
{
    Radius 2
    Height 2
    RenderStyle "Add"
    Alpha 0.85
    +CLIENTSIDEONLY
    +DONTSPLASH
    States
    {
      Spawn:
        TNT1 A 0
        TNT1 A 0 A_Jump(64,"Spawn2")
      Spawn1:
        H123 A 1 Bright
        H123 B 2 Bright
        H123 C 1 Bright
        H123 D 2 Bright
        H123 E 1 Bright
        H123 F 2 Bright
        H123 G 1 Bright
        stop

      Spawn2:
        H123 H 4 Bright
        H123 I 3 Bright
        H123 J 2 Bright
        TNT1 A 0 A_Jump(128,"Spawn1")
        stop
    }
}
