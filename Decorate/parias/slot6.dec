actor SamsaraHexenQuickspellGauntletsMeleeHitCount : Counter { Inventory.MaxAmount 2 }
actor SamsaraHexenQuickspellGauntletsMeleeAmmoTaken : Boolean {} // for spread rune
actor SamsaraHexenQuickspellGauntletsRangedFrame : Boolean {}

actor Firestorm : MWeapLightning // lmao hardcoded
{
    Inventory.PickupMessage "Firestorm"
    Inventory.PickupSound "hexen/weaponget"
    Inventory.RestrictedTo "HexenPlayer"
    Weapon.SlotNumber 6
    Weapon.SelectionOrder 1000
    Weapon.KickBack 150
    Weapon.YAdjust 10
    Weapon.AmmoType "Cell"
    Weapon.AmmoUse 0 // 4
    Obituary "$SAMSARA_HEXEN_OB_SLOT6"
    Tag "Firestorm"
    +AMMO_OPTIONAL
    +NOALERT
    +UNDROPPABLE
    States
    {
      Spawn:
        WCFM ABCDEFGH 4 Bright
        loop

      // Parias
      Ready:
        TNT1 A 0 A_JumpIfInventory("HexenClassMode",2,"ReadyFighter")
        TNT1 A 0 A_JumpIfInventory("HexenClassMode",1,"ReadyMage")
      ReadyCleric:
        CFLM AAAABBBBCCCC 1 Bright A_WeaponReady
        goto Ready

      Deselect:
        TNT1 A 0 A_JumpIfInventory("HexenClassMode",2,"DeselectFighter")
        TNT1 A 0 A_JumpIfInventory("HexenClassMode",1,"DeselectMage")
      DeselectCleric:
        CFLM A 1 Bright A_Lower
        goto Deselect

      Select:
        TNT1 A 0 A_JumpIfInventory("HexenClassMode",2,"SelectFighter")
        TNT1 A 0 A_JumpIfInventory("HexenClassMode",1,"SelectMage")
      SelectCleric:
        CFLM A 1 Bright A_Raise
        goto Select

      Fire:
        TNT1 A 0 A_JumpIfInventory("HexenClassMode",2,"FireFighter")
        TNT1 A 0 A_JumpIfInventory("HexenClassMode",1,"FireMage")
      FireCleric:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Cell",4,1)
        goto NoAmmoSwitch
        CFLM A 2 Bright Offset(0,40) A_AlertMonsters
        CFLM D 2 Bright Offset(0,50)
        CFLM D 2 Bright Offset(0,36)
        CFLM E 4 Bright
        CFLM F 4 Bright A_GiveInventory("SamsaraHexenFirestormAttackHandler")
        CFLM E 4 Bright
        CFLM G 4 Bright Offset(0,40)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Cell",4,1)
        goto NoAmmoSwitch
        CFLM G 1 Bright A_ReFire
        goto Ready

      Hold:
        TNT1 A 0 A_JumpIfInventory("HexenClassMode",2,"HoldFighter")
        TNT1 A 0 A_JumpIfInventory("HexenClassMode",1,"FireMage")
        goto FireCleric

      // Daedolon (Arc of Death)
      ReadyMage:
        H109 AAAAA 1 Bright A_WeaponReady
        TNT1 A 0 A_Jump(96,2)
        TNT1 A 0 A_PlaySound("MageLightningReady",CHAN_WEAPON)
        H109 ABBBBBBCCCCC 1 Bright A_WeaponReady
        TNT1 A 0 A_Jump(96,2)
        TNT1 A 0 A_PlaySound("MageLightningReady",CHAN_WEAPON)
        H109 CBBBBBB 1 Bright A_WeaponReady
        goto Ready

      DeselectMage:
        H109 A 1 Bright A_Lower
        goto Deselect

      SelectMage:
        H109 A 1 Bright A_Raise
        goto Select

      FireMage:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Cell",5,1)
        goto NoAmmoSwitch
        TNT1 A 0 A_ClearReFire
        H109 D 3 Bright A_AlertMonsters
        H109 E 3 Bright
      // start attack handler code
      FireMageAmmoCheck:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireMageAttack")
        TNT1 A 0 A_JumpIfInventory("Cell",5,1)
        goto FireMageContinue
        TNT1 A 0 A_TakeInventory("Cell",5)
        goto FireMageAttack

      FireMageAttack:
        //TNT1 A 0 A_PlaySound("MageLightningFire") // already called in A_MLightningAttack
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"FireMageAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"FireMageAttackCoop")
      FireMageAttackDM:
        TNT1 A 0 A_MLightningAttack("LightningFloorDM","LightningCeilingDM")
        goto FireMageContinue

      FireMageAttackCoop:
        TNT1 A 0 A_MLightningAttack("LightningFloor2","LightningCeiling2")
        goto FireMageContinue
      // end attack handler code

      FireMageContinue:
        H109 FG 4 Bright
        H109 HI 3 Bright
        H109 I 6 Bright Offset(0,199)
        H109 C 2 Bright Offset(0,55)
        H109 B 2 Bright Offset(0,50)
        H109 B 2 Bright Offset(0,45)
        H109 B 2 Bright Offset(0,40)
        goto Ready

      // Baratus (Quickspell Gauntlets)
      ReadyFighter:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReadyFighterAmmo")
        TNT1 A 0 A_JumpIfInventory("Cell",1,"ReadyFighterAmmo")
        goto ReadyFighterNoAmmo

      ReadyFighterNoAmmo:
        H105 E 1 A_WeaponReady
        goto Ready

      ReadyFighterAmmo:
        H105 F 1 Bright A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Cell",1,1)
        goto Ready
        H105 F 1 Bright A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Cell",1,1)
        goto Ready
        H105 F 1 Bright A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Cell",1,1)
        goto Ready
        H105 F 1 Bright A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Cell",1,1)
        goto Ready
        H105 F 1 Bright A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Cell",1,1)
        goto Ready
        H105 F 1 Bright A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Cell",1,1)
        goto Ready
        H105 G 1 Bright A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Cell",1,1)
        goto Ready
        H105 G 1 Bright A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Cell",1,1)
        goto Ready
        H105 G 1 Bright A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Cell",1,1)
        goto Ready
        H105 G 1 Bright A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Cell",1,1)
        goto Ready
        H105 G 1 Bright A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Cell",1,1)
        goto Ready
        H105 G 1 Bright A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Cell",1,1)
        goto Ready
        H105 H 1 Bright A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Cell",1,1)
        goto Ready
        H105 H 1 Bright A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Cell",1,1)
        goto Ready
        H105 H 1 Bright A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Cell",1,1)
        goto Ready
        H105 H 1 Bright A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Cell",1,1)
        goto Ready
        H105 H 1 Bright A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Cell",1,1)
        goto Ready
        H105 H 1 Bright A_WeaponReady
        goto Ready

      DeselectFighter:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"DeselectFighterAmmo")
        TNT1 A 0 A_JumpIfInventory("Cell",1,"DeselectFighterAmmo")
        goto DeselectFighterNoAmmo

      DeselectFighterNoAmmo:
        H105 E 1 A_Lower
        goto Deselect

      DeselectFighterAmmo:
        H105 F 1 Bright A_Lower
        goto Deselect

      SelectFighter:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"SelectFighterAmmo")
        TNT1 A 0 A_JumpIfInventory("Cell",1,"SelectFighterAmmo")
        goto SelectFighterNoAmmo

      SelectFighterNoAmmo:
        H105 E 1 A_Raise
        goto Select

      SelectFighterAmmo:
        H105 F 1 Bright A_Raise
        goto Select

      FireFighter:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Cell",1,1)
        goto NoAmmoSwitch
        TNT1 A 0 A_AlertMonsters
        H105 F 2 Bright A_PlaySound("heretic/gauntletsactivate")
        H105 I 5 Bright
        goto HoldStartFighter

      HoldFighter:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Cell",1,1)
        goto NoAmmoSwitch
        TNT1 A 0 A_AlertMonsters
      HoldStartFighter:
        TNT1 A 0 A_JumpIfCloser(65,"HoldFighterMelee")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"HoldFighterRanged")
        TNT1 A 0 A_JumpIfInventory("Cell",2,"HoldFighterRanged")
      HoldFighterMelee:
        H106 IJK 4 Bright A_GiveInventory("SamsaraHexenQuickspellGauntletsMeleeAttackHandler")
        goto HoldFighterFinish

      HoldFighterRanged:
        H105 J 5 Bright
        TNT1 A 0 A_JumpIfInventory("SamsaraHexenQuickspellGauntletsRangedFrame",1,"HoldFighterRangedRight")
      HoldFighterRangedLeft:
        H105 Y 3 Bright A_GiveInventory("SamsaraHexenQuickspellGauntletsRangedAttackHandler")
        goto HoldFighterFinish

      HoldFighterRangedRight:
        H105 Z 3 Bright A_GiveInventory("SamsaraHexenQuickspellGauntletsRangedAttackHandler")
        goto HoldFighterFinish

      HoldFighterFinish:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Cell",1,1)
        goto NoAmmoSwitchLightDone
        H105 J 4 Bright A_ReFire
        H105 I 3 Bright A_Light0
        goto Ready

      // "No ammo" switch
      NoAmmoSwitchLightDone:
        TNT1 A 0 A_Light0
      NoAmmoSwitch:
        TNT1 A 0 A_ClearReFire
        TNT1 A 0 ACS_NamedExecuteWithResult("Hexen_SwitchPriority")
        goto Deselect
    }
}

// Parias

actor SamsaraHexenFirestormAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Cell",4,1)
        stop
        TNT1 A 0 A_TakeInventory("Cell",4)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("ClericFlameFire",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("CFlameMissileDM",0,false)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("CFlameMissile2",0,false)
        stop
    }
}

actor CFlameMissileDM : CFlameMissile
{
    Speed 150
    +BLOODSPLATTER
    +HITTRACER
    States
    {
      Spawn:
        CFFX A 4 Bright
        CFFX A 1 Bright A_CFlamePuff
        goto Death+1

      Death:
        CFFX A 1 Bright A_CFlamePuff
        CFFX ABC 3 Bright
        CFFX D 4 Bright
        CFFX E 3 Bright
        CFFX F 4 Bright
        CFFX G 3 Bright
        CFFX H 4 Bright
        CFFX I 3 Bright
        CFFX J 4 Bright
        CFFX K 3 Bright
        CFFX L 4 Bright
        CFFX M 3 Bright
        stop

      XDeath:
      Crash:
        TNT1 A 0 A_SpawnItemEx("CFlameMissileHit")
        TNT1 A 0 A_Warp(AAPTR_TRACER,0,0,10,0,WARPF_NOCHECKPOSITION)
        TNT1 A 0 A_SpawnItemEx("CircleFlameDM",40,0,0,0,0,0,0,SXF_ABSOLUTEANGLE|SXF_ABSOLUTEVELOCITY|SXF_SETMASTER)
        TNT1 A 0 A_SpawnItemEx("CircleFlameDM",40,0,0,0,0,0,45,SXF_ABSOLUTEANGLE|SXF_ABSOLUTEVELOCITY|SXF_SETMASTER)
        TNT1 A 0 A_SpawnItemEx("CircleFlameDM",40,0,0,0,0,0,90,SXF_ABSOLUTEANGLE|SXF_ABSOLUTEVELOCITY|SXF_SETMASTER)
        TNT1 A 0 A_SpawnItemEx("CircleFlameDM",40,0,0,0,0,0,135,SXF_ABSOLUTEANGLE|SXF_ABSOLUTEVELOCITY|SXF_SETMASTER)
        TNT1 A 0 A_SpawnItemEx("CircleFlameDM",40,0,0,0,0,0,180,SXF_ABSOLUTEANGLE|SXF_ABSOLUTEVELOCITY|SXF_SETMASTER)
        TNT1 A 0 A_SpawnItemEx("CircleFlameDM",40,0,0,0,0,0,225,SXF_ABSOLUTEANGLE|SXF_ABSOLUTEVELOCITY|SXF_SETMASTER)
        TNT1 A 0 A_SpawnItemEx("CircleFlameDM",40,0,0,0,0,0,270,SXF_ABSOLUTEANGLE|SXF_ABSOLUTEVELOCITY|SXF_SETMASTER)
        TNT1 A 0 A_SpawnItemEx("CircleFlameDM",40,0,0,0,0,0,315,SXF_ABSOLUTEANGLE|SXF_ABSOLUTEVELOCITY|SXF_SETMASTER)
        TNT1 A 1 A_CFlamePuff
        stop
    }
}

actor CFlameMissile2 : CFlameMissileDM
{
    Speed 200
    Species "Player"
    +THRUSPECIES
    States
    {
      XDeath:
      Crash:
        TNT1 A 0 A_SpawnItemEx("CFlameMissileHit")
        TNT1 A 0 A_Warp(AAPTR_TRACER,0,0,10,0,WARPF_NOCHECKPOSITION)
        TNT1 A 0 A_SpawnItemEx("CircleFlame1",40,0,0,0,0,0,0,SXF_ABSOLUTEANGLE|SXF_ABSOLUTEVELOCITY|SXF_SETMASTER)
        TNT1 A 0 A_SpawnItemEx("CircleFlame2",40,0,0,0,0,0,45,SXF_ABSOLUTEANGLE|SXF_ABSOLUTEVELOCITY|SXF_SETMASTER)
        TNT1 A 0 A_SpawnItemEx("CircleFlame1",40,0,0,0,0,0,90,SXF_ABSOLUTEANGLE|SXF_ABSOLUTEVELOCITY|SXF_SETMASTER)
        TNT1 A 0 A_SpawnItemEx("CircleFlame2",40,0,0,0,0,0,135,SXF_ABSOLUTEANGLE|SXF_ABSOLUTEVELOCITY|SXF_SETMASTER)
        TNT1 A 0 A_SpawnItemEx("CircleFlame1",40,0,0,0,0,0,180,SXF_ABSOLUTEANGLE|SXF_ABSOLUTEVELOCITY|SXF_SETMASTER)
        TNT1 A 0 A_SpawnItemEx("CircleFlame2",40,0,0,0,0,0,225,SXF_ABSOLUTEANGLE|SXF_ABSOLUTEVELOCITY|SXF_SETMASTER)
        TNT1 A 0 A_SpawnItemEx("CircleFlame1",40,0,0,0,0,0,270,SXF_ABSOLUTEANGLE|SXF_ABSOLUTEVELOCITY|SXF_SETMASTER)
        TNT1 A 0 A_SpawnItemEx("CircleFlame2",40,0,0,0,0,0,315,SXF_ABSOLUTEANGLE|SXF_ABSOLUTEVELOCITY|SXF_SETMASTER)
        TNT1 A 1 A_CFlamePuff
        stop
    }
}

actor CFlameMissileHit
{
    Radius 14
    Height 8
    RenderStyle "Add"
    +NOINTERACTION
    States
    {
      Spawn:
        CFFX A 3 Bright
        CFFX B 3 Bright Light("CFLAME1")
        CFFX C 3 Bright Light("CFLAME2")
        CFFX D 4 Bright Light("CFLAME2")
        CFFX E 3 Bright Light("CFLAME2")
        CFFX F 4 Bright Light("CFLAME2")
        CFFX G 3 Bright Light("CFLAME2")
        CFFX H 4 Bright Light("CFLAME2")
        CFFX I 3 Bright Light("CFLAME2")
        CFFX J 4 Bright Light("CFLAME3")
        CFFX K 3 Bright Light("CFLAME4")
        CFFX L 4 Bright Light("CFLAME5")
        CFFX M 3 Bright
        stop
    }
}

actor CircleFlameDM : CircleFlame
{
    Damage (random(1,4)*2) // Originally 2
    Obituary "$SAMSARA_HEXEN_OB_SLOT6"
    +BLOODSPLATTER
    States
    {
      Death:
        CFCF QR 3 Bright
        TNT1 A 0 A_Explode(5,20,0)
      DeathFinish:
        CFCF STUVWXYZ 3 Bright
        stop
    }
}

actor CircleFlame1 : CircleFlameDM
{
    Species "Player"
    +THRUSPECIES
}

actor CircleFlame2 : CircleFlame1
{
    Damage 2
    States
    {
      Death:
        CFCF QR 3 Bright
        TNT1 A 0 A_Explode(32,64,0)
        goto DeathFinish
    }
}

// Daedolon

actor LightningCeilingDM : LightningCeiling
{
    Speed 50
    DamageType "Electric"
    MissileType "LightningZapDM"
    +BLOODSPLATTER
    +NODAMAGETHRUST
}

actor LightningFloorDM : LightningFloor
{
    Speed 50
    DamageType "Electric"
    MissileType "LightningZapDM"
    +BLOODSPLATTER
    +NODAMAGETHRUST
}

actor LightningZapDM : LightningZap
{
    DamageType "Electric"
    +BLOODSPLATTER
    +NODAMAGETHRUST
}

actor LightningCeiling2 : LightningCeilingDM
{
    Speed 25
    MissileType "LightningZap2"
    Species "Player"
    +THRUSPECIES
}

actor LightningFloor2 : LightningFloorDM
{
    Speed 25
    MissileType "LightningZap2"
    Species "Player"
    +THRUSPECIES
}

actor LightningZap2 : LightningZapDM
{
    Damage 3
    Species "Player"
    +THRUSPECIES
}

// Baratus

// Melee

actor SamsaraHexenQuickspellGauntletsMeleeAttackHandler : Trigger
{
    States
    {
      // Initializing stuff
      Pickup:
        TNT1 A 0 A_TakeInventory("SamsaraHexenQuickspellGauntletsMeleeAmmoTaken")
        goto PickupAmmoCheck

      // Ammo checking stuff
      PickupAmmoCheck:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Cell",1,"PickupAttack")
        stop

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttackInfinite")
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_Saw("heretic/gauntletson","heretic/gauntletshit",4,"HexenGauntletPlasmaSpark",SF_RANDOMLIGHTHIT|SF_NOUSEAMMO,65,0,0,1)
        TNT1 A 0 A_Saw("","",4,"HexenGauntletPlasmaSparkInfinite",SF_RANDOMLIGHTHIT|SF_NOUSEAMMO,65,0)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_Saw("heretic/gauntletson","heretic/gauntletshit",4,"HexenGauntletPlasmaSparkCoop",SF_RANDOMLIGHTHIT|SF_NOUSEAMMO,65,0,0,1)
        TNT1 A 0 A_Saw("","",4,"HexenGauntletPlasmaSparkInfiniteCoop",SF_RANDOMLIGHTHIT|SF_NOUSEAMMO,65,0)
        stop

      PickupAttackInfinite:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackInfiniteCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackInfiniteCoop")
      PickupAttackInfiniteDM:
        TNT1 A 0 A_Saw("heretic/gauntletson","heretic/gauntletshit",4,"HexenGauntletPlasmaSparkInfinite",SF_RANDOMLIGHTHIT|SF_NOUSEAMMO,65,0,0,1)
        TNT1 A 0 A_Saw("","",4,"HexenGauntletPlasmaSparkInfinite",SF_RANDOMLIGHTHIT|SF_NOUSEAMMO,65,0)
        stop

      PickupAttackInfiniteCoop:
        TNT1 A 0 A_Saw("heretic/gauntletson","heretic/gauntletshit",4,"HexenGauntletPlasmaSparkInfiniteCoop",SF_RANDOMLIGHTHIT|SF_NOUSEAMMO,65,0,0,1)
        TNT1 A 0 A_Saw("","",4,"HexenGauntletPlasmaSparkInfiniteCoop",SF_RANDOMLIGHTHIT|SF_NOUSEAMMO,65,0)
        stop
    }
}

actor HexenGauntletPlasmaSpark : GauntletPuff1
{
    VSpeed 0
    RenderStyle "Add"
    Alpha 1
    +CANNOTPUSH // trail
    +NOTELEPORT // trail
    +NOTIMEFREEZE
    +PUFFGETSOWNER
    States
    {
      Spawn:
      Melee:
      XDeath:
        H105 P 0 Bright NoDelay A_JumpIfInventory("SamsaraHexenQuickspellGauntletsMeleeAmmoTaken",1,"Crash",AAPTR_TARGET)
        TNT1 A 0 A_JumpIfInventory("SamsaraHexenQuickspellGauntletsMeleeHitCount",0,2,AAPTR_TARGET)
        TNT1 A 0 A_GiveInventory("SamsaraHexenQuickspellGauntletsMeleeHitCount",1,AAPTR_TARGET)
        goto AmmoTaken
        TNT1 A 0 A_TakeInventory("Cell",1,0,AAPTR_TARGET)
        TNT1 A 0 A_TakeInventory("SamsaraHexenQuickspellGauntletsMeleeHitCount",0x7FFFFFFF,0,AAPTR_TARGET)
        goto AmmoTaken

      AmmoTaken:
        TNT1 A 0 A_GiveInventory("SamsaraHexenQuickspellGauntletsMeleeAmmoTaken",1,AAPTR_TARGET)
        goto Crash

      Crash:
        H105 P 12 Bright A_ChangeFlag("NOTIMEFREEZE",false)
        H105 QR 12 Bright
        stop
    }
}

actor HexenGauntletPlasmaSparkCoop : HexenGauntletPlasmaSpark { +MTHRUSPECIES }

actor HexenGauntletPlasmaSparkInfinite : HexenGauntletPlasmaSpark // also melee supplement and ranged projectile's trail
{
    States
    {
      Spawn:
      Melee:
      XDeath:
        goto Crash
    }
}

actor HexenGauntletPlasmaSparkInfiniteCoop : HexenGauntletPlasmaSparkInfinite { +MTHRUSPECIES }

// Ranged

actor SamsaraHexenQuickspellGauntletsRangedAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Cell",2,1)
        stop
        TNT1 A 0 A_TakeInventory("Cell",2)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("hexen/weapons/gauntletlightning",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SamsaraHexenQuickspellGauntletsRangedFrame",1,"PickupAttackRight")
      // Left
      PickupAttackLeft:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackLeftCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackLeftCoop")
      PickupAttackLeftDM:
        TNT1 A 0 A_FireCustomMissile("HexenGauntletPlasma",random(-5,5),false,-5)
        goto PickupFinishLeft

      PickupAttackLeftCoop:
        TNT1 A 0 A_FireCustomMissile("HexenGauntletPlasmaCoop",random(-5,5),false,-5)
        goto PickupFinishLeft

      // Right
      PickupAttackRight:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackRightCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackRightCoop")
      PickupAttackRightDM:
        TNT1 A 0 A_FireCustomMissile("HexenGauntletPlasma",random(-5,5),false,5)
        goto PickupFinishRight

      PickupAttackRightCoop:
        TNT1 A 0 A_FireCustomMissile("HexenGauntletPlasmaCoop",random(-5,5),false,5)
        goto PickupFinishRight

      // Finishing stuff
      // Left
      PickupFinishLeft:
        TNT1 A 0 A_GiveInventory("SamsaraHexenQuickspellGauntletsRangedFrame")
        stop

      // Right
      PickupFinishRight:
        TNT1 A 0 A_TakeInventory("SamsaraHexenQuickspellGauntletsRangedFrame")
        stop
    }
}

actor HexenGauntletPlasma
{
    Projectile
    Radius 5
    Height 6
    Speed 20
    Damage 6
    RenderStyle "Add"
    DeathSound "hexen/weapons/gauntlethit"
    Decal "HexenGauntletPlasmaLightning"
    //Obituary "%o was killed by %k."
    -ACTIVATEIMPACT
    -ACTIVATEPCROSS
    +BLOODSPLATTER
    States
    {
      Spawn:
        H105 MNO 3 Bright A_SpawnItemEx("HexenGauntletPlasmaSparkInfinite",0,0,0,random(-2,2),random(-2,2),random(1,2))
        loop

      Death:
        H105 S 5 Bright A_Explode(15,112,0)
        H105 TUVWX 5 Bright
        stop
    }
}

actor HexenGauntletPlasmaCoop : HexenGauntletPlasma
{
    Species "Player"
    +THRUSPECIES
}
