actor "Talon Ceramic Combat Blade" : SamsaraMarathonWeapon
{
    Weapon.SelectionOrder 3700
    Weapon.KickBack 300
    Weapon.AmmoType "KnifeAmmo"
    Weapon.AmmoGive 1
    Obituary "$SAMSARA_MARATHON_OB_PUNCHDRUNKB_1"
    Tag "Talon Ceramic Combat Blade"
    +CHEATNOTWEAPON
    +MELEEWEAPON
    States
    {
      Ready:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("KnifeAmmo",1,1)
        goto NoAmmoSwitch
        MRNF A 1 A_WeaponReady
        loop

      Deselect:
        TNT1 A 0 A_JumpIfHealthLower(1,"DeselectInstant")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"DeselectAmmo")
        TNT1 A 0 A_JumpIfInventory("KnifeAmmo",1,"DeselectAmmo")
        goto DeselectNoAmmo

      DeselectNoAmmo:
        TNT1 A 0 A_Lower
        TNT1 A 1 A_Lower
        goto Deselect

      DeselectAmmo:
        TNT1 A 0 A_Lower
        MRNF A 1 A_Lower
        goto Deselect

      Select:
        //TNT1 A 0 A_TakeInventory("UsingFists")
        TNT1 A 0 A_TakeInventory("UsingPistols")
        TNT1 A 0 A_TakeInventory("UsingDualPistols")
        TNT1 A 0 A_TakeInventory("UsingSMG")
        TNT1 A 0 A_TakeInventory("UsingPacifier")
        TNT1 A 0 A_TakeInventory("UsingShotguns")
        TNT1 A 0 A_TakeInventory("UsingDualShotguns")
        TNT1 A 0 A_TakeInventory("UsingFusionPistol")
        TNT1 A 0 A_TakeInventory("UsingAssaultRifle")
        TNT1 A 0 A_TakeInventory("UsingSpanker")
        TNT1 A 0 A_TakeInventory("UsingToasty")
        TNT1 A 0 A_TakeInventory("UsingWMC")
        TNT1 A 0 A_TakeInventory("UsingAlienWeapon")
        TNT1 A 0 A_TakeInventory("UsingAlienWeapon2")
        TNT1 A 0 A_TakeInventory("UsingMarathonInstagib")

        TNT1 A 0 A_GiveInventory("UsingFists")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"SelectAmmo")
        TNT1 A 0 A_JumpIfInventory("KnifeAmmo",1,"SelectAmmo")
        goto SelectNoAmmo

      SelectNoAmmo:
        TNT1 A 0 A_Raise
        TNT1 A 1 A_Raise
        goto Select

      SelectAmmo:
        TNT1 A 0 A_Raise
        MRNF A 1 A_Raise
        goto Select

      Fire:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart")
        TNT1 A 0 A_JumpIfInventory("KnifeAmmo",1,"FireStart")
        goto Ready

      FireStart:
        MRNF BC 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        MRNF D 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        MRNF E 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_GiveInventory("SamsaraMarathonTCCBPrimaryAttackHandler")
        MRNF F 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_GiveInventory("SamsaraMarathonTCCBPrimaryAttackHandler")
        MRNF G 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_GiveInventory("SamsaraMarathonTCCBPrimaryAttackHandler")
        MRNF H 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
      FireFinish:
        TNT1 A 10 Offset(0,128)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"Select")
        TNT1 A 0 A_JumpIfInventory("KnifeAmmo",1,"Select")
        goto NoAmmoSwitch

      AltFire:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"AltFireStart")
        TNT1 A 0 A_JumpIfInventory("KnifeAmmo",1,"AltFireStart")
        goto Ready

      AltFireStart:
        MRNT AB 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        MRNT C 3 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        MRNT DE 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_GiveInventory("SamsaraMarathonTCCBSecondaryAttackHandler")
        MRNT FGHI 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto FireFinish

      // "No ammo" switch
      NoAmmoSwitch:
        TNT1 A 0 A_ClearReFire
        TNT1 A 0 A_JumpIfInventory(".44 Magnum Mega Class A1",1,2)
        TNT1 A 0 A_SelectWeapon("Steel Knuckles")
        goto Deselect
        TNT1 A 0 A_SelectWeapon(".44 Magnum Mega Class A1")
        goto Deselect
    }
}

actor SamsaraMarathonTCCBPrimaryAttackHandler : Trigger
{
    States
    {
      // Ammo checking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("KnifeAmmo",1,"PickupAttack")
        stop

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupAttackDMSpread")
        TNT1 A 0 A_CustomPunch(random(36,40),true,0,"MarathonKnifePuff",96)
        stop

      PickupAttackDMSpread:
        TNT1 A 0 A_CustomPunch(random(36,40)*3,true,0,"MarathonKnifePuff",96)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupAttackCoopSpread")
        TNT1 A 0 A_CustomPunch(random(36,40),true,0,"MarathonKnifePuffCoop",96)
        stop

      PickupAttackCoopSpread:
        TNT1 A 0 A_CustomPunch(random(36,40)*3,true,0,"MarathonKnifePuffCoop",96)
        stop
    }
}

actor SamsaraMarathonTCCBSecondaryAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttackInfinite")
        TNT1 A 0 A_JumpIfInventory("KnifeAmmo",1,1)
        stop
        TNT1 A 0 A_TakeInventory("KnifeAmmo",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("marathon/knife/throw",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("TCCBThrown",0,false,0,2) //A_CustomPunch(random(100,120),true,0,"MarathonKnifePuff",96)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("TCCBThrownCoop",0,false,0,2)
        stop

      PickupAttackInfinite:
        TNT1 A 0 A_PlaySound("marathon/knife/throw",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackInfiniteCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackInfiniteCoop")
      PickupAttackInfiniteDM:
        TNT1 A 0 A_FireCustomMissile("TCCBThrownInfinite",0,false,0,2)
        stop

      PickupAttackInfiniteCoop:
        TNT1 A 0 A_FireCustomMissile("TCCBThrownInfiniteCoop",0,false,0,2)
        stop
    }
}

actor KnifeAmmo : Ammo // You need ammo to stab people. Makes sense.
{
    Inventory.MaxAmount 2
    Tag "Knives"
    +IGNORESKILL
}

actor TCCBThrown
{
    Projectile
    Radius 12
    Height 12
    Speed 50
    Damage (random(30,40))
    BounceType "Doom"
    BounceCount 12
    BounceFactor 1
    WallBounceFactor 1
    XScale 0.5
    YScale 0.25
    BounceSound "marathon/knife/bounce"
    Obituary "$SAMSARA_MARATHON_OB_PUNCHDRUNKB_2"
    +NOEXTREMEDEATH
    +NOGRAVITY
    +RIPPER
    States
    {
      Spawn:
        MRNP ABCD 1
        loop

      Death:
      Crash:
        TNT1 A 0 A_StopSound(CHAN_VOICE)
        TNT1 A 1 A_DropItem("TCCBPickup")
        stop
    }
}

actor TCCBThrownCoop : TCCBThrown
{
    Species "Player"
    +THRUSPECIES
}

actor TCCBThrownInfinite : TCCBThrown
{
    States
    {
      Death:
      Crash:
        TNT1 A 0 A_StopSound(CHAN_VOICE)
        TNT1 A 1 A_DropItem("TCCBPickupInfinite")
        stop
    }
}

actor TCCBThrownInfiniteCoop : TCCBThrownInfinite
{
    Species "Player"
    +THRUSPECIES
}

actor TCCBPickup : CustomInventory
{
    Inventory.PickupMessage "TALON CERAMIC COMBAT BLADE"
    Inventory.PickupSound "marathon/itemget"
    States
    {
      Spawn:
        MRNF Z 0 Bright NoDelay A_PlaySound("marathon/knife/wall",CHAN_VOICE)
      SpawnLoop:
        MRNF Z 5 Bright
        MRNF Z 15
        loop

      Pickup:
        TNT1 A 0 A_JumpIfInventory("MarathonClass",1,"PickupCheck")
        goto PickupFail

      PickupCheck:
        TNT1 A 0 A_JumpIfInventory("Talon Ceramic Combat Blade",1,"PickupCheckHasWeapon")
        TNT1 A 0 A_SetBlend("Green",0.4,5)
        TNT1 A 0 A_GiveInventory("Talon Ceramic Combat Blade")
        TNT1 A 0 A_JumpIfInventory("KnifeAmmo",2,1)
        stop
        // gross hack to set ammo to 1
        TNT1 A 0 A_TakeInventory("KnifeAmmo")
        TNT1 A 0 A_GiveInventory("KnifeAmmo")
        stop

      PickupCheckHasWeapon:
        TNT1 A 0 A_JumpIfInventory("KnifeAmmo",0,"PickupFail")
        TNT1 A 0 A_SetBlend("Green",0.4,5)
        TNT1 A 0 A_GiveInventory("KnifeAmmo")
        stop

      PickupFail:
        TNT1 A 0
        fail
    }
}

actor TCCBPickupInfinite
{
    +CLIENTSIDEONLY
    States
    {
      Spawn:
        MRNF Z 0 NoDelay A_PlaySound("marathon/knife/wall",CHAN_VOICE)
        MRNF Z 2 A_FadeOut(0.04)
        wait
    }
}

actor MarathonKnifePuff : BulletPuff
{
    DamageType "Pistol"
    Scale 0.25
    SeeSound "marathon/knife/hit"
    AttackSound "marathon/knife/wall"
    //+BLOODLESSIMPACT
    +CLIENTSIDEONLY
    +NOEXTREMEDEATH
    +PUFFONACTORS
    States
    {
      Crash:
        MPFF ABCDEFG 2 Bright
        stop

      Melee:
      Spawn:
        TNT1 A 1
        stop
    }
}

actor MarathonKnifePuffCoop : MarathonKnifePuff { +MTHRUSPECIES }
