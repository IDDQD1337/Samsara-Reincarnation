actor SamsaraMarathonWeaponSwapOffsetCount : Counter { Inventory.MaxAmount 43 } // Spanker/WMC
actor MarathonCasingSound : Counter { Inventory.MaxAmount 8 } // SMG/AssaultRifle
actor SamsaraLevelSwitch_DualWielding : Boolean {} // DualPistols/DualShotguns
actor LeftReload : Boolean {} // DualPistols
actor RightReload : Boolean {} // DualPistols

actor SamsaraMarathonWeapon : Weapon
{
    Inventory.RestrictedTo "MarathonPlayer"
    Weapon.BobStyle Smooth
    Weapon.BobRangeX 0
    Weapon.BobSpeed 1.25
    +ALT_AMMO_OPTIONAL
    +AMMO_OPTIONAL
    +NOALERT
    +UNDROPPABLE
    States
    {
      DeselectInstant:
        TNT1 A 0 A_TakeInventory("SamsaraMarathonWeaponSwapOffsetCount")
        TNT1 A 0 A_Lower
        loop
    }
}

actor SamsaraMarathonBullet : FastProjectile
{
    Radius 8
    Height 8
    Speed 100
    Mass 5
    Decal "BulletChip"
    +BLOODSPLATTER
    +NOEXTREMEDEATH
    +NOTIMEFREEZE
    States
    {
      Spawn:
        TNT1 A -1
        stop

      Death:
        TNT1 A 1 A_SpawnItemEx("MarathonPuff")
        stop

      XDeath:
        TNT1 A 1 A_PlaySound("marathon/puff/hit")
        stop

      Crash:
        TNT1 A 0 A_PlaySound("marathon/puff/ding")
        TNT1 A 1 A_SpawnItemEx("MarathonPuffQuiet")
        stop
    }
}

actor MarathonPuff : BulletPuff
{
    RenderStyle "Add"
    Alpha 1
    Scale 0.5
    //+CLIENTSIDEONLY // Enabling this has it spawn when the bullets hit bleeding objects. WAT?
    +FLOORCLIP
    +FORCEXYBILLBOARD
    +NOEXTREMEDEATH
    States
    {
      Spawn:
        MPFF A 0 Bright NoDelay A_PlaySound("marathon/puff/ric")
      SpawnFinish:
        MPFF ABCDEFG 2 Bright
        stop
    }
}

actor MarathonPuffQuiet : MarathonPuff
{
    States
    {
      Spawn:
        MPFF A 0 Bright NoDelay
        goto SpawnFinish
    }
}

actor SamsaraMarathonCasingSpawner
{
    Projectile
    Radius 1
    Height 1
    Speed 25
    ReactionTime 1
    +CLIENTSIDEONLY
    +DONTBLAST
    +DONTREFLECT
    +DONTSPLASH
    +NOTIMEFREEZE
    +NOTONAUTOMAP
    +NOTRIGGER
    +SKYEXPLODE
    States
    {
      Spawn:
        TNT1 A 1
        TNT1 A 1 A_Countdown
        wait

      Death:
        TNT1 A 1
        stop
    }
}

actor SamsaraMarathonCasing
{
    Radius 3
    Height 3
    Speed 7
    Mass 5
    BounceType "Doom"
    BounceCount 1
    +CLIENTSIDEONLY
    +DROPOFF
    +FLOORCLIP
    +FORCEXYBILLBOARD
    +MISSILE
    +NOTELEPORT
    +THRUACTORS
    States
    {
      Spawn:
        TNT1 A -1
        stop

      Death:
        TNT1 A 1
        stop
    }
}

actor SamsaraMarathonCasingSoundHandler : Trigger
{
    States
    {
      // Sound stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("MarathonCasingSound",0,1)
        goto PickupFinish
        TNT1 A 0 A_PlaySound("marathon/casings")
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 A_TakeInventory("MarathonCasingSound")
        stop
    }
}
