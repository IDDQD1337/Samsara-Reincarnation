actor freeman_slot4selected : Boolean { Inventory.InterHubAmount 0 }
actor SamsaraHalfLife9mmAutomaticFireState : Counter { Inventory.MaxAmount 3 }

Actor "MP5" : HLWeapon
{
	Weapon.Slotnumber 4
	Weapon.SelectionOrder 2250
	Weapon.BobRangeX 0
	Weapon.BobRangeY 0.75
	Weapon.BobSpeed 1.4
	Weapon.BobStyle Alpha 	
	Inventory.Pickupmessage "You got the MP5!"
	Inventory.Pickupsound "halflife/weaponpickup"
	DamageType "HLBullet"
	Weapon.AmmoType1 "HLMP5Counter"
    Weapon.AmmoUse1 1
	Weapon.AmmoType2 "Clip"
    Weapon.AmmoGive2 50
	Inventory.RestrictedTo "HalfLifePlayer"
    Tag "MP5"
	states
	{
	Spawn:
		HW40 Z -1
		stop

    // Gordon Freeman
	ReadyActual:
        HW41 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReadyActualReloadable")
    ReadyActualNonReloadable:
		HW41 A 0 A_JumpIfInventory("HalfLifeOpposingForce",1,"ReadyActualNonReloadableOpp")
		HW41 A 2 A_WeaponReady
        goto ReadyActualJump

    ReadyActualReloadable:
		HW41 A 0 A_JumpIfInventory("HalfLifeOpposingForce",1,"ReadyActualReloadableOpp")
		HW41 A 2 A_WeaponReady(WRF_ALLOWRELOAD)
        goto ReadyActualJump

    ReadyActualJump:
		HW41 A 0 A_Jump(4,"ReadyActualAmmoCheck")
		HW41 A 0 A_Jump(3,"ReadyActualAmmoCheck")
		HW41 A 0 A_Jump(2,"ReadyActualAmmoCheck")
		HW41 A 0 A_Jump(1,"ReadyActualIdle")
        goto ReadyActualAmmoCheck

	ReadyActualIdle:
        HW49 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReadyActualIdleReloadable")
    ReadyActualIdleNonReloadable:
		HW49 A 0 A_JumpIfInventory("HalfLifeOpposingForce",1,"ReadyActualIdleNonReloadableOpp")
		HW49 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady
		HW50 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady
		HW51 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady
		HW52 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady
		HW53 ABCDEFGH 1 A_WeaponReady
		goto ReadyActual

    ReadyActualIdleReloadable:
		HW49 A 0 A_JumpIfInventory("HalfLifeOpposingForce",1,"ReadyActualIdleReloadableOpp")
		HW49 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady(WRF_ALLOWRELOAD)
		HW50 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady(WRF_ALLOWRELOAD)
		HW51 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady(WRF_ALLOWRELOAD)
		HW52 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady(WRF_ALLOWRELOAD)
		HW53 ABCDEFGH 1 A_WeaponReady(WRF_ALLOWRELOAD)
		goto ReadyActual

    ReadyActualAmmoCheck:
        HW41 A 0 A_JumpIfInventory("SamsaraReloadMode",1,1)
        goto ReadyActual
        HW41 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"ReadyActual")
        HW41 A 0 A_JumpIfInventory("HLMP5Counter",1,"ReadyActual")
        goto ReloadCheck

	Deselect:
		TNT1 A 0 A_TakeInventory("freeman_slot4selected")
	DeselectLoop:		
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
		TNT1 A 1 A_Lower
		loop

	Select:
		TNT1 A 0 A_TakeInventory("freeman_fistselected")
		TNT1 A 0 A_TakeInventory("freeman_pistolselected")
		TNT1 A 0 A_TakeInventory("freeman_slot1selected")
		TNT1 A 0 A_TakeInventory("freeman_slot2selected")
		TNT1 A 0 A_TakeInventory("freeman_slot3selected")
		TNT1 A 0 A_GiveInventory("freeman_slot4selected")
		TNT1 A 0 A_TakeInventory("freeman_slot5selected")
		TNT1 A 0 A_TakeInventory("freeman_slot6selected")
		TNT1 A 0 A_TakeInventory("freeman_slot7selected")
		TNT1 A 0 A_TakeInventory("freeman_uniqueselected")
		TNT1 A 0 A_TakeInventory("freeman_unique2selected")
		TNT1 A 0 A_TakeInventory("freeman_unique3selected")
	SelectLoop:		
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
		TNT1 A 1 A_Raise
		loop

	Ready: // our "Select" state
		HW40 A 0 A_JumpIfInventory("HalfLifeOpposingForce",1,"ReadyOpp")
		HW40 ABCDEFGHIJK 1 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
		goto ReadyActual

    Fire:
		HW41 A 0 A_JumpIfInventory("HLOriginalKeySwitcherOpen",1,"ReadyActual")
        HW41 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"FireReloadable")
    FireNonReloadable:
        HW41 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireWaterCheck")
        HW41 A 0 A_JumpIfInventory("Clip",1,"FireWaterCheck")
        goto FireDry

    FireReloadable:
        HW41 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireWaterCheck")
        HW41 A 0 A_JumpIfInventory("HLMP5Counter",1,"FireWaterCheck")
        goto FireDry

    FireWaterCheck:
        HW41 A 0 A_JumpIfInventory("SamsaraCurrentWaterLevel",0,"FireDry")
        goto FireStart

    FireDry:
        HW41 A 0 A_PlaySound("halflife/weaponempty",CHAN_6)
        HW41 A 0 A_ClearReFire
        HW41 A 4 A_JumpIfInventory("HalfLifeOpposingForce",1,"FireDryOpp")
        goto ReadyActualAmmoCheck

    Hold:
        HW41 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"HoldReloadable")
    HoldNonReloadable:
        HW41 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"HoldWaterCheck")
        HW41 A 0 A_JumpIfInventory("Clip",1,"HoldWaterCheck")
        goto HoldDry

    HoldReloadable:
        HW41 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"HoldWaterCheck")
        HW41 A 0 A_JumpIfInventory("HLMP5Counter",1,"HoldWaterCheck")
        goto HoldDry

    HoldWaterCheck:
        HW41 A 0 A_JumpIfInventory("SamsaraCurrentWaterLevel",0,"HoldDry")
        goto FireStart

    HoldDry:
        HW44 B 0 A_PlaySound("halflife/weaponempty",CHAN_6)
        HW44 B 0 A_ClearReFire
        HW44 B 0 A_JumpIfInventory("SamsaraHalfLife9mmAutomaticFireState",0,"HoldDry3")
        HW44 B 0 A_JumpIfInventory("SamsaraHalfLife9mmAutomaticFireState",2,"HoldDry2")
    HoldDry1:
        HW44 B 1 A_JumpIfInventory("HalfLifeOpposingForce",1,"HoldDry1Opp")
        HW44 CDE 1
        HW44 FG 1 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
        goto ReadyActualAmmoCheck

    HoldDry2:
        HW45 B 1 A_JumpIfInventory("HalfLifeOpposingForce",1,"HoldDry2Opp")
        HW45 CDE 1
        HW45 F 1 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
        HW41 A 1 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
        goto ReadyActualAmmoCheck

    HoldDry3:
        HW46 B 1 A_JumpIfInventory("HalfLifeOpposingForce",1,"HoldDry3Opp")
        HW46 CDE 1
        HW46 FG 1 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
        goto ReadyActualAmmoCheck

	FireStart:
		HW41 A 0 A_AlertMonsters
        HW41 A 0 A_FireCustomMissile("HLBrassShellCaser",0,false)
        HW41 A 0 A_GiveInventory("SamsaraHalfLife9mmAutomaticPrimaryAttackHandler")
		HW41 A 0 A_JumpIfInventory("HalfLifeOpposingForce",1,"FireOpp")
		HW41 A 1 Bright A_Jump(83,1)
        goto FireAnimInit
		HW41 A 2 Bright
        goto FireAnimInit

    FireAnimInit:
        HW41 A 0 A_TakeInventory("SamsaraHalfLife9mmAutomaticFireState")
        HW41 A 0 A_GiveInventory("SamsaraHalfLife9mmAutomaticFireState",random(1,3))
        HW41 A 0 A_JumpIfInventory("SamsaraHalfLife9mmAutomaticFireState",0,"FireAnim3")
        HW41 A 0 A_JumpIfInventory("SamsaraHalfLife9mmAutomaticFireState",2,"FireAnim2")
	FireAnim1:
		HW44 A 2 Bright
		HW44 BCDEFG 1 A_ReFire
		goto ReadyActual

	FireAnim2:
		HW45 A 2 Bright
		HW45 BCDEF 1 A_ReFire
        HW41 A 1 A_ReFire
		goto ReadyActual

	FireAnim3:
		HW46 A 2 Bright
		HW46 BCDEFG 1 A_ReFire
		goto ReadyActual

    AltFire:
        HW42 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"AltFireWaterCheck")
        HW42 A 0 A_JumpIfInventory("RocketAmmo",1,"AltFireWaterCheck")
        goto FireDry

    AltFireWaterCheck:
        HW42 A 0 A_JumpIfInventory("SamsaraCurrentWaterLevel",0,"FireDry")
        goto AltFireStart

    AltHold:
        HW42 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"AltHoldWaterCheck")
        HW42 A 0 A_JumpIfInventory("RocketAmmo",1,"AltHoldWaterCheck")
        goto AltHoldDry

    AltHoldWaterCheck:
        HW42 A 0 A_JumpIfInventory("SamsaraCurrentWaterLevel",0,"AltHoldDry")
        goto AltFireStart

    AltHoldDry:
        HW43 A 0 A_PlaySound("halflife/weaponempty",CHAN_6)
        HW43 A 0 A_ClearReFire
        HW43 A 1 A_JumpIfInventory("HalfLifeOpposingForce",1,"AltHoldDryOpp")
        HW43 BCD 1
        HW43 EFGH 1 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
        goto ReadyActualAmmoCheck

	AltFireStart:
		HW42 A 0 A_AlertMonsters
		HW42 A 0 A_GiveInventory("SamsaraHalfLife9mmAutomaticSecondaryAttackHandler")
		HW42 A 2 A_JumpIfInventory("HalfLifeOpposingForce",1,"AltFireOpp")
		HW42 BCDEFGHIJKLMNOPQRSTUVWXYZ 1
		HW43 ABCDEFGH 1 A_ReFire
		goto ReadyActual

    Reload:
    ReloadCheck:
        HW41 A 0 A_JumpIfInventory("HLMP5Counter",0,"ReadyActual")
        HW41 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        HW41 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReloadStart")
        HW41 A 0 A_JumpIfInventory("Clip",1,"ReloadStart")
        goto ReadyActual

	ReloadStart:
		HW47 A 1 A_JumpIfInventory("HalfLifeOpposingForce",1,"ReloadOpp")
		HW47 BCD 1
		HW47 E 1 A_PlaySound("halflife/mp5_clipeject")
		HW47 FGHIJKLMNOPQRSTUVW 1
		HW47 X 1 A_PlaySound("halflife/mp5_clipinsert")
		HW47 YZ 1
		HW48 ABCDEFGHI 1
		HW48 JKLMN 2
		HW48 OP 1
		HW48 Q 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReloadAmmo")
	ReloadDone:
		HW48 QRST 1 A_WeaponReady(WRF_NOBOB)
		goto ReadyActual

    ReloadAmmo:
        HW48 Q 0 A_JumpIfInventory("HLMP5Counter",0,"ReloadDone")
        HW48 Q 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        HW48 Q 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        HW48 Q 0 A_JumpIfInventory("Clip",1,1)
        goto ReloadDone
        HW48 Q 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        HW48 Q 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        HW48 Q 0 A_TakeInventory("Clip",1)
        HW48 Q 0 A_GiveInventory("HLMP5Counter")
        loop

    // Adrian Shephard
    ReadyActualNonReloadableOpp:
		OP41 A 2 A_WeaponReady
        goto ReadyActualJump

    ReadyActualReloadableOpp:
		OP41 A 2 A_WeaponReady(WRF_ALLOWRELOAD)
        goto ReadyActualJump

    ReadyActualIdleNonReloadableOpp:
		OP49 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady
		OP50 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady
		OP51 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady
		OP52 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady
		OP53 ABCDEFGH 1 A_WeaponReady
		goto ReadyActual

    ReadyActualIdleReloadableOpp:
		OP49 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady(WRF_ALLOWRELOAD)
		OP50 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady(WRF_ALLOWRELOAD)
		OP51 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady(WRF_ALLOWRELOAD)
		OP52 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady(WRF_ALLOWRELOAD)
		OP53 ABCDEFGH 1 A_WeaponReady(WRF_ALLOWRELOAD)
		goto ReadyActual

	ReadyOpp:
		OP40 ABCDEFGHIJK 1 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
		goto ReadyActual

    FireDryOpp:
        OP41 A 4
        goto ReadyActualAmmoCheck

    HoldDry1Opp:
        OP44 BCDE 1
        OP44 FG 1 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
        goto ReadyActualAmmoCheck

    HoldDry2Opp:
        OP45 BCDE 1
        OP45 F 1 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
        OP41 A 1 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
        goto ReadyActualAmmoCheck

    HoldDry3Opp:
        OP46 BCDE 1
        OP46 FG 1 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
        goto ReadyActualAmmoCheck

	FireOpp:
		OP41 A 1 Bright A_Jump(83,1)
        goto FireAnimInitOpp
		OP41 A 2 Bright
        goto FireAnimInitOpp

    FireAnimInitOpp:
        OP41 A 0 A_TakeInventory("SamsaraHalfLife9mmAutomaticFireState")
        OP41 A 0 A_GiveInventory("SamsaraHalfLife9mmAutomaticFireState",random(1,3))
        OP41 A 0 A_JumpIfInventory("SamsaraHalfLife9mmAutomaticFireState",0,"FireAnim3Opp")
        OP41 A 0 A_JumpIfInventory("SamsaraHalfLife9mmAutomaticFireState",2,"FireAnim2Opp")
	FireAnim1Opp:
		OP44 A 2 Bright
		OP44 BCDEFG 1 A_ReFire
		goto ReadyActual

	FireAnim2Opp:
		OP45 A 2 Bright
		OP45 BCDEF 1 A_ReFire
		OP41 A 1 A_ReFire
		goto ReadyActual

	FireAnim3Opp:
		OP46 A 2 Bright
		OP46 BCDEFG 1 A_ReFire
		goto ReadyActual

    AltHoldDryOpp:
        OP43 ABCD 1
        OP43 EFGH 1 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
        goto ReadyActualAmmoCheck

	AltFireOpp:
		OP42 A 2
		OP42 BCDEFGHIJKLMNOPQRSTUVWXYZ 1
		OP43 ABCDEFGH 1 A_ReFire
		goto ReadyActual

	ReloadOpp:
		OP47 ABCD 1
		OP47 E 1 A_PlaySound("halflife/mp5_clipeject")
		OP47 FGHIJKLMNOPQRSTUVW 1
		OP47 X 1 A_PlaySound("halflife/mp5_clipinsert")
		OP47 YZ 1
		OP48 ABCDEFGHI 1
		OP48 JKLMN 2
		OP48 OP 1
		OP48 Q 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReloadAmmoOpp")
	ReloadDoneOpp:
		OP48 QRST 1 A_WeaponReady(WRF_NOBOB)
		goto ReadyActual

    ReloadAmmoOpp:
        OP48 Q 0 A_JumpIfInventory("HLMP5Counter",0,"ReloadDoneOpp")
        OP48 Q 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        OP48 Q 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        OP48 Q 0 A_JumpIfInventory("Clip",1,1)
        goto ReloadDoneOpp
        OP48 Q 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        OP48 Q 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        OP48 Q 0 A_TakeInventory("Clip",1)
        OP48 Q 0 A_GiveInventory("HLMP5Counter")
        loop
	}
}

actor SamsaraHalfLife9mmAutomaticPrimaryAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"PickupReloadable")
      PickupNonReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",1)
        goto PickupAttack

      PickupReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("HLMP5Counter",1,1)
        stop
        TNT1 A 0 A_TakeInventory("HLMP5Counter",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("halflife/mp5_shoot",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("HLMP5Bullet",random(-25,25)/10.0,false,5,3,0,random(-25,25)/10.0)
        goto PickupFinish

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("HLMP5BulletCoop",random(-25,25)/10.0,false,5,3,0,random(-25,25)/10.0)
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 ACS_NamedExecuteAlways("HalfLifeRecoil", 0, 4)
        stop
    }
}

actor SamsaraHalfLife9mmAutomaticSecondaryAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,1)
        stop
        TNT1 A 0 A_TakeInventory("RocketAmmo",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("halflife/mp5_shootgrenade",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("HLMP5Grenade",0,false,3,5)
        goto PickupFinish

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("HLMP5GrenadeCoop",0,false,3,5)
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 ACS_NamedExecuteAlways("HalfLifeRecoil", 0, 3)
        stop
    }
}

actor HLMP5Counter : Ammo
{
    Inventory.MaxAmount 50
    Tag "MP5 Magazine"
    +IGNORESKILL
}

Actor HLMP5Bullet : HLBullet { 
	+THRUGHOST
	Damage (0) 
	Scale 0.5
	States
	{
		Crash:
		Death:
			TNT1 A 0 A_Explode(CallACS("HalfLife_BulletDamage",13),8,!XF_HURTSOURCE,0,8)
			Goto Super::Death
		XDeath:
			TNT1 A 0 A_Explode(CallACS("HalfLife_BulletDamage",13),8,!XF_HURTSOURCE,0,8)
			Goto Super::XDeath
	}		
}
Actor HLMP5BulletCoop : HLBulletCoop { 
	+THRUGHOST
	Damage (0) 
	Scale 0.5
	States
	{
		Crash:
		Death:
			TNT1 A 0 A_Explode(CallACS("HalfLife_BulletDamage",13),8,!XF_HURTSOURCE,0,8)
			Goto Super::Death
		XDeath:
			TNT1 A 0 A_Explode(CallACS("HalfLife_BulletDamage",13),8,!XF_HURTSOURCE,0,8)
			Goto Super::XDeath
	}		
}

Actor HLMP5Grenade
{
	PROJECTILE
	-NOGRAVITY
	+FORCERADIUSDMG
	+EXTREMEDEATH
	Gravity 0.2
    Radius 8
    Height 12
    Mass 25
    Speed 20
    Damage (50)
    DamageType "HLExplosive"
    Scale 0.085
	Var Int User_Angle;
	states
	{
	Spawn:
		TNT1 A 0 NoDelay ThrustThingZ(0,2,0,1) 
		TNT1 A 0 A_Jump(128,2)
		TNT1 A 0 A_SetUserVar(User_Angle,Random(1,6))
		Goto SpawnLoop
		TNT1 A 0 A_SetUserVar(User_Angle,Random(-6,-1))
	SpawnLoop:	
		TNT1 A 0 A_SetAngle(Angle+User_Angle) 
		HP40 A 1 A_SetPitch(Pitch-15)	 //A_ChangeFlag("EXTREMEDEATH",random(0,2) > 0)
		loop
	Death:
		TNT1 A 0 A_NoGravity
		TNT1 A 0 A_Explode(100,124)
		TNT1 A 0 A_PlaySound("halflife/mp5_grenadehit",CHAN_BODY)
		TNT1 A 5 A_SpawnItemEx("HLExplosionBoom",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
		stop
	}
}

Actor HLMP5GrenadeCoop : HLMP5Grenade { Species "Player" +THRUSPECIES +DONTHARMCLASS +DONTHARMSPECIES +MTHRUSPECIES }

Actor HLMP5MuzzleFlash : HL9MMMuzzleFlash
{
	States
	{
		Spawn:
			TNT1 A 0
			HW29 L 1 Bright A_Warp(AAPTR_Master,cos(pitch)*30,2.4,37-(sin(pitch)*37),0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE)
			HW29 M 1 A_Warp(AAPTR_Master,cos(pitch)*30,2.4,37-(sin(pitch)*37),0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE)
			Stop
	}
}
