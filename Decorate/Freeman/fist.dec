actor freeman_fistselected : Boolean { Inventory.InterHubAmount 0 }
actor SamsaraHalfLifeCrowbarFireState : Counter { Inventory.MaxAmount 3 }
actor HLCrowHit : HLBoolean {}

Actor "Crowbar" : HLWeapon
{
	Inventory.PickupSound "halflife/weaponpickup"
	Inventory.Pickupmessage "You got the crowbar!"
	Weapon.SlotNumber 0
	Weapon.BobRangeX 0
	Weapon.BobRangeY 0.75
	Weapon.BobSpeed 1.4
	Weapon.BobStyle Alpha   
	Weapon.SelectionOrder 4000
	+WEAPON.MELEEWEAPON
	+WEAPON.WIMPY_WEAPON
	Inventory.RestrictedTo "HalfLifePlayer"
    Tag "Crowbar"
	States
	{
	Spawn:
		HWF0 Z -1
		stop

    // Gordon Freeman
	ReadyActual:
		HWF1 A 0 A_JumpIfInventory("HalfLifeOpposingForce",1,"ReadyActualOpp")
		HWF1 A 2 A_WeaponReady
        goto ReadyActualJump

    ReadyActualJump:
		HWF1 A 0 A_Jump(4,"ReadyActual")
		HWF1 A 0 A_Jump(3,"ReadyActual")
		HWF1 A 0 A_Jump(2,"ReadyActual")
		HWF1 A 0 A_JumpIfInventory("HalfLifeOpposingForce",1,"ReadyActualJumpOpp")
		HWF1 A 0 A_Jump(1,"ReadyActualIdle")
		goto ReadyActual

	ReadyActualIdle:
		HWF1 BCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady
		HWF0 MNOPQRSTUV 1 A_WeaponReady
		goto ReadyActual

	Deselect:
		TNT1 A 0 A_TakeInventory("freeman_fistselected")
	DeselectLoop:
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
		TNT1 A 1 A_Lower
		loop		

	Select:
		TNT1 A 0 A_GiveInventory("freeman_fistselected")
		TNT1 A 0 A_TakeInventory("freeman_pistolselected")
		TNT1 A 0 A_TakeInventory("freeman_slot1selected")
		TNT1 A 0 A_TakeInventory("freeman_slot2selected")
		TNT1 A 0 A_TakeInventory("freeman_slot3selected")
		TNT1 A 0 A_TakeInventory("freeman_slot4selected")
		TNT1 A 0 A_TakeInventory("freeman_slot5selected")
		TNT1 A 0 A_TakeInventory("freeman_slot6selected")
		TNT1 A 0 A_TakeInventory("freeman_slot7selected")
		TNT1 A 0 A_TakeInventory("freeman_uniqueselected")
		TNT1 A 0 A_TakeInventory("freeman_unique2selected")
		TNT1 A 0 A_TakeInventory("freeman_unique3selected")
	SelectLoop:	
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
		TNT1 A 1 A_Raise
		loop

	Ready: // our "Select" state
		HWF0 A 0 A_PlaySound("halflife/weaponselect",CHAN_7)
		HWF0 A 0 A_JumpIfInventory("HalfLifeOpposingForce",1,"ReadyOpp")
		HWF0 ABCDEFGHIJKL 1 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
		goto ReadyActual

    // lotta speed parity to be done here
	Fire:
        HWF3 A 0 A_TakeInventory("HLCrowHit")
        HWF3 A 0 A_TakeInventory("SamsaraHalfLifeCrowbarFireState")
        HWF3 A 0 A_GiveInventory("SamsaraHalfLifeCrowbarFireState",random(1,3))
		HWF3 A 0 A_JumpIfInventory("HalfLifeOpposingForce",1,"FireOpp")
		HWF3 A 0 A_PlaySound("halflife/crow_swing",CHAN_WEAPON)
        HWF3 A 0 A_JumpIfInventory("SamsaraHalfLifeCrowbarFireState",0,"FireAnim3")
        HWF3 A 0 A_JumpIfInventory("SamsaraHalfLifeCrowbarFireState",2,"FireAnim2")
	FireAnim1:
		HWF3 A 1 A_JumpIfInventory("SamsaraHasDoubleFiringSpeed",1,1)
		HWF3 A 1
		HWF3 B 1 A_JumpIfInventory("SamsaraHasDoubleFiringSpeed",1,1)
		HWF3 B 1
		HWF3 C 1 A_GiveInventory("SamsaraHalfLifeCrowbarAttackHandler")
		HWF3 D 1
		HWF4 A 1 A_JumpIfInventory("HLCrowHit",1,"FireAnim1Hit")
		HWF4 ABBCCDDEEFF 1
		HWF4 GG 1 A_ReFire
		HWF1 AA 1 A_ReFire
		goto ReadyActual

	FireAnim1Hit:
		HWF3 EFG 1
		HWF3 H 1 A_ReFire
		HWF1 A 1 A_ReFire
		goto ReadyActual

	FireAnim2:
		HWF5 A 1 A_JumpIfInventory("SamsaraHasDoubleFiringSpeed",1,1)
		HWF5 A 1
		HWF5 B 1 A_JumpIfInventory("SamsaraHasDoubleFiringSpeed",1,1)
		HWF5 B 1
		HWF5 C 1 A_GiveInventory("SamsaraHalfLifeCrowbarAttackHandler")
		HWF5 D 1
		HWF5 E 1 A_JumpIfInventory("HLCrowHit",1,"FireAnim2Hit")
		HWF5 FGHIJKLMN 1
		HWF1 AA 1
		HWF1 AAAA 1 A_ReFire
		goto ReadyActual

	FireAnim2Hit:
		HWF6 ABC 1
		HWF6 D 1 A_ReFire
		HWF1 A 1 A_ReFire
		goto ReadyActual

	FireAnim3:
		HWF7 A 1 A_JumpIfInventory("SamsaraHasDoubleFiringSpeed",1,1)
		HWF7 A 1
		HWF7 B 1 A_JumpIfInventory("SamsaraHasDoubleFiringSpeed",1,1)
		HWF7 B 1
		HWF7 C 1 A_GiveInventory("SamsaraHalfLifeCrowbarAttackHandler")
		HWF7 D 1
		HWF7 E 1 A_JumpIfInventory("HLCrowHit",1,"FireAnim3Hit")
		HWF7 FGHI 1
		HWF7 JKLMMNO 1
		HWF7 PQRS 1 A_ReFire
		goto ReadyActual

	FireAnim3Hit:
		HWF7 E 1
		HWF8 AB 1
		HWF8 CD 1 A_ReFire
		HWF8 E 1 A_WeaponReady(WRF_NOBOB)
		goto ReadyActual

    // Adrian Shephard (Combat Knife)
	ReadyActualOpp:
		OP00 A 2 A_WeaponReady
		goto ReadyActualJump

    ReadyActualJumpOpp:
		OP00 A 0 A_Jump(1,"ReadyActualIdle1Opp","ReadyActualIdle2Opp","ReadyActualIdle3Opp")
		goto ReadyActual

	ReadyActualIdle1Opp:
		OP00 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady
		OP01 ABCDEFGHI 1 A_WeaponReady
		goto ReadyActual

	ReadyActualIdle2Opp:
		OP10 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady
		OP11 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady
		OP12 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady
		OP13 AB 1 A_WeaponReady
		goto ReadyActual

	ReadyActualIdle3Opp:
		OP14 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady
		OP15 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady
		OP16 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady
		OP17 AB 1 A_WeaponReady
		goto ReadyActual

	ReadyOpp:
		OP02 ABCDEFGHIJK 1 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
		OP00 A 1 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
		goto ReadyActual

	FireOpp:
		OP04 A 0 A_PlaySound("HLKnife/Swing",CHAN_WEAPON)
        OP04 A 0 A_JumpIfInventory("SamsaraHalfLifeCrowbarFireState",0,"FireAnim3Opp")
        OP04 A 0 A_JumpIfInventory("SamsaraHalfLifeCrowbarFireState",2,"FireAnim2Opp")
    FireAnim1Opp:
		OP04 A 1 A_JumpIfInventory("SamsaraHasDoubleFiringSpeed",1,1)
		OP04 A 1
		OP04 B 1 A_JumpIfInventory("SamsaraHasDoubleFiringSpeed",1,1)
		OP04 B 1
		OP04 C 1 A_GiveInventory("SamsaraHalfLifeKnifeAttackHandler")
		OP04 D 1
		OP05 E 1 A_JumpIfInventory("HLCrowHit",1,"FireAnim1HitOpp")
		OP05 EFFGGHHIIJJ 1
		OP00 AAAA 1 A_ReFire
		goto ReadyActual

	FireAnim1HitOpp:
		OP04 EFG 1
		OP04 H 1 A_ReFire
		OP00 A 1 A_ReFire
		goto ReadyActual

	FireAnim2Opp:
		OP06 A 1 A_JumpIfInventory("SamsaraHasDoubleFiringSpeed",1,1)
		OP06 A 1
		OP06 B 1 A_JumpIfInventory("SamsaraHasDoubleFiringSpeed",1,1)
		OP06 B 1
		OP06 C 1 A_GiveInventory("SamsaraHalfLifeKnifeAttackHandler")
		OP06 D 1
		OP06 E 1 A_JumpIfInventory("HLCrowHit",1,"FireAnim2HitOpp")
		OP06 FGHIJKLM 1
		OP00 AAA 1
		OP00 AAAA 1 A_ReFire
		goto ReadyActual

	FireAnim2HitOpp:
		OP07 CDE 1
		OP07 FG 1 A_ReFire
		goto ReadyActual

	FireAnim3Opp:
		OP08 A 1 A_JumpIfInventory("SamsaraHasDoubleFiringSpeed",1,1)
		OP08 B 1
		OP08 C 1 A_JumpIfInventory("SamsaraHasDoubleFiringSpeed",1,1)
		OP08 D 1
		OP08 E 1 A_GiveInventory("SamsaraHalfLifeKnifeAttackHandler")
		OP08 F 1
		OP08 G 1 A_JumpIfInventory("HLCrowHit",1,"FireAnim3HitOpp")
		OP08 HIIJKLMNOPQ 1
		OP08 R 1 A_ReFire
		OP00 AAA 1 A_ReFire
		goto ReadyActual

	FireAnim3HitOpp:
		OP09 EFG 1
		OP09 HI 1 A_ReFire
		OP09 J 1 A_WeaponReady(WRF_NOBOB)
		goto ReadyActual
	}
}

actor SamsaraHalfLifeCrowbarAttackHandler : Trigger
{
    States
    {
      // Attack stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupCoop")
      PickupDM:
        TNT1 A 0 A_FireCustomMissile("HLCrowbarMissile",0,false,0,4)
        stop

      PickupCoop:
        TNT1 A 0 A_FireCustomMissile("HLCrowbarMissileCoop",0,false,0,4)
        stop
    }
}

Actor HLCrowbarMissile : FastProjectile
{
	PROJECTILE
	+BLOODSPLATTER
	+NOEXTREMEDEATH
	+NOTARGETSWITCH
	Radius 6
	Height 8
	Speed 40
	Damage (15)
	//Obituary "%o died....somehow?"
	DamageType "HLCrowbar"
	scale 0.25
	States
	{
	Spawn:
		TNT1 A 1
		stop
	Death:
	Crash:
		TNT1 A 0 A_GiveToTarget("HLCrowHit",1)
		TNT1 A 1 A_PlaySound("halflife/crow_clonk",CHAN_BODY)
		stop
	XDeath:
		TNT1 A 0 A_GiveToTarget("HLCrowHit",1)
		TNT1 A 1 A_PlaySound("halflife/crow_flesh",CHAN_BODY)
		stop
	}
}

Actor HLCrowbarMissileCoop : HLCrowbarMissile { +THRUSPECIES Species "Player" }

actor SamsaraHalfLifeKnifeAttackHandler : Trigger
{
    States
    {
      // Attack stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupCoop")
      PickupDM:
        TNT1 A 0 A_FireCustomMissile("HLKnifeMissile",0,false,0,4)
        stop

      PickupCoop:
        TNT1 A 0 A_FireCustomMissile("HLKnifeMissileCoop",0,false,0,4)
        stop
    }
}

Actor HLKnifeMissile : HLCrowbarMissile
{
	//Obituary "%o died....somehow?"
	States
	{
	Spawn:
		TNT1 A 1
		stop
	Death:
	Crash:
		TNT1 A 0 A_GiveToTarget("HLCrowHit",1)
		TNT1 A 1 A_PlaySound("HLKnife/Wall",CHAN_BODY)
		stop
	XDeath:
		TNT1 A 0 A_GiveToTarget("HLCrowHit",1)
		TNT1 A 1 A_PlaySound("HLKnife/Flesh",CHAN_BODY)
		stop
	}
}

Actor HLKnifeMissileCoop : HLKnifeMissile { +THRUSPECIES Species "Player" }
