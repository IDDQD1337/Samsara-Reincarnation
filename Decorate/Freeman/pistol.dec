actor freeman_pistolselected : Boolean { Inventory.InterHubAmount 0 }

Actor "9mm Pistol" : HLWeapon
{
	Weapon.Slotnumber 1
	Weapon.SelectionOrder 3750
	Weapon.BobRangeX 0
	Weapon.BobRangeY 0.75
	Weapon.BobSpeed 1.4
	Weapon.BobStyle Alpha 	
	Inventory.Pickupmessage "You got the 9mm!"
	Inventory.Pickupsound "halflife/weaponpickup"
	DamageType "HLBullet"
	Weapon.AmmoType "HL9mmCounter"
    Weapon.AmmoUse 1
	+WEAPON.WIMPY_WEAPON
	Inventory.RestrictedTo "HalfLifePlayer"
    Tag "9mm Pistol"
	states
	{
	Spawn:
		HWP0 Z -1
		stop

    // Gordon Freeman
	ReadyActual:
        HWP9 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReadyActualReloadable")
    ReadyActualNonReloadable:
		HWP9 A 0 A_JumpIfInventory("HalfLifeOpposingForce",1,"ReadyActualNonReloadableOpp")
		HWP9 A 2 A_WeaponReady
        goto ReadyActualJump

    ReadyActualReloadable:
		HWP9 A 0 A_JumpIfInventory("HalfLifeOpposingForce",1,"ReadyActualReloadableOpp")
		HWP9 A 2 A_WeaponReady(WRF_ALLOWRELOAD)
        goto ReadyActualJump

    ReadyActualJump:
		HWP9 A 0 A_Jump(4,"ReadyActualAmmoCheck")
		HWP9 A 0 A_Jump(3,"ReadyActualAmmoCheck")
		HWP9 A 0 A_Jump(2,"ReadyActualAmmoCheck")
		HWP9 A 0 A_Jump(1,"ReadyActualIdle1","ReadyActualIdle2","ReadyActualIdle3")
        goto ReadyActualAmmoCheck

	ReadyActualIdle1:
        HWP9 B 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReadyActualIdle1Reloadable")
    ReadyActualIdle1NonReloadable:
		HWP9 B 0 A_JumpIfInventory("HalfLifeOpposingForce",1,"ReadyActualIdle1NonReloadableOpp")
        HWP9 BCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady
		HWP8 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady
		HWP7 ABCDEFGHIJ 1 A_WeaponReady
		goto ReadyActual

    ReadyActualIdle1Reloadable:
		HWP9 B 0 A_JumpIfInventory("HalfLifeOpposingForce",1,"ReadyActualIdle1ReloadableOpp")
        HWP9 BCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady(WRF_ALLOWRELOAD)
		HWP8 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady(WRF_ALLOWRELOAD)
		HWP7 ABCDEFGHIJ 1 A_WeaponReady(WRF_ALLOWRELOAD)
		goto ReadyActual

	ReadyActualIdle2:
        HW10 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReadyActualIdle2Reloadable")
    ReadyActualIdle2NonReloadable:
		HW10 A 0 A_JumpIfInventory("HalfLifeOpposingForce",1,"ReadyActualIdle2NonReloadableOpp")
        HW10 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady
        HW11 ABCDEFGHIJKLMNO 1 A_WeaponReady
		goto ReadyActual

    ReadyActualIdle2Reloadable:
		HW10 A 0 A_JumpIfInventory("HalfLifeOpposingForce",1,"ReadyActualIdle2ReloadableOpp")
        HW10 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady(WRF_ALLOWRELOAD)
        HW11 ABCDEFGHIJKLMNO 1 A_WeaponReady(WRF_ALLOWRELOAD)
		goto ReadyActual
	
	ReadyActualIdle3:
        HW12 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReadyActualIdle3Reloadable")
    ReadyActualIdle3NonReloadable:
		HW12 A 0 A_JumpIfInventory("HalfLifeOpposingForce",1,"ReadyActualIdle3NonReloadableOpp")
        HW12 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady
        HW13 ABCDEFGHIJKLMNOPQRSTUVW 1 A_WeaponReady
		goto ReadyActual

    ReadyActualIdle3Reloadable:
		HW12 A 0 A_JumpIfInventory("HalfLifeOpposingForce",1,"ReadyActualIdle3ReloadableOpp")
        HW12 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady(WRF_ALLOWRELOAD)
        HW13 ABCDEFGHIJKLMNOPQRSTUVW 1 A_WeaponReady(WRF_ALLOWRELOAD)
		goto ReadyActual

    ReadyActualAmmoCheck:
        HWP9 A 0 A_JumpIfInventory("SamsaraReloadMode",1,1)
        goto ReadyActual
        HWP9 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"ReadyActual")
        HWP9 A 0 A_JumpIfInventory("HL9mmCounter",1,"ReadyActual")
        goto ReloadCheck

	Deselect:
		TNT1 A 0 A_TakeInventory("freeman_pistolselected")
	DeselectLoop:	
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
		TNT1 A 1 A_Lower
		loop

	Select:
		TNT1 A 0 A_TakeInventory("freeman_fistselected")
		TNT1 A 0 A_GiveInventory("freeman_pistolselected")
		TNT1 A 0 A_TakeInventory("freeman_slot1selected")
		TNT1 A 0 A_TakeInventory("freeman_slot2selected")
		TNT1 A 0 A_TakeInventory("freeman_slot3selected")
		TNT1 A 0 A_TakeInventory("freeman_slot4selected")
		TNT1 A 0 A_TakeInventory("freeman_slot5selected")
		TNT1 A 0 A_TakeInventory("freeman_slot6selected")
		TNT1 A 0 A_TakeInventory("freeman_slot7selected")
		TNT1 A 0 A_TakeInventory("freeman_uniqueselected")
		TNT1 A 0 A_TakeInventory("freeman_unique2selected")
		TNT1 A 0 A_TakeInventory("freeman_unique3selected")
	SelectLoop:	
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
		TNT1 A 1 A_Raise
		loop

	Ready: // our "Select" state
		HWP0 A 0 A_PlaySound("halflife/weaponselect",CHAN_7)
		HWP0 A 0 A_JumpIfInventory("HalfLifeOpposingForce",1,"ReadyOpp")
		HWP0 ABCDEFGHIJKLMNO 1 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
		goto ReadyActual

    Fire:
        HWP2 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"FireReloadable")
    FireNonReloadable:
        HWP2 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto FireStart
        HWP2 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart")
        HWP2 A 0 A_JumpIfInventory("Clip",1,"FireStart")
        goto FireDry

    FireReloadable:
        HWP2 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireStart")
        HWP2 A 0 A_JumpIfInventory("HL9mmCounter",1,"FireStart")
        goto FireDry

    FireDry:
        HWP9 A 0 A_PlaySound("halflife/weaponempty",CHAN_6)
        HWP9 A 0 A_ClearReFire
        HWP9 A 4 A_JumpIfInventory("HalfLifeOpposingForce",1,"FireDryOpp")
        goto ReadyActualAmmoCheck

    Hold:
        HWP2 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"HoldReloadable")
    HoldNonReloadable:
        HWP2 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto FireStart
        HWP2 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart")
        HWP2 A 0 A_JumpIfInventory("Clip",1,"FireStart")
        goto HoldDryAmmoless

    HoldReloadable:
        HWP2 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireStart")
        HWP2 A 0 A_JumpIfInventory("HL9mmCounter",1,"FireStart")
        goto HoldDryAmmoless

    HoldDry:
        HWP3 G 0 A_PlaySound("halflife/weaponempty",CHAN_6)
        HWP3 G 0 A_ClearReFire
        HWP3 G 2 A_JumpIfInventory("HalfLifeOpposingForce",1,"HoldDryOpp")
        HWP3 H 2
        HWP3 IJKLMN 2 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
        goto ReadyActualAmmoCheck

    HoldDryAmmoless:
        HW14 I 0 A_PlaySound("halflife/weaponempty",CHAN_6)
        HW14 I 0 A_ClearReFire
        HW14 I 2 A_JumpIfInventory("HalfLifeOpposingForce",1,"HoldDryAmmolessOpp")
        HW14 J 2
        HW14 KLMNO 2 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
        HWP9 A 2 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
        goto ReadyActualAmmoCheck

	FireStart:
        HWP2 A 0 A_AlertMonsters
        HWP2 A 0 A_FireCustomMissile("HLBrassShellCaser",0,false)
    FireLastShotCalc:
        HWP2 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"FireLastShotCalcReloadable")
    FireLastShotCalcNonReloadable:
        HWP2 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto FireContinue
        HWP2 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireContinue")
        HWP2 A 0 A_JumpIfInventory("Clip",2,"FireContinue")
        goto FireContinueLast

    FireLastShotCalcReloadable:
        HWP2 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireContinue")
        HWP2 A 0 A_JumpIfInventory("HL9mmCounter",2,"FireContinue")
        goto FireContinueLast

    FireContinue:
        HWP2 A 0 A_GiveInventory("SamsaraHalfLife9mmHandgunPrimaryAttackHandler")
		HWP2 A 1 Bright A_JumpIfInventory("HalfLifeOpposingForce",1,"FireOpp")
		HWP2 B 1 Bright
		HWP3 AB 2
		HWP3 CDEF 1
		HWP3 GHIJKLMN 2 A_ReFire
		goto ReadyActual

    FireContinueLast:
        HWP2 A 0 A_GiveInventory("SamsaraHalfLife9mmHandgunPrimaryAttackHandler")
		HWP2 A 1 Bright A_JumpIfInventory("HalfLifeOpposingForce",1,"FireLastOpp")
		HWP2 B 1 Bright
		HW14 CD 2
		HW14 EFGH 1
		HW14 IJKLMNO 2 A_ReFire
		HWP9 A 2 A_ReFire
		goto ReadyActual

    AltFire:
        HWP2 B 0 A_JumpIfInventory("SamsaraReloadMode",1,"AltFireReloadable")
    AltFireNonReloadable:
        HWP2 B 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto AltFireStart
        HWP2 B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"AltFireStart")
        HWP2 B 0 A_JumpIfInventory("Clip",1,"AltFireStart")
        goto FireDry

    AltFireReloadable:
        HWP2 B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"AltFireStart")
        HWP2 B 0 A_JumpIfInventory("HL9mmCounter",1,"AltFireStart")
        goto FireDry

    AltHold:
        HWP2 B 0 A_JumpIfInventory("SamsaraReloadMode",1,"AltHoldReloadable")
    AltHoldNonReloadable:
        HWP2 B 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto AltFireStart
        HWP2 B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"AltFireStart")
        HWP2 B 0 A_JumpIfInventory("Clip",1,"AltFireStart")
        goto AltHoldDryAmmoless

    AltHoldReloadable:
        HWP2 B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"AltFireStart")
        HWP2 B 0 A_JumpIfInventory("HL9mmCounter",1,"AltFireStart")
        goto AltHoldDryAmmoless

    AltHoldDry:
        HWP3 E 0 A_PlaySound("halflife/weaponempty",CHAN_6)
        HWP3 E 0 A_ClearReFire
        HWP3 E 2 A_JumpIfInventory("HalfLifeOpposingForce",1,"AltHoldDryOpp")
        HWP3 F 2
        HWP3 GHIJKLM 2 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
        goto ReadyActualAmmoCheck

    AltHoldDryAmmoless:
        HW14 G 0 A_PlaySound("halflife/weaponempty",CHAN_6)
        HW14 G 0 A_ClearReFire
        HW14 G 2 A_JumpIfInventory("HalfLifeOpposingForce",1,"AltHoldDryAmmolessOpp")
        HW14 H 2
        HW14 IJKLMNO 2 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
        goto ReadyActualAmmoCheck

	AltFireStart:
        HWP2 B 0 A_AlertMonsters
        HWP2 B 0 A_FireCustomMissile("HLBrassShellCaser",0,false)
    AltFireLastShotCalc:
        HWP2 B 0 A_JumpIfInventory("SamsaraReloadMode",1,"AltFireLastShotCalcReloadable")
    AltFireLastShotCalcNonReloadable:
        HWP2 B 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto AltFireContinue
        HWP2 B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"AltFireContinue")
        HWP2 B 0 A_JumpIfInventory("Clip",2,"AltFireContinue")
        goto AltFireContinueLast

    AltFireLastShotCalcReloadable:
        HWP2 B 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"AltFireContinue")
        HWP2 B 0 A_JumpIfInventory("HL9mmCounter",2,"AltFireContinue")
        goto AltFireContinueLast

    AltFireContinue:
        HWP2 B 0 A_GiveInventory("SamsaraHalfLife9mmHandgunSecondaryAttackHandler")
		HWP2 B 1 Bright A_JumpIfInventory("HalfLifeOpposingForce",1,"AltFireOpp")
		HWP3 AB 1
		HWP3 CD 2
		HWP3 EFGHIJKLM 2 A_ReFire
		goto ReadyActual

    AltFireContinueLast:
        HWP2 B 0 A_GiveInventory("SamsaraHalfLife9mmHandgunSecondaryAttackHandler")
		HWP2 B 1 Bright A_JumpIfInventory("HalfLifeOpposingForce",1,"AltFireLastOpp")
		HW14 CD 1
		HW14 EF 2
		HW14 GHIJKLMNO 2 A_ReFire
		goto ReadyActual

    Reload:
    ReloadCheck:
        HWP9 A 0 A_JumpIfInventory("HL9mmCounter",0,"ReadyActual")
        HWP9 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto ReloadStart
        HWP9 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        HWP9 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReloadStart")
        HWP9 A 0 A_JumpIfInventory("Clip",1,"ReloadStart")
        goto ReadyActual

	ReloadStart:
        TNT1 A 0 A_JumpIfInventory("HL9mmCounter",1,1)
        goto ReloadStartAmmoless
		HWP4 A 2 A_JumpIfInventory("HalfLifeOpposingForce",1,"ReloadOpp")
		HWP4 B 2
		HWP4 D 2 A_PlaySound("halflife/9mm_reload1")
		HWP4 FHIJKLMNOQSU 2
		HWP4 W 2 A_PlaySound("halflife/9mm_reload0")
		HWP4 XYZ 2
		HWP5 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReloadAmmo")
	ReloadDone:
		HWP5 ABCDFHIKLNO 2 A_WeaponReady(WRF_NOBOB)
		goto ReadyActual

    ReloadAmmo:
        HWP5 A 0 A_JumpIfInventory("PistolModeOn",1,"ReloadAmmoReserve")
        HWP5 A 0 A_JumpIfInventory("HL9mmCounter",0,"ReloadDone")
        HWP5 A 0 A_GiveInventory("HL9mmCounter")
        loop

    ReloadAmmoReserve:
        HWP5 A 0 A_JumpIfInventory("HL9mmCounter",0,"ReloadDone")
        HWP5 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        HWP5 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        HWP5 A 0 A_JumpIfInventory("Clip",1,1)
        goto ReloadDone
        HWP5 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        HWP5 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        HWP5 A 0 A_TakeInventory("Clip",1)
        HWP5 A 0 A_GiveInventory("HL9mmCounter")
        goto ReloadAmmo

	ReloadStartAmmoless:
		HW14 Q 2 A_JumpIfInventory("HalfLifeOpposingForce",1,"ReloadAmmolessOpp")
		HW14 R 2
		HW14 T 2 A_PlaySound("halflife/9mm_reload1")
		HW14 VXYZ 2
		HW15 ABCDEGIK 2
		HW15 M 2 A_PlaySound("halflife/9mm_reload0")
		HW15 NOP 2
		HW15 Q 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReloadAmmoAmmoless")
	ReloadDoneAmmoless:
		HW15 QRSTVXY 2 A_WeaponReady(WRF_NOBOB)
		HW16 ABD 2 A_WeaponReady(WRF_NOBOB)
		HWP9 A 2 A_WeaponReady(WRF_NOBOB)
		goto ReadyActual

    ReloadAmmoAmmoless:
        HW15 Q 0 A_JumpIfInventory("PistolModeOn",1,"ReloadAmmoAmmolessReserve")
        HW15 Q 0 A_JumpIfInventory("HL9mmCounter",0,"ReloadDoneAmmoless")
        HW15 Q 0 A_GiveInventory("HL9mmCounter")
        loop

    ReloadAmmoAmmolessReserve:
        HW15 Q 0 A_JumpIfInventory("HL9mmCounter",0,"ReloadDoneAmmoless")
        HW15 Q 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        HW15 Q 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        HW15 Q 0 A_JumpIfInventory("Clip",1,1)
        goto ReloadDoneAmmoless
        HW15 Q 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        HW15 Q 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        HW15 Q 0 A_TakeInventory("Clip",1)
        HW15 Q 0 A_GiveInventory("HL9mmCounter")
        goto ReloadAmmoAmmoless

    // Adrian Shephard
    ReadyActualNonReloadableOpp:
		OPP9 A 2 A_WeaponReady
        goto ReadyActualJump

    ReadyActualReloadableOpp:
		OPP9 A 2 A_WeaponReady(WRF_ALLOWRELOAD)
        goto ReadyActualJump

    ReadyActualIdle1NonReloadableOpp:
        OPP9 BCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady
		OPP8 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady
		OPP7 ABCDEFGHIJ 1 A_WeaponReady
		goto ReadyActual

    ReadyActualIdle1ReloadableOpp:
        OPP9 BCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady(WRF_ALLOWRELOAD)
		OPP8 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady(WRF_ALLOWRELOAD)
		OPP7 ABCDEFGHIJ 1 A_WeaponReady(WRF_ALLOWRELOAD)
		goto ReadyActual

    ReadyActualIdle2NonReloadableOpp:
        OP10 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady
        OP11 ABCDEFGHIJKLMNO 1 A_WeaponReady
		goto ReadyActual

    ReadyActualIdle2ReloadableOpp:
        OP10 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady(WRF_ALLOWRELOAD)
        OP11 ABCDEFGHIJKLMNO 1 A_WeaponReady(WRF_ALLOWRELOAD)
		goto ReadyActual

    ReadyActualIdle3NonReloadableOpp:
        OP12 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady
        OP13 ABCDEFGHIJKLMNOPQRSTUVW 1 A_WeaponReady
		goto ReadyActual

    ReadyActualIdle3ReloadableOpp:
        OP12 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady(WRF_ALLOWRELOAD)
        OP13 ABCDEFGHIJKLMNOPQRSTUVW 1 A_WeaponReady(WRF_ALLOWRELOAD)
		goto ReadyActual

	ReadyOpp:
		OPP0 ABCDEFGHIJKLMNO 1 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
		goto ReadyActual

    FireDryOpp:
        OPP9 A 4
        goto ReadyActualAmmoCheck

    HoldDryOpp:
        OPP3 GH 2
        OPP3 IJKLMN 2 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
        goto ReadyActualAmmoCheck

    HoldDryAmmolessOpp:
        OP14 IJ 2
        OP14 KLMNO 2 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
        OPP9 A 2 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
        goto ReadyActualAmmoCheck

	FireOpp:
		OPP2 AB 1 Bright
		OPP3 AB 2
		OPP3 CDEF 1
		OPP3 GHIJKLMN 2 A_ReFire
		goto ReadyActual

	FireLastOpp:
		OPP2 AB 1 Bright
		OP14 CD 2
		OP14 EFGH 1
		OP14 IJKLMNO 2 A_ReFire
		OPP9 A 2 A_ReFire
		goto ReadyActual

    AltHoldDryOpp:
        OPP3 EF 2
        OPP3 GHIJKLM 2 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
        goto ReadyActualAmmoCheck

    AltHoldDryAmmolessOpp:
        OP14 GH 2
        OP14 IJKLMNO 2 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
        goto ReadyActualAmmoCheck

	AltFireOpp:
		OPP2 B 1 Bright
		OPP3 AB 1
		OPP3 CD 2
		OPP3 EFGHIJKLM 2 A_ReFire
		goto ReadyActual

	AltFireLastOpp:
		OPP2 B 1 Bright
		OP14 CD 1
		OP14 EF 2
		OP14 GHIJKLMNO 2 A_ReFire
		goto ReadyActual

	ReloadOpp:
		OPP4 AB 2
		OPP4 D 2 A_PlaySound("halflife/9mm_reload1")
		OPP4 FHIJKLMNOQSU 2
		OPP4 W 2 A_PlaySound("halflife/9mm_reload0")
		OPP4 XYZ 2
		OPP5 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReloadAmmoOpp")
	ReloadDoneOpp:
		OPP5 ABCDFHIKLNO 2 A_WeaponReady(WRF_NOBOB)
		goto ReadyActual

    ReloadAmmoOpp:
        OPP5 A 0 A_JumpIfInventory("PistolModeOn",1,"ReloadAmmoReserveOpp")
        OPP5 A 0 A_JumpIfInventory("HL9mmCounter",0,"ReloadDoneOpp")
        OPP5 A 0 A_GiveInventory("HL9mmCounter")
        loop

    ReloadAmmoReserveOpp:
        OPP5 A 0 A_JumpIfInventory("HL9mmCounter",0,"ReloadDoneOpp")
        OPP5 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        OPP5 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        OPP5 A 0 A_JumpIfInventory("Clip",1,1)
        goto ReloadDoneOpp
        OPP5 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        OPP5 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        OPP5 A 0 A_TakeInventory("Clip",1)
        OPP5 A 0 A_GiveInventory("HL9mmCounter")
        goto ReloadAmmoOpp

	ReloadAmmolessOpp:
		OP14 QR 2
		OP14 T 2 A_PlaySound("halflife/9mm_reload1")
		OP14 VXYZ 2
		OP15 ABCDEGIK 2
		OP15 M 2 A_PlaySound("halflife/9mm_reload0")
		OP15 NOP 2
		OP15 Q 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReloadAmmoAmmolessOpp")
	ReloadDoneAmmolessOpp:
		OP15 QRSTVXY 2 A_WeaponReady(WRF_NOBOB)
		OP16 ABD 2 A_WeaponReady(WRF_NOBOB)
		OPP9 A 2 A_WeaponReady(WRF_NOBOB)
		goto ReadyActual

    ReloadAmmoAmmolessOpp:
        OP15 Q 0 A_JumpIfInventory("PistolModeOn",1,"ReloadAmmoAmmolessReserveOpp")
        OP15 Q 0 A_JumpIfInventory("HL9mmCounter",0,"ReloadDoneAmmolessOpp")
        OP15 Q 0 A_GiveInventory("HL9mmCounter")
        loop

    ReloadAmmoAmmolessReserveOpp:
        OP15 Q 0 A_JumpIfInventory("HL9mmCounter",0,"ReloadDoneAmmolessOpp")
        OP15 Q 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        OP15 Q 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        OP15 Q 0 A_JumpIfInventory("Clip",1,1)
        goto ReloadDoneAmmolessOpp
        OP15 Q 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        OP15 Q 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        OP15 Q 0 A_TakeInventory("Clip",1)
        OP15 Q 0 A_GiveInventory("HL9mmCounter")
        goto ReloadAmmoAmmolessOpp
	}
}

actor SamsaraHalfLife9mmHandgunPrimaryAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"PickupReloadable")
      PickupNonReloadable:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto PickupAttack
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",1)
        goto PickupAttack

      PickupReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("HL9mmCounter",1,1)
        stop
        TNT1 A 0 A_TakeInventory("HL9mmCounter",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("halflife/9mm_shoot",CHAN_WEAPON)
        TNT1 A 0 A_FireBullets(0,0,1,0,"SamsaraBlankPuff",FBF_NORANDOM|FBF_NOFLASH)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("HL9mmBullet",frandom(-1.21,1.21),false,5,3,0,frandom(-1.88,1.88))
        goto PickupFinish

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("HL9mmBulletCoop",frandom(-1.21,1.21),false,5,3,0,frandom(-1.88,1.88))
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 ACS_NamedExecuteAlways("HalfLifeRecoil", 0, 1)
        stop
    }
}

actor SamsaraHalfLife9mmHandgunSecondaryAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"PickupReloadable")
      PickupNonReloadable:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto PickupAttack
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",1)
        goto PickupAttack

      PickupReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("HL9mmCounter",1,1)
        stop
        TNT1 A 0 A_TakeInventory("HL9mmCounter",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("halflife/9mm_shoot",CHAN_WEAPON)
        TNT1 A 0 A_FireBullets(0,0,1,0,"SamsaraBlankPuff",FBF_NORANDOM|FBF_NOFLASH)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("HL9mmBullet",frandom(-5.00,5.00),false,5,3,0,frandom(-5.00,5.00))
        goto PickupFinish

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("HL9mmBulletCoop",frandom(-5.00,5.00),false,5,3,0,frandom(-5.00,5.00))
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 ACS_NamedExecuteAlways("HalfLifeRecoil", 0, 1)
        stop
    }
}

actor HL9mmCounter : Ammo
{
    Inventory.MaxAmount 17
    +IGNORESKILL
}

Actor HL9mmBullet : HLBullet 
{ 
	Damage (0) 
	States
	{
		Crash:
		Death:
			TNT1 A 0 A_Explode(CallACS("HalfLife_BulletDamage",20),8,!XF_HURTSOURCE,0,8)
			Goto Super::Death
		XDeath:
			TNT1 A 0 A_Explode(CallACS("HalfLife_BulletDamage",20),8,!XF_HURTSOURCE,0,8)
			Goto Super::XDeath
	}		
}

Actor HL9mmBulletCoop : HLBulletCoop 
{ 
	Damage (0) 
	States
	{
		Crash:
		Death:
			TNT1 A 0 A_Explode(CallACS("HalfLife_BulletDamage",20),8,!XF_HURTSOURCE,0,8)
			Goto Super::Death
		XDeath:
			TNT1 A 0 A_Explode(CallACS("HalfLife_BulletDamage",20),8,!XF_HURTSOURCE,0,8)
			Goto Super::XDeath
	}		
}

Actor HL9MMMuzzleFlash
{
	+FORCEXYBILLBOARD
	+NOINTERACTION
	+CLIENTSIDEONLY
	-SOLID
	+NOCLIP
	Speed 0
	RenderStyle Add
	Alpha 1.0
	Scale 0.2
	States
	{
		Spawn:
			TNT1 A 0
			HW29 J 1 Bright A_Warp(AAPTR_Master,cos(pitch)*30,4,35-(sin(pitch)*30),0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE)
			HW29 K 1 A_Warp(AAPTR_Master,cos(pitch)*30,4,35-(sin(pitch)*30),0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE)
			Stop
	}
}
