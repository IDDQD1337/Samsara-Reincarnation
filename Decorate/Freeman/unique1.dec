actor freeman_uniqueselected : Boolean { Inventory.InterHubAmount 0 }
actor HLGrenadeFuse : Counter { Inventory.MaxAmount 105 }

Actor "HL Squeaks" : HLWeapon
{
	Weapon.Slotnumber 9
	Weapon.SelectionOrder 1600
	Weapon.BobRangeX 0
	Weapon.BobRangeY 0.75
	Weapon.BobSpeed 1.4
	Weapon.BobStyle Alpha 
	Inventory.Pickupmessage "You found some squeaks!"
	Inventory.Pickupsound "halflife/weaponpickup"
	Weapon.AmmoType1 "HLSqueakAmmo"
	Weapon.AmmoGive1 5
	Weapon.AmmoUse1 1
	Weapon.AmmoType2 "RocketAmmo"
	Weapon.AmmoGive2 3
	+WEAPON.EXPLOSIVE
	//Weapon.AmmoUse2 1
	Tag "Snarks"
	Inventory.RestrictedTo "HalfLifePlayer"	
	states
	{
	Spawn:
		HWU0 ABCDEFGHIJKLMNO 2 bright
		loop
	Select:
		TNT1 A 0 A_TakeInventory("freeman_fistselected",1)	
		TNT1 A 0 A_TakeInventory("freeman_pistolselected",1)	
		TNT1 A 0 A_TakeInventory("freeman_slot1selected",1)	
		TNT1 A 0 A_TakeInventory("freeman_slot2selected",1)	
		TNT1 A 0 A_TakeInventory("freeman_slot3selected",1)	
		TNT1 A 0 A_TakeInventory("freeman_slot4selected",1)
		TNT1 A 0 A_TakeInventory("freeman_slot6selected",1)
		TNT1 A 0 A_TakeInventory("freeman_slot7selected",1)		
		TNT1 A 0 A_TakeInventory("freeman_slot5selected",1)
		TNT1 A 0 A_TakeInventory("freeman_unique2selected",1)	
		TNT1 A 0 A_GiveInventory("freeman_uniqueselected",1)	
		Goto SelectLoop
	SelectLoop:	
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
		TNT1 A 1 A_Raise
		loop
	Deselect:
		TNT1 A 0 A_TakeInventory("HLGrenadeFuse",105)
		TNT1 A 0 A_TakeInventory("freeman_uniqueselected",1)
		Goto DeselectLoop
	DeselectLoop:		
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
		TNT1 A 1 A_Lower
		loop
	Ready:
		TNT1 A 0 A_JumpIfInventory("HalfLifeOpposingForce",1,"ReadyOpp")
		TNT1 A 0 A_PlaySound("halflife/weaponselect",CHAN_WEAPON)
		HW39 WXYZ 1 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
		HW40 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
		HW41 ABCDEFGHIJKLMNOPQRST 1 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)		
		goto WIdle
	ReadyOpp:
		TNT1 A 0 A_PlaySound("halflife/weaponselect",CHAN_WEAPON)
		OP13 BCEFGHIJKLMNO 1 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)	
		goto WIdle
	WIdle:
		TNT1 A 0 A_JumpIfInventory("HalfLifeOpposingForce",1,"WIdleOpp")
		TNT1 A 0 A_TakeInventory("HLGrenadeFuse",105)
		TNT1 A 0 A_JumpIfInventory("HLSqueakAmmo",1,1)
		Goto NoSqueaks
		HW32 BCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady
		HW33 ABCDE 1 A_WeaponReady 
		TNT1 A 0 A_Jump(5,"WIdle")
		TNT1 A 0 A_Jump(4,"WIdle")
		TNT1 A 0 A_Jump(3,"WIdle")
		TNT1 A 0 A_Jump(2,"WIdle")
		TNT1 A 0 A_Jump(1,"WIdle")		
		TNT1 A 0 A_Jump(20,"WIdle2","WIdle3")
		goto WIdle
	WIdle2:
		HW39 A 1 A_WeaponReady
		HW35 YZ 1 A_WeaponReady
		HW36 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady
		HW37 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady
		HW38 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady
		goto WIdle
	WIdle3:
		HW33 FGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady
		HW34 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady
		HW35 ABCDEFGHIJKLMNOPQRSTUVWX 1 A_WeaponReady
		HW35 WVUTSRQPONMLKJIHGFEDCBA 1 A_WeaponReady		
		HW34 ZYXWVUTSRQPONMLKJIHGFEDCBA 1 A_WeaponReady				
		HW33 ZYXWVUTSRQPONMLKJIHGF 1 A_WeaponReady				
		goto WIdle
	NoSqueaks:
		TNT1 A 0 A_JumpIfInventory("HalfLifeOpposingForce",1,"NoGrenades")
		TNT1 A 0 A_JumpIfInventory("HLSqueakAmmo",1,"Ready")
		TNT1 A 2 A_WeaponReady(WRF_NOPRIMARY)
		Loop
	NoGrenades:
		TNT1 A 0 A_JumpIfInventory("HalfLifeOpposingForce",1,1)
		Goto NoSqueaks+1
		TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,"ReadyOpp")
		TNT1 A 2 A_WeaponReady(WRF_NOPRIMARY)
		Loop
	WIdleOpp:
		TNT1 A 0 A_JumpIfInventory("HalfLifeOpposingForce",1,1)
		Goto WIdle+1
		TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,1)
		Goto NoGrenades
		OP00 A 2 A_WeaponReady
		TNT1 A 0 A_Jump(5,"WIdleOpp")
		TNT1 A 0 A_Jump(4,"WIdleOpp")
		TNT1 A 0 A_Jump(3,"WIdleOpp")
		TNT1 A 0 A_Jump(2,"WIdleOpp")
		TNT1 A 0 A_Jump(1,"WIdle2Opp","WIdle3Opp")
		goto WIdleOpp
	WIdle2Opp:
		OP00 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady
		OP01 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady
		OP02 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady
		OP03 ABCDEFGHIJKL 1 A_WeaponReady
		goto WIdleOpp
	WIdle3Opp:
		OP04 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady
		OP05 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady
		OP06 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady
		OP07 ABCDEFGHIJKL 1 A_WeaponReady
		goto WIdleOpp
	Fire:
		TNT1 A 0 A_JumpIfInventory("HalfLifeOpposingForce",1,"FireOpp")
		HW41 UVWXYZ 1
		TNT1 A 0 A_TakeInventory("HLSqueakAmmo",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"FireCoop")
		TNT1 A 0 A_SpawnItemEx("HLSqueak",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"FireSpread")
		goto FireFinish
    FireSpread:
		TNT1 A 0 A_SpawnItemEx("HLSqueak",0,0,0,0,0,0,-15,SXF_NOCHECKPOSITION)
		TNT1 A 0 A_SpawnItemEx("HLSqueak",0,0,0,0,0,0,15,SXF_NOCHECKPOSITION)
        goto FireFinish
	FireCoop:
		TNT1 A 0 A_SpawnItemEx("HLSqueakCoop",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"FireCoopSpread")
		goto FireFinish
    FireCoopSpread:
		TNT1 A 0 A_SpawnItemEx("HLSqueakCoop",0,0,0,0,0,0,-15,SXF_NOCHECKPOSITION)
		TNT1 A 0 A_SpawnItemEx("HLSqueakCoop",0,0,0,0,0,0,15,SXF_NOCHECKPOSITION)
        goto FireFinish
	FireFinish:
		HW42 ABCDEFGHIJ 1
		TNT1 A 0 A_JumpIfInventory("HLSqueakAmmo",1,"WIdle")
		Goto NoSqueaks
	FireOpp:
		TNT1 A 0 A_TakeInventory("HLGrenadeFuse",105)
		OP08 ABCDEFGHIJKLMNOPQRSTUVWX 1
		TNT1 A 0 A_GiveInventory("HLGrenadeFuse",105)
		goto FireLoopOpp
	FireLoopOpp:
		TNT1 A 0 A_JumpIfInventory("HalfLifeOpposingForce", 1, 1)
		Goto WIdle+1
		TNT1 A 0 A_TakeInventory("HLGrenadeFuse",1)
		TNT1 A 1 
		TNT1 A 0 A_Refire("FireLoopOpp")
		TNT1 A 0 A_WeaponReady(WRF_NOPRIMARY)
		TNT1 A 0 A_TakeInventory("RocketAmmo",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"FireFinishCoopOpp")
		Goto FireFinishOpp
	FireFinishOpp:
		TNT1 A 0 A_FireCustomMissile("HLShephardGrenade",0,0,4)	
		TNT1 A 0 A_Jump(256,"Throw1","Throw2","Throw3")
		Goto Throw1
	FireFinishCoopOpp:
		TNT1 A 0 A_FireCustomMissile("HLShephardGrenadeCoop",0,0,4)	
		TNT1 A 0 A_Jump(256,"Throw1","Throw2","Throw3")
		Goto Throw1
	Throw1:
		OP09 ABCDEFGHIJKLMNOPQR 1
		Goto WIdle
	Throw2:
		OP10 ABCDEFGHIJKLMNOPQR 1
		Goto WIdle
	Throw3:
		OP11 ABCDEFGHIJKLMNOPQR 1
		Goto WIdle
	}
}

Actor HLSqueakSpawned : HLBoolean {}
Actor HLSqueakHitWall : HLBoolean {}
Actor HLSqueakIsEvil : HLBoolean {}
Actor HLSqueak
{
	MONSTER
	-SOLID
	-TELESTOMP
	-TELEPORT
	-COUNTKILL
	+FRIENDLY
	+FLOORCLIP
	+THRUSPECIES
	+DROPOFF
	+LOOKALLAROUND
	+NEVERFAST
	Species "Squeak"
	ReactionTime 500
	Gravity 0.8
	Health 13
	Radius 9
	Height 15
	Mass 50
	Speed 7
	Tag "Snark"
	SeeSound "halflife/squeak_active"
	ActiveSound "halflife/squeak_active"
	DeathSound "halflife/squeak_die"
	Obituary "%o was killed by a squeak."
	BloodColor "99 FF 33"
	states
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_JumpIfInventory("HLSqueakSpawned",1,"SpawnDeploy")
		TNT1 A 0 A_GiveInventory("HLSqueakSpawned",1)
		TNT1 A 0 A_PlaySound("halflife/squeak_deploy",CHAN_BODY)
		HL03 ABCDEFGH 2 A_Recoil(-1)
		goto SpawnDeploy
	SpawnDeploy:
		TNT1 A 0 A_LookEx(LOF_NOSOUNDCHECK,0,512)
		TNT1 A 0 A_CountDown
		HL00 ABCD 1 A_Recoil(-1)
		HL00 E 0 A_CustomMissile("HLSqueakCheckHitObject",3,0,0,CMF_AIMDIRECTION,0)
		HL00 EFGH 1 A_Recoil(-1)
		HL00 I 0 A_JumpIfInventory("HLSqueakHitWall",1,"SpawnFlip")
		HL00 I 0 A_LookEx(LOF_NOSOUNDCHECK,0,512)
		HL00 IJKL 1 A_Recoil(-1)
		HL00 M 0 A_CustomMissile("HLSqueakCheckHitObject",3,0,0,CMF_AIMDIRECTION,0)
		HL00 MNOP 1 A_Recoil(-1)
		HL00 A 0 A_JumpIfInventory("HLSqueakHitWall",1,"SpawnFlip")
		loop
	SpawnFlip:
		TNT1 A 0 A_SetAngle(angle+180)
		//TNT1 A 0 A_ChangeFlag("FRIENDLY",0)
		//TNT1 A 0 A_GiveInventory("HLSqueakIsEvil",1)
		TNT1 A 0 A_TakeInventory("HLSqueakHitWall",999)
		goto SpawnDeploy
	See:
		HL02 ABCDEFGH 1 A_Chase
		HL02 A 0 A_ChangeFlag("FRIENDLY",0)
		TNT1 A 0 A_GiveInventory("HLSqueakIsEvil",1)
		TNT1 A 0 A_CountDown
		loop
	Melee:
		TNT1 A 0 A_JumpIfInventory("HLSqueakIsEvil",1,2)
		TNT1 A 0 A_ChangeFlag("FRIENDLY",1) //Keep seeking targets if enemy successfully attacked
		HL00 A 0 A_FaceTarget
		HL00 A 0 A_CustomMeleeAttack(30,"halflife/squeak_attack","halflife/squeak_attack","HLSqueak",1)
		HL00 A 0 A_Stop
		HL00 ABCD 1 ThrustThingZ(0,7,0,1)
		HL00 EFGHIJKLM 1 A_Recoil(random(10.0,15.0)/10.0)
		HL00 A 0 A_CountDown
		goto See
	Death:
	XDeath:
		TNT1 A 0 A_Scream
		TNT1 A 5 A_SpawnItemEx("HLSqueakSplat",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
		stop
	}
}

Actor HLSqueakCoop : HLSqueak
{
	Species "Player"
	+DONTHARMCLASS
	+DONTHARMSPECIES
	+MTHRUSPECIES
	ReactionTime 2450	
	Speed 14	
	Health 1550	
	states
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_JumpIfInventory("HLSqueakSpawned",1,"SpawnDeploy")
		TNT1 A 0 A_GiveInventory("HLSqueakSpawned",1)
		TNT1 A 0 A_PlaySound("halflife/squeak_deploy",CHAN_BODY)
		HL03 ABCDEFGH 2 A_Recoil(-1)
		goto SpawnDeploy
	SpawnDeploy:
		TNT1 A 0 A_LookEx(LOF_NOSOUNDCHECK,0,512)
		TNT1 A 0 A_CountDown
		HL00 ABCD 1 A_Recoil(-1)
		HL00 E 0 A_CustomMissile("HLSqueakCheckHitObjectCoop",3,0,0,CMF_AIMDIRECTION,0)
		HL00 EFGH 1 A_Recoil(-1)
		HL00 I 0 A_JumpIfInventory("HLSqueakHitWall",1,"SpawnFlip")
		HL00 I 0 A_LookEx(LOF_NOSOUNDCHECK,0,512)
		HL00 IJKL 1 A_Recoil(-1)
		HL00 M 0 A_CustomMissile("HLSqueakCheckHitObjectCoop",3,0,0,CMF_AIMDIRECTION,0)
		HL00 MNOP 1 A_Recoil(-1)
		HL00 A 0 A_JumpIfInventory("HLSqueakHitWall",1,"SpawnFlip")
		loop
	SpawnFlip:
		TNT1 A 0 A_SetAngle(angle+180)
		TNT1 A 0 A_TakeInventory("HLSqueakHitWall",999)
		goto SpawnDeploy
	See:
		HL02 ABCDEFGH 1 A_Chase
		HL02 A 0 A_CountDown
		loop
	Melee:
		HL00 A 0 A_FaceTarget
		HL00 A 0 A_CustomMeleeAttack(30,"halflife/squeak_attack","halflife/squeak_attack","HLSqueak",1)
		HL00 A 0 A_Stop
		HL00 ABCD 1 ThrustThingZ(0,7,0,1)
		HL00 EFGHIJKLM 1 A_Recoil(random(10.0,15.0)/10.0)
		HL00 A 0 A_CountDown
		goto See
	Death:
	XDeath:
		TNT1 A 0 A_Scream
		TNT1 A 0 A_Explode(15,15)
		TNT1 A 1 A_SpawnItemEx("HLSqueakSplat",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
		stop
	}
}

Actor HLSqueakSplat
{
	+CLIENTSIDEONLY
	+NOINTERACTION
	-SOLID
	+NOBLOCKMAP
	scale 0.75
	states
	{
	Spawn:
		HGB9 ABCDEFGHIJ 1
		stop
	}
}

Actor HLSqueakCheckHitObject : BulletPuff
{
	Radius 4
	height 6
	Speed 30
	PROJECTILE
	+PUFFGETSOWNER
	+PUFFONACTORS
	+BLOODLESSIMPACT
	+NOBLOODDECALS
	+THRUSPECIES
	species "Squeak"
	States
	{
	Spawn:
		TNT1 A 1
		stop
	Death:
		TNT1 A 3 A_GiveToTarget("HLSqueakHitWall", 1)
		Stop
	}
}

Actor HLSqueakCheckHitObjectCoop : HLSqueakCheckHitObject
{
	species "Player"
	+THRUSPECIES
	+DONTHARMCLASS
	+DONTHARMSPECIES
	+MTHRUSPECIES
}

Actor HLShephardGrenade {
    Radius 8
    Height 8
    Speed 8
    PROJECTILE
	-NOGRAVITY
	+BOUNCEONWALLS
	+BOUNCEONFLOORS
	+BOUNCEONCEILINGS
	+BOUNCEONACTORS
	+ALLOWBOUNCEONACTORS
	Scale 0.75
	BounceCount 3
	BounceSound "HECU/Grenade/Bounce"
	WallBounceSound "HECU/Grenade/Bounce"	
	Damage (0)
    DamageType "HLExplosive"
	Gravity 0.234	
    States {
    Spawn:
		TNT1 A 0
		TNT1 A 0 ACS_NamedExecuteAlways("HalfLifeGrenadeFuse")
		TNT1 A 0 ThrustThingZ(0,15,0,1)
		TNT1 A 0 A_Jump(128,"SpawnLoop2")
	SpawnLoop1:	
      	HL23 ABCDEFGHIJKLMNOPQRSTUVWXYZ 2 Bright A_JumpIf(momx == 0 && momy == 0 && momz == 0, "OnFloor")
      	HL24 ABCDEFGHIJKLMNOPQRSTUVWXYZ 2 Bright A_JumpIf(momx == 0 && momy == 0 && momz == 0, "OnFloor")
      	HL25 ABCDEFGHI 2 Bright	A_JumpIf(momx == 0 && momy == 0 && momz == 0, "OnFloor")
        Loop
	SpawnLoop2:	
      	HL26 ABCDEFGHIJKLMNOPQRSTUVWXYZ 2 Bright A_JumpIf(momx == 0 && momy == 0 && momz == 0, "OnFloor")
      	HL27 ABCDEFGHIJKLMNOPQRSTUVWXYZ 2 Bright A_JumpIf(momx == 0 && momy == 0 && momz == 0, "OnFloor")
      	HL28 ABCDEFGHI 2 Bright	A_JumpIf(momx == 0 && momy == 0 && momz == 0, "OnFloor")
        Loop	
	OnFloor:
		TNT1 A 0 A_CheckFloor(1)
		Goto Death+1
		HL29 A 1
		Loop
    Death:
	XDeath:
		TNT1 A 0 A_JumpIfInventory("HLGrenadeFuse",1,"OnFloor")
		TNT1 A 0 A_PlaySound("halflife/mp5_grenadehit",CHAN_BODY)
		TNT1 A 0 A_Explode(300,124,0,true)
		TNT1 A 5 A_SpawnItemEx("HLExplosionBoom",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
        Stop
    }
}

Actor HLShephardGrenadeCoop : HLShephardGrenade 
{
    Species "Player"
	+THRUSPECIES
	+DONTHURTSPECIES
	+DONTHARMSPECIES
	+MTHRUSPECIES
}
