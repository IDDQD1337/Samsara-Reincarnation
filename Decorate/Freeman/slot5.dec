actor freeman_slot5selected : Boolean { Inventory.InterHubAmount 0 }
actor SamsaraHalfLifeRPGLaserActivated : Boolean {}
actor SamsaraHalfLifeRPGLaserMemory : Boolean {}
actor SamsaraHalfLifeRPGNotLoaded : Boolean {}

Actor " RPG " : HLWeapon
{
	Weapon.Slotnumber 5
	Weapon.SelectionOrder 1350
	Weapon.BobRangeX 0
	Weapon.BobRangeY 0.75
	Weapon.BobSpeed 1.4
	Weapon.BobStyle Alpha 
	Inventory.Pickupmessage "You got the rpg!"
	Inventory.Pickupsound "halflife/weaponpickup"
	Weapon.AmmoType1 "HLRPGCounter"
    Weapon.AmmoUse1 1
	Weapon.AmmoType2 "RocketAmmo"
    Weapon.AmmoGive2 10
	+WEAPON.EXPLOSIVE
	//+WEAPON.NOAUTOFIRE
	Inventory.RestrictedTo "HalfLifePlayer"
    Tag "RPG"
	states
	{
	Spawn:
		HW50 Z -1
		stop

    // Gordon Freeman
	ReadyActual:
        TNT1 A 0 A_TakeInventory("SamsaraHalfLifeLaserSightHidden")
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReadyActualReloadable")
    ReadyActualNonReloadable:
		TNT1 A 0 A_JumpIfInventory("HalfLifeOpposingForce",1,"ReadyActualNonReloadableOpp")
        TNT1 A 0 A_JumpIfInventory("SamsaraHalfLifeRPGNotLoaded",1,"ReadyActualNonReloadableNoAmmo")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReadyActualNonReloadableAmmo")
        TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,"ReadyActualNonReloadableAmmo")
        goto ReadyActualNonReloadableNoAmmo

    ReadyActualNonReloadableNoAmmo:
        HW55 A 0 A_GiveInventory("SamsaraHalfLifeRPGNotLoaded")
		HW55 A 2 A_WeaponReady
        goto ReadyActualJumpNoAmmo

    ReadyActualNonReloadableAmmo:
        HW54 A 0 A_TakeInventory("SamsaraHalfLifeRPGNotLoaded")
		HW54 A 2 A_WeaponReady
        goto ReadyActualJump

    ReadyActualReloadable:
		TNT1 A 0 A_JumpIfInventory("HalfLifeOpposingForce",1,"ReadyActualReloadableOpp")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"ReadyActualReloadableAmmo")
        TNT1 A 0 A_JumpIfInventory("HLRPGCounter",1,"ReadyActualReloadableAmmo")
        goto ReadyActualReloadableNoAmmo

    ReadyActualReloadableNoAmmo:
        HW55 A 0 A_GiveInventory("SamsaraHalfLifeRPGNotLoaded")
		HW55 A 2 A_WeaponReady(WRF_ALLOWRELOAD)
        goto ReadyActualJump

    ReadyActualReloadableAmmo:
        HW54 A 0 A_TakeInventory("SamsaraHalfLifeRPGNotLoaded")
		HW54 A 2 A_WeaponReady(WRF_ALLOWRELOAD)
        goto ReadyActualJump

    ReadyActualJumpNoAmmo:
		TNT1 A 0 A_JumpIfInventory("HalfLifeOpposingForce",1,"ReadyActualJump")
		TNT1 A 0 A_Jump(4,"ReadyActualNoAmmoCheck")
		TNT1 A 0 A_Jump(3,"ReadyActualNoAmmoCheck")
		TNT1 A 0 A_Jump(2,"ReadyActualNoAmmoCheck")
		TNT1 A 0 A_Jump(1,"ReadyActualIdle1","ReadyActualIdle2")
        goto ReadyActualNoAmmoCheck

    ReadyActualJump:
		TNT1 A 0 A_Jump(4,"ReadyActualAmmoCheck")
		TNT1 A 0 A_Jump(3,"ReadyActualAmmoCheck")
		TNT1 A 0 A_Jump(2,"ReadyActualAmmoCheck")
		TNT1 A 0 A_JumpIfInventory("HalfLifeOpposingForce",1,"ReadyActualJumpOpp")
		TNT1 A 0 A_Jump(1,"ReadyActualIdle1","ReadyActualIdle2")
        goto ReadyActualAmmoCheck

	ReadyActualIdle1:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReadyActualIdle1Reloadable")
    ReadyActualIdle1NonReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHalfLifeRPGNotLoaded",1,"ReadyActualIdle1NonReloadableNoAmmo")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReadyActualIdle1NonReloadableAmmo")
        TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,"ReadyActualIdle1NonReloadableAmmo")
        goto ReadyActualIdle1NonReloadableNoAmmo

    ReadyActualIdle1NonReloadableNoAmmo:
        HW65 A 0 A_GiveInventory("SamsaraHalfLifeRPGNotLoaded")
		HW65 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady
		HW66 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady
		HW67 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady
		HW68 ABCDEFGHIJKLM 1 A_WeaponReady
		goto ReadyActual

    ReadyActualIdle1NonReloadableAmmo:
        HW52 A 0 A_TakeInventory("SamsaraHalfLifeRPGNotLoaded")
		HW52 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady
		HW53 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady
		HW60 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady
		HW61 ABCDEFGHIJKLM 1 A_WeaponReady
		goto ReadyActual

    ReadyActualIdle1Reloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"ReadyActualIdle1ReloadableAmmo")
        TNT1 A 0 A_JumpIfInventory("HLRPGCounter",1,"ReadyActualIdle1ReloadableAmmo")
        goto ReadyActualIdle1ReloadableNoAmmo

    ReadyActualIdle1ReloadableNoAmmo:
        HW65 A 0 A_GiveInventory("SamsaraHalfLifeRPGNotLoaded")
		HW65 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady(WRF_ALLOWRELOAD)
		HW66 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady(WRF_ALLOWRELOAD)
		HW67 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady(WRF_ALLOWRELOAD)
		HW68 ABCDEFGHIJKLM 1 A_WeaponReady(WRF_ALLOWRELOAD)
		goto ReadyActual

    ReadyActualIdle1ReloadableAmmo:
        HW52 A 0 A_TakeInventory("SamsaraHalfLifeRPGNotLoaded")
		HW52 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady(WRF_ALLOWRELOAD)
		HW53 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady(WRF_ALLOWRELOAD)
		HW60 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady(WRF_ALLOWRELOAD)
		HW61 ABCDEFGHIJKLM 1 A_WeaponReady(WRF_ALLOWRELOAD)
		goto ReadyActual

	ReadyActualIdle2:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReadyActualIdle2Reloadable")
    ReadyActualIdle2NonReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHalfLifeRPGNotLoaded",1,"ReadyActualIdle2NonReloadableNoAmmo")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReadyActualIdle2NonReloadableAmmo")
        TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,"ReadyActualIdle2NonReloadableAmmo")
        goto ReadyActualIdle2NonReloadableNoAmmo

    ReadyActualIdle2NonReloadableNoAmmo:
        HW68 N 0 A_GiveInventory("SamsaraHalfLifeRPGNotLoaded")
		HW68 NOPQRSTUVWXYZ 1 A_WeaponReady
		HW69 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady
		HW70 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady
		HW71 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady
		goto ReadyActual

    ReadyActualIdle2NonReloadableAmmo:
        HW61 N 0 A_TakeInventory("SamsaraHalfLifeRPGNotLoaded")
		HW61 NOPQRSTUVWXYZ 1 A_WeaponReady
		HW62 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady
		HW63 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady
		HW64 ABCDEFGHIJKLMNOPQRSTUVWXY 1 A_WeaponReady
		HW54 A 1 A_WeaponReady
		goto ReadyActual

    ReadyActualIdle2Reloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"ReadyActualIdle2ReloadableAmmo")
        TNT1 A 0 A_JumpIfInventory("HLRPGCounter",1,"ReadyActualIdle2ReloadableAmmo")
        goto ReadyActualIdle2ReloadableNoAmmo

    ReadyActualIdle2ReloadableNoAmmo:
        HW68 N 0 A_GiveInventory("SamsaraHalfLifeRPGNotLoaded")
		HW68 NOPQRSTUVWXYZ 1 A_WeaponReady(WRF_ALLOWRELOAD)
		HW69 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady(WRF_ALLOWRELOAD)
		HW70 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady(WRF_ALLOWRELOAD)
		HW71 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady(WRF_ALLOWRELOAD)
		goto ReadyActual

    ReadyActualIdle2ReloadableAmmo:
        HW61 N 0 A_TakeInventory("SamsaraHalfLifeRPGNotLoaded")
		HW61 NOPQRSTUVWXYZ 1 A_WeaponReady(WRF_ALLOWRELOAD)
		HW62 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady(WRF_ALLOWRELOAD)
		HW63 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady(WRF_ALLOWRELOAD)
		HW64 ABCDEFGHIJKLMNOPQRSTUVWXY 1 A_WeaponReady(WRF_ALLOWRELOAD)
		HW54 A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		goto ReadyActual

    ReadyActualNoAmmoCheck:
		TNT1 A 0 A_JumpIfInventory("HalfLifeOpposingForce",1,"ReadyActualAmmoCheck")
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReadyActualAmmoCheck")
        TNT1 A 0 A_JumpIfInventory("SamsaraHalfLifeRPGLaserActivated",1,"ReadyActual") // don't autoreload with laser active
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReloadStart")
        TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,"ReloadStart")
        goto ReadyActual

    ReadyActualAmmoCheck:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,1)
        goto ReadyActual
		TNT1 A 0 A_JumpIfInventory("HalfLifeOpposingForce",1,2)
        TNT1 A 0 A_JumpIfInventory("SamsaraHalfLifeRPGLaserActivated",1,"ReadyActual") // don't autoreload with laser active
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"ReadyActual")
		TNT1 A 0 A_JumpIfInventory("HalfLifeOpposingForce",1,"ReadyActualAmmoCheckOpp")
        TNT1 A 0 A_JumpIfInventory("HLRPGCounter",1,"ReadyActual")
        goto ReloadCheck

	Deselect:
		TNT1 A 0 A_TakeInventory("freeman_slot5selected")
	DeselectLoop:
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
		TNT1 A 1 A_Lower
		loop

	Select:
		TNT1 A 0 A_TakeInventory("freeman_fistselected")
		TNT1 A 0 A_TakeInventory("freeman_pistolselected")
		TNT1 A 0 A_TakeInventory("freeman_slot1selected")
		TNT1 A 0 A_TakeInventory("freeman_slot2selected")
		TNT1 A 0 A_TakeInventory("freeman_slot3selected")
		TNT1 A 0 A_TakeInventory("freeman_slot4selected")
		TNT1 A 0 A_GiveInventory("freeman_slot5selected")
		TNT1 A 0 A_TakeInventory("freeman_slot6selected")
		TNT1 A 0 A_TakeInventory("freeman_slot7selected")
		TNT1 A 0 A_TakeInventory("freeman_uniqueselected")
		TNT1 A 0 A_TakeInventory("freeman_unique2selected")
		TNT1 A 0 A_TakeInventory("freeman_unique3selected")
	SelectLoop:
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
		TNT1 A 1 A_Raise
		loop

	Ready: // our "Select" state
		TNT1 A 0 A_PlaySound("halflife/weaponselect",CHAN_7)
		TNT1 A 0 A_JumpIfInventory("HalfLifeOpposingForce",1,"ReadyOpp")
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReadyReloadable")
    ReadyNonReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHalfLifeRPGNotLoaded",1,"ReadyNoAmmo")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReadyAmmo")
        TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,"ReadyAmmo")
        goto ReadyNoAmmo

    ReadyReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"ReadyAmmo")
        TNT1 A 0 A_JumpIfInventory("HLRPGCounter",1,"ReadyAmmo")
        goto ReadyNoAmmo

    ReadyNoAmmo:
        HW51 A 0 A_GiveInventory("SamsaraHalfLifeRPGNotLoaded") // map change ignores this
		HW51 ABCDEFGHIJKLMNO 2 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
		HW55 A 0 ACS_NamedExecuteAlways("HalfLifeLaserSight", 0, 2)
        HW55 A 0 A_JumpIfInventory("SamsaraHalfLifeRPGLaserMemory",1,1)
        goto ReadyActual
        HW55 A 0 A_GiveInventory("SamsaraHalfLifeRPGLaserEnabler")
        goto ReadyActual

	ReadyAmmo:
        HW50 A 0 A_TakeInventory("SamsaraHalfLifeRPGNotLoaded") // map change ignores this
		HW50 ABCDEFGHIJKLMNO 2 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
		HW54 A 0 ACS_NamedExecuteAlways("HalfLifeLaserSight", 0, 2)
        HW54 A 0 A_JumpIfInventory("SamsaraHalfLifeRPGLaserMemory",1,1)
        goto ReadyActual
        HW54 A 0 A_GiveInventory("SamsaraHalfLifeRPGLaserEnabler")
        goto ReadyActual

    Fire:
		HW56 A 0 A_JumpIfInventory("HLOriginalKeySwitcherOpen",1,"ReadyActual")
        HW56 A 0 A_TakeInventory("SamsaraHalfLifeLaserSightHidden")
        HW56 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"FireReloadable")
    FireNonReloadable:
        HW56 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        HW56 A 0 A_JumpIfInventory("RocketAmmo",1,1)
        goto FireDry
		HW56 A 0 A_JumpIfInventory("HalfLifeOpposingForce",1,"FireStart")
        HW56 A 0 A_JumpIfInventory("SamsaraHalfLifeRPGNotLoaded",1,"ReloadStart")
        goto FireStart

    FireReloadable:
        HW56 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireStart")
		HW56 A 0 A_JumpIfInventory("HalfLifeOpposingForce",1,"FireReloadableOpp")
        HW56 A 0 A_JumpIfInventory("HLRPGCounter",1,"FireStart")
        goto FireDry

    FireDry:
        HW55 A 0 A_ClearReFire
        HW55 A 0 A_JumpIfInventory("HalfLifeOpposingForce",1,"FireDryOpp")
        HW55 A 4 A_PlaySound("halflife/weaponempty",CHAN_6)
    FireDryFinish:
        HW55 A 0 A_JumpIfInventory("HalfLifeOpposingForce",1,"ReadyActualAmmoCheck")
        goto FireDryNoAmmoCheck

    FireDryNoAmmoCheck:
        HW55 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"FireDryAmmoCheck")
        HW55 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReloadStart")
        HW55 A 0 A_JumpIfInventory("RocketAmmo",1,"ReloadStart")
        goto ReadyActual

    FireDryAmmoCheck:
        HW55 A 0 A_JumpIfInventory("SamsaraReloadMode",1,1)
        goto ReadyActual
        HW55 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"ReadyActual")
        HW55 A 0 A_JumpIfInventory("HLRPGCounter",1,"ReadyActual")
        goto ReloadCheck

	FireStart:
		HW56 A 0 A_AlertMonsters
        HW56 A 0 A_JumpIfInventory("HalfLifeOpposingForce",1,"FireOpp")
		HW56 A 2 A_GiveInventory("SamsaraHalfLifeRPGAttackHandler")
        HW56 B 2
		HW56 CDEFGHIJKLMNOPQRSTUVWXY 1
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReadyActual")
		goto ReadyActualNoAmmoCheck

	AltFire:
        TNT1 A 0 A_TakeInventory("SamsaraHalfLifeLaserSightHidden")
		TNT1 A 0 A_JumpIfInventory("HalfLifeOpposingForce",1,"AltFireOpp")
		TNT1 A 0 A_JumpIfInventory("SamsaraHalfLifeRPGLaserActivated",1,"AltFireDeactivate")
    AltFireActivate:
        TNT1 A 0 A_GiveInventory("SamsaraHalfLifeRPGLaserMemory")
        TNT1 A 0 A_GiveInventory("SamsaraHalfLifeRPGLaserEnabler")
        goto AltFireFinish

    AltFireDeactivate:
        TNT1 A 0 A_TakeInventory("SamsaraHalfLifeRPGLaserMemory")
        TNT1 A 0 A_GiveInventory("SamsaraHalfLifeRPGLaserDisabler")
        goto AltFireFinish

    AltFireFinish:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"AltFireFinishReloadable")
    AltFireFinishNonReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHalfLifeRPGNotLoaded",1,"AltFireFinishNoAmmo")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"AltFireFinishAmmo")
        TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,"AltFireFinishAmmo")
        goto AltFireFinishNoAmmo

    AltFireFinishReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"AltFireFinishAmmo")
        TNT1 A 0 A_JumpIfInventory("HLRPGCounter",1,"AltFireFinishAmmo")
        goto AltFireFinishNoAmmo

    AltFireFinishNoAmmo:
        HW55 A 8
        goto ReadyActual

    AltFireFinishAmmo:
        HW54 A 8
        goto ReadyActual

    Reload:
    ReloadCheck:
        TNT1 A 0 A_JumpIfInventory("HalfLifeOpposingForce",1,"ReloadCheckOpp")
        TNT1 A 0 A_JumpIfInventory("HLRPGCounter",0,"ReadyActual")
    ReloadCheckContinue:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReloadStart")
        TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,"ReloadStart")
        goto ReadyActual

	ReloadStart:
        HW57 A 0 A_GiveInventory("SamsaraHalfLifeLaserSightHidden")
        HW57 A 1 A_JumpIfInventory("HalfLifeOpposingForce",1,"ReloadOpp")
		HW57 BCDEFGHIJKLMNOPQRSTUVWXYZ 1
		HW58 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1
		HW59 ABCDEFGH 1
        HW54 A 0 A_TakeInventory("SamsaraHalfLifeRPGNotLoaded")
		HW54 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReloadAmmo")
    ReloadDone:
        HW54 A 0 A_TakeInventory("SamsaraHalfLifeLaserSightHidden")
		goto ReadyActual

	ReloadAmmo:
        HW54 A 0 A_JumpIfInventory("HLRPGCounter",0,"ReloadDone")
        HW54 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        HW54 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        HW54 A 0 A_JumpIfInventory("RocketAmmo",1,1)
        goto ReloadDone
        HW54 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        HW54 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        HW54 A 0 A_TakeInventory("RocketAmmo",1)
        HW54 A 0 A_GiveInventory("HLRPGCounter")
        loop

    // Adrian Shephard (Spore Launcher)
	ReadyActualNonReloadableOpp:
		OP00 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady
		OP01 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady
		OP02 ABCDEFGH 1 A_WeaponReady
        goto ReadyActualJump

	ReadyActualReloadableOpp:
		OP00 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady(WRF_ALLOWRELOAD)
		OP01 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady(WRF_ALLOWRELOAD)
		OP02 ABCDEFGH 1 A_WeaponReady(WRF_ALLOWRELOAD)
        goto ReadyActualJump

    ReadyActualJumpOpp:
		OP00 A 0 A_Jump(1,"ReadyActualIdle1Opp","ReadyActualIdle2Opp")
		goto ReadyActualAmmoCheck

	ReadyActualIdle1Opp:
        OP17 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReadyActualIdle1ReloadableOpp")
	ReadyActualIdle1NonReloadableOpp:
		OP17 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady
		OP18 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady
		OP19 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady
		OP20 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady
		OP21 ABCDEFGHIJKLMNOP 1 A_WeaponReady
		goto ReadyActual

	ReadyActualIdle1ReloadableOpp:
		OP17 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady(WRF_ALLOWRELOAD)
		OP18 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady(WRF_ALLOWRELOAD)
		OP19 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady(WRF_ALLOWRELOAD)
		OP20 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady(WRF_ALLOWRELOAD)
		OP21 ABCDEFGHIJKLMNOP 1 A_WeaponReady(WRF_ALLOWRELOAD)
		goto ReadyActual

	ReadyActualIdle2Opp: // commence petting
		OP03 A 0 A_PlaySound("HLSporeLauncher/Pet")
        OP03 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReadyActualIdle2ReloadableOpp")
	ReadyActualIdle2NonReloadableOpp:
		OP03 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady
		OP04 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady
		OP05 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady
		OP06 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady
		OP07 ABCDEFGHIJKLMNOP 1 A_WeaponReady
		goto ReadyActual

	ReadyActualIdle2ReloadableOpp:
		OP03 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady(WRF_ALLOWRELOAD)
		OP04 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady(WRF_ALLOWRELOAD)
		OP05 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady(WRF_ALLOWRELOAD)
		OP06 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady(WRF_ALLOWRELOAD)
		OP07 ABCDEFGHIJKLMNOP 1 A_WeaponReady(WRF_ALLOWRELOAD)
		goto ReadyActual

    ReadyActualAmmoCheckOpp:
        OP00 A 0 A_JumpIfInventory("HLSporeCounter",1,"ReadyActual")
        goto ReloadCheck

	ReadyOpp:
		OP15 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
		OP16 ABCD 1 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
		OP00 A 0 ACS_NamedExecuteAlways("HalfLifeLaserSight", 0, 2)
		goto ReadyActual

	FireReloadableOpp:
        OP12 A 0 A_JumpIfInventory("HLSporeCounter",1,"FireStart")
        goto FireDry

    FireDryOpp:
        OP00 A 4
        TNT1 A 0
        goto FireDryFinish

	FireOpp:
		OP12 A 2 A_GiveInventory("SamsaraHalfLifeSporeLauncherPrimaryAttackHandler")
		OP12 B 2
		OP12 CDEFGHIJKLMO 1
		goto ReadyActual

    AltFireOpp:
        OP12 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"AltFireReloadableOpp")
    AltFireNonReloadableOpp:
        OP12 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"AltFireStartOpp")
        OP12 A 0 A_JumpIfInventory("RocketAmmo",1,"AltFireStartOpp")
        goto FireDry

    AltFireReloadableOpp:
        OP12 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"AltFireStartOpp")
        OP12 A 0 A_JumpIfInventory("HLSporeCounter",1,"AltFireStartOpp")
        goto FireDry

	AltFireStartOpp:
        OP12 A 0 A_AlertMonsters
		OP12 A 2 A_GiveInventory("SamsaraHalfLifeSporeLauncherSecondaryAttackHandler")
		OP12 B 2
		OP12 CDEFGHIJKLMO 1
		goto ReadyActual

    ReloadCheckOpp:
        OP00 A 0 A_JumpIfInventory("HLSporeCounter",0,"ReadyActual")
        goto ReloadCheckContinue

	ReloadOpp:
		OP08 ABCDEFGHIJKLMNOPQRST 1
    ReloadLoopInitOpp:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,1)
        goto ReloadFinishOpp
        // allow interrupting the sequence before ammo calculations take effect by firing either mode
        TNT1 A 0 A_JumpIfInventory("SynthFireLeft",1,"ReloadLoopFireCheckPrimaryOpp")
        TNT1 A 0 A_JumpIfInventory("SynthFireRight",1,"ReloadLoopFireCheckSecondaryOpp")
    ReloadLoopAmmoCheckOpp:
        TNT1 A 0 A_JumpIfInventory("HLSporeCounter",0,"ReloadFinishOpp")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReloadAmmoOpp")
        TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,"ReloadAmmoOpp")
        goto ReloadFinishOpp

    ReloadLoopFireCheckPrimaryOpp:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"Fire")
        TNT1 A 0 A_JumpIfInventory("HLSporeCounter",1,"Fire")
        goto ReloadLoopAmmoCheckOpp

    ReloadLoopFireCheckSecondaryOpp:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"AltFire")
        TNT1 A 0 A_JumpIfInventory("HLSporeCounter",1,"AltFire")
        goto ReloadLoopAmmoCheckOpp

	ReloadLoopAnimOpp:
		OP09 A 0 A_PlaySound("HLSporeLauncher/Reload")
		OP09 ABCDEFGHIJK 1 A_WeaponReady(WRF_NOBOB)
        TNT1 A 0
		OP09 LMNOPQRSTUVWYZ 1 A_WeaponReady(WRF_NOBOB)
		OP10 ABCD 1 A_WeaponReady(WRF_NOBOB)
		goto ReloadLoopInitOpp

	ReloadFinishOpp:
		OP11 ABCDEFGHIJKLMNOPQRSTUVWXY 1 A_WeaponReady(WRF_NOBOB)
		goto ReadyActual

    ReloadAmmoOpp: // calculation timing is done in accordance to holding the reload button during the animation in opfor, this is reflected here as well
        OP09 A 0 A_TakeInventory("SamsaraReloadCalcs") // reset
    ReloadAmmoCalcOpp:
        OP09 A 0 A_JumpIfInventory("HLSporeCounter",0,"ReloadLoopAnimOpp")
        OP09 A 0 A_JumpIfInventory("SamsaraReloadCalcs",2,"ReloadLoopAnimOpp")
        OP09 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        OP09 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        OP09 A 0 A_JumpIfInventory("RocketAmmo",1,1)
        goto ReloadLoopAnimOpp
        OP09 A 0 A_GiveInventory("SamsaraReloadCalcs")
        OP09 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        OP09 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        OP09 A 0 A_TakeInventory("RocketAmmo",1)
        OP09 A 0 A_GiveInventory("HLSporeCounter")
        loop
	}
}

actor SamsaraHalfLifeRPGLaserEnabler : Trigger
{
    States
    {
      Pickup:
        TNT1 A 0 A_TakeInventory("SamsaraHalfLifeLaserSightHidden")
        TNT1 A 0 A_GiveInventory("SamsaraHalfLifeRPGLaserActivated")
        stop
    }
}

actor SamsaraHalfLifeRPGLaserDisabler : Trigger
{
    States
    {
      Pickup:
        TNT1 A 0 A_TakeInventory("SamsaraHalfLifeRPGLaserActivated")
        stop
    }
}

actor SamsaraHalfLifeRPGAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"PickupReloadable")
      PickupNonReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,1)
        stop
        TNT1 A 0 A_TakeInventory("RocketAmmo",1)
        goto PickupAttack

      PickupReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("HLRPGCounter",1,1)
        stop
        TNT1 A 0 A_TakeInventory("HLRPGCounter",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("halflife/rpg_shoot",CHAN_WEAPON)
        TNT1 A 0 A_PlaySound("halflife/mp5_shootgrenade",CHAN_5)
        TNT1 A 0 A_JumpIfInventory("SamsaraHalfLifeRPGLaserActivated",1,"PickupAttackGuided")
      // Dumbfire (laser off)
      PickupAttackDumbfire:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackDumbfireCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackDumbfireCoop")
      PickupAttackDumbfireDM:
        TNT1 A 0 A_SpawnItemEx("HLRPGRocketBase2",14,8,36,cos(-pitch)*12,0,sin(-pitch)*12,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH|SXF_SETMASTER)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,1)
        goto PickupFinish
        TNT1 A 0 A_SpawnItemEx("HLRPGRocketBase2",14,8,36,cos(-pitch)*12,0,sin(-pitch)*12,-15,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH|SXF_SETMASTER)
        TNT1 A 0 A_SpawnItemEx("HLRPGRocketBase2",14,8,36,cos(-pitch)*12,0,sin(-pitch)*12,15,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH|SXF_SETMASTER)
        goto PickupFinish

      PickupAttackDumbfireCoop:
        TNT1 A 0 A_SpawnItemEx("HLRPGRocketBaseCoop2",14,8,36,cos(-pitch)*12,0,sin(-pitch)*12,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH|SXF_SETMASTER)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,1)
        goto PickupFinish
        TNT1 A 0 A_SpawnItemEx("HLRPGRocketBaseCoop2",14,8,36,cos(-pitch)*12,0,sin(-pitch)*12,-15,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH|SXF_SETMASTER)
        TNT1 A 0 A_SpawnItemEx("HLRPGRocketBaseCoop2",14,8,36,cos(-pitch)*12,0,sin(-pitch)*12,15,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH|SXF_SETMASTER)
        goto PickupFinish

      // Guided (laser on)
      PickupAttackGuided:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackGuidedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackGuidedCoop")
      PickupAttackGuidedDM:
        TNT1 A 0 A_SpawnItemEx("HLRPGRocketBase",14,8,36,cos(-pitch)*12,0,sin(-pitch)*12,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH|SXF_SETMASTER)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,1)
        goto PickupFinish
        TNT1 A 0 A_SpawnItemEx("HLRPGRocketBase",14,8,36,cos(-pitch)*12,0,sin(-pitch)*12,-15,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH|SXF_SETMASTER)
        TNT1 A 0 A_SpawnItemEx("HLRPGRocketBase",14,8,36,cos(-pitch)*12,0,sin(-pitch)*12,15,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH|SXF_SETMASTER)
        goto PickupFinish

      PickupAttackGuidedCoop:
        TNT1 A 0 A_SpawnItemEx("HLRPGRocketBaseCoop",14,8,36,cos(-pitch)*12,0,sin(-pitch)*12,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH|SXF_SETMASTER)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,1)
        goto PickupFinish
        TNT1 A 0 A_SpawnItemEx("HLRPGRocketBaseCoop",14,8,36,cos(-pitch)*12,0,sin(-pitch)*12,-15,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH|SXF_SETMASTER)
        TNT1 A 0 A_SpawnItemEx("HLRPGRocketBaseCoop",14,8,36,cos(-pitch)*12,0,sin(-pitch)*12,15,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH|SXF_SETMASTER)
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 A_GiveInventory("SamsaraHalfLifeRPGNotLoaded")
        TNT1 A 0 ACS_NamedExecuteAlways("HalfLifeRecoil", 0, 5)
        stop
    }
}

actor HLRPGCounter : Ammo
{
    Inventory.MaxAmount 1
    Tag "RPG Magazine"
    +IGNORESKILL
}

Actor HLRPGRocketBase
{
	PROJECTILE
	+EXTREMEDEATH
	+FORCERADIUSDMG
	-NOGRAVITY
	Speed 12
	Radius 7
	Height 8
	Damagetype "HLRPGExplosive"
	Damage (50)
	scale 0.9
	States
	{
	Spawn:
		TNT1 A 0
		HP50 A 12 bright ThrustThingZ(0,27,0,1)
		TNT1 A 0 A_SpawnItemEx("HLRPGRocket",0,0,4,cos(-pitch)*11,0,sin(-pitch)*11,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH|SXF_SETMASTER)
		stop
	Death:
		TNT1 A 0 A_StopSound
		TNT1 A 0 A_Explode(250,168)
		TNT1 A 5 A_SpawnItemEx("HLMegaExplosionBoom",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
		stop
	}
}

Actor HLRPGRocketBase2 : HLRPGRocketBase
{
	States
	{
	Spawn:
		TNT1 A 0
		HP50 A 12 bright ThrustThingZ(0,27,0,1)
		TNT1 A 0 A_SpawnItemEx("HLRPGRocket2",0,0,4,cos(-pitch)*11,0,sin(-pitch)*11,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)
		stop
	}
}

Actor HLRPGRocketBaseCoop : HLRPGRocketBase
{
	Species "Player"
	+THRUSPECIES
	+DONTHARMCLASS
	+DONTHARMSPECIES
	States
	{
	Spawn:
		TNT1 A 0
		HP50 A 12 bright ThrustThingZ(0,27,0,1)
		TNT1 A 0 A_SpawnItemEx("HLRPGRocketCoop",0,0,4,cos(-pitch)*11,0,sin(-pitch)*11,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH|SXF_SETMASTER)
		Stop
	}
}

Actor HLRPGRocketBaseCoop2 : HLRPGRocketBase2
{
	Species "Player"
	+THRUSPECIES
	+DONTHARMCLASS
	+DONTHARMSPECIES
	States
	{
	Spawn:
		TNT1 A 0
		HP50 A 12 bright ThrustThingZ(0,27,0,1)
		TNT1 A 0 A_SpawnItemEx("HLRPGRocketCoop2",0,0,4,cos(-pitch)*11,0,sin(-pitch)*11,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)
		Stop
	}
}

Actor HLRPGStartCounter : HLCounter {}
Actor HLRPGRocket
{
	PROJECTILE
	+EXTREMEDEATH
	+FORCERADIUSDMG
	Speed 11 //55
	Radius 7
	Height 8
	Damagetype "HLRPGExplosive"
	Damage (0)
	Scale 0.9
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_PlaySound("halflife/rpg_rockstart",CHAN_BODY)
		TNT1 A 0 ACS_NamedExecuteAlways("rocketFollow",0,0,0,0)
		goto SpawnStart
	SpawnStart:
		TNT1 A 0 A_JumpIfInventory("HLRPGStartCounter",18,"SpawnLoop")
		TNT1 A 0 A_ScaleVelocity(1.093) //11 * x^18 = 55 ----> x = 1.093
		TNT1 A 0 A_GiveInventory("HLRPGStartCounter",1)
		TNT1 A 0 A_SpawnItemEx("HLRPGFlare",0,0,0,0,0,0,0,SXF_CLIENTSIDE|SXF_TRANSFERPITCH)
		HP50 A 1 bright A_SpawnItemEx("HLRPGSmoke",0,0,0,0,0,0,0,SXF_CLIENTSIDE|SXF_TRANSFERPITCH)
		TNT1 A 0 A_SpawnItemEx("HLRPGFlare",0,0,0,0,0,0,0,SXF_CLIENTSIDE|SXF_TRANSFERPITCH)
		HP50 A 1 bright A_SpawnItemEx("HLRPGSmoke",0,0,0,0,0,0,0,SXF_CLIENTSIDE|SXF_TRANSFERPITCH)
		loop
	SpawnLoop:
		TNT1 A 0 A_PlaySound("halflife/rpg_rockloop",CHAN_BODY,1.0,1) //A_LoopActiveSound
		TNT1 A 0 A_SpawnItemEx("HLRPGFlare",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
		HP50 A 1 bright A_SpawnItemEx("HLRPGSmoke",0,0,0,0,0,0,0,SXF_CLIENTSIDE|SXF_TRANSFERPITCH)
		goto SpawnLoop+1
	Death:
		TNT1 A 0 A_PlaySound("halflife/mp5_grenadehit",CHAN_BODY,1.0,0) //A_StopSound
		TNT1 A 0 A_Explode(300,168)
		TNT1 A 5 A_SpawnItemEx("HLMegaExplosionBoom",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
		TNT1 A 0 ACS_NamedExecuteAlways("rocketCleanup",0,0,0,0)
		stop
	}
}

Actor HLRPGRocket2 : HLRPGRocket
{
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_PlaySound("halflife/rpg_rockstart",CHAN_BODY)
		goto SpawnStart
	SpawnStart:
		TNT1 A 0 A_JumpIfInventory("HLRPGStartCounter",18,"SpawnLoop")
		TNT1 A 0 A_ScaleVelocity(1.093) //11 * x^18 = 55 ----> x = 1.093
		TNT1 A 0 A_GiveInventory("HLRPGStartCounter",1)
		TNT1 A 0 A_SpawnItemEx("HLRPGFlare",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
		HP50 A 1 bright A_SpawnItemEx("HLRPGSmoke",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
		TNT1 A 0 A_SpawnItemEx("HLRPGFlare",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
		HP50 A 1 bright A_SpawnItemEx("HLRPGSmoke",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
		loop 
	SpawnLoop:
		TNT1 A 0 A_PlaySound("halflife/rpg_rockloop",CHAN_BODY,1.0,1) //A_LoopActiveSound
		TNT1 A 0 A_SpawnItemEx("HLRPGFlare",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
		HP50 A 1 bright A_SpawnItemEx("HLRPGSmoke",0,0,0,0,0,0,0,SXF_CLIENTSIDE|SXF_TRANSFERPITCH)
		goto SpawnLoop+1
	Death:
		TNT1 A 0 A_PlaySound("halflife/mp5_grenadehit",CHAN_BODY,1.0,0) //A_StopSound
		TNT1 A 0 A_Explode(300,168)
		TNT1 A 5 A_SpawnItemEx("HLMegaExplosionBoom",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
		stop
	}
}

Actor HLRPGRocketCoop : HLRPGRocket { Species "Player" +THRUSPECIES  }
Actor HLRPGRocketCoop2 : HLRPGRocket2 { Species "Player" +THRUSPECIES  }

Actor HLRPGSmoke
{
	+CLIENTSIDEONLY
	+NOINTERACTION
	+NOBLOCKMAP
	-SOLID
	+NOGRAVITY
	renderstyle add
	scale 0.2
	alpha 0.5
	states
	{
	Spawn:
		HWX2 ABCDEFGHIJKLM 6 A_Fadeout(0.1)
		stop
	}
}

Actor HLRPGFlare
{
	+CLIENTSIDEONLY
	+NOINTERACTION
	+NOBLOCKMAP
	-SOLID
	+NOGRAVITY
	renderstyle add
	scale 0.65
	states
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_Jump(256,"Flare1","Flare2","Flare3","Flare4","Flare5","Flare6","Flare7")
		goto Flare1
	Flare1:
		HWX6 A 1 bright
		stop
	Flare2:
		HWX6 B 1 bright
		stop
	Flare3:
		HWX6 C 1 bright
		stop
	Flare4:
		HWX6 D 1 bright
		stop
	Flare5:
		HWX6 E 1 bright
		stop
	Flare6:
		HWX6 F 1 bright
		stop
	Flare7:
		HWX6 G 1 bright
		stop
	}
}

Actor RPGLaserDot
{
	RenderStyle Add
    +NOGRAVITY
    +DONTSPLASH
    +PUFFONACTORS
	+BLOODLESSIMPACT
	+ALWAYSPUFF
	+CLIENTSIDEONLY
	+FIXMAPTHINGPOS
	+FORCEXYBILLBOARD
	+NOTIMEFREEZE
	Scale 0.5
    Radius 1
    Height 1
	states
	{
	Spawn:
		TNT1 A 0
		HW59 H 1 ACS_NamedExecute("getLaserCoords",0,x,y,z)
		stop
	}
}

Actor RPGLaserDotCoop : RPGLaserDot { Species "Player" +THRUSPECIES +MTHRUSPECIES }

actor SamsaraHalfLifeSporeLauncherPrimaryAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"PickupReloadable")
      PickupNonReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,1)
        stop
        TNT1 A 0 A_TakeInventory("RocketAmmo",1)
        goto PickupAttack

      PickupReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("HLSporeCounter",1,1)
        stop
        TNT1 A 0 A_TakeInventory("HLSporeCounter",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("HLSporeLauncher/Fire",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("HLSporeLauncherSporeMissile",0,false,8)
        goto PickupFinish

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("HLSporeLauncherSporeMissileCoop",0,false,8)
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 ACS_NamedExecuteAlways("HalfLifeRecoil", 0, 2)
        stop
    }
}

actor SamsaraHalfLifeSporeLauncherSecondaryAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"PickupReloadable")
      PickupNonReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,1)
        stop
        TNT1 A 0 A_TakeInventory("RocketAmmo",1)
        goto PickupAttack

      PickupReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("HLSporeCounter",1,1)
        stop
        TNT1 A 0 A_TakeInventory("HLSporeCounter",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("HLSporeLauncher/AltFire",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("HLSporeLauncherSporeGrenade",0,false,8)
        goto PickupFinish

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("HLSporeLauncherSporeGrenadeCoop",0,false,8)
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 ACS_NamedExecuteAlways("HalfLifeRecoil", 0, 2)
        stop
    }
}

actor HLSporeCounter : Ammo
{
    Inventory.MaxAmount 10
    Tag "Spore Launcher Magazine"
    +IGNORESKILL
}

Actor HLSporeLauncherSporeMissile
{
	Renderstyle add
	Alpha 0.85
	Radius 8
    Height 16
    Speed 20
    Damage (75)
    PROJECTILE
	+NOGRAVITY
    +EXTREMEDEATH
	Scale 0.9
	DamageType "HLRPGExplosive"
	PoisonDamage 1, 3, 1
	Decal "SporeSplat"
	States
	{
		Spawn:
			TNT1 A 0
		Spawn2:
			TNT1 A 0 A_SpawnItemEx("HLSporeFlare",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
			TNT1 A 0 A_SpawnItemEx("HLSporeLauncherSporeGrenadeTrail",0,0,0,0,FRandom(-2,2),0,0,SXF_NOCHECKPOSITION)
			HL00 A 1 Bright
			Loop
		Death:
			TNT1 A 0 A_PlaySound("HLSporeLauncher/Impact", CHAN_BODY, 1.0)
			TNT1 A 0 A_Explode(150,104)
			TNT1 A 5 A_SpawnItemEx("HLSporeExplode",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
			Stop
	}
}

Actor HLSporeLauncherSporeGrenade
{
	Renderstyle add
	Alpha 0.85
	Radius 8
    Height 16
    Speed 20
    Damage (75)
    PROJECTILE
	-NOGRAVITY
    +EXTREMEDEATH
	+BOUNCEONWALLS
	+BOUNCEONFLOORS
	+BOUNCEONCEILINGS
	BounceFactor 1.0
	WallBounceFactor 1.0
	Scale 0.9
	BounceCount 6
	BounceSound "HLSporeLauncher/Bounce"
	WallBounceSound "HLSporeLauncher/Bounce"	
	DamageType "HLRPGExplosive"
	PoisonDamage 1, 3, 1
	Gravity 0.234
	Decal "SporeSplat"
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 ThrustThingZ(0,Random((30-Random(5,0)),15)-Random((Random(10,5)),0),0,1)
		Spawn2:
			TNT1 A 0 A_SpawnItemEx("HLSporeFlare",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
			TNT1 A 0 A_SpawnItemEx("HLSporeLauncherSporeGrenadeTrail",0,0,0,0,FRandom(-2,2),0,0,SXF_NOCHECKPOSITION)
			HL00 A 1 Bright
			Loop
		Death:
			TNT1 A 0 A_PlaySound("HLSporeLauncher/Impact", CHAN_BODY, 1.0)
			TNT1 A 0 A_Explode(150,104)
			TNT1 A 5 A_SpawnItemEx("HLSporeExplode",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
			Stop
	}
}

Actor HLSporeLauncherSporeMissileCoop : HLSporeLauncherSporeMissile { Species "Player" +MTHRUSPECIES +THRUSPECIES }
Actor HLSporeLauncherSporeGrenadeCoop : HLSporeLauncherSporeGrenade { Species "Player" +MTHRUSPECIES +THRUSPECIES }

Actor HLSporeLauncherSporeGrenadeTrail
{
	Renderstyle shaded
	Alpha 0.75
	Gravity 0.25
	-NOGRAVITY
	+CLIENTSIDEONLY
	-NOINTERACTION 
	+BRIGHT
	StencilColor "C8DE47"
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_CheckFloor("Death")
			TNT1 A 0 A_Jump(256,"Trail1", "Trail2", "Trail3", "Trail4")
		Trail1:
			HL16 A 1 A_Fadeout(0.033)
			Goto Spawn
		Trail2:
			HL16 B 1 A_Fadeout(0.033)
			Goto Spawn
		Trail3:
			HL16 C 1 A_Fadeout(0.033)
			Goto Spawn
		Trail4:
			HL16 D 1 A_Fadeout(0.033)
			Goto Spawn
		Death:
			TNT1 A 5
			Stop
	}
}

Actor HLSporeExplode
{
	+EXTREMEDEATH
	+NOGRAVITY
	+CLIENTSIDEONLY
	+NOINTERACTION
	Renderstyle Add
	Scale 1.5
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_Jump(128,"Spawn2")
			HL17 ABCDEFGHIJK 2 Bright
			Stop
		Spawn2:
			HL15 ABCDEFGHIJKL 2 Bright
			Stop
	}
}

Actor HLSporeFlare
{
	Renderstyle shaded
	Alpha 0.75
	scale 0.66
	+NOGRAVITY
	+CLIENTSIDEONLY
	+NOINTERACTION 
	+BRIGHT
	StencilColor "C8DE47"
	States
	{
	Spawn:
		TNT1 A 0
		HPI0 F 2 bright
		stop
	}
}
