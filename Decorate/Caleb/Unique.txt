actor "Guns Akimbo" : CustomInventory
{
	Inventory.PickupMessage "Guns Akimbo!"
	Inventory.pickupsound "misc/p_pkup"
	+COUNTITEM
	+Inventory.AUTOACTIVATE
	+Inventory.ALWAYSPICKUP
	+Inventory.FANCYPICKUPSOUND
	Inventory.RestrictedTo "Caleb"
	States
	{
	Spawn:
		PSTR B -1
		Stop
	Pickup:
		TNT1 A 0 ACS_NamedExecuteWithResult("CalebAkimboCooldown")
		TNT1 A 0 A_GiveInventory("GunsAkimboP")
		Stop
	}
}

actor GunsAkimboP : Boolean { Inventory.MaxAmount 1 Inventory.InterHubAmount 1 }

actor AkimboStay : Boolean { }

actor BloodGunsAkimboCooldown : Inventory { Inventory.MaxAmount 120 }

actor VoodooDoll : CalebWeapon
{
	Obituary "%k \cutortured\c- %o."
	Inventory.PickupMessage "Voodoo Doll! Find some victims!"
	Inventory.PickupSound "Caleb/Weaponup2"
	Weapon.SelectionOrder 500
	Weapon.Kickback 0
	Weapon.AmmoType "Voodoo"
	Weapon.AmmoUse 0
	Weapon.AmmoUse2 0
	Weapon.AmmoGive 100
	Weapon.AmmoType2 "Voodoo"
	Weapon.SlotNumber 8
	Weapon.SlotPriority 8
	Tag "Voodoo Doll"
	+FRIGHTENED
	+WEAPON.NOALERT
	States
	{
	Ready:
		TNT1 A 0 A_TakeInventory("UsingPitchfork", 1)
		TNT1 A 0 A_JumpIfInventory("CalebReady",1,"IntroTaunt")
		TNT1 A 0 A_JumpIfInventory("CalebWaiting",60,"WaitingTaunt")
		TNT1 A 0 A_JumpIf(momx >= 6,"ReadyMoving")
		TNT1 A 0 A_JumpIf(momx <= -6,"ReadyMoving")
		VOO3 A 1 A_WeaponReady
		Goto Ready+1
	ReadyMoving:
		VOO3 AAARRARRAAARRRRAAA 7 A_WeaponReady
		Goto Ready+1
	Deselect:
		VOO3 A 0 A_JumpIfInventory("Voodoo", 1, "DeselectLoop")
		VOO3 A 0 A_TakeInventory("VoodooDoll", 1)
		Goto Super::DeselectLoop
	Select:
		VOO3 A 0
		Goto Super::SelectLoop
	Fire:
		VOO3 O 0 A_JumpIfTargetInLOS(1)
		Goto Self
		VOO3 O 0 A_Jump(96,"Chest","Shoulder")
		VOO3 O 0 A_Jump(256,"Chest","Shoulder","Eye","Pelvis")
		Goto Chest
	Self:
		VOO3 BC 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		VDOE D 0 A_PlayWeaponSound("Vodoo/Stab")
		VDOE D 0 Thing_Damage(0,1,18)
		VDOE DEEFFGGHH 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		VOO3 CBA 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		VOO3 A 0 A_Refire
		Goto Ready
	Chest:
		VOO3 BC 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		VOO3 D 0 A_PlayWeaponSound("Vodoo/Stab")
		VOO3 D 0 A_JumpIfInventory("CoopModeOn", 1, 2)
		VOO3 D 0 A_FireBullets(1, 0, 1, 25, "VoodooPuff", FBF_NORANDOM|FBF_USEAMMO)
		Goto ChestEnd
		VOO3 D 0 A_FireBullets(1, 0, 1, 25, "VoodooPuffCoop", FBF_NORANDOM|FBF_USEAMMO)
		Goto ChestEnd
	ChestEnd:
		VOO3 D 0 A_TakeInventory("Voodoo", 1, TIF_NOTAKEINFINITE)
		VOO3 D 6 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		VOO3 CBA 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		Goto CheckVoodooAmmo
	Shoulder:
		VOO3 BC 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		VOO3 E 0 A_PlayWeaponSound("Vodoo/Stab")
		VOO3 E 0 A_JumpIfInventory("CoopModeOn", 1, 2)
		VOO3 E 0 A_FireBullets(1, 0, 1, 25, "VoodooPuff", FBF_NORANDOM|FBF_USEAMMO)
		Goto ShoulderEnd
		VOO3 E 0 A_FireBullets(1, 0, 1, 25, "VoodooPuffCoop", FBF_NORANDOM|FBF_USEAMMO)
		Goto ShoulderEnd
	ShoulderEnd:
		VOO3 E 0 A_TakeInventory("Voodoo", 1, TIF_NOTAKEINFINITE)
		VOO3 E 6 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		VOO3 CBA 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		Goto CheckVoodooAmmo
	Pelvis:
		VOO3 MN 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		VOO3 M 0 A_PlayWeaponSound("Vodoo/Stab")
		VOO3 M 0 A_PlaySound("Vodoo/Doll")
		VOO3 M 0 A_JumpIfInventory("CoopModeOn", 1, 2)
		VOO3 M 0 A_FireBullets(1, 0, 1, 25, "VoodooPuff", FBF_NORANDOM|FBF_USEAMMO)
		Goto PelvisEnd
		VOO3 M 0 A_FireBullets(1, 0, 1, 25, "VoodooPuffCoop", FBF_NORANDOM|FBF_USEAMMO)
		Goto PelvisEnd
	PelvisEnd:
		VOO3 P 0 A_TakeInventory("Voodoo", 3, TIF_NOTAKEINFINITE)
		VOO3 PQPQPQP 4 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		VOO3 NMA 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		Goto CheckVoodooAmmo
	Eye:
		VOO3 FG 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		VOO3 I 0 A_PlayWeaponSound("Vodoo/Stab")
		VOO3 I 0 A_JumpIfInventory("CoopModeOn", 1, 2)
		VOO3 I 0 A_FireBullets(1, 0, 1, 25, "VoodooPuff", FBF_NORANDOM|FBF_USEAMMO)
		Goto EyeEnd
		VOO3 I 0 A_FireBullets(1, 0, 1, 25, "VoodooPuffCoop", FBF_NORANDOM|FBF_USEAMMO)
		Goto EyeEnd
	EyeEnd:
		VOO3 I 0 A_TakeInventory("Voodoo", 1, TIF_NOTAKEINFINITE)
		VOO3 I 3 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		VOO3 JKLKJ 4 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		VOO3 GFA 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		Goto CheckVoodooAmmo
	CheckVoodooAmmo:
		TNT1 A 0 A_JumpIfInventory("Voodoo", 1, 1)
		Goto Drop
		TNT1 A 0
		Goto Ready
	AltFire:
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound ("Vodoo/Altfire",CHAN_WEAPON)
		VOO3 C 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		VDOL ABCDEF 4 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_JumpIfInventory("CoopModeOn", 1, 2)
		TNT1 A 0 A_FireCustomMissile("VoodooPuff2")
		Goto AltFireEnd
		TNT1 A 0 A_FireCustomMissile("VoodooPuff2Coop")
		Goto AltFireEnd
	AltFireEnd:
		VDOL G 19 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		Goto Drop
	Flash:
		VDOL J 0
		Stop
	Spawn:
		VDLF A -1
		Stop
	Drop:
		TNT1 A 0 A_TakeInventory("VoodooDoll", 1)
		Goto Ready
	}
}

actor VoodooPuff
{
	RenderStyle None
	+INVISIBLE
	+NOBLOCKMAP
	+DONTSPLASH
	+NOGRAVITY
	+ALWAYSPUFF
	+FOILINVUL
	+FORCEPAIN
	DamageType "Vodoo"
	States
	{
	Spawn:
		VDLF A 5
		Stop
	}
}

Actor VoodooPuffCoop : VoodooPuff { +MTHRUSPECIES +THRUSPECIES Species "Player" }

actor VoodooPuff2 : BulletPuff // Uses tid of it as ammo, in the script the tid becomes half of the ammo amount.
{
	+NOBLOCKMAP
	+DONTSPLASH
	+NOGRAVITY
	+ALWAYSPUFF
	+FOILINVUL
	+FORCERADIUSDMG
	+PUFFGETSOWNER
	Radius 255
	Height 255
	speed 1000
	damage 10000
	PROJECTILE
	DamageType "Vodoo"
	States
	{
	Spawn:
		TNT1 A 1
		Goto Death
	Death:
		TNT1 A 1 ACS_NamedExecuteAlways("CalebUnique", 0)
		TNT1 A 0 A_BFGSpray("VoodooPuff",tid,15)
		Stop
	}
}

actor VoodooPuff2Coop : VoodooPuff2 { +MTHRUSPECIES +THRUSPECIES Species "Player" }

actor Voodoo : Ammo
{
    Inventory.MaxAmount 100
    Tag "Voodoo Doll Ammo"
    +IGNORESKILL
}

Actor CalebSprayReadyDummy : Boolean {}
Actor SprayCan2 : Boolean {}
actor Spraycan : CalebWeapon
{
	Obituary "%o was toasted by %k's Aerosol Can!"
	Radius 20
	Height 16
	Inventory.PickupMessage "You got the Spraycan!"
	Inventory.PickupSound "Caleb/Weaponup"
	Weapon.SelectionOrder 540
	Weapon.Kickback 0
	Weapon.AmmoType1 "SprayCanAmmo"
	Weapon.AmmoUse1 1
	Weapon.AmmoGive1 48
	Weapon.AmmoType2 "SprayCanAmmo"
	Weapon.SlotNumber 9
	Weapon.AmmoUse2 1
	Tag "Aerosol Can"
	+WEAPON.NOALERT
	+NOAUTOAIM
	States
	{
	Ready:
		TNT1 A 0 A_TakeInventory("ThrowPowerBig")
		TNT1 A 0 A_TakeInventory("ThrowPower")
		TNT1 A 0 A_TakeInventory("UsingPitchfork", 1)
		TNT1 A 0 A_JumpIfInventory("CalebSprayReadyDummy",1,"ReadyLoop")
		DYHN O 1 A_WeaponReady(WRF_NOFIRE)
		TNT1 A 0 A_PlayWeaponSound("TNT/Zip")
		DYHN P 1 A_WeaponReady(WRF_NOFIRE)
		TNT1 A 0 A_PlaySound("TNT/Match",6)
		DYHN QRS 1 A_WeaponReady(WRF_NOFIRE)
		TNT1 A 0 A_TakeInventory("CalebTNTDeselectDummy",1)
		TNT1 A 0 A_GiveInventory("CalebSprayReadyDummy",1)
		SPRC HHIIJJ 1 A_WeaponReady(WRF_NOFIRE)
		Goto ReadyLoop+8
	ReadyLoop:
		TNT1 A 0 A_TakeInventory("ThrowPowerBig")
		TNT1 A 0 A_TakeInventory("ThrowPower")
		TNT1 A 0 A_JumpIfInventory("CalebReady",1,"IntroTaunt")
		TNT1 A 0 A_JumpIfInventory("CalebWaiting",60,"WaitingTaunt")
		TNT1 A 0 A_StopSound(CHAN_WEAPON)
		TNT1 A 0 A_JumpIfInventory("SamsaraCurrentWaterLevel",0,"Cannot")
		SPRF B 3 A_WeaponReady
		TNT1 A 0 A_JumpIfInventory("SamsaraCurrentWaterLevel",0,"Cannot")
		SPRF C 3 A_WeaponReady
		TNT1 A 0 A_JumpIfInventory("SamsaraCurrentWaterLevel",0,"Cannot")
		SPRF D 3 A_WeaponReady
		Loop
	ReadyFire:
		TNT1 A 0 A_GiveInventory("ThrowPowerBig", 3)
		SPAF GAB 2 bright A_WeaponReady(WRF_NOSWITCH)
		TNT1 A 0 A_JumpIfInventory("TNTSoundDelay", 1, 3)
		TNT1 A 0 A_GiveInventory("TNTSoundDelay", 1)
		TNT1 A 0 A_PlayWeaponSound("TNT/FUSE")
		TNT1 A 0 A_GiveInventory("ThrowPowerBig", 3)
		SPAF CD 2 bright A_WeaponReady(WRF_NOSWITCH)
		TNT1 A 0 A_GiveInventory("ThrowPowerBig", 3)
		SPAF EF 2 bright A_WeaponReady(WRF_NOSWITCH)
		TNT1 A 0 A_JumpIfInventory("ThrowPowerBig", 0, "Explode")
		Loop
	Deselect:
		TNT1 A 0 A_TakeInventory("CalebSprayReadyDummy",1)
		TNT1 A 0 A_JumpIfInventory("WhoopsIDied",1,"DeselectDead")
		TNT1 A 0 A_JumpIfInventory("CalebTNTDeselectDummy",1,"DeselectAmmoLess")
		TNT1 A 0 //A_JumpIfnoammo("DeselectAmmoLess")
		SPRC DCB 1
		SPRC A 0 A_PlayWeaponSound("TNT/Zip")
		Goto Super::DeselectLoop
	DeselectDead:
		TNT1 A 0 A_TakeInventory("WhoopsIDied",999)
		TNTH S 4 A_TakeInventory("ThrowPower",255)
		TNTH T 3 A_Lower
		TNTH U 1 A_Lower
		Goto DeselectDead+3
	DeselectAmmoLess:
		DYHN TPO 1
		DYHN N 0 A_PlayWeaponSound("TNT/Zip")
		Goto Super::DeselectLoop
	Select:
		DYHN N 0 A_GiveInventory("CalebTNTDeselectDummy",1)
		DYHN N 0 A_TakeInventory("CalebSprayReadyDummy",1)
		DYHN N 0 A_TakeInventory("ThrowPower",300)
		Goto Super::SelectLoop
	Cannot:
		TNT1 A 0 A_TakeInventory("CalebSprayReadyDummy",1)
		TNT1 A 0 A_TakeInventory("ThrowPowerBig")
		TNT1 A 0 A_TakeInventory("ThrowPower")
		TNT1 A 0 A_SelectWeapon("Pitchfork")
		Goto Deselect
	Fire:
		TNT1 A 0 A_JumpIfNoAmmo("TakeAway")
		TNT1 A 0 A_JumpIfInventory("SamsaraCurrentWaterLevel",0,"Cannot")
		TNT1 A 0 A_JumpIfInventory("SprayCan2", 1, "AltHold")
		TNT1 A 0 A_PlaySound("Napalm/Act2", CHAN_WEAPON, 1.0, true)
		Goto Hold+3
	Hold:
		TNT1 A 0 A_JumpIfNoAmmo("TakeAway")
		TNT1 A 0 A_JumpIfInventory("SamsaraCurrentWaterLevel",0,"Cannot")
		TNT1 A 0 A_JumpIfInventory("SprayCan2", 1, "AltHold")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,2)
		TNT1 A 0 A_FireCustomMissile("SprayCanFire",frandom(-1.0,1.0),1,frandom(-1.0,1.0),4+frandom(-1.0,1.0))
		Goto FireEnd
		TNT1 A 0 A_FireCustomMissile("SprayCanFireCoop",frandom(-1.0,1.0),1,frandom(-1.0,1.0),4+frandom(-1.0,1.0))
		Goto FireEnd
	FireEnd:
		SPRF A 3 BRIGHT A_WeaponReady(WRF_NOBOB|WRF_NOSWITCH|WRF_NOFIRE)
		TNT1 A 0 A_ReFire
		TNT1 A 0 A_StopSound(CHAN_WEAPON)
		Goto ReadyLoop
	TakeAway:
		TNT1 A 0 A_TakeInventory("CalebSprayReadyDummy",1)
		TNT1 A 1 A_SelectWeapon("Pitchfork")
		Goto Deselect
	AltFire:
		SPLG J 0 A_JumpIfInventory("SprayCanAmmo", 48, 1)
		Goto Cannot
		SPLG J 0 A_JumpIfInventory("SprayCan2", 1, "DropCan")
		SPLG JA 2 bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
		DYNL A 0 A_PlayWeaponSound("Spray/Can")
		SPLG BCDEFGHI 2 bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
		// TNT1 A 0 A_Refire
		TNT1 A 0 A_GiveInventory("SprayCan2", 1)
		TNT1 A 0 A_JumpIfInventory("ThrowPowerBig", 0, "Explode")
		TNT1 A 0 A_StopSound(CHAN_WEAPON)
		Goto ReadyFire
	AltHold:
		TNT1 A 0 A_Refire(1)
		Goto AltHold+2
		TNT1 A 0 A_GiveInventory("ThrowPower", 5)
		TNT1 A 0 A_GiveInventory("ThrowPowerBig", 3)
		TNT1 A 0 A_GiveInventory("TNTCounter", 1)
		TNT1 A 0 A_JumpIfInventory("TNTSoundDelay", 1, 3)
		TNT1 A 0 A_GiveInventory("TNTSoundDelay", 1)
		TNT1 A 0 A_PlayWeaponSound("TNT/FUSE")
		TNT1 A 0 A_JumpIfInventory("ThrowPowerBig", 0, "Explode")
		TNT1 A 0 A_JumpIfInventory("TNTCounter", 2, "Hold3")
		TNT1 A 0 A_JumpIfInventory("TNTCounter", 1, "Hold2")
		Goto Hold1
	Hold1:
		SPAF GAB 2 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_JumpIfInventory("SamsaraCurrentWaterLevel",0,"Cannot")
		TNT1 A 0 A_Refire("AltHold")
		TNT1 A 0 A_StopSound(CHAN_WEAPON)
		Goto Throw
	Hold2:
		SPAF CD 2 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_JumpIfInventory("SamsaraCurrentWaterLevel",0,"Cannot")
		TNT1 A 0 A_Refire("AltHold")
		TNT1 A 0 A_StopSound(CHAN_WEAPON)
		Goto Throw
	Hold3:
		SPAF EF 2 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_JumpIfInventory("SamsaraCurrentWaterLevel",0,"Cannot")
		TNT1 A 0 A_TakeInventory("TNTCounter")
		TNT1 A 0 A_Refire("AltHold")
		TNT1 A 0 A_PlayWeaponSound("Spray/Can")
		TNT1 A 0 A_StopSound(CHAN_WEAPON)
		Goto Throw
	DropCan:
		TNT1 A 0 A_TakeInventory("ThrowPower")
		TNT1 A 0 A_TakeInventory("TNTCounter")
		TNT1 A 0 A_TakeInventory("SprayCan2")
		TNT1 A 0 A_GiveInventory("SamsaraBloodSprayCanAltAttackHandler")
		TNT1 A 0 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH|WRF_NOBOB) SPLG I 1 Bright A_Lower SPLG III 0 A_Lower
		TNT1 A 0 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH|WRF_NOBOB) SPLG I 1 Bright A_Lower SPLG III 0 A_Lower
		TNT1 A 0 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH|WRF_NOBOB) SPLG I 1 Bright A_Lower SPLG III 0 A_Lower
		TNT1 A 0 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH|WRF_NOBOB) SPLG I 1 Bright A_Lower SPLG III 0 A_Lower
		TNT1 A 0 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH|WRF_NOBOB) SPLG I 1 Bright A_Lower SPLG III 0 A_Lower
		TNT1 A 0 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH|WRF_NOBOB) SPLG I 1 Bright A_Lower SPLG III 0 A_Lower
		Goto ReadyLoop
	Explode:
		TNT1 A 0 A_StopSound(CHAN_WEAPON)
		TNT1 A 0 A_TakeInventory("ThrowPowerBig")
		TNT1 A 0 A_TakeInventory("ThrowPower")
		TNT1 A 0 A_TakeInventory("TNTCounter")
		TNT1 A 0 A_GiveInventory("WhoopsIDied",1)
		TNT1 A 0 A_PlaySound("Napalm/Explode",CHAN_ITEM)
		TNT1 A 0 A_SpawnItem("FlareKaboom5")
		TNT1 A 0 ACS_NamedExecuteWithResult("CalebSelfDamage",1)
		TNT1 AAAAA 0 A_SpawnItemEx("Blower",-16)
		TNT1 AAA 1 A_SpawnItemEx("Blower",-16)
		TNTH S 4
		TNTH T 8
		TNTH U 5
		Goto ReadyLoop
	Throw:
		ZIPU BDFG 1 Bright
		DYNH ABCD 1
		TNT1 A 0 A_GiveInventory("SamsaraBloodSprayCanAltAttackHandler")
		TNT1 A 0 A_StopSound(CHAN_WEAPON)
		TNT1 A 0 A_PlayWeaponSound("TNT/Throw")
		TNT1 A 0 A_TakeInventory("SprayCanAmmo", 48, TIF_NOTAKEINFINITE)
		TNT1 A 0 A_TakeInventory("SprayCan2", 5)
		DYHN EFGHIJ 2
		TNT1 A 0 A_JumpIfInventory("SprayCanAmmo", 1, 1)
		Goto TakeAway
		ZIPU LKI 2
		DYHN K 2
		SPRC HIJ 2
		Goto ReadyLoop
	Spawn:
		SPFI J 1
		Stop
	}
}

actor SprayCanAmmo : Ammo
{
    Inventory.MaxAmount 288
    Ammo.BackpackAmount 48
    Ammo.BackpackMaxAmount 576
    Inventory.Icon "AMSMT0"
    Tag "Aerosol Can Ammo"
    +IGNORESKILL
}

Actor SamsaraBloodSprayCanAltAttackHandler : Trigger
{
	States
	{
		Pickup:
			TNT1 A 0
			TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,2)
			TNT1 A 0 A_FireCustomMissile("SprayCanAlt",0,0)
			Stop
			TNT1 A 0 A_FireCustomMissile("SprayCanAlt_Coop",0,0)
			Stop
	}
}

ACTOR SprayCanAlt
{
	Decal Scorch
	Radius 3
	Height 4
	Scale 0.5
	Speed 5
	Damage (0)
	PROJECTILE
	-NOGRAVITY
	+DONTSPLASH
	+THRUGHOST
	+NODAMAGETHRUST
	+BRIGHT
	damagetype "CalebFire"
	var int user_strength;
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_PlaySound("TNT/FUSE", CHAN_BODY, 1.0, true)
		TNT1 A 0 A_SetUserVar("user_strength",CallACS("CalebThrowStrength"))
		TNT1 A 0 ACS_NamedExecuteWithResult("CalebThrowDuration")
		TNT1 A 0 A_ChangeVelocity((5+(user_strength))*(user_strength>0)*(sqrt(velx*velx + vely*vely)/5.0),0,(((5+(user_strength/2))*(sqrt(velx*velx + vely*vely)/5.0))+((5+(user_strength))*(velz/5.0)))*(user_strength>0),CVF_RELATIVE|CVF_REPLACE)
		TNT1 A 0 A_SetUserVar("user_strength",sqrt(velx*velx + vely*vely + velz*velz))
		TNT1 A 0 ACS_NamedExecuteWithResult("CalebClearThrowableBar")
	SpawnLoop:
		STOS F 1 Light("BLOOD_NAPALM_ROCKET") A_CountDown TNT1 A 0 A_ScaleVelocity(0.9)
		STOS F 1 Light("BLOOD_NAPALM_ROCKET") A_CountDown TNT1 A 0 A_ScaleVelocity(0.9)
		STOS G 0 A_SpawnItemEx("TNTSmoke",0,-15,-5,0,0,1,0,SXF_CLIENTSIDE)
		STOS G 1 Light("BLOOD_NAPALM_ROCKET") A_CountDown TNT1 A 0 A_ScaleVelocity(0.9)
		STOS G 1 Light("BLOOD_NAPALM_ROCKET") A_CountDown TNT1 A 0 A_ScaleVelocity(0.9)
		STOS H 1 Light("BLOOD_NAPALM_ROCKET") A_CountDown TNT1 A 0 A_ScaleVelocity(0.9)
		STOS H 1 Light("BLOOD_NAPALM_ROCKET") A_CountDown TNT1 A 0 A_ScaleVelocity(0.9)
		STOS G 0 A_SpawnItemEx("TNTSmoke",0,-15,-5,0,0,1,0,SXF_CLIENTSIDE)
		STOS A 1 Light("BLOOD_NAPALM_ROCKET") A_CountDown TNT1 A 0 A_ScaleVelocity(0.9)
		STOS A 1 Light("BLOOD_NAPALM_ROCKET") A_CountDown TNT1 A 0 A_ScaleVelocity(0.9)
		STOS B 1 Light("BLOOD_NAPALM_ROCKET") A_CountDown TNT1 A 0 A_ScaleVelocity(0.9)
		STOS B 1 Light("BLOOD_NAPALM_ROCKET") A_CountDown TNT1 A 0 A_ScaleVelocity(0.9)
		STOS C 0 A_SpawnItemEx("TNTSmoke",0,-15,-5,0,0,1,0,SXF_CLIENTSIDE)
		STOS C 1 Light("BLOOD_NAPALM_ROCKET") A_CountDown TNT1 A 0 A_ScaleVelocity(0.9)
		STOS C 1 Light("BLOOD_NAPALM_ROCKET") A_CountDown TNT1 A 0 A_ScaleVelocity(0.9)
		STOS D 1 Light("BLOOD_NAPALM_ROCKET") A_CountDown TNT1 A 0 A_ScaleVelocity(0.9)
		STOS D 1 Light("BLOOD_NAPALM_ROCKET") A_CountDown TNT1 A 0 A_ScaleVelocity(0.9)
		STOS E 0 A_SpawnItemEx("TNTSmoke",0,-15,-5,0,0,1,0,SXF_CLIENTSIDE)
		STOS E 1 Light("BLOOD_NAPALM_ROCKET") A_CountDown TNT1 A 0 A_ScaleVelocity(0.9)
		STOS E 1 Light("BLOOD_NAPALM_ROCKET") A_CountDown TNT1 A 0 A_ScaleVelocity(0.9)
		Loop
	Stay:
		TNT1 A 0 A_PlaySound("TNT/FUSE", CHAN_BODY, 1.0, true)
		STOS I 0 A_SpawnItemEx("TNTSmoke",0,-15,-5,0,0,1,0,SXF_CLIENTSIDE)
		STOS IIJJ 1 Light("BLOOD_NAPALM_ROCKET") A_CountDown
		STOS K 0 A_SpawnItemEx("TNTSmoke",0,-15,-5,0,0,1,0,SXF_CLIENTSIDE)
		STOS KKLL 1 Light("BLOOD_NAPALM_ROCKET") A_CountDown
		Loop
	Death:
		"####" "#" 1 Light("BLOOD_NAPALM_ROCKET") ACS_NamedExecuteWithResult("CalebTossableResolve",reactiontime,user_strength)
		Wait
	Crash:
	XDeath:
		TNT1 A 0 A_StopSound(CHAN_BODY)
		TNT1 A 0 A_SpawnItemEx("SprayCanAltKaboom", 0, 0, 16, 0, 0, 0, 0, SXF_CLIENTSIDE)
		TNT1 A 1 A_PlayWeaponSound("Flare/Explode2")
		TNT1 A 0 A_RadiusGive("BloodBurnVFXTrigger",120,RGF_MONSTERS|RGF_PLAYERS)
		TNT1 A 3 A_Explode(100, 100)
		//I made it burn the area.. oh well until somoene ACS burning
		TNT1 A 3 A_Explode(5,104)
		TNT1 A 2 A_Explode(5,114)
		TNT1 A 2 A_Explode(5,124)
		TNT1 A 3 A_Explode(5,134)
		TNT1 A 2 A_Explode(5,154)
		TNT1 A 2 A_Explode(5,164)
		TNT1 A 3 A_Explode(5,174)
		TNT1 A 3 A_Explode(5,184)
		TNT1 A 3 A_Explode(5,194)
		TNT1 A 2 A_Explode(5,204)
		TNT1 A 2 A_Explode(5,184)
		TNT1 A 3 A_Explode(5,164)
		TNT1 A 2 A_Explode(5,144)
		TNT1 A 2 A_Explode(5,124)
		TNT1 A 3 A_Explode(5,104)
		TNT1 A 3 A_Explode(5, 84)
		TNT1 AAAAAAAAAAAAA 3 A_Explode(3, 64)
		Stop
	Detonate:
		"####" "#" 1 A_CountDown
		Wait
	 }
}

ACTOR SprayCanAlt_Coop : SprayCanAlt { Species "Player" +THRUSPECIES +DONTHARMCLASS +DONTHARMSPECIES +MTHRUSPECIES }

actor SprayCanAltKaboom
{
	Scale 1.0
	RenderStyle Normal
	Radius 64
	Height 16
	+BRIGHT
	+NOBLOCKMAP
	+NOGRAVITY
	+DONTSPLASH
	-WINDTHRUST
	+FIXMAPTHINGPOS
	-SOLID
	+CLIENTSIDEONLY
	states
	{
	Spawn:
		LLSX A 0
		LLSX AAAAAAAAAAA 0 A_SpawnDebris("BloodFlareDebris")
		LLSX ABCDEFGHIJK 4
		stop
	}
}

actor SprayCanFire
{
	Radius 13
	Height 8
	Speed 12
	Scale 0.12
	Damage (18)
	PROJECTILE
	+BRIGHT
	+RANDOMIZE
	+CANNOTPUSH
	+NODAMAGETHRUST
	+BLOODLESSIMPACT
	+HITTRACER
	DamageType "CalebFire"
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_Jump(128,"SpawnEnd")
		SPFI AB 2
		SPFI C 1
		Goto SpawnEnd
	SpawnEnd:
		SPFI C 1
		SPFI D 2
		SPFI E 2 ThrustThingZ(0, 4, 0, 1)
		SPFI FGHI 2
		Goto Death
	Death:
		TNT1 A 0 ACS_NamedExecuteWithResult("CalebBurner")
		SPFI G 0 A_Jump(128,"Death2")
		SPFI GHI 2 A_SpawnItem("BFireTrail",8+random(-5,5),8+random(-5,5),0,0)
		SFIR ABCDEFGH 2
		Stop
	Death2:
		TNT1 A 0 ACS_NamedExecuteWithResult("CalebBurner")
		SPFI GHI 2 A_SpawnItem("BFireTrail2",8+random(-5,5),8+random(-5,5),0,0)
		SFIR IJKLMNOP 2
		Stop
	XDeath:
		TNT1 A 0 ACS_NamedExecuteWithResult("CalebBurner")
		FTRL H 0
		Stop
	}
}

actor SprayCanFireCoop : SprayCanFire { +THRUSPECIES +DONTHARMCLASS +DONTHARMSPECIES +MTHRUSPECIES Species "Player" }