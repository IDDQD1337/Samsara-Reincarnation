Actor TeslaCannon : CalebWeapon
{
	inventory.pickupmessage "Picked up a Tesla Rifle"
	Inventory.PickupSound "Caleb/Weaponup"
	Obituary "%k \cusaw into\c- %o."
	weapon.selectionorder 20
	weapon.ammotype1 "Cell"
	weapon.ammotype2 "Cell"
	weapon.ammouse1 1
	weapon.ammouse2 1
	weapon.ammogive 40
	Weapon.SlotNumber 6
	Tag "Tesla Cannon"
	States
	{
	Spawn:
		TSPI A -1
		Stop
	Ready:
		TNT1 A 0 A_TakeInventory("UsingPitchfork", 1)
		TNT1 A 0 A_JumpIfInventory("GunsAkimboP", 1, "AkimboReady")
		TNT1 A 0 A_JumpIfInventory("CalebReady",1,"IntroTaunt")
		TNT1 A 0 A_JumpIfInventory("CalebWaiting",60,"WaitingTaunt")
		TESR ABCDEF 2 A_WeaponReady
		Goto Ready+1
	Select:
		TESR A 0 A_JumpIfInventory("GunsAkimboP", 1, 1)
		Goto Super::SelectLoop
		TAKR A 0
		Goto Super::SelectLoop
	Deselect:
		TESR A 0 A_JumpIfInventory("GunsAkimboP", 1, 1)
		Goto Super::DeselectLoop
		TAKR A 0
		Goto Super::DeselectLoop
	Fire:
		TNT1 A 0 A_JumpIfInventory("GunsAkimboP", 1, "AkimboFire")
		TEXX AB 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_GiveInventory("SamsaraBloodTeslaCannonAttackHandler")
		TESL B 1 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_JumpIfNoAmmo("Finish")
		TESL BC 1 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_GiveInventory("SamsaraBloodTeslaCannonAttackHandler")
		TESL C 1 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_JumpIfNoAmmo("Finish")
		TESL D 2 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_GiveInventory("SamsaraBloodTeslaCannonAttackHandler")
		TESL E 1 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_JumpIfNoAmmo("Finish")
		TESL EF 1 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_GiveInventory("SamsaraBloodTeslaCannonAttackHandler")
		TESL F 1 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_JumpIfNoAmmo("Finish")
		TNT1 A 0 A_Refire
		TESL G 2 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_GiveInventory("SamsaraBloodTeslaCannonAttackHandler")
		Goto Finish
	Hold: // The Tesla Coil will stop if the player is not firing at this point
		TNT1 A 0 A_JumpIfInventory("GunsAkimboP", 1, "AkimboHold")
		TESL G 2 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_GiveInventory("SamsaraBloodTeslaCannonAttackHandler")
		TESL B 1 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_JumpIfNoAmmo("Finish")
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("RoTT992")==0, "Finish")
		TESL BC 1 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_GiveInventory("SamsaraBloodTeslaCannonAttackHandler")
		TESL C 1 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_JumpIfNoAmmo("Finish")
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("RoTT992")==0, "Finish")
		TESL D 2 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_GiveInventory("SamsaraBloodTeslaCannonAttackHandler")
		TESL E 1 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_JumpIfNoAmmo("Finish")
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("RoTT992")==0, "Finish")
		TESL EF 1 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_GiveInventory("SamsaraBloodTeslaCannonAttackHandler")
		TESL F 1 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_JumpIfNoAmmo("Finish")
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("RoTT992")==0, "Finish")
		TNT1 A 0 A_Refire
		Goto Finish
	Finish:
		TNT1 A 0 A_FireCustomMissile("TeslaSmoke",random(-8,0),0,1,7,0)
		TEXX BA 3 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_ClearRefire
		Goto Ready
	Flash:
		TNT1 A 1 A_Light(3)
		TNT1 A 3 A_Light1
		TNT1 A 1 A_Light0
		Stop
	AltFire:
		TNT1 A 0 A_JumpIfInventory("GunsAkimboP", 1, "AkimboAltFire")
		TNT1 A 0 A_JumpIfInventory("Cell", 35, 1)
		Goto Ready
		TESL A 0 A_PlayWeaponSound("Tesla/Charge") // 1.2 seconds (38 Tics)
		TES2 KL 3 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH) // 6 Tics
		TES2 MNABCDEFGHIJ 2 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH) // 30 Tics
		TES2 EFGHIJ 1 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH) // 36 Tics
		TNT1 A 0 A_PlayWeaponSound ("Tesla/Altfire")
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"CoopAltFire")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopAltFire")
		TNT1 A 0 A_FireCustomMissile("TeslaBallBig",0,0,5)
		Goto AltFireEnd
	CoopAltFire:
		TNT1 A 0 A_FireCustomMissile("TeslaBallBigCoop",0,0,5)
		Goto AltFireEnd
	AltFireEnd:
		TNT1 A 0 A_TakeInventory("Cell", 35, TIF_NOTAKEINFINITE)
		BTES L 2 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		BTES MN 1 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TE2F D 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TESR ABCD 3 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		Goto Ready
	AltFlash:
		TE2F A 2 bright A_Light(5)
		TE2F BC 1 bright A_Light(3)
		TNT1 A 1 A_Light(0)
		Stop
	AkimboAltFire:
		TNT1 A 0 A_JumpIfInventory("Cell", 70, 1)
		Goto Ready
		TAKM A 0 A_PlayWeaponSound("Tesla/AltCharge")
		TAKM C 3 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TESR GHIUJKLMVNWOPQRW 2 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TESR VST 1 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_PlayWeaponSound ("Tesla/Altfire")
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"AkimboCoopAltFire")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"AkimboCoopAltFire")
		TNT1 A 0 A_FireCustomMissile("TeslaBallBig",0,0,-5)
		TNT1 A 0 A_FireCustomMissile("TeslaBallBig",0,0,5)
		Goto AkimboAltFireEnd
	AkimboCoopAltFire:
		TNT1 A 0 A_FireCustomMissile("TeslaBallBigCoop",0,0,-5)
		TNT1 A 0 A_FireCustomMissile("TeslaBallBigCoop",0,0,5)
		Goto AkimboAltFireEnd
	AkimboAltFireEnd:
		TNT1 A 0 A_TakeInventory("Cell", 70, TIF_NOTAKEINFINITE)
		BTES Q 2 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		BTES R 1 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		BTES S 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TESR Z 1 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		BTES O 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		BTES P 3 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		Goto Ready
	AkimboAltFlash:
		TESR X 2 bright A_Light(5)
		TESR Y 1 bright A_Light(3)
		TNT1 A 1 A_Light(0)
		Stop
	AkimboFlash:
		TNT1 A 2 bright A_Light(5)
		TNT1 A 1 bright A_Light(3)
		TNT1 A 1 A_Light(0)
		Stop
	AkimboReady:
	 	TNT1 A 0 A_JumpIfInventory("CalebReady",1,"IntroTaunt")
		TNT1 A 0 A_JumpIfInventory("CalebWaiting",60,"WaitingTaunt")
		TAKR ABCDEF 2 A_WeaponReady
		Goto Ready+1
	AkimboFire:
		TAKM B 3 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TAKM C 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_GiveInventory("SamsaraBloodTeslaCannonAkimboAttackHandler")
		TNT1 A 0 A_JumpIfInventory("Cell", 2, 1)
		Goto AkimboFinish
		TAKM DE 1 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_GiveInventory("SamsaraBloodTeslaCannonAkimboAttackHandler")
		TNT1 A 0 A_JumpIfInventory("Cell", 2, 1)
		Goto AkimboFinish
		TAKM FG 1 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_GiveInventory("SamsaraBloodTeslaCannonAkimboAttackHandler")
		TNT1 A 0 A_JumpIfInventory("Cell", 2, 1)
		Goto AkimboFinish
		TAKM HC 1 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_GiveInventory("SamsaraBloodTeslaCannonAkimboAttackHandler")
		TNT1 A 0 A_JumpIfInventory("Cell", 2, 1)
		Goto AkimboFinish
		TAKM DE 1 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_GiveInventory("SamsaraBloodTeslaCannonAkimboAttackHandler")
		TNT1 A 0 A_Refire
		Goto Ready
	AkimboHold:
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_GiveInventory("SamsaraBloodTeslaCannonAkimboAttackHandler")
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("RoTT992")==0, "AkimboFinish")
		TNT1 A 0 A_JumpIfInventory("Cell", 2, 1)
		Goto AkimboFinish
		TAKM FG 1 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_GiveInventory("SamsaraBloodTeslaCannonAkimboAttackHandler")
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("RoTT992")==0, "AkimboFinish")
		TNT1 A 0 A_JumpIfInventory("Cell", 2, 1)
		Goto AkimboFinish
		TAKM HC 1 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_GiveInventory("SamsaraBloodTeslaCannonAkimboAttackHandler")
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("RoTT992")==0, "AkimboFinish")
		TNT1 A 0 A_JumpIfInventory("Cell", 2, 1)
		Goto AkimboFinish
		TAKM DE 1 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_GiveInventory("SamsaraBloodTeslaCannonAkimboAttackHandler")
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("RoTT992")==0, "AkimboFinish")
		TNT1 A 0 A_JumpIfInventory("Cell", 2, 1)
		Goto AkimboFinish
		TAKM FG 1 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_GiveInventory("SamsaraBloodTeslaCannonAkimboAttackHandler")
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("RoTT992")==0, "AkimboFinish")
		TNT1 A 0 A_JumpIfInventory("Cell", 2, 1)
		Goto AkimboFinish
		TAKM HC 1 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_GiveInventory("SamsaraBloodTeslaCannonAkimboAttackHandler")
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("RoTT992")==0, "AkimboFinish")
		TNT1 A 0 A_JumpIfInventory("Cell", 2, 1)
		Goto AkimboFinish
		TAKM DE 1 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_GiveInventory("SamsaraBloodTeslaCannonAkimboAttackHandler")
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("RoTT992")==0, "AkimboFinish")
		TNT1 A 0 A_Refire
		Goto AkimboFinish
	AkimboFinish:
		TNT1 A 0 A_FireCustomMissile("TeslaSmoke",random(-8,0),0,1,7,0)
		TNT1 A 0 A_FireCustomMissile("TeslaSmoke",random(0,8),0,-1,7,0)
		TAKM CB 3 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_ClearRefire
		Goto Ready
	}
}

Actor SamsaraBloodTeslaCannonAttackHandler : Trigger
{
	States
	{
		Pickup:
			TNT1 A 0
			TNT1 A 0 A_PlayWeaponSound("Tesla/Fire")
			TNT1 A 0 A_JumpIfInventory("CoopModeOn", 1, "PickupAttackCoop")
			TNT1 A 0 A_FireCustomMissile("TeslaProjectile",0,1,5)
			Stop
		PickupAttackCoop:
			TNT1 A 0 A_FireCustomMissile("TeslaProjectileCoop",0,1,5)
			Stop
	}
}

Actor SamsaraBloodTeslaCannonAkimboAttackHandler : Trigger
{
	States
	{
		Pickup:
			TNT1 A 0
			TNT1 AA 0 A_PlayWeaponSound("Tesla/Fire")
			TNT1 A 0 A_JumpIfInventory("CoopModeOn", 1, "PickupAttackCoop")
			TNT1 A 0 A_FireCustomMissile("TeslaProjectile",0,1,20)
			TNT1 A 0 A_FireCustomMissile("TeslaProjectile",0,1,-20)
			Stop
		PickupAttackCoop:
			TNT1 A 0 A_FireCustomMissile("TeslaProjectileCoop",0,1,20)
			TNT1 A 0 A_FireCustomMissile("TeslaProjectileCoop",0,1,-20)
			Stop
	}
}

Actor TeslaProjectile : FastProjectile
{
	Speed 72
	Radius 8
	Height 8
	PROJECTILE
	Damage (15+Random(0,10))
	Scale 0.85
	DamageType "Tesla"
	Obituary "%o was overcharged by %k."
	Renderstyle add
	+WINDTHRUST
	+FOILINVUL
	+BRIGHT
	ActiveSound "Tesla/Elect"
	DeathSound "Tesla/Zap"
	States
	{
	Spawn:
		TESP A 0
		TESP A 0 A_LoopActiveSound
		Goto SpawnLoop
	SpawnLoop:
		TESP A 1 A_SpawnItemEx("TeslaProjectileBall",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
		Loop
	Death:
		TNT1 AAAAA 1 A_SpawnItemEx("TeslaFX", random(-8,8), random(-8,8), random(0,3), random(-12,12), random(-12,12), random(0,3), 0, SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
		Stop
	}
}

Actor TeslaProjectileBall
{
	RenderStyle Normal
	+NOINTERACTION
	+RANDOMIZE
	+CLIENTSIDEONLY
	Scale 0.80
	States
	{
	Spawn:
		TNT1 A 0
		BTES F 1 bright
		stop
	}
}

Actor TeslaProjectileCoop : TeslaProjectile { Species "Player" +THRUSPECIES }
Actor StopFiring : Boolean {}
Actor TeslaCounterX : Counter { inventory.maxamount 24 }

Actor TeslaSmoke
{
	Height 1
	Radius 1
	PROJECTILE
	Damage 0
	Speed 1
	+RIPPER
	+BLOODLESSIMPACT
	+CLIENTSIDEONLY
	-ACTIVATEIMPACT
	-ACTIVATEPCROSS
	Alpha 0.9
	Renderstyle add
	Scale 0.065
	States
	{
	Spawn:
		DS95 FFGGHH 3 A_Fadeout(0.15)
		Stop
	}
}

Actor TeslaFX
{
	RenderStyle Add
	+NOINTERACTION
	+CLIENTSIDEONLY
	+BRIGHT
	Scale 0.55
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_PlaySound("TeslFX", CHAN_BODY)
		TNT1 A 0 A_Jump(128, "Spawn2")
	SpawnNormal:
		TSFX ABCDEF 2 A_FadeOut(0.1)
		Loop
	Spawn2:
		TSFX GHIJKL 2 A_FadeOut(0.1)
		Loop
	}
}

ACTOR TeslaBallBig
{
	Radius 14
	Yscale 0.35
 	XScale 1.75
	Height 16
	Speed 48
	Damage (0)
	PROJECTILE
	DamageType "Tesla"
	+DONTSPLASH
	+FORCERADIUSDMG
	+WINDTHRUST
	+FULLVOLACTIVE
	ActiveSound "Tesla/Air2"
	States
	{
	Spawn:
		BTES A 0
		BTES A 0 A_LoopActiveSound
		BTES A 1 A_SpawnItemEx("BloodTeslaDebris",random(-5,5),random(-5,5),random(-5,5),random(-4,4),random(-4,4),random(-1,1),0,SXF_CLIENTSIDE)
		BTES B 0 A_SpawnItemEx("BlueFlareSmall",8)
		BTES B 0 A_SpawnDebris("BloodTeslaSparkDebris")
		BTES B 1 A_SpawnItemEx("BloodTeslaDebris",random(-5,5),random(-5,5),random(-5,5),random(-1,1),random(-1,1),random(-1,1),0,SXF_CLIENTSIDE)
		BTES C 0 A_SpawnItemEx("BlueFlareSmall",8)
		BTES C 1 A_SpawnItemEx("BloodTeslaDebris",random(-5,5),random(-3,3),random(-3,3),random(-1,1),random(-1,1),random(-1,1),0,SXF_CLIENTSIDE)
		BTES D 0 A_SpawnItemEx("BlueFlareSmall",8)
		BTES D 1 A_SpawnItemEx("BloodTeslaDebris",random(-2,2),random(-2,2),random(-2,2),random(-1,1),random(-1,1),random(-1,1),0,SXF_CLIENTSIDE)
		BTES A 0 A_SpawnItemEx("BlueFlareSmall",8)
		BTES A 0 A_SpawnDebris("BloodTeslaSparkDebris")
		Goto Spawn+2
	Death:
	Crash:
		TNT1 A 0 A_SpawnItemEx("TeslaWave",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
		TNT1 A 0 A_Explode(300,300)//random(12,22),164) /2op4u? /According to my blood gameplay, this had a lot of radius.
		Goto DeathEffect
	XDeath:
		TNT1 A 0 A_Explode(300,300)
		Goto DeathEffect
	DeathEffect:
		TNT1 AAAAAAAAAAA 0 A_SpawnItemEx("BloodTeslaDebris",random(-15,15),random(-15,15),random(-15,15),random(-1,1),random(-3,3),random(0,1),0,SXF_CLIENTSIDE)
		TNT1 AAAAAAAAAAAAAAAAAAAAA 0 A_SpawnDebris("BloodTeslaSparkDebris")
		TNT1 A 0 A_SpawnItemEx("TeslaExplosion", 0, 64)
		TNT1 A 0 A_SpawnItemEx("TeslaExplosion", 0, -64)
		TNT1 A 5 A_PlaySound("Tesla/Boom2")
		Stop
	}
}

Actor TeslaBallBigCoop : TeslaBallBig
{
	Species "Player"
	+THRUSPECIES
	+DONTHARMCLASS
	+DONTHARMSPECIES
	+MTHRUSPECIES
	Damage (Random(200,350)+Random(Random(0,0),Random(0,100)))
	States
	{
	Death:
	Crash:
		TNT1 A 0 A_SpawnItemEx("TeslaWave",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
		TNT1 A 0 A_Explode(random(152,186),random(254,321))//random(12,22),164) /2op4u? /According to my blood gameplay, this had a lot of radius.
		Goto Super::DeathEffect
	XDeath:
		TNT1 A 0 A_Explode(random(152,186),random(254,321))
		Goto Super::DeathEffect
	}
}

Actor TeslaExplosion
{
	Speed 0
	Damage 0
	DamageType "Shock"
	Projectile
	+RIPPER
	+BLOODLESSIMPACT
	+FORCERADIUSDMG
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 1 A_Explode(random(22,38),84)
		Stop
	}
}

actor BloodTeslaDebris : BloodNapalmDebris
{
	Health 10
	Scale 0.35
	+NOINTERACTION
	+FLOATBOB
	+CLIENTSIDEONLY
	+BRIGHT
	RenderStyle Translucent
	Alpha 0.75
	VSpeed 1
	states
	{
	Spawn:
		TSFX A 0
		TSFX A 0 A_Jump(128,"ZapB")
	Zap:
		TSFX A 0 A_Jump(128,1,2,3,4,5,6)
		TSFX AABBCCCDDEEFFAAABBBCCCCDDDDEEEFFF 1 A_FadeOut(0.025)
		Loop
	ZapB:
		TSFX A 0 A_Jump(128,1,2,3,4,5,6)
		TSFX GGGHHHIIIIJJJKKKKLLLGGHHIIIJJKKLL 1 A_FadeOut(0.025)
		Loop
	}
}

Actor TeslaWave
{
	+NOINTERACTION
	+CLIENTSIDEONLY
	+BRIGHT
	Scale 0.9
	states
	{
	Spawn:
		BTES GHIJ 3
		BTES KKK 1 A_FadeOut
		Stop
	}
}