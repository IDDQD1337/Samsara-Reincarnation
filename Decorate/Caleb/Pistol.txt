actor "   Revolver   " : CalebWeapon
{
	obituary "%o was shot by %k's revolver."
	attacksound "weapons/bloodpistol"
	weapon.selectionorder 1900
	weapon.kickback 100
	Weapon.AmmoUse 0
	Weapon.AmmoGive 0
	Weapon.AmmoType ""
	inventory.pickupsound "PICKWEAP"
	Weapon.SlotNumber 1
	+NOALERT
	+WEAPON.WIMPY_WEAPON
	Decal ZBloodBulletChip1
	Tag "Revolver"
	states
	{
	Spawn:
		TNT1 A 1
		Stop
	Ready:
		TNT1 A 0 A_TakeInventory("UsingPitchfork", 1)
		TNT1 A 0 A_JumpIfInventory("GunsAkimboP", 1, "AkimboReady")
		TNT1 A 0 A_JumpIfInventory("CalebReady",1,"IntroTaunt")
		TNT1 A 0 A_JumpIfInventory("CalebWaiting",60,"WaitingTaunt")
		BLRV A 1 A_WeaponReady
		Goto Ready+1
	AkimboReady:
		TNT1 A 0 A_JumpIfInventory("CalebReady",1,"IntroTaunt")
		TNT1 A 0 A_JumpIfInventory("CalebWaiting",60,"WaitingTaunt")
		BLRV L 1 A_WeaponReady
		Goto Ready
	Deselect:
		BLRV A 0 A_JumpIfInventory("GunsAkimboP", 1, 1)
		Goto Super::DeselectLoop
		BLRV L 0
		Goto Super::DeselectLoop
	Select:
		BLRV A 0 A_JumpIfInventory("GunsAkimboP", 1, 1)
		Goto Super::SelectLoop
		BLRV L 0
		Goto Super::SelectLoop
	Fire:
		TNT1 A 0 A_JumpIfInventory("GunsAkimboP", 1, "AkimboFire")
		TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,"CheckForAmmo")
	KeepFiring:
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,2)
		TNT1 A 0 A_FireBullets (2.6, 1.6, -1, 12,"ZBBulletPuff", FBF_NORANDOM|FBF_USEAMMO)
		Goto KeepFiringEnd
		TNT1 A 0 A_FireBullets (2.6, 1.6, -1, 12,"ZBBulletPuffCoop", FBF_NORANDOM|FBF_USEAMMO)
		Goto KeepFiringEnd
	KeepFiringEnd:
		BLRV I 2 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		BLRV JK 2 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 5 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_Refire
		Goto Ready
	AkimboFire:
		TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,"TakeAmmoCheckAkimbo")
		Goto KeepFiringAkimbo
	KeepFiringAkimbo:
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GunFlash("AkimboFlash")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,2)
		TNT1 A 0 A_FireBullets(2.6, 1.6, -1, 12,"ZBBulletPuff", FBF_NORANDOM|FBF_USEAMMO)
		Goto KeepFiringAkimboEnd
		TNT1 A 0 A_FireBullets(2.6, 1.6, -1, 12,"ZBBulletPuffCoop", FBF_NORANDOM|FBF_USEAMMO)
		Goto KeepFiringAkimboEnd
	KeepFiringAkimboEnd:
		BLRV I 2 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		BLRV JK 1 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 4 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,"AkimboSecondFire")
		Goto KeepFiringAkimbo2
	KeepFiringAkimbo2:
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,2)
		TNT1 A 0 A_FireBullets(2.6, 1.6, -1, 12,"ZBBulletPuff", FBF_NORANDOM|FBF_USEAMMO)
		Goto KeepFiringAkimbo2End
		TNT1 A 0 A_FireBullets(2.6, 1.6, -1, 12,"ZBBulletPuffCoop", FBF_NORANDOM|FBF_USEAMMO)
		Goto KeepFiringAkimbo2End
	KeepFiringAkimbo2End:
		BLRV "[" 2 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		BLRV "\]" 1 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 4 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_Refire
		Goto Ready
	Flash:
		TNT1 A 0 A_Light2
		BLRV B 2 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_Light1
		BLRV C 2 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_Light0
		BLRV D 2 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		BLRV EFG 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		BLRV H 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		stop
	AkimboFlash:
		TNT1 A 0 A_Light2
		BLRV M 2 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_Light1
		BLRV N 1 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_Light0
		BLRV O 1 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		BLRV PQRS 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,"ForceThisCheck")
	KeepFlashing:
		TNT1 A 0 A_Light2
		BLRV T 2 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_Light1
		BLRV U 1 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_Light0
		BLRV V 1 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		BLRV WXYZ 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		stop
	CheckForAmmo:
		TNT1 A 0 A_JumpIfInventory("Clip",1,2)
		TNT1 A 0 A_SelectWeapon("Pitchfork")
		goto Deselect
		TNT1 A 0 A_TakeInventory("Clip",1)
		Goto KeepFiring
	TakeAmmoCheckAkimbo:
		TNT1 A 0 A_JumpIfInventory("Clip",1,2)
		TNT1 A 0 A_SelectWeapon("Pitchfork")
		goto Deselect
		TNT1 A 0 A_TakeInventory("Clip",1)
		Goto KeepFiringAkimbo
	AkimboSecondFire:
		TNT1 A 0 A_JumpIfInventory("Clip",1,2)
		TNT1 A 0 A_SelectWeapon("Pitchfork")
		goto Deselect
		TNT1 A 0 A_TakeInventory("Clip",1)
		Goto KeepFiringAkimbo2
	ForceThisCheck:
		TNT1 A 0 A_JumpIfNoAmmo("LightDone")
		Goto KeepFlashing
	}
}
