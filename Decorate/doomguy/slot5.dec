actor DGHasRocketLauncher : Boolean {}
actor DGStrHasLandMines : Boolean {}
actor SamsaraDoomguyStrHRLSecondaryPoweredAttackAmount : Counter { Inventory.MaxAmount 4 }

actor "Rocket Launcher" : RocketLauncher
{
    Weapon.SlotNumber 5
	Weapon.AmmoGive 0
    +ALT_USES_BOTH
    +UNDROPPABLE
	Inventory.RestrictedTo "DoomguyPlayer"
	Tag "Rocket Launcher"
    States
    {
      Spawn:
        WDRL A -1
        stop

      // Classic
      Ready:
        TNT1 A 0 A_JumpIfInventory("DoomClassMode",2,"ReadyStr")
		TNT1 A 0 A_JumpIfInventory("Doom64Mode",1,"Ready64")
        DMIS A 1 A_WeaponReady(WRF_NOSECONDARY)
        loop

      Deselect:
        TNT1 A 0 A_JumpIfInventory("DoomClassMode",2,"DeselectStr")
		TNT1 A 0 A_JumpIfInventory("Doom64Mode",1,"Deselect64")
        DMIS A 1 A_Lower
        loop

      Select:
        TNT1 A 0 A_JumpIfInventory("DoomClassMode",2,"SelectStr")
		TNT1 A 0 A_JumpIfInventory("Doom64Mode",1,"Select64")
        DMIS A 1 A_Raise
        loop

      Fire:
        TNT1 A 0 A_JumpIfInventory("DoomClassMode",2,"FireStr")
		TNT1 A 0 A_JumpIfInventory("Doom64Mode",1,"Fire64")
        TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"FireSmooth")
      FireVanilla:
        DMIS B 8 A_GunFlash
        DMIS B 12 A_GiveInventory("SamsaraDoomguyRocketLauncherAttackHandler")
      FireFinish:
        TNT1 A 0 A_ReFire // 20 frames total
        goto Ready

      Flash:
        DMSF B 3 Bright A_Light1
        DMSF C 4 Bright
        DMSF FG 4 Bright A_Light2
        goto LightDone

      FireSmooth:
        DMIS A 2
        DMIS A 4 Bright A_GunFlash("FlashSmooth")
        DMIS B 2 Bright
        TNT1 A 0 A_GiveInventory("SamsaraDoomguyRocketLauncherAttackHandler")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasDoubleFiringSpeed",1,"FireSmoothRage")
        DMIS DC 3 Bright
        DMIS BE 2
        DMIS FG 1
        goto FireFinish

      FireSmoothRage:
        DMIS DC 2 Bright
        DMIS BEFG 2
        goto FireFinish

      FlashSmooth:
        DMSF A 3 Bright A_Light1
        DMSF B 2 Bright
        DMSF C 2 Bright A_Light2
        DMSF DE 3 Bright
        goto LightDone

      // Doom 64 (has recoil visual and kickback)
      Ready64:
        64ML A 1 A_WeaponReady(WRF_NOSECONDARY)
        goto Ready

      Deselect64:
        64ML A 1 A_Lower
        goto Deselect

      Select64:
        64ML A 1 A_Raise
        goto Select

      Fire64:
		TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"Fire64Smooth")
	  Fire64Vanilla:
        64ML B 8 A_GunFlash("Flash64")
        64ML B 13 A_GiveInventory("SamsaraDoomguy64RocketLauncherAttackHandler")
        goto FireFinish

      Flash64:
        64MM A 2 Bright A_Light1
        64MM B 4 Bright
        64MM CD 4 Bright A_Light2
        goto LightDone

      Fire64Smooth:
        64ML C 2 A_GunFlash("Flash64Smooth")
        64ML DEF 2
        64ML F 3 A_GiveInventory("SamsaraDoomguy64RocketLauncherAttackHandler")
        64ML GH 3
        64ML I 4
        goto FireFinish

      Flash64Smooth:
        64MM E 8 Bright A_Light1
        64MM F 2 Bright
        64MM GH 2 Bright A_Light2
        goto LightDone

      // Stronghold (Homing Rocket Launcher)
      // Unpowered
      ReadyStr:
        TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"ReadyStrPowered")
        HRLG A 1 A_WeaponReady(WRF_NOSECONDARY)
        goto Ready

      DeselectStr:
        TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"DeselectStrPowered")
      DeselectStrContinue:
        TNT1 A 0 A_Lower
        HRLG A 1 A_Lower
        goto Deselect

      SelectStr:
        TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"SelectStrPowered")
      SelectStrContinue:
        TNT1 A 0 A_Raise
        HRLG A 1 A_Raise
        goto Select

      FireStr:
        HRLG A 4
        HRLG C 5 Bright A_GunFlash("FlashStr")
        TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"FireStrPowered")
        TNT1 A 0 A_GiveInventory("SamsaraDoomguyStrHRLAttackHandler")
      FireStrFinish:
        HRLG D 6 Bright
        HRLG E 5 Bright
        HRLG F 4 Bright
        HRLG B 3 A_ReFire
        goto Ready

      FlashStr:
        TNT1 A 5 A_Light1
        TNT1 A 6 A_Light2
      FlashStrFinish:
        TNT1 A 5 A_Light1
        goto LightDone

      // Powered
      ReadyStrPowered:
        HRLG A 1 A_WeaponReady
        goto Ready

      DeselectStrPowered:
        TNT1 A 0 A_Lower
        goto DeselectStrContinue

      SelectStrPowered:
        TNT1 A 0 A_Raise
        goto SelectStrContinue

      FireStrPowered:
        TNT1 A 0 A_GiveInventory("SamsaraDoomguyStrHRLPoweredPrimaryAttackHandler")
        goto FireStrFinish

      AltFire:
        HRLG A 4
        HRLG C 5 Bright A_GunFlash
        TNT1 A 0 A_TakeInventory("SamsaraDoomguyStrHRLSecondaryPoweredAttackAmount") // reset
      AltFirePoweredLoop:
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrHRLSecondaryPoweredAttackAmount",0,"AltFireContinue")
        TNT1 A 0 A_GiveInventory("SamsaraDoomguyStrHRLSecondaryPoweredAttackAmount")
        TNT1 A 0 A_GunFlash("AltFlash2")
        HRLG D 4 Bright A_FireCustomMissile("SamsaraDoomguyStrPowerHomRocket1",random(-600,600)/100.0,true,0,4.5,0,random(-150,150)/100.0)
        loop

      AltFireContinue:
        HRLG E 5 Bright
        HRLG F 4 Bright
        TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"AltFireFinishReFire")
        TNT1 A 0 A_CheckReload
      AltFireFinish:
        HRLG B 3
        goto Ready

      AltFireFinishReFire:
        TNT1 A 0 A_JumpIfInventory("DoomClassMode",2,2)
        TNT1 A 0 A_CheckReload
        goto AltFireFinish
        TNT1 A 0 A_ReFire
        goto AltFireFinish

      AltFlash:
        TNT1 A 5 A_Light1
      AltFlash2:
        TNT1 A 4 A_Light2
        goto FlashStrFinish
    }
}

actor SamsaraDoomguyRocketLauncherAttackHandler : Trigger
{
    States
    {
      // Ammo check stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupTakeAmmo")
        TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,"PickupTakeAmmo")
        stop

      // Ammo taking stuff
      PickupTakeAmmo:
        TNT1 A 0 A_TakeInventory("RocketAmmo",1,TIF_NOTAKEINFINITE)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SamsaraImprovedDoomguyBalance",1,"PickupAttackImproved")
      // Vanilla
      PickupAttackVanilla:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackVanillaCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackVanillaCoop")
      PickupAttackVanillaDM:
        TNT1 A 0 A_FireCustomMissile("DoomRocketVanilla",0,false)
        stop

      PickupAttackVanillaCoop:
        TNT1 A 0 A_FireCustomMissile("DoomRocketVanilla2",0,false)
        stop

      // Improved
      PickupAttackImproved:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackImprovedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackImprovedCoop")
      PickupAttackImprovedDM:
        TNT1 A 0 A_FireCustomMissile("DoomRocket",0,false)
        stop

      PickupAttackImprovedCoop:
        TNT1 A 0 A_FireCustomMissile("DoomRocket2",0,false)
        stop
    }
}

actor SamsaraDoomguy64RocketLauncherAttackHandler : Trigger
{
    States
    {
      // Ammo check stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupTakeAmmo")
        TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,"PickupTakeAmmo")
        stop

      // Ammo taking stuff
      PickupTakeAmmo:
        TNT1 A 0 A_TakeInventory("RocketAmmo",1,TIF_NOTAKEINFINITE)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SamsaraImprovedDoomguyBalance",1,"PickupAttackImproved")
      // Vanilla
      PickupAttackVanilla:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackVanillaCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackVanillaCoop")
      PickupAttackVanillaDM:
        TNT1 A 0 A_FireCustomMissile("Doom64RocketVanilla",0,false)
        goto PickupFinish

      PickupAttackVanillaCoop:
        TNT1 A 0 A_FireCustomMissile("Doom64RocketVanilla2",0,false)
        goto PickupFinish

      // Improved
      PickupAttackImproved:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackImprovedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackImprovedCoop")
      PickupAttackImprovedDM:
        TNT1 A 0 A_FireCustomMissile("Doom64Rocket",0,false)
        goto PickupFinish

      PickupAttackImprovedCoop:
        TNT1 A 0 A_FireCustomMissile("Doom64Rocket2",0,false)
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 A_Recoil(2)
        TNT1 A 0 ACS_NamedExecuteAlways("SamsaraRecoil", 0, 2, 0, 20)
        stop
    }
}

actor SamsaraDoomguyStrHRLAttackHandler : Trigger
{
    States
    {
      Pickup:
      PickupRightCheck:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupRight")
        TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,"PickupRight")
      PickupLeftCheck:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupLeft")
        TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,"PickupLeft")
        stop

      PickupRight:
        TNT1 A 0 A_JumpIfInventory("DMModeOn",1,"DMPickupRight")
		TNT1 A 0 A_TakeInventory("RocketAmmo",1,TIF_NOTAKEINFINITE)
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrHomRocket1",-4,false)
        goto PickupLeftCheck

      PickupLeft:
        TNT1 A 0 A_JumpIfInventory("DMModeOn",1,"DMPickupLeft")
		TNT1 A 0 A_TakeInventory("RocketAmmo",1,TIF_NOTAKEINFINITE)
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrHomRocket1",4,false)
        stop
		
      DMPickupRight:
        TNT1 A 0 A_TakeInventory("RocketAmmo",1,TIF_NOTAKEINFINITE)
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrHomRocket1_DM",-4,false)
        goto PickupLeftCheck

      DMPickupLeft:
        TNT1 A 0 A_TakeInventory("RocketAmmo",1,TIF_NOTAKEINFINITE)
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrHomRocket1_DM",4,false)
        stop
    }
}

actor SamsaraDoomguyStrHRLPoweredPrimaryAttackHandler : Trigger
{
    States
    {
      Pickup:
      PickupRightCheck:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupRight")
        TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,"PickupRight")
      PickupLeftCheck:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupLeft")
        TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,"PickupLeft")
        stop

      PickupRight:
        TNT1 A 0 A_JumpIfInventory("DMModeOn",1,"DMPickupRight")
		TNT1 A 0 A_TakeInventory("RocketAmmo",1,TIF_NOTAKEINFINITE)
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrPowerHomRocket1",-4,false,0,4.5)
        goto PickupLeftCheck

      PickupLeft:
        TNT1 A 0 A_JumpIfInventory("DMModeOn",1,"DMPickupLeft")
		TNT1 A 0 A_TakeInventory("RocketAmmo",1,TIF_NOTAKEINFINITE)
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrPowerHomRocket1",4,false,0,4.5)
        stop
		
      DMPickupRight:
        TNT1 A 0 A_TakeInventory("RocketAmmo",1,TIF_NOTAKEINFINITE)
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrPowerHomRocket1_DM",-4,false,0,4.5)
        goto PickupLeftCheck

      DMPickupLeft:
        TNT1 A 0 A_TakeInventory("RocketAmmo",1,TIF_NOTAKEINFINITE)
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrPowerHomRocket1_DM",4,false,0,4.5)
        stop
    }
}

actor DoomRocket : Rocket
{
    Decal Scorch
    SeeSound "doomguy/rocklf"
    DeathSound "doomguy/rocklx"
	Damage (100)
    Damagetype "DoomDamageType"
    Obituary "$SAMSARA_DOOMGUY_OB_SLOT5"
    States
    {
      Spawn:
        DMSL A 1 bright
        Loop

      Death:
        DMSL B 8 bright A_Explode
        DMSL C 6 bright
        DMSL D 4 bright
        Stop
    }
}

actor DoomRocket2 : DoomRocket { +THRUSPECIES Species "Player" }

actor DoomRocketVanilla : DoomRocket { Damage 20 }

actor DoomRocketVanilla2 : DoomRocketVanilla { +THRUSPECIES Species "Player" }

actor SamsaraDoomguyStrHomRocket1 : Rocket
{
    Speed 30
    Damage 15
    DamageType "DoomDamageType"
    SeeSound "doomguy/stronghold/hrlfir"
    DeathSound "doomguy/stronghold/hrlexp"
    Decal "Scorch"
    Obituary "%k destroyed %o with the homing rocket launcher."
    -DEHEXPLOSION
    -RANDOMIZE
    +SEEKERMISSILE
    States
    {
      Spawn:
        HMIS A 4 Bright Light("ROCKET") A_SeekerMissile(10,25,SMF_LOOK)
        loop

      Death:
        TNT1 A 0 A_SetTranslucent(0.67,1)
        DMSL B 8 Bright Light("ROCKET_X1") A_Explode
        DMSL C 6 Bright Light("ROCKET_X2")
        DMSL D 4 Bright Light("ROCKET_X3")
        stop
    }
}

actor SamsaraDoomguyStrHomRocket1_DM : SamsaraDoomguyStrHomRocket1 { -SEEKERMISSILE }

actor SamsaraDoomguyStrPowerHomRocket1 : SamsaraDoomguyStrHomRocket1
{
    Radius 11
    Height 8
    Speed 42
    Damage (80)
    Scale 1.25
    +FORCERADIUSDMG
    States
    {
      Spawn:
        DMSL E 2 Bright Light("POWERHOMROCKET") A_SeekerMissile(25,35,SMF_LOOK)
        loop

      Death:
        TNT1 A 0 A_SetTranslucent(0.67,1)
        TNT1 A 0 A_Explode(80,240,0)
        TNT1 A 0 A_ChangeFlag("FORCERADIUSDMG",false)
        DMSL B 8 Bright Light("POWERHOMROCKET_X1") A_Explode(80,160)
        DMSL C 6 Bright Light("POWERHOMROCKET_X2")
        DMSL D 4 Bright Light("POWERHOMROCKET_X3")
        stop
    }
}

actor SamsaraDoomguyStrPowerHomRocket1_DM : SamsaraDoomguyStrPowerHomRocket1 { -SEEKERMISSILE }

actor Doom64Rocket : Rocket
{
    Decal Scorch
    SeeSound "doom64guy/rocklf"
    DeathSound "doom64guy/rocklx"
    -DEHEXPLOSION
    -ROCKETTRAIL
    +BLOODLESSIMPACT
	Damage (100)
    DamageType "DoomDamageType"
    Scale 0.75
    Obituary "%k splattered %o's brains with the Rocket Launcher."
    States
    {
      Spawn:
        64MI AA 2 bright A_SpawnItemEx("Doom64RocketSmokeTrail")
        Loop

      Death:
        64MI B 8 bright A_Explode
        64MI C 6 bright A_FadeOut(0.2)
        64MI D 3 bright A_FadeOut(0.2)
        64MI EF 3 bright A_FadeOut(0.2)
        Stop
    }
}

actor Doom64Rocket2 : Doom64Rocket { +THRUSPECIES Species "Player" }

actor Doom64RocketVanilla : Doom64Rocket { Damage 20 }

actor Doom64RocketVanilla2 : Doom64RocketVanilla { +THRUSPECIES Species "Player" }

actor Doom64RocketSmokeTrail : RocketSmokeTrail
{
    +CLIENTSIDEONLY
    RenderStyle Translucent
    Alpha 0.5
    VSpeed 0
    Scale 0.75
    States
    {
    Spawn:
        64RS ABCDE 2 bright
        stop
    }
}

// Stronghold land mines
actor "Land Mine Layer" : Weapon
{
    Inventory.PickupMessage "You got some land mines!"
    Weapon.AmmoType "DoomguyStrMines"
    Weapon.AmmoGive 5
    Weapon.AmmoUse 1
	Weapon.SlotNumber 8
    Tag "Land Mines"
	Inventory.RestrictedTo "DoomguyPlayer"
    +ALT_USES_BOTH
    +NOALERT
    +UNDROPPABLE
    States
    {
      Spawn:
        M_N3 A -1
        stop

      Ready:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReadyAmmo")
        TNT1 A 0 A_JumpIfInventory("DoomguyStrMines",1,"ReadyAmmo")
      ReadyNoAmmo:
        TNT1 A 1 A_WeaponReady
        goto Ready

      ReadyAmmo:
        LAYR A 1 A_WeaponReady
        goto Ready

      // Unpowered
      Deselect:
        TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"DeselectPowered")
      DeselectContinue:
        TNT1 A 0 A_Lower
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"DeselectAmmo")
        TNT1 A 0 A_JumpIfInventory("DoomguyStrMines",1,"DeselectAmmo")
      DeselectNoAmmo:
        TNT1 A 1 A_Lower
        goto Deselect

      DeselectAmmo:
        LAYR A 1 A_Lower
        goto Deselect

      Select:
        TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"SelectPowered")
      SelectContinue:
        TNT1 A 0 A_Raise
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"SelectAmmo")
        TNT1 A 0 A_JumpIfInventory("DoomguyStrMines",1,"SelectAmmo")
      SelectNoAmmo:
        TNT1 A 1 A_Raise
        goto Select

      SelectAmmo:
        LAYR A 1 A_Raise
        goto Select

      Fire:
        LAYR B 8 A_PlaySound("doomguy/stronghold/minebeep",CHAN_WEAPON)
        LAYD ABCDE 1
        TNT1 A 5 A_JumpIfInventory("PowerWeaponLevel2",1,"FirePowered")
        TNT1 A 9 A_FireCustomMissile("SamsaraDoomguyStrLandmine3")
      FireFinish:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireFinishAmmo")
        TNT1 A 0 A_JumpIfInventory("DoomguyStrMines",1,"FireFinishAmmo")
      FireFinishNoAmmo:
        TNT1 AAAAA 1
        TNT1 A 4
        goto Ready

      FireFinishAmmo:
        LAYU ABCDE 1
        LAYR A 4
        goto Ready

      AltFire:
        LAYR B 8 A_PlaySound("doomguy/stronghold/minebeep",CHAN_WEAPON)
        LAYD ABCDE 1
        TNT1 A 10 A_JumpIfInventory("PowerWeaponLevel2",1,"AltFirePowered")
        LAYT A 1 A_PlaySound("doomguy/stronghold/minethrow",CHAN_WEAPON)
        LAYT BCDEFGH 1
        LAYT I 1 A_FireCustomMissile("SamsaraDoomguyStrLandmine2",0,true,0,8)
        LAYT JKLMNOPQR 1
        TNT1 A 10
      AltFireFinish:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"AltFireFinishAmmo")
        TNT1 A 0 A_JumpIfInventory("DoomguyStrMines",1,"AltFireFinishAmmo")
      AltFireFinishNoAmmo:
        TNT1 AAAAA 1
        TNT1 A 6
        goto Ready

      AltFireFinishAmmo:
        LAYU ABCDE 1
        LAYR A 6
        goto Ready

      // Powered
      DeselectPowered:
        TNT1 A 0 A_Lower
        goto DeselectContinue

      SelectPowered:
        TNT1 A 0 A_Raise
        goto SelectContinue

      FirePowered:
        TNT1 A 3
        TNT1 A 6 A_GiveInventory("SamsaraDoomguyStrLandMineLayerPoweredAttackHandler")
      FirePoweredFinish:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FirePoweredFinishAmmo")
        TNT1 A 0 A_JumpIfInventory("DoomguyStrMines",1,"FirePoweredFinishAmmo")
      FirePoweredFinishNoAmmo:
        TNT1 AAAAA 1
        TNT1 A 2
        goto Ready

      FirePoweredFinishAmmo:
        LAYU ABCDE 1
        LAYR A 2
        goto Ready

      AltFirePowered:
        TNT1 A 5
        LAYT A 1 A_PlaySound("doomguy/stronghold/minethrow",CHAN_WEAPON)
        LAYT BCDEFGH 1
        LAYT I 1 A_GiveInventory("SamsaraDoomguyStrLandMineLayerPoweredAltAttackHandler")
        LAYT JKLMNOPQR 1
        TNT1 A 5
        goto AltFireFinish
    }
}

actor SamsaraDoomguyStrLandMineLayerPoweredAttackHandler : Trigger
{
    States
    {
      Pickup:
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrPowerLandmine3B",-7,false)
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrPowerLandmine3B",7,false)
      PickupMainCheck:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupMain")
        TNT1 A 0 A_JumpIfInventory("DoomguyStrMines",1,"PickupMain")
        stop

      PickupMain:
        TNT1 A 0 A_TakeInventory("DoomguyStrMines",1,TIF_NOTAKEINFINITE)
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrPowerLandmine3A",0,false)
        stop
    }
}

actor SamsaraDoomguyStrLandMineLayerPoweredAltAttackHandler : Trigger
{
    States
    {
      Pickup:
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrPowerLandmine2B",-7,false,0,8)
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrPowerLandmine2B",7,false,0,8)
      PickupMainCheck:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupMain")
        TNT1 A 0 A_JumpIfInventory("DoomguyStrMines",1,"PickupMain")
        stop

      PickupMain:
        TNT1 A 0 A_TakeInventory("DoomguyStrMines",1,TIF_NOTAKEINFINITE)
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrPowerLandmine2A",0,false,0,8)
        stop
    }
}

actor SamsaraDoomguyStrLandmine
{
    Projectile
    Health 20
    Radius 8
    Height 5
    Damage 10
    DamageType "DoomDamageType"
    Mass 1000000
    Decal "Scorch"
    Tag "Land Mine"
    Obituary "%k killed %o with a land mine."
    Species "Landmine"
    +MOVEWITHSECTOR
    +NEVERTARGET
    //-NOBLOCKMAP
    +NOBLOOD
    -NOGRAVITY
    +NOTARGETSWITCH // don't replace ownership with whoever attacked it
    //+SHOOTABLE
    +THRUSPECIES
    States
    {
      Spawn:
        M_N3 A 0 NoDelay A_PlaySound("doomguy/stronghold/tink",CHAN_VOICE)
      SpawnLoop:
        M_N3 A 5
        M_N3 B 5 Light("LandMineSpawnB")
        loop

      Death:
        /*TNT1 A 0 A_ChangeFlag("NOBLOCKMAP",true)
        TNT1 A 0 A_UnSetShootable*/
        TNT1 A 0 A_PlaySound("doomguy/stronghold/mineexpl",CHAN_VOICE)
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_NoGravity
        TNT1 A 0 A_Jump(256,"DeathCheck")
      DeathCheck:
        TNT1 A 0 A_Explode(384,160,0)
      DeathFinish:
        DMSL B 8 Bright Light("ROCKET_X1")
        DMSL C 6 Bright Light("ROCKET_X2")
        DMSL D 4 Bright Light("ROCKET_X3")
        stop
    }
}

actor SamsaraDoomguyStrLandmine2 : SamsaraDoomguyStrLandmine
{
    Speed 15
    BounceType "Doom"
    BounceSound "doomguy/stronghold/tink"
    Decal ""
    /*+NOBLOCKMAP
    -SHOOTABLE*/
    States
    {
      Spawn:
        goto SpawnLoop

      Death:
        TNT1 A 0 A_StopSound
        TNT1 A 0 A_SpawnItemEx("SamsaraDoomguyStrLandmine",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
        stop
    }
}

actor SamsaraDoomguyStrLandmine3 : SamsaraDoomguyStrLandmine2 { Speed 2 }

actor SamsaraDoomguyStrPowerLandmineA : SamsaraDoomguyStrLandmine
{
    Damage 20
    States
    {
      DeathCheck:
        TNT1 A 0 A_Explode(768,320,0)
        goto DeathFinish
    }
}

actor SamsaraDoomguyStrPowerLandmineB : SamsaraDoomguyStrPowerLandmineA
{
    States
    {
      DeathCheck:
        TNT1 A 0 A_Explode(384,160,0)
        goto DeathFinish
    }
}

actor SamsaraDoomguyStrPowerLandmine2A : SamsaraDoomguyStrPowerLandmineA
{
    Speed 15
    Damage 10
    BounceType "Doom"
    BounceSound "doomguy/stronghold/tink"
    Decal ""
    /*+NOBLOCKMAP
    -SHOOTABLE*/
    States
    {
      Spawn:
        goto SpawnLoop

      Death:
        TNT1 A 0 A_StopSound
        TNT1 A 0 A_SpawnItemEx("SamsaraDoomguyStrPowerLandmineA",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
        stop
    }
}

actor SamsaraDoomguyStrPowerLandmine2B : SamsaraDoomguyStrPowerLandmine2A
{
    States
    {
      Death:
        TNT1 A 0 A_StopSound
        TNT1 A 0 A_SpawnItemEx("SamsaraDoomguyStrPowerLandmineB",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
        stop
    }
}

actor SamsaraDoomguyStrPowerLandmine3A : SamsaraDoomguyStrPowerLandmine2A { Speed 5 }
actor SamsaraDoomguyStrPowerLandmine3B : SamsaraDoomguyStrPowerLandmine2B { Speed 5 }

// Ammo

actor DoomguyStrMines : Ammo 25085
{
    Inventory.PickupMessage "Picked up some additional land mines."
    Inventory.Icon "AMSMO0"
    Inventory.Amount 5
    Inventory.MaxAmount 40
    Ammo.BackpackAmount 5
    Ammo.BackpackMaxAmount 40
    Tag "Land Mines"
    States
    {
      Spawn:
        M_N3 A -1
        stop
    }
}
