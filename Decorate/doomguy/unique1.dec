// Token

actor DGStrHasHomingRocketLauncher : Boolean {}

// Weapon

actor "Homing Rocket Launcher" : Weapon
{
    Inventory.PickupMessage "You got the homing rocket launcher!"
    Inventory.RestrictedTo "DoomguyPlayer"
    Weapon.SlotNumber 8
    Weapon.AmmoType "RocketAmmo"
    //Weapon.AmmoGive 4
    Weapon.AmmoUse 1
    Tag "Homing Rocket Launcher"
    +ALT_USES_BOTH // only one ammo type with same ammo amount use, so this makes alt fire behave as intended
    +NOAUTOFIRE
    +UNDROPPABLE
    States
    {
      Spawn:
        WHRL A -1
        stop

      // Unpowered
      Ready:
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPower",1,"ReadyPowered")
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPowerPerma",1,"ReadyPowered")
        HRLG A 1 A_WeaponReady(WRF_NOSECONDARY)
        loop

      Deselect:
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPower",1,"DeselectPowered")
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPowerPerma",1,"DeselectPowered")
      DeselectContinue:
        TNT1 A 0 A_Lower
        HRLG A 1 A_Lower
        goto Deselect

      Select:
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPower",1,"SelectPowered")
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPowerPerma",1,"SelectPowered")
      SelectContinue:
        TNT1 A 0 A_Raise
        HRLG A 1 A_Raise
        goto Select

      Fire:
        HRLG A 4
        HRLG C 5 Bright A_GunFlash
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPower",1,"FirePowered")
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPowerPerma",1,"FirePowered")
        TNT1 A 0 A_GiveInventory("SamsaraDoomguyStrHRLAttackHandler")
      FireFinish:
        HRLG D 6 Bright
        HRLG E 5 Bright
        HRLG F 4 Bright
        HRLG B 3 A_ReFire
        goto Ready

      Flash:
        TNT1 A 5 A_Light1
        TNT1 A 6 A_Light2
      FlashFinish:
        TNT1 A 5 A_Light1
        goto LightDone

      // Powered
      ReadyPowered:
        HRLG A 1 A_WeaponReady
        goto Ready

      DeselectPowered:
        TNT1 A 0 A_Lower
        goto DeselectContinue

      SelectPowered:
        TNT1 A 0 A_Raise
        goto SelectContinue

      FirePowered:
        TNT1 A 0 A_GiveInventory("SamsaraDoomguyStrHRLPoweredPrimaryAttackHandler")
        goto FireFinish

      AltFire:
        HRLG A 4
        HRLG C 5 Bright A_GunFlash
        TNT1 A 0 A_TakeInventory("SamsaraDoomguyStrHRLSecondaryPoweredAttackAmount") // reset
      AltFirePoweredLoop:
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrHRLSecondaryPoweredAttackAmount",0,"AltFireContinue")
        TNT1 A 0 A_GiveInventory("SamsaraDoomguyStrHRLSecondaryPoweredAttackAmount")
        TNT1 A 0 A_GunFlash("AltFlash2")
        HRLG D 4 Bright A_GiveInventory("SamsaraDoomguyStrHRLPoweredSecondaryAttackHandler")
        loop

      AltFireContinue:
        HRLG E 5 Bright
        HRLG F 4 Bright
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPower",1,"AltFireFinishReFire")
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPowerPerma",1,"AltFireFinishReFire") // so you can't exploit it
        TNT1 A 0 A_CheckReload
      AltFireFinish:
        HRLG B 3
        goto Ready

      AltFireFinishReFire:
        TNT1 A 0 A_JumpIfInventory("DoomClassMode",2,2)
        TNT1 A 0 A_CheckReload
        goto AltFireFinish
        TNT1 A 0 A_ReFire
        goto AltFireFinish

      AltFlash:
        TNT1 A 5 A_Light1
      AltFlash2:
        TNT1 A 4 A_Light2
        goto FlashFinish
    }
}

// Unpowered attack handler

actor SamsaraDoomguyStrHRLAttackHandler : Trigger
{
    States
    {
      Pickup:
      // Ammo check stuff (right)
      Pickup1:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"Pickup1TakeAmmo")
        TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,"Pickup1TakeAmmo")
        goto Pickup2

      // Ammo taking stuff (right)
      Pickup1TakeAmmo:
        TNT1 A 0 A_TakeInventory("RocketAmmo",1,TIF_NOTAKEINFINITE)
        goto Pickup1Attack

      // Attack stuff (right)
      Pickup1Attack:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"Pickup1AttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"Pickup1AttackCoop")
      Pickup1AttackDM:
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrHomRocket1",-4,false)
        goto Pickup2

      Pickup1AttackCoop:
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrHomRocket1Coop",-4,false)
        goto Pickup2

      // Ammo check stuff (left)
      Pickup2:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"Pickup2TakeAmmo")
        TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,"Pickup2TakeAmmo")
        stop

      // Ammo taking stuff (left)
      Pickup2TakeAmmo:
        TNT1 A 0 A_TakeInventory("RocketAmmo",1,TIF_NOTAKEINFINITE)
        goto Pickup2Attack

      // Attack stuff (left)
      Pickup2Attack:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"Pickup2AttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"Pickup2AttackCoop")
      Pickup2AttackDM:
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrHomRocket1",4,false)
        stop

      Pickup2AttackCoop:
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrHomRocket1Coop",4,false)
        stop
    }
}

// Powered attack handlers

actor SamsaraDoomguyStrHRLPoweredPrimaryAttackHandler : Trigger
{
    States
    {
      Pickup:
      // Ammo check stuff (right)
      Pickup1:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"Pickup1TakeAmmo")
        TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,"Pickup1TakeAmmo")
        goto Pickup2

      // Ammo taking stuff (right)
      Pickup1TakeAmmo:
        TNT1 A 0 A_TakeInventory("RocketAmmo",1,TIF_NOTAKEINFINITE)
        goto Pickup1Attack

      // Attack stuff (right)
      Pickup1Attack:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"Pickup1AttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"Pickup1AttackCoop")
      Pickup1AttackDM:
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrPowerHomRocket1",-4,false)
        goto Pickup2

      Pickup1AttackCoop:
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrPowerHomRocket1Coop",-4,false)
        goto Pickup2

      // Ammo check stuff (left)
      Pickup2:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"Pickup2TakeAmmo")
        TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,"Pickup2TakeAmmo")
        stop

      // Ammo taking stuff (left)
      Pickup2TakeAmmo:
        TNT1 A 0 A_TakeInventory("RocketAmmo",1,TIF_NOTAKEINFINITE)
        goto Pickup2Attack

      // Attack stuff (left)
      Pickup2Attack:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"Pickup2AttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"Pickup2AttackCoop")
      Pickup2AttackDM:
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrPowerHomRocket1",4,false)
        stop

      Pickup2AttackCoop:
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrPowerHomRocket1Coop",4,false)
        stop
    }
}

actor SamsaraDoomguyStrHRLPoweredSecondaryAttackHandler : Trigger
{
    States
    {
      // Ammo check stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupTakeAmmo")
        TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,"PickupTakeAmmo")
        stop

      // Ammo taking stuff
      PickupTakeAmmo:
        TNT1 A 0 A_TakeInventory("RocketAmmo",1,TIF_NOTAKEINFINITE)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrPowerHomRocket1",random(-600,600)/100.0,false,0,4.5,0,random(-150,150)/100.0)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrPowerHomRocket1Coop",random(-600,600)/100.0,false,0,4.5,0,random(-150,150)/100.0)
        stop
    }
}

// Homing rocket

actor SamsaraDoomguyStrHomRocket1 : Rocket
{
    Speed 30
    Damage 15
    DamageType "DoomDamageType"
    SeeSound "doomguy/stronghold/hrlfir"
    DeathSound "doomguy/stronghold/hrlexp"
    Decal "Scorch"
    Obituary "%k destroyed %o with the homing rocket launcher."
    -DEHEXPLOSION
    -RANDOMIZE
    States
    {
      Spawn:
        HMIS A -1 Bright Light("ROCKET")
        stop

      Death:
        TNT1 A 0 A_SetTranslucent(0.67,1)
        DMSL B 8 Bright Light("ROCKET_X1") A_Explode
        DMSL C 6 Bright Light("ROCKET_X2")
        DMSL D 4 Bright Light("ROCKET_X3")
        stop
    }
}

actor SamsaraDoomguyStrHomRocket1Coop : SamsaraDoomguyStrHomRocket1
{
    Species "Player"
    +SEEKERMISSILE
    +THRUSPECIES
    States
    {
      Spawn:
        HMIS A 4 Bright Light("ROCKET") A_SeekerMissile(10,25,SMF_LOOK)
        loop
    }
}

// Powered homing rocket

actor SamsaraDoomguyStrPowerHomRocket1 : SamsaraDoomguyStrHomRocket1
{
    Radius 11
    Height 8
    Speed 42
    Damage (80)
    Scale 1.25
    +FORCERADIUSDMG
    States
    {
      Spawn:
        DMSL E -1 Bright Light("POWERHOMROCKET")
        stop

      Death:
        TNT1 A 0 A_SetTranslucent(0.67,1)
        TNT1 A 0 A_Explode(80,240,0)
        TNT1 A 0 A_ChangeFlag("FORCERADIUSDMG",false)
        DMSL B 8 Bright Light("POWERHOMROCKET_X1") A_Explode(80,160)
        DMSL C 6 Bright Light("POWERHOMROCKET_X2")
        DMSL D 4 Bright Light("POWERHOMROCKET_X3")
        stop
    }
}

actor SamsaraDoomguyStrPowerHomRocket1Coop : SamsaraDoomguyStrPowerHomRocket1
{
    Species "Player"
    +SEEKERMISSILE
    +THRUSPECIES
    States
    {
      Spawn:
        DMSL E 2 Bright Light("POWERHOMROCKET") A_SeekerMissile(25,35,SMF_LOOK)
        loop
    }
}
