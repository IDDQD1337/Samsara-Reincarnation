actor SamsaraDoomguyStrAutoShotgunPoweredAttackAmount : Counter { Inventory.MaxAmount 4 }

actor " Shotgun " : Shotgun
{
    Weapon.AmmoType2 "DoomguyStrASGClip"
    Weapon.SlotNumber 2
    +AMMO_OPTIONAL
    +NOALERT
    +UNDROPPABLE
    AttackSound ""
	Tag "Shotgun"
    Obituary "$SAMSARA_DOOMGUY_OB_SLOT2"
	Inventory.RestrictedTo "DoomguyPlayer"
    States
    {
      Spawn:
        WSHT A -1
        stop

      // Classic
      Ready:
        TNT1 A 0 A_JumpIfInventory("DoomClassMode",2,"ReadyStr")
		TNT1 A 0 A_JumpIfInventory("Doom64Mode",1,"Ready64")
        DSHT A 1 A_WeaponReady(WRF_NOSECONDARY)
        loop

      Deselect:
        TNT1 A 0 A_JumpIfInventory("DoomClassMode",2,"DeselectStr")
		TNT1 A 0 A_JumpIfInventory("Doom64Mode",1,"Deselect64")
        DSHT A 1 A_Lower
        loop

      Select:
        TNT1 A 0 A_JumpIfInventory("DoomClassMode",2,"SelectStr")
		TNT1 A 0 A_JumpIfInventory("Doom64Mode",1,"Select64")
        DSHT A 1 A_Raise
        loop

      Fire:
        TNT1 A 0 A_JumpIfInventory("DoomClassMode",2,"FireStr")
        TNT1 A 0 A_JumpIfInventory("Doom64Mode",1,"Fire64")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Shell",1,1)
        goto Nothing
        DSHT A 3 A_AlertMonsters
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireAttack")
        TNT1 A 0 A_JumpIfInventory("Shell",1,"FireAttack")
        goto FireContinue

      FireAttack:
        TNT1 A 0 A_GunFlash
      FireContinue:
        TNT1 A 0 A_GiveInventory("SamsaraDoomguyShotgunAttackHandler")
        TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"FireSmooth")
      FireVanilla:
        DSHT A 7 Bright
        DSHT IJ 5
        DSHT K 4
        DSHT JI 5
        DSHT A 3
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Shell",1,1)
        goto Nothing
        DSHT A 7 A_ReFire
        goto Ready

      FireSmooth:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasDoubleFiringSpeed",1,"FireSmoothRage")
        DSHT A 2 Bright A_ZoomFactor(0.995,ZOOM_INSTANT)
        DSHT A 5 Bright A_ZoomFactor(1,ZOOM_INSTANT)
        DSHT BCD 1
        DSHT E 2
        DSHT FG 3
        DSHT HG 4
        DSHT F 3
        DSHT ED 2
        DSHT C 1
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Shell",1,1)
        goto Nothing
        DSHT B 3 A_ReFire
        DSHT A 4
        goto Ready

      FireSmoothRage:
        DSHT A 8 Bright
        DSHT CE 2
        DSHT FG 4
        DSHT H 6
        DSHT GF 4
        DSHT EDC 2
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Shell",1,1)
        goto Nothing
        DSHT B 2 A_ReFire
        DSHT A 4
        goto Ready

      Flash:
        DSHF A 3 Bright A_Light1
        DSHF B 4 Bright A_Light2
        goto LightDone

      // Doom 64 (has recoil visual)
      Ready64:
		TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"Ready64Smooth")
      Ready64Vanilla:
        64SG A 1 A_WeaponReady(WRF_NOSECONDARY)
        goto Ready

      Ready64Smooth:
        64SG D 1 A_WeaponReady(WRF_NOSECONDARY)
        goto Ready

      Deselect64:
		TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"Deselect64Smooth")
      Deselect64Vanilla:
        64SG A 1 A_Lower
        goto Deselect

      Deselect64Smooth:
        64SG D 1 A_Lower
        goto Deselect

      Select64:
		TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"Select64Smooth")
      Select64Vanilla:
        64SG A 1 A_Raise
        goto Select

      Select64Smooth:
        64SG D 1 A_Raise
        goto Select

      Fire64:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Shell",1,1)
        goto Nothing
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_GunFlash("Flash64")
        TNT1 A 0 A_GiveInventory("SamsaraDoomguy64ShotgunAttackHandler")
        TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"Fire64Smooth")
	  Fire64Vanilla:
        64SG A 5
        64SG C 21
        64SG B 5
        64SG A 7
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Shell",1,1)
        goto Nothing
        64SG A 7 A_ReFire
        goto Ready

      Fire64Smooth:
        64SG M 5 Bright
		64SG D 2
        64SG EFG 1
        64SG H 2
		64SG IJ 3
		64SG KJ 4
		64SG IH 3
		64SG G 3
        64SG F 3
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Shell",1,1)
        goto Nothing
        64SG E 3 A_ReFire
        64SG D 4
        goto Ready

      Flash64:
        64SG N 2 Bright A_Light1
        64SG N 2 Bright A_Light2
        goto LightDone

      // Stronghold (Auto Shotgun)
      ReadyStr:
        TNT1 A 0 A_JumpIfInventory("DoomguyStrASGClip",0,"ReadyStrNonReloadable")
        TNT1 A 0 A_JumpIfInventory("Shell",1,"ReadyStrReloadable")
      ReadyStrNonReloadable:
        ASGG A 1 A_WeaponReady(WRF_NOSECONDARY)
        goto Ready

      ReadyStrReloadable:
        ASGG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
        goto Ready

      // Unpowered
      DeselectStr:
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPower",1,"DeselectStrPowered")
		TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPowerPerma",1,"DeselectStrPowered")
      DeselectStrContinue:
        TNT1 A 0 A_Lower
        ASGG A 1 A_Lower
        goto Deselect

      SelectStr:
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPower",1,"SelectStrPowered")
		TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPowerPerma",1,"SelectStrPowered")
      SelectStrContinue:
        TNT1 A 0 A_Raise
        ASGG A 1 A_Raise
        goto Select

      FireStr:
        TNT1 A 0 A_JumpIfInventory("DoomguyStrASGClip",1,"FireStrActual")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStrActual")
        TNT1 A 0 A_JumpIfInventory("Shell",1,"Reload")
        ASGG A 2 A_PlaySound("doomguy/stronghold/asgout")
        goto Ready

      FireStrActual:
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPower",1,"FireStrPowered")
		TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPowerPerma",1,"FireStrPowered")
		ASGG A 1
        TNT1 A 0 A_GunFlash("FlashStr")
        TNT1 A 0 A_PlaySound("doomguy/stronghold/asgfir",CHAN_WEAPON)
        TNT1 A 0 A_TakeInventory("DoomguyStrASGClip",1,TIF_NOTAKEINFINITE)
        ASGG L 2 Bright A_FireBullets(3.1,2.6,7,5,"SamsaraDoomguyStrBulletPuffBallisticThruGhost",0)
        ASGG M 3 Bright
        ASGG N 2 Bright
        ASGG A 7 A_PlaySound("doomguy/stronghold/asgld1")
      FireStrFinish:
        TNT1 A 0 A_ReFire
        goto Ready

      FlashStr:
        TNT1 A 4 Bright A_Light1
        TNT1 A 3 Bright A_Light2
        goto LightDone

      AltFire:
      Reload:
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPower",1,"ReloadPowered")
		TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPowerPerma",1,"ReloadPowered")
        ASGG B 3
		ASGG CDEF 3
        ASGG G 3 A_PlaySound("doomguy/stronghold/asgout")
        ASGG HIJ 3
        ASGG K 15
        ASGG JIH 3
      Reloading:
        TNT1 A 0 A_JumpIfInventory("DoomguyStrASGClip",0,"ReloadFinish")
        TNT1 A 0 A_JumpIfInventory("Shell",1,"ReloadingAmmoCalc")
      ReloadFinish:
        ASGG G 3 A_PlaySound("doomguy/stronghold/asgin")
        ASGG FEDCB 3
        ASGG A 8 A_PlaySound("doomguy/stronghold/asgld1")
        goto Ready

      ReloadingAmmoCalc:
        TNT1 A 0 A_TakeInventory("Shell",1)
        TNT1 A 0 A_GiveInventory("DoomguyStrASGClip")
        goto Reloading

      // Powered
      DeselectStrPowered:
        TNT1 A 0 A_Lower
        goto DeselectStrContinue

      SelectStrPowered:
        TNT1 A 0 A_Raise
        goto SelectStrContinue

      FireStrPowered:
        TNT1 A 0 A_TakeInventory("SamsaraDoomguyStrAutoShotgunPoweredAttackAmount") // reset
      FireStrPoweredLoop:
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrAutoShotgunPoweredAttackAmount",0,"FireStrFinish")
        TNT1 A 0 A_GiveInventory("SamsaraDoomguyStrAutoShotgunPoweredAttackAmount")
        TNT1 A 0 A_GunFlash("FlashStrPowered")
        TNT1 A 0 A_PlaySound("doomguy/stronghold/asgfir",CHAN_WEAPON)
        TNT1 A 0 A_GiveInventory("SamsaraDoomguyStrAutoShotgunPoweredAttackHandler")
        ASGG LMN 1 Bright
        ASGG A 1 A_PlaySound("doomguy/stronghold/asgld1")
        loop

      FlashStrPowered:
        TNT1 A 2 Bright A_Light1
        TNT1 A 2 Bright A_Light2
        goto LightDone

      ReloadPowered:
        ASGG BCDEF 2
        ASGG G 3 A_PlaySound("doomguy/stronghold/asgout")
        ASGG HIJ 2
        ASGG K 10
        ASGG JIH 2
        goto Reloading

      Nothing:
        TNT1 A 0 A_SelectWeapon(" Pistol ")
        goto Deselect
    }
}

actor SamsaraDoomguyShotgunAttackHandler : Trigger
{
    States
    {
      // Ammo check stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupTakeAmmo")
        TNT1 A 0 A_JumpIfInventory("Shell",1,"PickupTakeAmmo")
        stop

      // Ammo taking stuff
      PickupTakeAmmo:
        TNT1 A 0 A_TakeInventory("Shell",1,TIF_NOTAKEINFINITE)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("doomguy/shotgf",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SamsaraImprovedDoomguyBalance",1,"PickupAttackImproved")
      // Vanilla
      PickupAttackVanilla:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackVanillaCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackVanillaCoop")
      PickupAttackVanillaDM:
        TNT1 A 0 A_FireBullets(5.6,0,7,5,"DoomBulletPuff2ThruGhost",FBF_NOFLASH)
        stop

      PickupAttackVanillaCoop:
        TNT1 A 0 A_FireBullets(5.6,0,7,5,"DoomBulletPuff2ThruGhost_Coop",FBF_NOFLASH)
        stop

      // Improved
      PickupAttackImproved:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackImprovedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackImprovedCoop")
      PickupAttackImprovedDM:
        TNT1 A 0 A_FireBullets(5.6,0,7,7,"DoomBulletPuff2ThruGhost",FBF_NOFLASH)
        stop

      PickupAttackImprovedCoop:
        TNT1 A 0 A_FireBullets(5.6,0,7,7,"DoomBulletPuff2ThruGhost_Coop",FBF_NOFLASH)
        stop
    }
}

actor SamsaraDoomguy64ShotgunAttackHandler : Trigger
{
    States
    {
      // Ammo check stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupTakeAmmo")
        TNT1 A 0 A_JumpIfInventory("Shell",1,"PickupTakeAmmo")
        stop

      // Ammo taking stuff
      PickupTakeAmmo:
        TNT1 A 0 A_TakeInventory("Shell",1,TIF_NOTAKEINFINITE)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("doom64guy/shotgf",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SamsaraImprovedDoomguyBalance",1,"PickupAttackImproved")
      // Vanilla
      PickupAttackVanilla:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackVanillaCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackVanillaCoop")
      PickupAttackVanillaDM:
        TNT1 A 0 A_FireBullets(5.6,0,7,5,"Doom64BulletPuffThruGhost",FBF_NOFLASH)
        goto PickupFinish

      PickupAttackVanillaCoop:
        TNT1 A 0 A_FireBullets(5.6,0,7,5,"Doom64BulletPuffThruGhost_Coop",FBF_NOFLASH)
        goto PickupFinish

      // Improved
      PickupAttackImproved:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackImprovedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackImprovedCoop")
      PickupAttackImprovedDM:
        TNT1 A 0 A_FireBullets(5.6,0,7,7,"Doom64BulletPuffThruGhost",FBF_NOFLASH)
        goto PickupFinish

      PickupAttackImprovedCoop:
        TNT1 A 0 A_FireBullets(5.6,0,7,7,"Doom64BulletPuffThruGhost_Coop",FBF_NOFLASH)
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 ACS_NamedExecuteAlways("SamsaraRecoil", 0, 2, 0, 20)
        stop
    }
}

actor SamsaraDoomguyStrAutoShotgunPoweredAttackHandler : Trigger
{
    States
    {
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrAutoShotgunPoweredAttackAmount",0,"PickupTakeAmmo")
      PickupContinue:
        TNT1 A 0 A_FireBullets(8.6,5.1,8,5,"SamsaraDoomguyStrElectricalPuff",FBF_NOFLASH) //Yes, I nerfed its accuracy. However, you get a chain-shotgun.
        stop

      PickupTakeAmmo:
        TNT1 A 0 A_TakeInventory("DoomguyStrASGClip",1,TIF_NOTAKEINFINITE)
        goto PickupContinue
    }
}

// Ammo

actor DoomguyStrASGClip : Ammo
{
    Inventory.MaxAmount 20
    Tag "Auto Shotgun Magazine"
    +IGNORESKILL
    +UNTOSSABLE
}
