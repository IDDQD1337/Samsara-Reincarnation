actor DGHasMinigun : Boolean {}
actor SamsaraDoomguyStrMinigunFiringSoundCheck : Boolean {}
actor SamsaraDoomguyStrMinigunPoweredAttackAmount : Counter { Inventory.MaxAmount 2 }

ACTOR " Minigun " : Weapon
{
	Radius 20
	Height 16
	Weapon.AmmoType "Clip" 
	Weapon.AmmoGive 0
	Weapon.AmmoUse 1
	Weapon.Selectionorder 700
	Weapon.Kickback 100
	Weapon.SlotNumber 4 // This line isn't in skulltag.pk3, which instead defines the slot directly in DoomPlayer
	Inventory.PickupMessage "$PICKUP_MINIGUN" // "You got the minigun!"
	Inventory.RestrictedTo "DoomguyPlayer"
	Obituary "$OB_MINIGUN" // "%o was drilled by %k's minigun."
	Tag "Minigun"
    +ALT_USES_BOTH
    +UNDROPPABLE
	States
	{
      Spawn:
        MNGN A -1
        loop

      // Classic
      Ready:
        TNT1 A 0 A_TakeInventory("SamsaraDoomguyStrMinigunFiringSoundCheck")
        TNT1 A 0 A_JumpIfInventory("DoomClassMode",2,"ReadyStr")
        MNGG A 1 A_WeaponReady(WRF_NOSECONDARY)
        loop

      Deselect:
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrMinigunFiringSoundCheck",1,"DeselectStopSound")
      DeselectStart:
        TNT1 A 0 A_JumpIfInventory("DoomClassMode",2,"DeselectStartStr")
        MNGG A 1 A_Lower
        goto Deselect

      DeselectStopSound:
        TNT1 A 0 A_PlaySound("silence",CHAN_6)
        TNT1 A 0 A_StopSound(CHAN_6)
        TNT1 A 0 A_TakeInventory("SamsaraDoomguyStrMinigunFiringSoundCheck")
        goto DeselectStart

      Select:
        TNT1 A 0 A_TakeInventory("SamsaraDoomguyStrMinigunFiringSoundCheck")
        TNT1 A 0 A_JumpIfInventory("DoomClassMode",2,"SelectStr")
        MNGG A 1 A_Raise 
        loop

      Fire:
        TNT1 A 0 A_JumpIfInventory("DoomClassMode",2,"FireStr")
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrMinigunFiringSoundCheck",1,"FireStopSound")
      FireStart:
        TNT1 A 0 A_GunFlash
        MNGG A 2 A_GiveInventory("SamsaraDoomguyMinigunAttackHandler")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"Fire2Attack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"Fire2Attack")
        goto Fire2
      Fire2Attack:
        TNT1 A 0 A_GunFlash("Flash2")
      Fire2:
        MNGG B 2 A_GiveInventory("SamsaraDoomguyMinigunAttackHandler")
      FireFinish:
        MNGG A 2 A_ReFire
        MNGG B 2
        MNGG AB 4
        MNGG AB 8
        goto Ready

      FireStopSound:
        TNT1 A 0 A_PlaySound("silence",CHAN_6)
        TNT1 A 0 A_StopSound(CHAN_6)
        TNT1 A 0 A_TakeInventory("SamsaraDoomguyStrMinigunFiringSoundCheck")
        goto FireStart

      Flash:
        MNGF A 3 Bright A_Light1
        goto LightDone

      Flash2:
        MNGF B 3 Bright A_Light2
        goto LightDone

      // Stronghold
      // Unpowered
      ReadyStr:
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPower",1,"ReadyStrPowered")
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPowerPerma",1,"ReadyStrPowered")
        MNGG A 1 A_WeaponReady(WRF_NOSECONDARY)
        goto Ready

      DeselectStartStr:
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPower",1,"DeselectStartStrPowered")
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPowerPerma",1,"DeselectStartStrPowered")
      DeselectStartStrContinue:
        TNT1 A 0 A_Lower
        MNGG A 1 A_Lower
        goto Deselect

      SelectStr:
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPower",1,"SelectStrPowered")
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPowerPerma",1,"SelectStrPowered")
      SelectStrContinue:
        TNT1 A 0 A_Raise
        MNGG A 1 A_Raise
        goto Select

      FireStr:
        TNT1 A 0 A_PlaySound("doomguy/stronghold/minigunloop",CHAN_6,1,true)
        TNT1 A 0 A_GiveInventory("SamsaraDoomguyStrMinigunFiringSoundCheck")
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPower",1,"FireStrPowered")
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPowerPerma",1,"FireStrPowered")
        TNT1 A 0 A_GunFlash("FlashStr")
        TNT1 A 0 A_PlaySound("doomguy/stronghold/minigun",CHAN_WEAPON)
        MNGG A 2 A_FireBullets(4,2.3,1,5,"SamsaraDoomguyStrBulletPuffBallisticThruGhost")
        TNT1 A 0 A_GunFlash("FlashStr2")
        TNT1 A 0 A_PlaySound("doomguy/stronghold/minigun",CHAN_WEAPON)
        MNGG B 2 A_FireBullets(4,2.3,1,5,"SamsaraDoomguyStrBulletPuffBallisticThruGhost")
      FireStrFinish:
        TNT1 A 0 A_ReFire
      FireStrFinishAnim:
        TNT1 A 0 A_PlaySound("doomguy/stronghold/minigundown",CHAN_6)
        MNGG A 2 A_TakeInventory("SamsaraDoomguyStrMinigunFiringSoundCheck")
        MNGG B 2
        MNGG AB 4
        MNGG AB 8
        goto Ready

      FlashStr:
        MNGF A 3 Bright A_Light1
        goto LightDone

      FlashStr2:
        MNGF B 3 Bright A_Light2
        goto LightDone

      // Powered
      ReadyStrPowered:
        MNGG A 1 A_WeaponReady
        goto Ready

      DeselectStartStrPowered:
        TNT1 A 0 A_Lower
        goto DeselectStartStrContinue

      SelectStrPowered:
        TNT1 A 0 A_Raise
        goto SelectStrContinue

      FireStrPowered:
        TNT1 A 0 A_TakeInventory("SamsaraDoomguyStrMinigunPoweredAttackAmount") // reset
      FireStrPoweredLoop:
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrMinigunPoweredAttackAmount",0,"FireStrPoweredFinish")
        TNT1 A 0 A_GiveInventory("SamsaraDoomguyStrMinigunPoweredAttackAmount")
        TNT1 A 0 A_GunFlash("FlashStrPowered")
        TNT1 A 0 A_PlaySound("doomguy/stronghold/minigun",CHAN_WEAPON)
        MNGG A 1 A_FireBullets(4,2.3,2,10,"SamsaraDoomguyStrElectricalPuff",0)
        TNT1 A 0 A_GunFlash("FlashStrPowered2")
        TNT1 A 0 A_PlaySound("doomguy/stronghold/minigun",CHAN_WEAPON)
        MNGG B 1 A_FireBullets(4,2.3,2,10,"SamsaraDoomguyStrElectricalPuff")
        loop

      FireStrPoweredFinish:
        TNT1 A 0 A_ReFire
      FireStrPoweredFinishAnim:
        TNT1 A 0 A_PlaySound("doomguy/stronghold/minigundown",CHAN_6)
        MNGG A 2 A_TakeInventory("SamsaraDoomguyStrMinigunFiringSoundCheck")
        MNGG B 2
        MNGG AB 4 //Extremely shortened refire delay
        goto Ready

      AltFire:
        TNT1 A 0 A_PlaySound("doomguy/stronghold/minigunloop",CHAN_6,1,true)
        TNT1 A 0 A_GiveInventory("SamsaraDoomguyStrMinigunFiringSoundCheck")
        TNT1 A 0 A_TakeInventory("SamsaraDoomguyStrMinigunPoweredAttackAmount") // reset
      AltFireLoop:
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrMinigunPoweredAttackAmount",0,"AltFireFinish")
        TNT1 A 0 A_GiveInventory("SamsaraDoomguyStrMinigunPoweredAttackAmount")
        TNT1 A 0 A_GunFlash("FlashStrPowered")
        TNT1 A 0 A_PlaySound("doomguy/stronghold/minigun",CHAN_WEAPON)
        MNGG A 1 A_FireBullets(8,4.6,4,10,"SamsaraDoomguyStrElectricalPuff")
        TNT1 A 0 A_GunFlash("FlashStrPowered2")
        TNT1 A 0 A_PlaySound("doomguy/stronghold/minigun",CHAN_WEAPON)
        MNGG B 1 A_FireBullets(8,4.6,4,10,"SamsaraDoomguyStrElectricalPuff")
        loop

      AltFireFinish:
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPower",1,"AltFireFinishReFire")
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPowerPerma",1,"AltFireFinishReFire")
        TNT1 A 0 A_CheckReload
        goto FireStrFinishAnim

      AltFireFinishReFire:
        TNT1 A 0 A_JumpIfInventory("DoomClassMode",2,2)
        TNT1 A 0 A_CheckReload
        goto FireStrFinishAnim
        TNT1 A 0 A_ReFire
        goto FireStrFinishAnim

      FlashStrPowered:
        MNGF A 1 Bright A_Light1
        MNGF A 1 Bright
        goto LightDone

      FlashStrPowered2:
        MNGF B 1 Bright A_Light2
        MNGF B 1 Bright
        goto LightDone
	}
}

actor SamsaraDoomguyMinigunAttackHandler : Trigger
{
    States
    {
      // Ammo check stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupTakeAmmo")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"PickupTakeAmmo")
        stop

      // Ammo taking stuff
      PickupTakeAmmo:
        TNT1 A 0 A_TakeInventory("Clip",1,TIF_NOTAKEINFINITE)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("weapons/minigun",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SamsaraImprovedDoomguyBalance",1,"PickupAttackImproved")
      // Vanilla
      PickupAttackVanilla:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackVanillaCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackVanillaCoop")
      PickupAttackVanillaDM:
        TNT1 A 0 A_FireBullets(5.6,0,1,5,"DoomBulletPuff2ThruGhost",FBF_NOFLASH)
        stop

      PickupAttackVanillaCoop:
        TNT1 A 0 A_FireBullets(5.6,0,1,5,"DoomBulletPuff2ThruGhost_Coop",FBF_NOFLASH)
        stop

      // Improved
      PickupAttackImproved:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackImprovedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackImprovedCoop")
      PickupAttackImprovedDM:
        TNT1 A 0 A_FireBullets(5.6,0,1,random(8,10)*2,"DoomBulletPuff2ThruGhost",FBF_NORANDOM|FBF_NOFLASH)
        stop

      PickupAttackImprovedCoop:
        TNT1 A 0 A_FireBullets(5.6,0,1,random(8,10)*2,"DoomBulletPuff2ThruGhost_Coop",FBF_NORANDOM|FBF_NOFLASH)
        stop
    }
}
