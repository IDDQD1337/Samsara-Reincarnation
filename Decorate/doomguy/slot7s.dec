actor DGHasBFG10K : Boolean {}

ACTOR " BFG10K " : Weapon
{
	Radius 20
	Height 20
	Weapon.Selectionorder 2800
	Weapon.AmmoUse 5
	Weapon.AmmoGive 0
	Weapon.AmmoType "Cell"
	Weapon.Kickback 100
	Inventory.Pickupmessage "$PICKUP_BFG10K"
	Inventory.RestrictedTo "DoomguyPlayer"
	+WEAPON.NOAUTOFIRE
	+WEAPON.NOLMS
	Tag "BFG10000"
	States
	{
	Spawn:
		BFG2 A -1
		stop
	Ready:
		BG2G A 0 A_PlaySound("weapons/bfg10kidle")
		BG2G AAABBBCCCDDD 1 A_WeaponReady
		loop
	Deselect:
		BG2G E 1 A_Lower
		loop
	Select:
		BG2G E 1 A_Raise
		loop
	Fire:
		BG2G E 20 A_PlaySound("weapons/bfg10kf")
		BG2G F 4
		BG2G GHIJ 1
	Hold:
		BG2G K 2 A_GunFlash
		BG2G L 2 A_GiveInventory("SamsaraDoomguyBFG10KAttackHandler")
		BG2G M 2
		TNT1 A 0 A_ReFire
		BG2G O 35 A_PlaySound("weapons/bfg10kcool")
		goto Ready
	Flash:
		TNT1 A 2 Bright A_Light1
		TNT1 A 3 Bright
		goto LightDone
	}
}

actor SamsaraDoomguyBFG10KAttackHandler : Trigger
{
    States
    {
      // Ammo check stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupTakeAmmo")
        TNT1 A 0 A_JumpIfInventory("Cell",5,"PickupTakeAmmo")
        stop

      // Ammo taking stuff
      PickupTakeAmmo:
        TNT1 A 0 A_TakeInventory("Cell",5,TIF_NOTAKEINFINITE)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireBullets(0,0,1,0,"ST_BFG10kShot",FBF_NOFLASH)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireBullets(0,0,1,0,"ST_BFG10kShot_Coop",FBF_NOFLASH)
        stop
    }
}

Actor ST_BFG10kShot
{
  Radius 11
  Height 8
  Speed 20
  DamageType "DoomDamageType"
  +NoBlockMap
  +NoGravity
  +ActivateImpact
  +ActivatePCross
  +NoTeleport
  +PuffOnActors
  +PuffGetsOwner
  +ForceRadiusDmg
  Renderstyle Add
  Alpha 0.75
  SeeSound "Weapons/BFG10KX"
  AttackSound "Weapons/BFG10KX"
  States
  {
  Spawn:
    DBFE A 0 Bright
    DBFE A 3 Bright A_Explode(160,160)
    DBFE BCDEF 3 Bright
    stop
  }
}

Actor ST_BFG10kShot_Coop : ST_BFG10kShot { Species "Player" +THRUSPECIES +MTHRUSPECIES +DONTHARMSPECIES }
