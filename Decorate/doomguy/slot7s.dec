actor DGHasBFG10K : Boolean {}

ACTOR " BFG10K " : Weapon
{
	Radius 20
	Height 20
	Weapon.Selectionorder 2800
	Weapon.AmmoUse 5
	Weapon.AmmoGive 0
	Weapon.AmmoType "Cell"
	Weapon.Kickback 100
	Inventory.Pickupmessage "You got the BFG10000! Hell, yes!"
	Inventory.RestrictedTo "DoomguyPlayer"
    Obituary "%k blasted %o with the BFG10K."
	+WEAPON.NOAUTOFIRE
	+WEAPON.NOLMS
    +UNDROPPABLE
	Tag "BFG10000"
	States
	{
	Spawn:
		WBG2 A -1
		stop
	Ready:
		TNT1 A 0 A_PlaySound("doomguy/bfg10kidle")
		DBG2 AAABBBCCCDDD 1 A_WeaponReady
		loop
	Deselect:
		DBG2 E 1 A_Lower
		loop
	Select:
		DBG2 E 1 A_Raise
		loop
	Fire:
		DBG2 E 20 A_PlaySound("doomguy/bfg10kf")
		DBG2 F 4
		DBG2 GHIJ 1
	Hold:
		DBG2 K 2 A_GunFlash
		DBG2 L 2 A_GiveInventory("SamsaraDoomguyBFG10KAttackHandler")
		DBG2 M 2
		TNT1 A 0 A_ReFire
		DBG2 O 35 A_PlaySound("doomguy/bfg10kcool")
		goto Ready
	Flash:
		TNT1 A 2 Bright A_Light1
		TNT1 A 3 Bright
		goto LightDone
	}
}

actor SamsaraDoomguyBFG10KAttackHandler : Trigger
{
    States
    {
      // Ammo check stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupTakeAmmo")
        TNT1 A 0 A_JumpIfInventory("Cell",5,"PickupTakeAmmo")
        stop

      // Ammo taking stuff
      PickupTakeAmmo:
        TNT1 A 0 A_TakeInventory("Cell",5,TIF_NOTAKEINFINITE)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireBullets(0,0,1,0,"ST_BFG10kShot",FBF_NOFLASH)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireBullets(0,0,1,0,"ST_BFG10kShot_Coop",FBF_NOFLASH)
        stop
    }
}

Actor ST_BFG10kShot
{
  Radius 11
  Height 8
  Speed 20
  DamageType "DoomDamageType"
  +NoBlockMap
  +NoGravity
  +ActivateImpact
  +ActivatePCross
  +NoTeleport
  +PuffOnActors
  +PuffGetsOwner
  +ForceRadiusDmg
  Renderstyle Add
  Alpha 0.75
  SeeSound "doomguy/bfg10kx"
  AttackSound "doomguy/bfg10kx"
  Decal "BFGLightning"
  States
  {
  Spawn:
    DBFE A 0 Bright
    DBFE A 3 Bright A_Explode(160,160)
    DBFE BCDEF 3 Bright
    stop
  }
}

Actor ST_BFG10kShot_Coop : ST_BFG10kShot { Species "Player" +THRUSPECIES +MTHRUSPECIES +DONTHARMSPECIES }
