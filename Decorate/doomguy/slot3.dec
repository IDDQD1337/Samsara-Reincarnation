actor SamsaraDoomguyStrSuperShotgunSecondaryPoweredAttackAmount : Counter { Inventory.MaxAmount 2 }

actor "Super Shotgun" : SuperShotgun
{
    Weapon.AmmoType "Shell"
    Decal BulletChip
    Weapon.SlotNumber 3
    +INVENTORY.UNDROPPABLE
    AttackSound ""
	Tag "Super Shotgun"
    Obituary "$SAMSARA_DOOMGUY_OB_SLOT3"
	Inventory.RestrictedTo "DoomguyPlayer"
    States
    {
      // Classic
      Ready:
		TNT1 A 0 A_JumpIfInventory("Doom64Mode",1,"Ready64")
        DSSG A 1 A_WeaponReady
        loop
	
      Deselect:
		TNT1 A 0 A_JumpIfInventory("Doom64Mode",1,"Deselect64")
        DSSG A 1 A_Lower
        loop

      Select:
		TNT1 A 0 A_JumpIfInventory("Doom64Mode",1,"Select64")
        DSSG A 1 A_Raise
        loop

      Fire:
        DSSG A 3 A_JumpIfInventory("Doom64Mode",1,"Fire64")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireAttack")
        TNT1 A 0 A_JumpIfInventory("Shell",2,"FireAttack")
        goto FireContinue

      FireAttack:
        TNT1 A 0 A_GunFlash
      FireContinue:
        TNT1 A 0 A_GiveInventory("SamsaraDoomguySuperShotgunAttackHandler")
        TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"FireSmooth")
      FireVanilla:
        DSSG A 7 Bright
        DSG2 B 7
        DSG2 C 7 A_CheckReload
        DSG2 D 7 A_PlaySound("doomguy/sshoto",CHAN_WEAPON)
        DSG2 E 7
        DSG2 F 7 A_PlaySound("doomguy/sshotl",CHAN_WEAPON)
        DSG2 G 6
        TNT1 A 0 A_PlaySound("doomguy/sshotc",CHAN_WEAPON)
        DSG2 H 6 A_ReFire
        DSSG A 5 A_ReFire
        goto Ready

      FireSmooth:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasDoubleFiringSpeed",1,"FireSmoothRage")
        DSSG A 2 Bright A_ZoomFactor(0.985,ZOOM_INSTANT)
        DSSG A 4 Bright A_ZoomFactor(1,ZOOM_INSTANT)
        DSSG A 3
        DSSG B 2
        DSSG C 3
        DSSG D 4 A_CheckReload
        DSSG E 4
        DSSG F 2 A_PlaySound("doomguy/sshoto",CHAN_WEAPON)
        DSSG GHIJ 2
        DSSG K 3
        DSSG L 3 A_PlaySound("doomguy/sshotl",CHAN_WEAPON)
        DSSG MNOP 2
        DSSG Q 1
        DSSG Q 1 A_PlaySound("doomguy/sshotc",CHAN_WEAPON)
        DSSG R 3 A_ReFire
        DSSG S 3
        DSSG T 3 A_ReFire
        DSSG A 2
        goto Ready

      FireSmoothRage:
        DSSG A 6 Bright
        DSSG A 4
        DSSG B 2
        DSSG C 4
        DSSG D 4 A_CheckReload
        DSSG E 4
        DSSG F 2 A_PlaySound("doomguy/sshoto",CHAN_WEAPON)
        DSSG GHIJJK 2
        DSSG L 4 A_PlaySound("doomguy/sshotl",CHAN_WEAPON)
        DSSG MNOOP 2
        DSSG Q 2 A_PlaySound("doomguy/sshotc",CHAN_WEAPON)
        DSSG R 2 A_ReFire
        DSSG S 4
        DSSG T 4 A_ReFire
        DSSG A 2
        goto Ready

      Flash:
        DSSF A 3 Bright A_Light1
        DSSF B 3 Bright A_Light2
        goto LightDone

      // Doom 64 (has recoil visual and kickback)
      Ready64:
		TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"Ready64Smooth")
      Ready64Vanilla:
        64DB A 1 A_WeaponReady
        goto Ready

      Ready64Smooth:
        64DB D 1 A_WeaponReady
        goto Ready

      Deselect64:
		TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"Deselect64Smooth")
      Deselect64Vanilla:
        64DB A 1 A_Lower
        goto Deselect

      Deselect64Smooth:
        64DB D 1 A_Lower
        goto Deselect

      Select64:
		TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"Select64Smooth")
      Select64Vanilla:
        64DB A 1 A_Raise
        goto Select

      Select64Smooth:
        64DB D 1 A_Raise
        goto Select

      Fire64:
        TNT1 A 0 A_GunFlash("Flash64")
        TNT1 A 0 A_GiveInventory("SamsaraDoomguy64SuperShotgunAttackHandler")
		TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"Fire64Smooth")
	  Fire64Vanilla:
        64DB A 5
        64DB C 10
        64DB C 23 A_CheckReload
        64DB B 6 A_PlaySound("doom64guy/sshoto",CHAN_WEAPON)
        64DB A 6 A_PlaySound("doom64guy/sshotc",CHAN_WEAPON)
        64DB A 5 A_ReFire
        goto Ready

      Fire64Smooth:
        64DB Y 5 Bright
		64DB EF 3
		64DB GH 2
		64DB I 2 A_CheckReload
		64DB JK 2
        64DB L 3 A_PlaySound("doom64guy/sshoto",CHAN_WEAPON)
		64DB M 4 
        64DB N 3 
		64DB O 4 A_PlaySound("doom64guy/sshotl",CHAN_WEAPON)
		64DB P 3
        64DB Q 4 A_PlaySound("doom64guy/sshotc",CHAN_WEAPON)
		64DB RTU 2
		64DB VX 1
        64DB D 5 A_ReFire
        goto Ready

      Flash64:
        64DM A 2 Bright A_Light1
        64DM A 2 Bright A_Light2
        goto LightDone		

      Spawn:
        WSSG A -1
        stop
    }
}

actor SamsaraDoomguySuperShotgunAttackHandler : Trigger
{
    States
    {
      // Ammo check stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupTakeAmmo")
        TNT1 A 0 A_JumpIfInventory("Shell",2,"PickupTakeAmmo")
        stop

      // Ammo taking stuff
      PickupTakeAmmo:
        TNT1 A 0 A_TakeInventory("Shell",2,TIF_NOTAKEINFINITE)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("doomguy/sshotf",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireBullets(11.2,7.1,20,5,"DoomBulletPuff2ThruGhost",FBF_NOFLASH)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireBullets(11.2,7.1,20,5,"DoomBulletPuff2ThruGhost_Coop",FBF_NOFLASH)
        stop
    }
}

actor SamsaraDoomguy64SuperShotgunAttackHandler : Trigger
{
    States
    {
      // Ammo check stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupTakeAmmo")
        TNT1 A 0 A_JumpIfInventory("Shell",2,"PickupTakeAmmo")
        stop

      // Ammo taking stuff
      PickupTakeAmmo:
        TNT1 A 0 A_TakeInventory("Shell",2,TIF_NOTAKEINFINITE)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("doom64guy/sshotf",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireBullets(11.2,7.1,20,5,"Doom64BulletPuffThruGhost",FBF_NOFLASH)
        goto PickupFinish

      PickupAttackCoop:
        TNT1 A 0 A_FireBullets(11.2,7.1,20,5,"Doom64BulletPuffThruGhost_Coop",FBF_NOFLASH)
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 A_Recoil(2)
        TNT1 A 0 ACS_NamedExecuteAlways("SamsaraRecoil", 0, 2, 0, 20)
        stop
    }
}
