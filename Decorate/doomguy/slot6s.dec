actor DGHasRailGun : Boolean {}
actor SamsaraDoomguyStrRailgunScope : Boolean {}
actor SamsaraDoomguyStrRailgunPoweredHeldCheck : Boolean {}

ACTOR " RailGun " : Weapon
{
	Radius 20
	Height 16
	Weapon.Selectionorder 100
    Weapon.AmmoType1 "DoomguyRailgunClip"
    Weapon.AmmoUse1 1
    Weapon.AmmoType2 "Cell"
    //Weapon.AmmoGive2 40
	Inventory.Pickupmessage "You got the railgun!"
	Inventory.RestrictedTo "DoomguyPlayer"
    Decal "RailScorch"
	Obituary "%k railed %o."
	Tag "Railgun"
    +AMMO_OPTIONAL
    +NOALERT
    +UNDROPPABLE
    States
    {
      Spawn:
        WRLG A -1
        stop

      // Classic
      Ready:
        TNT1 A 0 A_JumpIfInventory("DoomClassMode",2,"ReadyStr")
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrRailgunScope",1,"ReadyZoomOut")
        goto ReadyContinue

      ReadyZoomOut:
        TNT1 A 0 A_ZoomFactor(1)
        TNT1 A 0 A_TakeInventory("SamsaraDoomguyStrRailgunScope")
        goto ReadyContinue

      ReadyContinue:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",0,"ReadyModernReloadable")
      ReadyNonReloadable:
        DRLG A 1 A_WeaponReady(WRF_NOSECONDARY)
        goto Ready

      ReadyModernReloadable:
        TNT1 A 0 A_JumpIfInventory("DoomguyRailgunClip",0,"ReadyNonReloadable")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Cell",10,1)
        goto ReadyNonReloadable
        DRLG A 1 A_WeaponReady(WRF_NOSECONDARY|WRF_ALLOWRELOAD)
        goto Ready

      Deselect:
        TNT1 A 0 A_JumpIfInventory("DoomClassMode",2,"DeselectStr")
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrRailgunScope",1,"DeselectZoomOut")
      DeselectContinue:
        DRLG A 1 A_Lower
        goto Deselect

      DeselectZoomOut:
        TNT1 A 0 A_ZoomFactor(1)
        TNT1 A 0 A_TakeInventory("SamsaraDoomguyStrRailgunScope")
        goto DeselectContinue

      Select:
        TNT1 A 0 A_JumpIfInventory("DoomClassMode",2,"SelectStr")
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrRailgunScope",1,"SelectZoomOut")
      SelectContinue:
        DRLG A 1 A_Raise
        goto Select

      SelectZoomOut:
        TNT1 A 0 A_ZoomFactor(1,ZOOM_INSTANT) // map change
        TNT1 A 0 A_TakeInventory("SamsaraDoomguyStrRailgunScope")
        goto SelectContinue

      Fire:
        TNT1 A 0 A_JumpIfInventory("DoomClassMode",2,"FireStr")
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"FireReloadable")
      FireNonReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart")
        TNT1 A 0 A_JumpIfInventory("Cell",10,"FireStart")
        goto Nothing

      FireReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireStart")
        TNT1 A 0 A_JumpIfInventory("DoomguyRailgunClip",1,"FireStart")
      FireReloadableDryCheck:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"Reload")
        TNT1 A 0 A_JumpIfInventory("Cell",10,"Reload")
        goto Nothing

      FireStart:
        TNT1 A 0 A_AlertMonsters
        DRLG E 12 A_GiveInventory("SamsaraDoomguyRailgunAttackHandler")
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"FireFinishReloadable")
        goto FireFinish

      FireFinishReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireFinish")
        TNT1 A 0 A_JumpIfInventory("DoomguyRailgunClip",1,"FireFinish")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReloadStart")
        TNT1 A 0 A_JumpIfInventory("Cell",10,"ReloadStart")
        goto FireFinish

      FireFinish:
        DRLG A 6
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"FireReFireReloadable")
        goto FireReFireNonReloadable

      FireReFire:
        TNT1 A 0 A_ReFire
        goto Ready

      FireReFireNonReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireReFire")
        TNT1 A 0 A_JumpIfInventory("Cell",10,"FireReFire")
        goto Nothing

      FireReFireReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireReFire")
        TNT1 A 0 A_JumpIfInventory("DoomguyRailgunClip",1,"FireReFire")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireReFire")
        TNT1 A 0 A_JumpIfInventory("Cell",10,"FireReFire")
        goto Nothing

      Flash:
        TNT1 A 5 Bright A_Light1
        TNT1 A 5 Bright A_Light2
        goto LightDone

      Reload:
        DRLG E 6
      ReloadStart:
        DRLG F 6 A_PlaySound("doomguy/railgu")
        DRLG GHIJ 6
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReloadAmmo")
      ReloadFinish:
        DRLG K 6 A_PlaySound("doomguy/railgl")
        DRLG L 6
        goto FireFinish

      ReloadAmmo:
        TNT1 A 0 A_JumpIfInventory("DoomguyRailgunClip",0,"ReloadFinish")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Cell",10,1)
        goto ReloadFinish
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_TakeInventory("Cell",10)
        TNT1 A 0 A_GiveInventory("DoomguyRailgunClip")
        loop

      // Stronghold (its own version)
      ReadyStr:
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrRailgunScope",1,"ReadyStrZoom")
        RAIG A 1 A_WeaponReady(WRF_ALLOWZOOM)
        goto Ready

      ReadyStrZoom:
        RSCP A 1 Bright A_WeaponReady(WRF_NOBOB|WRF_ALLOWZOOM)
        goto Ready

      // Unpowered
      DeselectStr:
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrRailgunScope",1,"DeselectStrZoomOut")
      DeselectStrStart:
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPower",1,"DeselectStrStartPowered")
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPowerPerma",1,"DeselectStrStartPowered")
      DeselectStrContinue:
        TNT1 A 0 A_Lower
        RAIG A 1 A_Lower
        goto Deselect

      DeselectStrZoomOut:
        TNT1 A 0 A_PlaySound("doomguy/stronghold/railzoomout")
        TNT1 A 0 A_ZoomFactor(1)
        TNT1 A 0 A_TakeInventory("SamsaraDoomguyStrRailgunScope")
        goto DeselectStrStart

      SelectStr:
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrRailgunScope",1,"SelectStrZoomOut")
      SelectStrStart:
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPower",1,"SelectStrStartPowered")
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPowerPerma",1,"SelectStrStartPowered")
      SelectStrContinue:
        TNT1 A 0 A_Raise
        RAIG A 1 A_Raise
        goto Select

      SelectStrZoomOut:
        // no need for a sound
        TNT1 A 0 A_ZoomFactor(1,ZOOM_INSTANT) // map change
        TNT1 A 0 A_TakeInventory("SamsaraDoomguyStrRailgunScope")
        goto SelectStrStart

      AltFire:
      Zoom:
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrRailgunScope",1,"Unzoom")
        TNT1 A 0 A_PlaySound("doomguy/stronghold/railzoomin")
        TNT1 A 0 A_ZoomFactor(3)
        RSCP A 8 Bright A_GiveInventory("SamsaraDoomguyStrRailgunScope")
        goto Ready

      Unzoom:
        TNT1 A 0 A_PlaySound("doomguy/stronghold/railzoomout")
        TNT1 A 0 A_ZoomFactor(1)
        RAIG A 8 A_TakeInventory("SamsaraDoomguyStrRailgunScope")
        goto Ready

      FireStr:
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPower",1,"FireStrPowered")
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPowerPerma",1,"FireStrPowered")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStrCharge")
        TNT1 A 0 A_JumpIfInventory("Cell",5,"FireStrCharge")
        goto Nothing

      FireStrCharge:
        TNT1 A 0 A_PlaySound("doomguy/stronghold/railch",CHAN_WEAPON)
      FireStrCharge0:
        RAIG B 12 Bright A_JumpIfInventory("SamsaraDoomguyStrRailgunScope",1,"FireStrCharge0Zoomed")
        TNT1 A 0 A_WeaponReady(WRF_NOBOB|WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_ReFire("FireStrCharge0Check")
        RAIG E 4 Bright A_GunFlash("FlashStr")
        TNT1 A 0 A_AlertMonsters
        RAIG F 6 Bright A_GiveInventory("SamsaraDoomguyStrRailgunCharge0AttackHandler")
      FireStrChargeFinish:
        RAIG A 4
        RAIG K 15
        goto Ready

      FireStrCharge0Zoomed:
        RSCP A 12 Bright
        TNT1 A 0 A_WeaponReady(WRF_NOBOB|WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_ReFire("FireStrCharge0Check")
        RSCP A 4 Bright A_GunFlash("FlashStr")
        TNT1 A 0 A_AlertMonsters
        RSCP A 6 Bright A_GiveInventory("SamsaraDoomguyStrRailgunCharge0AttackHandler")
      FireStrChargeFinishZoomed:
        RSCP A 4 Bright
        RSCP A 15 Bright
        goto Ready

      FireStrCharge0Check:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStrCharge1")
        TNT1 A 0 A_JumpIfInventory("Cell",10,"FireStrCharge1")
        goto FireStrCharge0

      FireStrCharge1:
        RAIG C 12 Bright A_JumpIfInventory("SamsaraDoomguyStrRailgunScope",1,"FireStrCharge1Zoomed")
        TNT1 A 0 A_WeaponReady(WRF_NOBOB|WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_ReFire("FireStrCharge1Check")
        RAIG G 4 Bright A_GunFlash("FlashStr")
        TNT1 A 0 A_AlertMonsters
        RAIG H 6 Bright A_GiveInventory("SamsaraDoomguyStrRailgunCharge1AttackHandler")
        goto FireStrChargeFinish

      FireStrCharge1Zoomed:
        RSCP A 12 Bright
        TNT1 A 0 A_WeaponReady(WRF_NOBOB|WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_ReFire("FireStrCharge1Check")
        RSCP A 4 Bright A_GunFlash("FlashStr")
        TNT1 A 0 A_AlertMonsters
        RSCP A 6 Bright A_GiveInventory("SamsaraDoomguyStrRailgunCharge1AttackHandler")
        goto FireStrChargeFinishZoomed

      FireStrCharge1Check:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStrCharge2")
        TNT1 A 0 A_JumpIfInventory("Cell",15,"FireStrCharge2")
        goto FireStrCharge1

      FireStrCharge2:
        RAIG D 12 Bright A_JumpIfInventory("SamsaraDoomguyStrRailgunScope",1,"FireStrCharge2Zoomed")
        TNT1 A 0 A_WeaponReady(WRF_NOBOB|WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_ReFire("FireStrCharge2")
        RAIG I 4 Bright A_GunFlash("FlashStr")
        TNT1 A 0 A_AlertMonsters
        RAIG J 6 Bright A_GiveInventory("SamsaraDoomguyStrRailgunCharge2AttackHandler")
        goto FireStrChargeFinish

      FireStrCharge2Zoomed:
        RSCP A 12 Bright
        TNT1 A 0 A_WeaponReady(WRF_NOBOB|WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_ReFire("FireStrCharge2")
        RSCP A 4 Bright A_GunFlash("FlashStr")
        TNT1 A 0 A_AlertMonsters
        RSCP A 6 Bright A_GiveInventory("SamsaraDoomguyStrRailgunCharge2AttackHandler")
        goto FireStrChargeFinishZoomed

      FlashStr:
        TNT1 A 4 A_Light1
        TNT1 A 6 A_Light2
        goto LightDone

      // Powered
      DeselectStrStartPowered:
        TNT1 A 0 A_Lower
        goto DeselectStrContinue

      SelectStrStartPowered:
        TNT1 A 0 A_Raise
        goto SelectStrContinue

      FireStrPowered:
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrRailgunScope",1,"FireStrPoweredZoomed")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStrPoweredStart")
        TNT1 A 0 A_JumpIfInventory("Cell",2,"FireStrPoweredStart")
        goto Nothing

      FireStrPoweredStart:
        RAIG A 1
        TNT1 A 0 A_PlaySound("doomguy/stronghold/railch",CHAN_WEAPON)
        RAIG BCD 1 Bright
        RAIG I 1 Bright A_GunFlash("FlashStrPowered")
        TNT1 A 0 A_TakeInventory("SamsaraDoomguyStrRailgunPoweredHeldCheck")
      FireStrPoweredContinue:
        TNT1 A 0 A_AlertMonsters
        RAIG J 1 Bright A_GiveInventory("SamsaraDoomguyStrRailgunPoweredAttackHandler")
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPower",1,"FireStrPoweredFinishReFire")
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPowerPerma",1,"FireStrPoweredFinishReFire")
      FireStrPoweredFinish:
        RAIG K 10
        goto Ready

      FireStrPoweredFinishReFire:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStrPoweredFinishReFireStart")
        TNT1 A 0 A_JumpIfInventory("Cell",2,"FireStrPoweredFinishReFireStart")
        goto Nothing

      FireStrPoweredFinishReFireStart:
        TNT1 A 0 A_JumpIfInventory("DoomClassMode",2,1)
        goto FireStrPoweredFinish
        TNT1 A 0 A_ReFire(1)
        goto FireStrPoweredFinish
        RAIG A 1
        RAIG BCD 1 Bright
        RAIG I 1 Bright A_GunFlash("FlashStrPowered")
        TNT1 A 0 A_GiveInventory("SamsaraDoomguyStrRailgunPoweredHeldCheck")
        goto FireStrPoweredContinue

      FireStrPoweredZoomed:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStrPoweredStartZoomed")
        TNT1 A 0 A_JumpIfInventory("Cell",4,"FireStrPoweredStartZoomed")
        goto Nothing

      FireStrPoweredStartZoomed:
        RSCP A 1 Bright
        RSCP A 7 Bright A_PlaySound("doomguy/stronghold/railch",CHAN_WEAPON)
        RSCP A 1 Bright A_GunFlash("FlashStrPowered")
        TNT1 A 0 A_TakeInventory("SamsaraDoomguyStrRailgunPoweredHeldCheck")
      FireStrPoweredContinueZoomed:
        TNT1 A 0 A_AlertMonsters
        RSCP A 1 Bright A_GiveInventory("SamsaraDoomguyStrRailgunPoweredAttackHandler")
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPower",1,"FireStrPoweredFinishReFireZoomed")
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPowerPerma",1,"FireStrPoweredFinishReFireZoomed")
      FireStrPoweredFinishZoomed:
        RSCP A 10 Bright
        goto Ready

      FireStrPoweredFinishReFireZoomed:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStrPoweredFinishReFireStartZoomed")
        TNT1 A 0 A_JumpIfInventory("Cell",4,"FireStrPoweredFinishReFireStartZoomed")
        goto Nothing

      FireStrPoweredFinishReFireStartZoomed:
        TNT1 A 0 A_JumpIfInventory("DoomClassMode",2,1)
        goto FireStrPoweredFinishZoomed
        TNT1 A 0 A_ReFire(1)
        goto FireStrPoweredFinishZoomed
        RSCP A 1 Bright
        RSCP A 7 Bright
        RSCP A 1 Bright A_GunFlash("FlashStrPowered")
        TNT1 A 0 A_GiveInventory("SamsaraDoomguyStrRailgunPoweredHeldCheck")
        goto FireStrPoweredContinueZoomed

      FlashStrPowered:
        TNT1 A 1 A_Light1
        TNT1 A 1 A_Light2
        goto LightDone

      Nothing:
        TNT1 A 0 A_ClearReFire
        TNT1 A 0 A_SelectWeapon(" Pistol ")
        goto Deselect
    }
}

actor SamsaraDoomguyRailgunAttackHandler : Trigger
{
    States
    {
      // Ammo check stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"PickupCheckAmmoReloadable")
      PickupCheckAmmoNonReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupTakeAmmoNonReloadable")
        TNT1 A 0 A_JumpIfInventory("Cell",10,"PickupTakeAmmoNonReloadable")
        stop

      PickupCheckAmmoReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("DoomguyRailgunClip",1,"PickupTakeAmmoReloadable")
        stop

      // Ammo taking stuff
      PickupTakeAmmoNonReloadable:
        TNT1 A 0 A_TakeInventory("Cell",10,TIF_NOTAKEINFINITE)
        goto PickupAttack

      PickupTakeAmmoReloadable:
        TNT1 A 0 A_TakeInventory("DoomguyRailgunClip",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("doomguy/railgf",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_RailAttack(75,0,false,"","",RGF_SILENT|RGF_FULLBRIGHT,0,"DoomRailPuff")
        stop

      PickupAttackCoop:
        TNT1 A 0 A_RailAttack(200,0,false,"","",RGF_SILENT|RGF_FULLBRIGHT,0,"DoomRailPuff_Coop")
        stop
    }
}

actor SamsaraDoomguyRailgunReloader : Trigger // used for (re)spawn
{
    States
    {
      Pickup:
        TNT1 A 0 A_JumpIfInventory("DoomClassMode",2,"PickupStop")
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,1)
        stop
        TNT1 A 0 A_JumpIfInventory("DoomguyRailgunClip",0,"PickupStop")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Cell",10,1)
        stop
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_TakeInventory("Cell",10)
        TNT1 A 0 A_GiveInventory("DoomguyRailgunClip")
        loop
    }
}

// Ammo

actor DoomguyRailgunClip : Ammo
{
    Inventory.MaxAmount 4
    Tag "Railgun Magazine"
    +IGNORESKILL
    +UNTOSSABLE
}

actor DoomRailPuff : DoomBulletPuff2
{
    Decal ""
    +PIERCEARMOR
}

actor DoomRailPuff_Coop : DoomRailPuff
{
    //+ALLOWTHRUFLAGS // doesn't exist in zandronum yet
    +MTHRUSPECIES
}

// Unpowered attack handlers

actor SamsaraDoomguyStrRailgunCharge0AttackHandler : Trigger
{
    States
    {
      // Ammo check stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupTakeAmmo")
        TNT1 A 0 A_JumpIfInventory("Cell",5,"PickupTakeAmmo")
        stop

      // Ammo taking stuff
      PickupTakeAmmo:
        TNT1 A 0 A_TakeInventory("Cell",5,TIF_NOTAKEINFINITE)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("doomguy/stronghold/railf1",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrRailgunScope",1,"PickupAttackZoomed")
      // Unzoomed
      PickupAttackUnzoomed:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackUnzoomedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackUnzoomedCoop")
      PickupAttackUnzoomedDM:
        TNT1 A 0 A_RailAttack(100,0,false,"FF 00 00","FF FF FF",RGF_SILENT|RGF_FULLBRIGHT,0,"SamsaraDoomguyStrRailgunPuff")
        stop

      PickupAttackUnzoomedCoop:
        TNT1 A 0 A_RailAttack(100,0,false,"FF 00 00","FF FF FF",RGF_SILENT|RGF_FULLBRIGHT,0,"SamsaraDoomguyStrRailgunPuffCoop")
        stop

      // Zoomed
      PickupAttackZoomed:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackZoomedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackZoomedCoop")
      PickupAttackZoomedDM:
        TNT1 A 0 A_RailAttack(125,0,false,"FF 00 00","FF FF FF",RGF_SILENT|RGF_FULLBRIGHT,0,"SamsaraDoomguyStrRailgunPuff")
        stop

      PickupAttackZoomedCoop:
        TNT1 A 0 A_RailAttack(125,0,false,"FF 00 00","FF FF FF",RGF_SILENT|RGF_FULLBRIGHT,0,"SamsaraDoomguyStrRailgunPuffCoop")
        stop
    }
}

actor SamsaraDoomguyStrRailgunCharge1AttackHandler : Trigger
{
    States
    {
      // Ammo check stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupTakeAmmo")
        TNT1 A 0 A_JumpIfInventory("Cell",10,"PickupTakeAmmo")
        stop

      // Ammo taking stuff
      PickupTakeAmmo:
        TNT1 A 0 A_TakeInventory("Cell",10,TIF_NOTAKEINFINITE)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("doomguy/stronghold/railf2",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrRailgunScope",1,"PickupAttackZoomed")
      // Unzoomed
      PickupAttackUnzoomed:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackUnzoomedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackUnzoomedCoop")
      PickupAttackUnzoomedDM:
        TNT1 A 0 A_RailAttack(150,0,false,"FF FF 00","FF 45 00",RGF_SILENT|RGF_FULLBRIGHT,0,"SamsaraDoomguyStrRailgunPuff")
        stop

      PickupAttackUnzoomedCoop:
        TNT1 A 0 A_RailAttack(150,0,false,"FF FF 00","FF 45 00",RGF_SILENT|RGF_FULLBRIGHT,0,"SamsaraDoomguyStrRailgunPuffCoop")
        stop

      // Zoomed
      PickupAttackZoomed:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackZoomedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackZoomedCoop")
      PickupAttackZoomedDM:
        TNT1 A 0 A_RailAttack(175,0,false,"FF FF 00","FF 45 00",RGF_SILENT|RGF_FULLBRIGHT,0,"SamsaraDoomguyStrRailgunPuff")
        stop

      PickupAttackZoomedCoop:
        TNT1 A 0 A_RailAttack(175,0,false,"FF FF 00","FF 45 00",RGF_SILENT|RGF_FULLBRIGHT,0,"SamsaraDoomguyStrRailgunPuffCoop")
        stop
    }
}

actor SamsaraDoomguyStrRailgunCharge2AttackHandler : Trigger
{
    States
    {
      // Ammo check stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupTakeAmmo")
        TNT1 A 0 A_JumpIfInventory("Cell",15,"PickupTakeAmmo")
        stop

      // Ammo taking stuff
      PickupTakeAmmo:
        TNT1 A 0 A_TakeInventory("Cell",15,TIF_NOTAKEINFINITE)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("doomguy/stronghold/railf3",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrRailgunScope",1,"PickupAttackZoomed")
      // Unzoomed
      PickupAttackUnzoomed:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackUnzoomedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackUnzoomedCoop")
      PickupAttackUnzoomedDM:
        TNT1 A 0 A_RailAttack(200,0,false,"00 00 FF","00 FF 00",RGF_SILENT|RGF_FULLBRIGHT,0,"SamsaraDoomguyStrRailgunPuff")
        stop

      PickupAttackUnzoomedCoop:
        TNT1 A 0 A_RailAttack(200,0,false,"00 00 FF","00 FF 00",RGF_SILENT|RGF_FULLBRIGHT,0,"SamsaraDoomguyStrRailgunPuffCoop")
        stop

      // Zoomed
      PickupAttackZoomed:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackZoomedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackZoomedCoop")
      PickupAttackZoomedDM:
        TNT1 A 0 A_RailAttack(225,0,false,"00 00 FF","00 FF 00",RGF_SILENT|RGF_FULLBRIGHT,0,"SamsaraDoomguyStrRailgunPuff")
        stop

      PickupAttackZoomedCoop:
        TNT1 A 0 A_RailAttack(225,0,false,"00 00 FF","00 FF 00",RGF_SILENT|RGF_FULLBRIGHT,0,"SamsaraDoomguyStrRailgunPuffCoop")
        stop
    }
}

// Powered attack handler

actor SamsaraDoomguyStrRailgunPoweredAttackHandler : Trigger
{
    States
    {
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrRailgunScope",1,"PickupZoomed")
      // Unzoomed
      // Ammo check stuff (unzoomed)
      PickupUnzoomed:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupUnzoomedTakeAmmo")
        TNT1 A 0 A_JumpIfInventory("Cell",2,"PickupUnzoomedTakeAmmo")
        stop

      // Ammo taking stuff (unzoomed)
      PickupUnzoomedTakeAmmo:
        TNT1 A 0 A_TakeInventory("Cell",2,TIF_NOTAKEINFINITE)
        goto PickupUnzoomedPlaySound

      // Sound stuff (unzoomed)
      PickupUnzoomedPlaySound:
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrRailgunPoweredHeldCheck",1,"PickupUnzoomedPlaySoundHold")
      PickupUnzoomedPlaySoundFire:
        TNT1 A 0 A_PlaySound("doomguy/stronghold/railf2",CHAN_WEAPON)
        goto PickupUnzoomedAttack

      PickupUnzoomedPlaySoundHold:
        TNT1 A 0 A_PlaySound("doomguy/stronghold/railf1",CHAN_WEAPON)
        goto PickupUnzoomedAttack

      // Attack stuff (unzoomed)
      PickupUnzoomedAttack:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupUnzoomedAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupUnzoomedAttackCoop")
      PickupUnzoomedAttackDM:
        TNT1 A 0 A_RailAttack(45,0,false,"00 00 FF","FF FF FF",RGF_SILENT|RGF_FULLBRIGHT,0,"SamsaraDoomguyStrRailgunPuff")
        TNT1 A 0 A_RailAttack(45,0,false,"00 FF 00","FF FF FF",RGF_SILENT|RGF_FULLBRIGHT,0,"SamsaraDoomguyStrRailgunPuff")
        stop

      PickupUnzoomedAttackCoop:
        TNT1 A 0 A_RailAttack(45,0,false,"00 00 FF","FF FF FF",RGF_SILENT|RGF_FULLBRIGHT,0,"SamsaraDoomguyStrRailgunPuffCoop")
        TNT1 A 0 A_RailAttack(45,0,false,"00 FF 00","FF FF FF",RGF_SILENT|RGF_FULLBRIGHT,0,"SamsaraDoomguyStrRailgunPuffCoop")
        stop

      // Zoomed
      // Ammo check stuff (zoomed)
      PickupZoomed:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupZoomedTakeAmmo")
        TNT1 A 0 A_JumpIfInventory("Cell",4,"PickupZoomedTakeAmmo")
        stop

      // Ammo taking stuff (zoomed)
      PickupZoomedTakeAmmo:
        TNT1 A 0 A_TakeInventory("Cell",4,TIF_NOTAKEINFINITE)
        goto PickupZoomedPlaySound

      // Sound stuff (zoomed)
      PickupZoomedPlaySound:
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrRailgunPoweredHeldCheck",1,"PickupZoomedPlaySoundHold")
      PickupZoomedPlaySoundFire:
        TNT1 A 0 A_PlaySound("doomguy/stronghold/railf2",CHAN_WEAPON)
        goto PickupZoomedAttack

      PickupZoomedPlaySoundHold:
        TNT1 A 0 A_PlaySound("doomguy/stronghold/railf3",CHAN_WEAPON)
        goto PickupZoomedAttack

      // Attack stuff (zoomed)
      PickupZoomedAttack:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupZoomedAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupZoomedAttackCoop")
      PickupZoomedAttackDM:
        TNT1 AAAA 0 A_RailAttack(30,0,false,"00 FF FF","FF FF FF",RGF_SILENT|RGF_FULLBRIGHT,0,"SamsaraDoomguyStrRailgunPuff") // total damage: 120
        stop

      PickupZoomedAttackCoop:
        TNT1 AAAA 0 A_RailAttack(30,0,false,"00 FF FF","FF FF FF",RGF_SILENT|RGF_FULLBRIGHT,0,"SamsaraDoomguyStrRailgunPuffCoop")
        stop
    }
}

// Bullet puff (railgun)

actor SamsaraDoomguyStrRailgunPuff : SamsaraDoomguyStrBulletPuff { +PIERCEARMOR }

actor SamsaraDoomguyStrRailgunPuffCoop : SamsaraDoomguyStrRailgunPuff
{
    //+ALLOWTHRUFLAGS // doesn't exist in zandronum yet
    +MTHRUSPECIES
}
