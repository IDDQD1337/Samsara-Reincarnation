actor DGHasRailGun : Boolean {}
actor SamsaraDoomguyStrRailgunScope : Boolean {}

ACTOR " RailGun " : Weapon
{
	Radius 20
	Height 16
	Weapon.Selectionorder 100
	Weapon.AmmoUse 10
	Weapon.AmmoGive 0
	Weapon.AmmoType "Cell"
	Inventory.Pickupmessage "$PICKUP_RAILGUN"
	Inventory.RestrictedTo "DoomguyPlayer"
    Decal "RailScorch"
	Obituary "$OB_RAILGUN"
	Tag "Railgun"
    +AMMO_OPTIONAL
    +NOALERT
    +UNDROPPABLE
    States
    {
      Spawn:
        RAIL A -1
        stop

      // Classic
      Ready:
        TNT1 A 0 A_JumpIfInventory("DoomClassMode",2,"ReadyStr")
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrRailgunScope",1,"ReadyZoomOut")
      ReadyContinue:
        RLGG A 1 A_WeaponReady(WRF_NOSECONDARY)
        goto Ready

      ReadyZoomOut:
        TNT1 A 0 A_ZoomFactor(1)
        TNT1 A 0 A_TakeInventory("SamsaraDoomguyStrRailgunScope")
        goto ReadyContinue

      Deselect:
        TNT1 A 0 A_JumpIfInventory("DoomClassMode",2,"DeselectStr")
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrRailgunScope",1,"DeselectZoomOut")
      DeselectContinue:
        RLGG A 1 A_Lower
        goto Deselect

      DeselectZoomOut:
        TNT1 A 0 A_ZoomFactor(1)
        TNT1 A 0 A_TakeInventory("SamsaraDoomguyStrRailgunScope")
        goto DeselectContinue

      Select:
        TNT1 A 0 A_JumpIfInventory("DoomClassMode",2,"SelectStr")
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrRailgunScope",1,"SelectZoomOut")
      SelectContinue:
        RLGG A 1 A_Raise
        goto Select

      SelectZoomOut:
        TNT1 A 0 A_ZoomFactor(1,ZOOM_INSTANT) // map change
        TNT1 A 0 A_TakeInventory("SamsaraDoomguyStrRailgunScope")
        goto SelectContinue

      Fire:
        TNT1 A 0 A_JumpIfInventory("DoomClassMode",2,"FireStr")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Cell",10,1)
        goto Nothing
        TNT1 A 0 A_AlertMonsters
        RLGG E 12 A_GiveInventory("SamsaraDoomguyRailgunAttackHandler")
        RLGG F 6 A_CheckForReload(4,7)
        RLGG GHIJKLA 6
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Cell",10,1)
        goto Nothing
        TNT1 A 0 A_ReFire
        goto Ready

      Flash:
        TNT1 A 5 Bright A_Light1
        TNT1 A 5 Bright A_Light2
        goto LightDone

      // Stronghold (its own version)
      ReadyStr:
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrRailgunScope",1,"ReadyStrZoom")
        RAIG A 1 A_WeaponReady(WRF_ALLOWZOOM)
        loop

      ReadyStrZoom:
        RSCP A 1 Bright A_WeaponReady(WRF_NOBOB|WRF_ALLOWZOOM)
        goto Ready

      // Unpowered
      DeselectStr:
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrRailgunScope",1,"DeselectStrZoomOut")
      DeselectStrStart:
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPower",1,"DeselectStrStartPowered")
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPowerPerma",1,"DeselectStrStartPowered")
      DeselectStrContinue:
        TNT1 A 0 A_Lower
        RAIG A 1 A_Lower
        goto Deselect

      DeselectStrZoomOut:
        TNT1 A 0 A_PlaySound("doomguy/stronghold/railzoomout")
        TNT1 A 0 A_ZoomFactor(1)
        TNT1 A 0 A_TakeInventory("SamsaraDoomguyStrRailgunScope")
        goto DeselectStrStart

      SelectStr:
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrRailgunScope",1,"SelectStrZoomOut")
      SelectStrStart:
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPower",1,"SelectStrStartPowered")
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPowerPerma",1,"SelectStrStartPowered")
      SelectStrContinue:
        TNT1 A 0 A_Raise
        RAIG A 1 A_Raise
        goto Select

      SelectStrZoomOut:
        // no need for a sound
        TNT1 A 0 A_ZoomFactor(1,ZOOM_INSTANT) // map change
        TNT1 A 0 A_TakeInventory("SamsaraDoomguyStrRailgunScope")
        goto SelectStrStart

      AltFire:
      Zoom:
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrRailgunScope",1,"Unzoom")
        TNT1 A 0 A_PlaySound("doomguy/stronghold/railzoomin")
        TNT1 A 0 A_ZoomFactor(3)
        RSCP A 8 Bright A_GiveInventory("SamsaraDoomguyStrRailgunScope")
        goto Ready

      Unzoom:
        TNT1 A 0 A_PlaySound("doomguy/stronghold/railzoomout")
        TNT1 A 0 A_ZoomFactor(1)
        RAIG A 8 A_TakeInventory("SamsaraDoomguyStrRailgunScope")
        goto Ready

      FireStr:
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPower",1,"FireStrPowered")
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPowerPerma",1,"FireStrPowered")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStrCharge")
        TNT1 A 0 A_JumpIfInventory("Cell",5,"FireStrCharge")
        goto Nothing

      FireStrCharge:
        TNT1 A 0 A_PlaySound("doomguy/stronghold/railch",CHAN_WEAPON)
      FireStrCharge0:
        RAIG B 12 Bright A_JumpIfInventory("SamsaraDoomguyStrRailgunScope",1,"FireStrCharge0Zoomed")
        TNT1 A 0 A_WeaponReady(WRF_NOBOB|WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_ReFire("FireStrCharge0Check")
        RAIG E 4 Bright A_GunFlash("FlashStr")
        TNT1 A 0 A_AlertMonsters
        RAIG F 6 Bright A_GiveInventory("SamsaraDoomguyStrRailgunCharge0AttackHandler")
      FireStrChargeFinish:
        RAIG A 4
        RAIG K 15
        goto Ready

      FireStrCharge0Zoomed:
        RSCP A 12 Bright
        TNT1 A 0 A_WeaponReady(WRF_NOBOB|WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_ReFire("FireStrCharge0Check")
        RSCP A 4 Bright A_GunFlash("FlashStr")
        TNT1 A 0 A_AlertMonsters
        RSCP A 6 Bright A_GiveInventory("SamsaraDoomguyStrRailgunCharge0AttackHandler")
      FireStrChargeFinishZoomed:
        RSCP A 4 Bright
        RSCP A 15 Bright
        goto Ready

      FireStrCharge0Check:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStrCharge1")
        TNT1 A 0 A_JumpIfInventory("Cell",10,"FireStrCharge1")
        goto FireStrCharge0

      FireStrCharge1:
        RAIG C 12 Bright A_JumpIfInventory("SamsaraDoomguyStrRailgunScope",1,"FireStrCharge1Zoomed")
        TNT1 A 0 A_WeaponReady(WRF_NOBOB|WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_ReFire("FireStrCharge1Check")
        RAIG G 4 Bright A_GunFlash("FlashStr")
        TNT1 A 0 A_AlertMonsters
        RAIG H 6 Bright A_GiveInventory("SamsaraDoomguyStrRailgunCharge1AttackHandler")
        goto FireStrChargeFinish

      FireStrCharge1Zoomed:
        RSCP A 12 Bright
        TNT1 A 0 A_WeaponReady(WRF_NOBOB|WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_ReFire("FireStrCharge1Check")
        RSCP A 4 Bright A_GunFlash("FlashStr")
        TNT1 A 0 A_AlertMonsters
        RSCP A 6 Bright A_GiveInventory("SamsaraDoomguyStrRailgunCharge1AttackHandler")
        goto FireStrChargeFinishZoomed

      FireStrCharge1Check:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStrCharge2")
        TNT1 A 0 A_JumpIfInventory("Cell",15,"FireStrCharge2")
        goto FireStrCharge1

      FireStrCharge2:
        RAIG D 12 Bright A_JumpIfInventory("SamsaraDoomguyStrRailgunScope",1,"FireStrCharge2Zoomed")
        TNT1 A 0 A_WeaponReady(WRF_NOBOB|WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_ReFire("FireStrCharge2")
        RAIG I 4 Bright A_GunFlash("FlashStr")
        TNT1 A 0 A_AlertMonsters
        RAIG J 6 Bright A_GiveInventory("SamsaraDoomguyStrRailgunCharge2AttackHandler")
        goto FireStrChargeFinish

      FireStrCharge2Zoomed:
        RSCP A 12 Bright
        TNT1 A 0 A_WeaponReady(WRF_NOBOB|WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_ReFire("FireStrCharge2")
        RSCP A 4 Bright A_GunFlash("FlashStr")
        TNT1 A 0 A_AlertMonsters
        RSCP A 6 Bright A_GiveInventory("SamsaraDoomguyStrRailgunCharge2AttackHandler")
        goto FireStrChargeFinishZoomed

      FlashStr:
        TNT1 A 4 A_Light1
        TNT1 A 6 A_Light2
        goto LightDone

      // Powered
      DeselectStrStartPowered:
        TNT1 A 0 A_Lower
        goto DeselectStrContinue

      SelectStrStartPowered:
        TNT1 A 0 A_Raise
        goto SelectStrContinue

      FireStrPowered:
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrRailgunScope",1,"FireStrPoweredZoomed")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStrPoweredStart")
        TNT1 A 0 A_JumpIfInventory("Cell",2,"FireStrPoweredStart")
        goto Nothing

      FireStrPoweredStart:
        RAIG A 1
        TNT1 A 0 A_PlaySound("doomguy/stronghold/railch",CHAN_WEAPON)
        RAIG BCD 1 Bright
        RAIG I 1 Bright A_GunFlash("FlashStrPowered")
        TNT1 A 0 A_PlaySound("doomguy/stronghold/railf2",CHAN_WEAPON)
      FireStrPoweredContinue:
        TNT1 A 0 A_AlertMonsters
        RAIG J 1 Bright A_GiveInventory("SamsaraDoomguyStrRailgunPoweredAttackHandler")
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPower",1,"FireStrPoweredFinishReFire")
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPowerPerma",1,"FireStrPoweredFinishReFire")
      FireStrPoweredFinish:
        RAIG K 10
        goto Ready

      FireStrPoweredFinishReFire:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStrPoweredFinishReFireStart")
        TNT1 A 0 A_JumpIfInventory("Cell",2,"FireStrPoweredFinishReFireStart")
        goto Nothing

      FireStrPoweredFinishReFireStart:
        TNT1 A 0 A_JumpIfInventory("DoomClassMode",2,1)
        goto FireStrPoweredFinish
        TNT1 A 0 A_ReFire(1)
        goto FireStrPoweredFinish
        RAIG A 1
        RAIG BCD 1 Bright
        RAIG I 1 Bright A_GunFlash("FlashStrPowered")
        TNT1 A 0 A_PlaySound("doomguy/stronghold/railf1",CHAN_WEAPON)
        goto FireStrPoweredContinue

      FireStrPoweredZoomed:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStrPoweredStartZoomed")
        TNT1 A 0 A_JumpIfInventory("Cell",4,"FireStrPoweredStartZoomed")
        goto Nothing

      FireStrPoweredStartZoomed:
        RSCP A 1 Bright
        RSCP A 7 Bright A_PlaySound("doomguy/stronghold/railch",CHAN_WEAPON)
        RSCP A 1 Bright A_GunFlash("FlashStrPowered")
        TNT1 A 0 A_PlaySound("doomguy/stronghold/railf2",CHAN_WEAPON)
      FireStrPoweredContinueZoomed:
        TNT1 A 0 A_AlertMonsters
        RSCP A 1 Bright A_GiveInventory("SamsaraDoomguyStrRailgunPoweredAttackHandler")
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPower",1,"FireStrPoweredFinishReFireZoomed")
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPowerPerma",1,"FireStrPoweredFinishReFireZoomed")
      FireStrPoweredFinishZoomed:
        RSCP A 10 Bright
        goto Ready

      FireStrPoweredFinishReFireZoomed:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStrPoweredFinishReFireStartZoomed")
        TNT1 A 0 A_JumpIfInventory("Cell",4,"FireStrPoweredFinishReFireStartZoomed")
        goto Nothing

      FireStrPoweredFinishReFireStartZoomed:
        TNT1 A 0 A_JumpIfInventory("DoomClassMode",2,1)
        goto FireStrPoweredFinishZoomed
        TNT1 A 0 A_ReFire(1)
        goto FireStrPoweredFinishZoomed
        RSCP A 1 Bright
        RSCP A 7 Bright
        RSCP A 1 Bright A_GunFlash("FlashStrPowered")
        TNT1 A 0 A_PlaySound("doomguy/stronghold/railf3",CHAN_WEAPON)
        goto FireStrPoweredContinueZoomed

      FlashStrPowered:
        TNT1 A 1 A_Light1
        TNT1 A 1 A_Light2
        goto LightDone

      Nothing:
        TNT1 A 0 A_SelectWeapon(" Pistol ")
        goto Deselect
    }
}

actor SamsaraDoomguyRailgunAttackHandler : Trigger
{
    States
    {
      // Ammo check stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupTakeAmmo")
        TNT1 A 0 A_JumpIfInventory("Cell",10,"PickupTakeAmmo")
        stop

      // Ammo taking stuff
      PickupTakeAmmo:
        TNT1 A 0 A_TakeInventory("Cell",10,TIF_NOTAKEINFINITE)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("weapons/railgf",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_RailAttack(75,0,false,"","",RGF_SILENT|RGF_FULLBRIGHT,0,"DoomRailPuff")
        stop

      PickupAttackCoop:
        TNT1 A 0 A_RailAttack(200,0,false,"","",RGF_SILENT|RGF_FULLBRIGHT,0,"DoomRailPuff_Coop")
        stop
    }
}

actor SamsaraDoomguyStrRailgunCharge0AttackHandler : Trigger
{
    States
    {
      Pickup:
        TNT1 A 0 A_TakeInventory("Cell",5,TIF_NOTAKEINFINITE)
        TNT1 A 0 A_PlaySound("doomguy/stronghold/railf1",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrRailgunScope",1,"PickupZoomed")
      PickupUnzoomed:
        TNT1 A 0 A_RailAttack(100,0,false,"FF 00 00","FF FF FF",RGF_SILENT,0,"SamsaraDoomguyStrBulletPuff")
        stop

      PickupZoomed:
        TNT1 A 0 A_RailAttack(125,0,false,"FF 00 00","FF FF FF",RGF_SILENT,0,"SamsaraDoomguyStrBulletPuff")
        stop
    }
}

actor SamsaraDoomguyStrRailgunCharge1AttackHandler : Trigger
{
    States
    {
      Pickup:
        TNT1 A 0 A_TakeInventory("Cell",10,TIF_NOTAKEINFINITE)
        TNT1 A 0 A_PlaySound("doomguy/stronghold/railf2",CHAN_WEAPON)
        TNT1 A 0 A_RailAttack(0,0,false,"FF FF 00","FF FF FF",RGF_SILENT,0,"SamsaraDoomguyStrBulletPuff")
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrRailgunScope",1,"PickupZoomed")
      PickupUnzoomed:
        TNT1 A 0 A_RailAttack(150,0,false,"FF FF 00","FF 45 00",RGF_SILENT,0,"SamsaraDoomguyStrBulletPuff")
        stop

      PickupZoomed:
        TNT1 A 0 A_RailAttack(175,0,false,"FF FF 00","FF 45 00",RGF_SILENT,0,"SamsaraDoomguyStrBulletPuff")
        stop
    }
}

actor SamsaraDoomguyStrRailgunCharge2AttackHandler : Trigger
{
    States
    {
      Pickup:
        TNT1 A 0 A_TakeInventory("Cell",15,TIF_NOTAKEINFINITE)
        TNT1 A 0 A_PlaySound("doomguy/stronghold/railf3",CHAN_WEAPON)
        TNT1 A 0 A_RailAttack(0,0,false,"00 00 FF","00 45 FF",RGF_SILENT,0,"SamsaraDoomguyStrBulletPuff")
        TNT1 A 0 A_RailAttack(0,0,false,"00 00 FF","FF FF FF",RGF_SILENT,0,"SamsaraDoomguyStrBulletPuff")
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrRailgunScope",1,"PickupZoomed")
      PickupUnzoomed:
        TNT1 A 0 A_RailAttack(200,0,false,"00 00 FF","00 FF 00",RGF_SILENT,0,"SamsaraDoomguyStrBulletPuff")
        stop

      PickupZoomed:
        TNT1 A 0 A_RailAttack(225,0,false,"00 00 FF","00 FF 00",RGF_SILENT,0,"SamsaraDoomguyStrBulletPuff")
        stop
    }
}

actor SamsaraDoomguyStrRailgunPoweredAttackHandler : Trigger
{
    States
    {
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrRailgunScope",1,"PickupZoomed")
      PickupUnzoomed:
        TNT1 A 0 A_TakeInventory("Cell",2,TIF_NOTAKEINFINITE)
        TNT1 A 0 A_RailAttack(45,0,false,"00 00 FF","FF FF FF",RGF_SILENT,0,"SamsaraDoomguyStrBulletPuff")
        TNT1 A 0 A_RailAttack(45,0,false,"00 FF 00","FF FF FF",RGF_SILENT,0,"SamsaraDoomguyStrBulletPuff")
        stop

      PickupZoomed:
        TNT1 A 0 A_TakeInventory("Cell",4,TIF_NOTAKEINFINITE)
        TNT1 AAAA 0 A_RailAttack(30,0,false,"00 FF FF","FF FF FF",RGF_SILENT,0,"SamsaraDoomguyStrBulletPuff") // total damage: 120
        stop
    }
}

actor DoomRailPuff : DoomBulletPuff2
{
    Decal ""
    +PIERCEARMOR
}

actor DoomRailPuff_Coop : DoomRailPuff
{
    //+ALLOWTHRUFLAGS // doesn't exist in zandronum yet
    +MTHRUSPECIES
}
