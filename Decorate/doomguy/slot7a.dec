actor DGHasBFG9000 : Boolean {}
actor SamsaraDoomguyStrBFGReadySoundCheck : Boolean {}

Actor "B.F.G. 9000" : BFG9000
{
	//Weapon.SlotNumber 7
	Weapon.SlotPriority 1
	Weapon.AmmoGive 0
	DamageType "BFG"
	Obituary "$SAMSARA_DOOMGUY_OB_SLOT7_1"
	Inventory.RestrictedTo "DoomguyPlayer"
	Tag "B.F.G. 9000"
	+AMMO_OPTIONAL
	+NOALERT
	+UNDROPPABLE
	+WEAPON.BFG
	States
	{
	  Spawn:
		WBFG A -1
		stop

	  // Original
	  Ready:
		TNT1 A 0 A_JumpIfInventory("DoomClassMode",2,"ReadyStr")
		TNT1 A 0 A_JumpIfInventory("DoomClassMode",1,"Ready64")
		TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrBFGReadySoundCheck",1,"ReadyStopSound")
	  ReadyContinue:
		DBFG A 1 A_WeaponReady
		goto Ready

	  ReadyStopSound:
		TNT1 A 0 A_PlaySound("silence",CHAN_6)
		TNT1 A 0 A_StopSound(CHAN_6)
		TNT1 A 0 A_TakeInventory("SamsaraDoomguyStrBFGReadySoundCheck")
		goto ReadyContinue

	  Deselect:
		TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrBFGReadySoundCheck",1,"DeselectStopSound")
	  DeselectStart:
		TNT1 A 0 A_JumpIfInventory("DoomClassMode",2,"DeselectStartStr")
		TNT1 A 0 A_JumpIfInventory("DoomClassMode",1,"DeselectStart64")
		DBFG A 1 A_Lower
		goto Deselect

	  DeselectStopSound:
		TNT1 A 0 A_PlaySound("silence",CHAN_6)
		TNT1 A 0 A_StopSound(CHAN_6)
		TNT1 A 0 A_TakeInventory("SamsaraDoomguyStrBFGReadySoundCheck")
		goto DeselectStart

	  Select:
		TNT1 A 0 A_TakeInventory("SamsaraDoomguyStrBFGReadySoundCheck")
		TNT1 A 0 A_JumpIfInventory("DoomClassMode",2,"SelectStr")
		TNT1 A 0 A_JumpIfInventory("DoomClassMode",1,"Select64")
		DBFG A 1 A_Raise
		loop

	  Fire:
		TNT1 A 0 A_JumpIfInventory("DoomClassMode",2,"FireStr")
		TNT1 A 0 A_JumpIfInventory("DoomClassMode",1,"Fire64")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
		TNT1 A 0 A_JumpIfInventory("Cell",40,1)
		goto Nothing
		TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrBFGReadySoundCheck",1,"FireStopSound")
	  FireStart:
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("doomguy/bfgf",CHAN_WEAPON)
		TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"FireSmooth")
	  FireVanilla:
		DBFG A 20
		DBFG B 10 Bright A_GunFlash
		DBFG B 10 Bright A_GiveInventory("SamsaraDoomguyBFG9000AttackHandler")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
		TNT1 A 0 A_JumpIfInventory("Cell",40,1)
		goto Nothing
		DBFG B 20 A_ReFire
		goto Ready

	  FireSmooth:
		DBFG A 1 Offset(-1,33) A_JumpIfInventory("SamsaraHasDoubleFiringSpeed",1,"FireSmoothRage")
		DBFG A 1 Offset(0,33)
		DBFG A 1 Offset(1,33)
		DBFG A 1 Offset(0,32)
		DBFG A 1 Offset(-1,32)
		DBFG A 1 Offset(0,32)
		DBFG A 1 Offset(1,31)
		DBFG A 1 Offset(0,31)
		DBFG A 1 Offset(-1,31)
		DBFG A 11 Offset(0,30)
		DBFG B 10 Bright A_GunFlash
		TNT1 A 0 A_GiveInventory("SamsaraDoomguyBFG9000AttackHandler")
		DBFG B 1 Bright Offset(0,36) A_ZoomFactor(0.99,ZOOM_INSTANT)
		DBFG B 2 Bright Offset(0,40)
		DBFG C 2 Bright Offset(0,32)
		DBFG C 5 Bright A_ZoomFactor(0.995,ZOOM_INSTANT)
		TNT1 A 0 A_ZoomFactor(1,ZOOM_INSTANT)
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
		TNT1 A 0 A_JumpIfInventory("Cell",40,1)
		goto Nothing
		DBFG C 6 Bright Offset(0,26) A_ReFire
		DBFG B 4 Bright Offset(0,32)
		DBFG A 10
		goto Ready

	  FireSmoothRage:
		DBFG A 2 Offset(-1,33)
		DBFG A 2 Offset(1,33)
		DBFG A 2 Offset(-1,32)
		DBFG A 2 Offset(1,31)
		DBFG A 2 Offset(-1,31)
		DBFG A 10 Offset(0,30)
		DBFG B 10 Bright A_GunFlash
		TNT1 A 0 A_GiveInventory("SamsaraDoomguyBFG9000AttackHandler")
		DBFG B 2 Bright Offset(0,36) A_ZoomFactor(0.99,ZOOM_INSTANT)
		DBFG B 2 Bright Offset(0,40)
		DBFG C 2 Bright Offset(0,32)
		DBFG C 4 Bright A_ZoomFactor(0.995,ZOOM_INSTANT)
		TNT1 A 0 A_ZoomFactor(1,ZOOM_INSTANT)
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
		TNT1 A 0 A_JumpIfInventory("Cell",40,1)
		goto Nothing
		DBFG C 6 Bright Offset(0,26) A_ReFire
		DBFG B 4 Bright Offset(0,32)
		DBFG A 10
		goto Ready

	  FireStopSound:
		TNT1 A 0 A_PlaySound("silence",CHAN_6)
		TNT1 A 0 A_StopSound(CHAN_6)
		TNT1 A 0 A_TakeInventory("SamsaraDoomguyStrBFGReadySoundCheck")
		goto FireStart

	  Flash:
		DBFF A 11 bright A_Light1
		DBFF B 6 bright A_Light2
		goto LightDone

	  // Doom 64 (fires slightly slower, has blend effect on projectile impact)
	  Ready64:
		TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrBFGReadySoundCheck",1,"Ready64StopSound")
	  Ready64Continue:
		64BF A 1 A_WeaponReady
		goto Ready

	  Ready64StopSound:
		TNT1 A 0 A_PlaySound("silence",CHAN_6)
		TNT1 A 0 A_StopSound(CHAN_6)
		TNT1 A 0 A_TakeInventory("SamsaraDoomguyStrBFGReadySoundCheck")
		goto Ready64Continue

	  DeselectStart64:
		64BF A 1 A_Lower
		goto Deselect

	  Select64:
		64BF A 1 A_Raise
		goto Select

	  Fire64:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
		TNT1 A 0 A_JumpIfInventory("Cell",40,1)
		goto Nothing
		TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrBFGReadySoundCheck",1,"Fire64StopSound")
	  Fire64Start:
		TNT1 A 0 A_AlertMonsters
		64BF A 20 A_PlaySound("doom64guy/bfgf",CHAN_WEAPON)
		64BF A 10 A_GunFlash("Flash64")
		64BF B 5
		64BF B 10 A_GiveInventory("SamsaraDoomguy64BFG9000AttackHandler")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
		TNT1 A 0 A_JumpIfInventory("Cell",40,1)
		goto Nothing
		64BF B 25 A_ReFire
		goto Ready

	  Fire64StopSound:
		TNT1 A 0 A_PlaySound("silence",CHAN_6)
		TNT1 A 0 A_StopSound(CHAN_6)
		TNT1 A 0 A_TakeInventory("SamsaraDoomguyStrBFGReadySoundCheck")
		goto Fire64Start

	  Flash64:
		64BM A 11 Bright A_Light1
		64BM B 5 Bright
		64BM C 5 Bright A_Light2
		goto LightDone

	  // Stronghold
	  ReadyStr:
		TNT1 A 0 A_PlaySound("doomguy/stronghold/bfghum",CHAN_6,1,true)
		TNT1 A 0 A_GiveInventory("SamsaraDoomguyStrBFGReadySoundCheck")
		DBFG DEFG 3 Bright A_WeaponReady
		goto Ready

	  // Unpowered
	  DeselectStartStr:
		TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPower",1,"DeselectStartStrPowered")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"DeselectStartStrPowered")
	  DeselectStartStrContinue:
		TNT1 A 0 A_Lower
		DBFG R 1 A_Lower
		goto Deselect

	  SelectStr:
		TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPower",1,"SelectStrPowered")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"SelectStrPowered")
	  SelectStrContinue:
		TNT1 A 0 A_Raise
		DBFG R 1 A_Raise
		goto Select

	  FireStr:
		TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPower",1,"FireStrPowered")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"FireStrPowered")
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
		TNT1 A 0 A_JumpIfInventory("Cell",40,1)
		goto Nothing
		TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrBFGReadySoundCheck",1,"FireStrStopSound")
	  FireStrStart:
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("doomguy/stronghold/bfgf",CHAN_WEAPON)
		DBFG IJKLM 5 Bright
		DBFG N 5 Bright A_GunFlash("FlashStr")
        TNT1 A 0 A_JumpIfInventory("SamsaraImprovedDoomguyBalance",1,"FireStrFinishImproved")
      FireStrFinishVanilla:
		DBFG O 5 Bright A_GiveInventory("SamsaraDoomguyStrBFG9000AttackHandler")
		DBFG P 5 Bright
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
		TNT1 A 0 A_JumpIfInventory("Cell",40,1)
		goto Nothing
		DBFG Q 5 Bright A_ReFire
		DBFG H 15 Bright
		goto Ready

      FireStrFinishImproved:
		DBFG O 5 Bright A_GiveInventory("SamsaraDoomguyStrBFG9000AttackHandler")
		DBFG P 5 Bright
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
		TNT1 A 0 A_JumpIfInventory("Cell",40,1)
		goto Nothing
		DBFG Q 5 Bright A_ReFire
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
		TNT1 A 0 A_JumpIfInventory("Cell",40,1)
		goto Nothing
		DBFG H 5 Bright A_ReFire
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
		TNT1 A 0 A_JumpIfInventory("Cell",40,1)
		goto Nothing
		DBFG H 5 Bright A_ReFire
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
		TNT1 A 0 A_JumpIfInventory("Cell",40,1)
		goto Nothing
		DBFG H 5 Bright A_ReFire
		goto Ready

	  FireStrStopSound:
		TNT1 A 0 A_PlaySound("silence",CHAN_6)
		TNT1 A 0 A_StopSound(CHAN_6)
		TNT1 A 0 A_TakeInventory("SamsaraDoomguyStrBFGReadySoundCheck")
		goto FireStrStart

	  FlashStr:
		TNT1 A 5 A_Light1
		TNT1 A 5 A_Light2
		TNT1 A 5 A_Light1
		goto LightDone

	  // Powered
	  DeselectStartStrPowered:
		TNT1 A 0 A_Lower
		goto DeselectStartStrContinue

	  SelectStrPowered:
		TNT1 A 0 A_Raise
		goto SelectStrContinue

	  FireStrPowered:
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
		TNT1 A 0 A_JumpIfInventory("Cell",5,1)
		goto Nothing
		TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrBFGReadySoundCheck",1,"FireStrPoweredStopSound")
	  FireStrPoweredStart:
		TNT1 A 0 A_AlertMonsters
		DBFG IKM 1 Bright
		DBFG N 1 Bright A_GunFlash("FlashStrPowered")
		TNT1 A 0 A_GiveInventory("SamsaraDoomguyStrBFG9000PoweredAttackHandler")
		DBFG OQ 1 Bright
		TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
		TNT1 A 0 A_JumpIfInventory("Cell",5,1)
		goto Nothing
		DBFG H 7 Bright A_ReFire
		goto Ready

	  FireStrPoweredStopSound:
		TNT1 A 0 A_PlaySound("silence",CHAN_6)
		TNT1 A 0 A_StopSound(CHAN_6)
		TNT1 A 0 A_TakeInventory("SamsaraDoomguyStrBFGReadySoundCheck")
		goto FireStrPoweredStart

	  FlashStrPowered:
		TNT1 A 1 A_Light1
		TNT1 A 1 A_Light2
		TNT1 A 1 A_Light1
		goto LightDone

	  Nothing:
        TNT1 A 0 A_ClearReFire
		TNT1 A 0 A_SelectWeapon(" Pistol ")
		goto Deselect
	}
}

actor SamsaraDoomguyBFG9000AttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Cell",40,1)
        stop
        TNT1 A 0 A_TakeInventory("Cell",40)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("DoomNoBFGAim",1,"PickupAttackNoAim")
        TNT1 A 0 A_JumpIfInventory("SamsaraImprovedDoomguyBalance",1,"PickupAttackImproved")
      // Vanilla
      PickupAttackVanilla:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackVanillaCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackVanillaCoop")
      PickupAttackVanillaDM:
        TNT1 A 0 A_FireCustomMissile("DoomBFGBallVanilla",0,false)
        stop

      PickupAttackVanillaCoop:
        TNT1 A 0 A_FireCustomMissile("DoomBFGBallVanilla2",0,false)
        stop

      // Improved
      PickupAttackImproved:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackImprovedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackImprovedCoop")
      PickupAttackImprovedDM:
        TNT1 A 0 A_FireCustomMissile("DoomBFGBall",0,false)
        stop

      PickupAttackImprovedCoop:
        TNT1 A 0 A_FireCustomMissile("DoomBFGBall2",0,false)
        stop

      // No aiming attack stuff
      PickupAttackNoAim:
        TNT1 A 0 A_JumpIfInventory("SamsaraImprovedDoomguyBalance",1,"PickupAttackNoAimImproved")
      // Vanilla
      PickupAttackNoAimVanilla:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackNoAimVanillaCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackNoAimVanillaCoop")
      PickupAttackNoAimVanillaDM:
        TNT1 A 0 A_SpawnItemEx("DoomBFGBallVanilla",0,0,32,25)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,1)
        stop
        TNT1 A 0 A_SpawnItemEx("DoomBFGBallVanilla",0,0,32,25,0,0,-15)
        TNT1 A 0 A_SpawnItemEx("DoomBFGBallVanilla",0,0,32,25,0,0,15)
        stop

      PickupAttackNoAimVanillaCoop:
        TNT1 A 0 A_SpawnItemEx("DoomBFGBallVanilla2",0,0,32,25)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,1)
        stop
        TNT1 A 0 A_SpawnItemEx("DoomBFGBallVanilla2",0,0,32,25,0,0,-15)
        TNT1 A 0 A_SpawnItemEx("DoomBFGBallVanilla2",0,0,32,25,0,0,15)
        stop

      // Improved
      PickupAttackNoAimImproved:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackNoAimImprovedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackNoAimImprovedCoop")
      PickupAttackNoAimImprovedDM:
        TNT1 A 0 A_SpawnItemEx("DoomBFGBall",0,0,32,25)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,1)
        stop
        TNT1 A 0 A_SpawnItemEx("DoomBFGBall",0,0,32,25,0,0,-15)
        TNT1 A 0 A_SpawnItemEx("DoomBFGBall",0,0,32,25,0,0,15)
        stop

      PickupAttackNoAimImprovedCoop:
        TNT1 A 0 A_SpawnItemEx("DoomBFGBall2",0,0,32,25)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,1)
        stop
        TNT1 A 0 A_SpawnItemEx("DoomBFGBall2",0,0,32,25,0,0,-15)
        TNT1 A 0 A_SpawnItemEx("DoomBFGBall2",0,0,32,25,0,0,15)
        stop
    }
}

actor SamsaraDoomguy64BFG9000AttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Cell",40,1)
        stop
        TNT1 A 0 A_TakeInventory("Cell",40)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("DoomNoBFGAim",1,"PickupAttackNoAim")
        TNT1 A 0 A_JumpIfInventory("SamsaraImprovedDoomguyBalance",1,"PickupAttackImproved")
      // Vanilla
      PickupAttackVanilla:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackVanillaCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackVanillaCoop")
      PickupAttackVanillaDM:
        TNT1 A 0 A_FireCustomMissile("Doom64BFGBallVanilla",0,false)
        stop

      PickupAttackVanillaCoop:
        TNT1 A 0 A_FireCustomMissile("Doom64BFGBallVanilla2",0,false)
        stop

      // Improved
      PickupAttackImproved:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackImprovedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackImprovedCoop")
      PickupAttackImprovedDM:
        TNT1 A 0 A_FireCustomMissile("Doom64BFGBall",0,false)
        stop

      PickupAttackImprovedCoop:
        TNT1 A 0 A_FireCustomMissile("Doom64BFGBall2",0,false)
        stop

      // No aiming attack stuff
      PickupAttackNoAim:
        TNT1 A 0 A_JumpIfInventory("SamsaraImprovedDoomguyBalance",1,"PickupAttackNoAimImproved")
      // Vanilla
      PickupAttackNoAimVanilla:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackNoAimVanillaCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackNoAimVanillaCoop")
      PickupAttackNoAimVanillaDM:
        TNT1 A 0 A_SpawnItemEx("Doom64BFGBallVanilla",0,0,32,25)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,1)
        stop
        TNT1 A 0 A_SpawnItemEx("Doom64BFGBallVanilla",0,0,32,25,0,0,-15)
        TNT1 A 0 A_SpawnItemEx("Doom64BFGBallVanilla",0,0,32,25,0,0,15)
        stop

      PickupAttackNoAimVanillaCoop:
        TNT1 A 0 A_SpawnItemEx("Doom64BFGBallVanilla2",0,0,32,25)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,1)
        stop
        TNT1 A 0 A_SpawnItemEx("Doom64BFGBallVanilla2",0,0,32,25,0,0,-15)
        TNT1 A 0 A_SpawnItemEx("Doom64BFGBallVanilla2",0,0,32,25,0,0,15)
        stop

      // Improved
      PickupAttackNoAimImproved:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackNoAimImprovedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackNoAimImprovedCoop")
      PickupAttackNoAimImprovedDM:
        TNT1 A 0 A_SpawnItemEx("Doom64BFGBall",0,0,32,25)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,1)
        stop
        TNT1 A 0 A_SpawnItemEx("Doom64BFGBall",0,0,32,25,0,0,-15)
        TNT1 A 0 A_SpawnItemEx("Doom64BFGBall",0,0,32,25,0,0,15)
        stop

      PickupAttackNoAimImprovedCoop:
        TNT1 A 0 A_SpawnItemEx("Doom64BFGBall2",0,0,32,25)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,1)
        stop
        TNT1 A 0 A_SpawnItemEx("Doom64BFGBall2",0,0,32,25,0,0,-15)
        TNT1 A 0 A_SpawnItemEx("Doom64BFGBall2",0,0,32,25,0,0,15)
        stop
    }
}

actor DoomBFGBall : BFGBall
{
	DamageType "DoomDamageType"
	Decal DoomBFGLightning
	+BRIGHT
	+DONTREFLECT
	Damage (800)
	DeathSound "doomguy/bfgx"
	Obituary "$SAMSARA_DOOMGUY_OB_SLOT7_2"
	States
	{
	  Spawn:
		DBFS AB 4 bright
		loop
		
	  Death:
		DBFE AB 8 bright
		DBFE C 8 bright A_BFGSpray("DoomBFGSplash")
		DBFE DEF 8 bright
		stop
	}
}

actor DoomBFGBall2 : DoomBFGBall
{
	Species "Player"
	+THRUSPECIES
	States
	{
	  Death:
		DBFE AB 8 bright
		DBFE C 8 bright A_BFGSpray("DoomBFGSplashCoop")
		DBFE DEF 8 bright
		stop
	}
}

actor DoomBFGBallVanilla : DoomBFGBall { Damage 100 }

actor DoomBFGBallVanilla2 : DoomBFGBallVanilla
{
	Species "Player"
	+THRUSPECIES
	States
	{
	  Death:
		DBFE AB 8 bright
		DBFE C 8 bright A_BFGSpray("DoomBFGSplashCoop")
		DBFE DEF 8 bright
		stop
	}
}

actor DoomBFGSplash
{
	+NOBLOCKMAP
	+CLIENTSIDEONLY
	+NOGRAVITY
	DamageType "DoomDamageType"
	RenderStyle Add
	Alpha 0.75
	States
	{
	  Spawn:
		DBFP ABCD 8 bright
		stop
	}
}

actor DoomBFGSplashCoop : DoomBFGSplash { +MTHRUSPECIES }

actor Doom64BFGImpactFlash : Trigger
{
	States
	{
	  Pickup:
		TNT1 A 0 A_JumpIfInventory("WeaponFlashesAreDisabled",1,"PickupStop")
		TNT1 A 0 A_SetBlend("Green",0.45,20)
		stop
	}
}

actor Doom64BFGBall : BFGBall
{
	Renderstyle Normal
	scale 0.75
	Decal DoomBFGLightning
	+BRIGHT
	+DONTREFLECT
	+BLOODLESSIMPACT
	Damage (800)
	DeathSound "doom64guy/bfgx"
	Obituary "%k \cpvaporized\c- %o \cpwith the B.F.G. 9000.\c-"
	States
	{
	  Spawn:
		64BB AB 2 bright
		loop
		
	  Death:
		64BB C 8 bright A_GiveToTarget("Doom64BFGImpactFlash")
		64BB D 7 bright A_FadeOut(0.142857)
		64BB E 0 bright A_FadeOut(0.142857)
		64BB E 3 bright A_BFGSpray("Doom64BFGSplash")
		64BB F 3 bright A_FadeOut(0.142857)
		64BB G 3 bright A_FadeOut(0.142857)
		64BB H 3 bright A_FadeOut(0.142857)
		stop
	}
}

actor Doom64BFGBall2 : Doom64BFGBall
{
	Species "Player"
	+THRUSPECIES
	States
	{
	  Death:
		64BB C 8 bright A_GiveToTarget("Doom64BFGImpactFlash")
		64BB D 7 bright A_FadeOut(0.142857)
		TNT1 A 0 A_FadeOut(0.142857)
		64BB E 3 bright A_BFGSpray("Doom64BFGSplashCoop")
		64BB FGH 3 bright A_FadeOut(0.142857)
		stop
	}
}

actor Doom64BFGBallVanilla : Doom64BFGBall { Damage 100 }

actor Doom64BFGBallVanilla2 : Doom64BFGBallVanilla
{
	Species "Player"
	+THRUSPECIES
	States
	{
	  Death:
		64BB C 8 bright A_GiveToTarget("Doom64BFGImpactFlash")
		64BB D 7 bright A_FadeOut(0.142857)
		TNT1 A 0 A_FadeOut(0.142857)
		64BB E 3 bright A_BFGSpray("Doom64BFGSplashCoop")
		64BB FGH 3 bright A_FadeOut(0.142857)
		stop
	}
}

actor Doom64BFGSplash
{
	+NOBLOCKMAP
	+CLIENTSIDEONLY
	+NOGRAVITY
	RenderStyle Normal
	States
	{
	  Spawn:
		64B2 ABCDEF 3 bright
		stop
	}
}

actor Doom64BFGSplashCoop : Doom64BFGSplash { +MTHRUSPECIES }

// Unpowered attack handler

actor SamsaraDoomguyStrBFG9000AttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Cell",40,1)
        stop
        TNT1 A 0 A_TakeInventory("Cell",40)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("DoomNoBFGAim",1,"PickupAttackNoAim")
        TNT1 A 0 A_JumpIfInventory("SamsaraImprovedDoomguyBalance",1,"PickupAttackImproved")
      // Vanilla
      PickupAttackVanilla:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackVanillaCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackVanillaCoop")
      PickupAttackVanillaDM:
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrBFGBall",0,false)
        stop

      PickupAttackVanillaCoop:
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrBFGBallCoop",0,false)
        stop

      // Improved
      PickupAttackImproved:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackImprovedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackImprovedCoop")
      PickupAttackImprovedDM:
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrBFGBallImproved",0,false)
        stop

      PickupAttackImprovedCoop:
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrBFGBallImprovedCoop",0,false)
        stop

      // No aiming attack stuff
      PickupAttackNoAim:
        TNT1 A 0 A_JumpIfInventory("SamsaraImprovedDoomguyBalance",1,"PickupAttackNoAimImproved")
      // Vanilla
      PickupAttackNoAimVanilla:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackNoAimVanillaCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackNoAimVanillaCoop")
      PickupAttackNoAimVanillaDM:
        TNT1 A 0 A_SpawnItemEx("SamsaraDoomguyStrBFGBall",0,0,32,25)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,1)
        stop
        TNT1 A 0 A_SpawnItemEx("SamsaraDoomguyStrBFGBall",0,0,32,25,0,0,-15)
        TNT1 A 0 A_SpawnItemEx("SamsaraDoomguyStrBFGBall",0,0,32,25,0,0,15)
        stop

      PickupAttackNoAimVanillaCoop:
        TNT1 A 0 A_SpawnItemEx("SamsaraDoomguyStrBFGBallCoop",0,0,32,25)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,1)
        stop
        TNT1 A 0 A_SpawnItemEx("SamsaraDoomguyStrBFGBallCoop",0,0,32,25,0,0,-15)
        TNT1 A 0 A_SpawnItemEx("SamsaraDoomguyStrBFGBallCoop",0,0,32,25,0,0,15)
        stop

      // Improved
      PickupAttackNoAimImproved:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackNoAimImprovedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackNoAimImprovedCoop")
      PickupAttackNoAimImprovedDM:
        TNT1 A 0 A_SpawnItemEx("SamsaraDoomguyStrBFGBallImproved",0,0,32,25)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,1)
        stop
        TNT1 A 0 A_SpawnItemEx("SamsaraDoomguyStrBFGBallImproved",0,0,32,25,0,0,-15)
        TNT1 A 0 A_SpawnItemEx("SamsaraDoomguyStrBFGBallImproved",0,0,32,25,0,0,15)
        stop

      PickupAttackNoAimImprovedCoop:
        TNT1 A 0 A_SpawnItemEx("SamsaraDoomguyStrBFGBallImprovedCoop",0,0,32,25)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,1)
        stop
        TNT1 A 0 A_SpawnItemEx("SamsaraDoomguyStrBFGBallImprovedCoop",0,0,32,25,0,0,-15)
        TNT1 A 0 A_SpawnItemEx("SamsaraDoomguyStrBFGBallImprovedCoop",0,0,32,25,0,0,15)
        stop
    }
}

// Powered attack handler

actor SamsaraDoomguyStrBFG9000PoweredAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Cell",5,1)
        stop
        TNT1 A 0 A_TakeInventory("Cell",5)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("DoomNoBFGAim",1,"PickupAttackNoAim")
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrPowerBFGBall",0,false)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrPowerBFGBallCoop",0,false)
        stop

      // No aiming attack stuff
      PickupAttackNoAim:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackNoAimCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackNoAimCoop")
      PickupAttackNoAimDM:
        TNT1 A 0 A_SpawnItemEx("SamsaraDoomguyStrPowerBFGBallNoAim",0,0,32,25)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,1)
        stop
        TNT1 A 0 A_SpawnItemEx("SamsaraDoomguyStrPowerBFGBallNoAim",0,0,32,25,0,0,-15)
        TNT1 A 0 A_SpawnItemEx("SamsaraDoomguyStrPowerBFGBallNoAim",0,0,32,25,0,0,15)
        stop

      PickupAttackNoAimCoop:
        TNT1 A 0 A_SpawnItemEx("SamsaraDoomguyStrPowerBFGBallNoAimCoop",0,0,32,25)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,1)
        stop
        TNT1 A 0 A_SpawnItemEx("SamsaraDoomguyStrPowerBFGBallNoAimCoop",0,0,32,25,0,0,-15)
        TNT1 A 0 A_SpawnItemEx("SamsaraDoomguyStrPowerBFGBallNoAimCoop",0,0,32,25,0,0,15)
        stop
    }
}

// BFG ball

actor SamsaraDoomguyStrBFGBall : BFGBall
{
    DamageType "DoomDamageType"
    DeathSound "doomguy/stronghold/bfgx"
    Decal "DoomBFGLightning"
    +DONTREFLECT
    States
    {
      Spawn:
        DBFS CD 4 Bright Light("BFGBALL")
        loop

      Death:
        DBFE G 8 Bright Light("BFGBALL_X1")
        DBFE H 8 Bright Light("BFGBALL_X2")
        TNT1 A 0 A_BFGSpray("SamsaraDoomguyStrBFGExtra")
      DeathFinish:
        DBFE I 8 Bright Light("BFGBALL_X3")
        DBFE J 8 Bright Light("BFGBALL_X1")
        DBFE K 8 Bright Light("BFGBALL_X4")
        DBFE L 8 Bright Light("BFGBALL_X5")
        stop
    }
}

actor SamsaraDoomguyStrBFGBallCoop : SamsaraDoomguyStrBFGBall
{
    Species "Player"
    +THRUSPECIES
    States
    {
      Death:
        DBFE G 8 Bright Light("BFGBALL_X1")
        DBFE H 8 Bright Light("BFGBALL_X2")
        TNT1 A 0 A_BFGSpray("SamsaraDoomguyStrBFGExtraCoop")
        goto DeathFinish
    }
}

actor SamsaraDoomguyStrBFGBallImproved : SamsaraDoomguyStrBFGBall
{
    +FORCERADIUSDMG
    States
    {
      Death:
        DBFE G 8 Bright Light("BFGBALL_X1") A_BFGSpray("SamsaraDoomguyStrBFGExtra",40,8)
        DBFE H 8 Bright Light("BFGBALL_X2") A_Explode(128,256,0)
        TNT1 A 0 A_BFGSpray("SamsaraDoomguyStrBFGExtra",40,16)
        goto DeathFinish
    }
}

actor SamsaraDoomguyStrBFGBallImprovedCoop : SamsaraDoomguyStrBFGBallImproved
{
    Species "Player"
    +THRUSPECIES
    States
    {
      Death:
        DBFE G 8 Bright Light("BFGBALL_X1") A_BFGSpray("SamsaraDoomguyStrBFGExtraCoop",40,8)
        DBFE H 8 Bright Light("BFGBALL_X2") A_Explode(128,256,0)
        TNT1 A 0 A_BFGSpray("SamsaraDoomguyStrBFGExtraCoop",40,16)
        goto DeathFinish
    }
}

// BFG tracer

actor SamsaraDoomguyStrBFGExtra : BFGExtra
{
    DamageType "DoomDamageType"
    States
    {
      Spawn:
        DBFP E 8 Bright Light("BFGBALL")
        DBFP F 8 Bright Light("BFGBALL_X1")
        DBFP G 8 Bright Light("BFGBALL_X4")
        DBFP H 8 Bright Light("BFGBALL_X5")
        stop
    }
}

actor SamsaraDoomguyStrBFGExtraCoop : SamsaraDoomguyStrBFGExtra { +MTHRUSPECIES }

// Powered BFG ball

actor SamsaraDoomguyStrPowerBFGBall : SamsaraDoomguyStrBFGBall
{
    Speed 35
    Damage (100)
    DamageType "DoomDamageTypeNoSelfDamage"
    Scale 0.6
    SeeSound "doomguy/stronghold/powerbfgfire"
    DeathSound "doomguy/stronghold/powerbfgexplode"
    Decal "DoomBFGScorch"
    //SelfObituary "%o couldn't handle the overwhelming power."
    +EXTREMEDEATH
    +FORCERADIUSDMG
    States
    {
      Spawn:
        DBFS CD 4 Bright Light("BFGBALLp")
        loop

      Death:
        TNT1 A 0 A_GiveToTarget("SamsaraDoomguyNoSelfDamage")
        TNT1 A 0 A_Explode(96,144)
        TNT1 A 0 A_TakeFromTarget("PowerSamsaraDoomguyNoSelfDamage")
        TNT1 A 0 A_Explode(96,288,0)
        TNT1 A 0 A_BFGSpray("SamsaraDoomguyStrBFGExtra",20,12)
      DeathFinish:
        DBFE G 3 Bright Light("BFGBALL_X1p")
        DBFE H 3 Bright Light("BFGBALL_X2p")
        DBFE I 3 Bright Light("BFGBALL_X3p")
        DBFE J 3 Bright Light("BFGBALL_X1p")
        DBFE K 3 Bright Light("BFGBALL_X4p")
        DBFE L 3 Bright Light("BFGBALL_X5p")
        stop
    }
}

actor SamsaraDoomguyStrPowerBFGBallCoop : SamsaraDoomguyStrPowerBFGBall
{
    Species "Player"
    +THRUSPECIES
    States
    {
      Death:
        TNT1 A 0 A_GiveToTarget("SamsaraDoomguyNoSelfDamage")
        TNT1 A 0 A_Explode(96,144)
        TNT1 A 0 A_TakeFromTarget("PowerSamsaraDoomguyNoSelfDamage")
        TNT1 A 0 A_Explode(96,288,0)
        TNT1 A 0 A_BFGSpray("SamsaraDoomguyStrBFGExtraCoop",20,12)
        goto DeathFinish
    }
}

actor SamsaraDoomguyStrPowerBFGBallNoAim : SamsaraDoomguyStrPowerBFGBall
{
    SeeSound ""
    DeathSound ""
    +NOTIMEFREEZE
    States
    {
      Spawn:
        DBFS C 0 Bright NoDelay Light("BFGBALLp") A_PlaySound("doomguy/stronghold/powerbfgfire",CHAN_VOICE)
        TNT1 A 0 A_GiveInventory("SamsaraProjectileSpawnInit")
        TNT1 A 0 A_ChangeFlag("NOTIMEFREEZE",false)
        goto Super::Spawn

      Death:
        TNT1 A 0 A_JumpIfInventory("SamsaraProjectileSpawnInit",1,"DeathStart")
        TNT1 A 0 A_PlaySound("doomguy/stronghold/powerbfgfire",CHAN_VOICE)
        TNT1 A 0 A_GiveInventory("SamsaraProjectileSpawnInit")
      DeathStart:
        TNT1 A 0 A_PlaySound("doomguy/stronghold/powerbfgexplode",CHAN_VOICE)
        TNT1 A 0 A_GiveToTarget("SamsaraDoomguyNoSelfDamage")
        TNT1 A 0 A_Explode(96,144)
        TNT1 A 0 A_TakeFromTarget("PowerSamsaraDoomguyNoSelfDamage")
        TNT1 A 0 A_Explode(96,288,0)
        TNT1 A 0 A_BFGSpray("SamsaraDoomguyStrBFGExtra",20,12)
        TNT1 A 0 A_ChangeFlag("NOTIMEFREEZE",false)
        goto DeathFinish
    }
}

actor SamsaraDoomguyStrPowerBFGBallNoAimCoop : SamsaraDoomguyStrPowerBFGBallNoAim
{
    Species "Player"
    +THRUSPECIES
    States
    {
      Death:
        TNT1 A 0 A_JumpIfInventory("SamsaraProjectileSpawnInit",1,"DeathStart")
        TNT1 A 0 A_PlaySound("doomguy/stronghold/powerbfgfire",CHAN_VOICE)
        TNT1 A 0 A_GiveInventory("SamsaraProjectileSpawnInit")
      DeathStart:
        TNT1 A 0 A_PlaySound("doomguy/stronghold/powerbfgexplode",CHAN_VOICE)
        TNT1 A 0 A_GiveToTarget("SamsaraDoomguyNoSelfDamage")
        TNT1 A 0 A_Explode(96,144)
        TNT1 A 0 A_TakeFromTarget("PowerSamsaraDoomguyNoSelfDamage")
        TNT1 A 0 A_Explode(96,288,0)
        TNT1 A 0 A_BFGSpray("SamsaraDoomguyStrBFGExtraCoop",20,12)
        TNT1 A 0 A_ChangeFlag("NOTIMEFREEZE",false)
        goto DeathFinish
    }
}
