actor DGHasChaingun : Boolean {}
actor SamsaraDoomguyStrChaingunFiringSoundCheck : Boolean {}
actor SamsaraDoomguyStrChaingunPoweredAttackAmount : Counter { Inventory.MaxAmount 2 }
actor Doom64ChaingunSoundChannelCount : Counter { Inventory.MaxAmount 3 }

actor " Chaingun " : Weapon
{
    Weapon.SelectionOrder 700
    Weapon.AmmoUse 1
    Weapon.AmmoGive 0
    Weapon.AmmoType "Clip"
    Weapon.SlotNumber 4
	Tag "Chaingun"
    Inventory.PickupMessage "You got the Chaingun!"
    +ALT_USES_BOTH
    +UNDROPPABLE
    Obituary "$SAMSARA_DOOMGUY_OB_SLOT4"
	Inventory.RestrictedTo "DoomguyPlayer"
    States
    {
      // Classic
      Ready:
        TNT1 A 0 A_TakeInventory("SamsaraDoomguyStrChaingunFiringSoundCheck")
		TNT1 A 0 A_JumpIfInventory("Doom64Mode",1,"Ready64")
        TNT1 A 0 A_JumpIfInventory("DoomClassMode",1,"ReadyStr")
        DCHG A 1 A_WeaponReady(WRF_NOSECONDARY)
        loop

      Deselect:
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrChaingunFiringSoundCheck",1,"DeselectStopSound")
      DeselectStart:
		TNT1 A 0 A_JumpIfInventory("Doom64Mode",1,"DeselectStart64")
        TNT1 A 0 A_JumpIfInventory("DoomClassMode",1,"DeselectStartStr")
        DCHG A 1 A_Lower
        goto Deselect

      DeselectStopSound:
        TNT1 A 0 A_PlaySound("silence",CHAN_WEAPON)
        TNT1 A 0 A_StopSound(CHAN_WEAPON)
        TNT1 A 0 A_TakeInventory("SamsaraDoomguyStrChaingunFiringSoundCheck")
        goto DeselectStart

      Select:
        TNT1 A 0 A_TakeInventory("SamsaraDoomguyStrChaingunFiringSoundCheck")
		TNT1 A 0 A_JumpIfInventory("Doom64Mode",1,"Select64")
        TNT1 A 0 A_JumpIfInventory("DoomClassMode",1,"SelectStr")
        DCHG A 1 A_Raise
        loop

      Fire:
		TNT1 A 0 A_JumpIfInventory("Doom64Mode",1,"Fire64")
        TNT1 A 0 A_JumpIfInventory("DoomClassMode",1,"FireStr")
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrChaingunFiringSoundCheck",1,"FireStopSound")
      FireStart:
        TNT1 A 0 A_GunFlash
        TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"FireSmooth")
      FireVanilla:
        DCHG F 4 Bright A_GiveInventory("SamsaraDoomguyChaingunAttackHandler")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireVanilla2Attack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"FireVanilla2Attack")
        goto FireVanilla2

      FireVanilla2Attack:
        TNT1 A 0 A_GunFlash("Flash2")
      FireVanilla2:
        DCHG G 4 Bright A_GiveInventory("SamsaraDoomguyChaingunAttackHandler")
      FireFinish:
        TNT1 A 0 A_ReFire
        goto Ready

      FireSmooth:
        TNT1 A 0 A_GiveInventory("SamsaraDoomguyChaingunAttackHandler")
        DCHG BC 2 Bright
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireSmooth2Attack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"FireSmooth2Attack")
        goto FireSmooth2

      FireSmooth2Attack:
        TNT1 A 0 A_GunFlash("Flash2")
      FireSmooth2:
        TNT1 A 0 A_GiveInventory("SamsaraDoomguyChaingunAttackHandler")
        DCHG DE 2 Bright
        goto FireFinish

      FireStopSound:
        TNT1 A 0 A_PlaySound("silence",CHAN_WEAPON)
        TNT1 A 0 A_StopSound(CHAN_WEAPON)
        TNT1 A 0 A_TakeInventory("SamsaraDoomguyStrChaingunFiringSoundCheck")
        goto FireStart

      Flash:
        TNT1 A 5 Bright A_Light1
        goto LightDone

      Flash2:
        TNT1 A 5 Bright A_Light2
        goto LightDone

      // Stronghold
      // Unpowered
      ReadyStr:
        TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"ReadyStrPowered")
        DCHG H 1 A_WeaponReady(WRF_NOSECONDARY)
        goto Ready

      DeselectStartStr:
        TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"DeselectStartStrPowered")
      DeselectStartStrContinue:
        TNT1 A 0 A_Lower
        DCHG H 1 A_Lower
        goto Deselect

      SelectStr:
        TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"SelectStrPowered")
      SelectStrContinue:
        TNT1 A 0 A_Raise
        DCHG H 1 A_Raise
        goto Select

      FireStr:
        TNT1 A 0 A_PlaySound("doomguy/stronghold/chnglp",CHAN_WEAPON,1,true)
        TNT1 A 0 A_GiveInventory("SamsaraDoomguyStrChaingunFiringSoundCheck")
        TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"FireStrPowered")
        TNT1 A 0 A_GunFlash("FlashStr")
        DCHG J 2 Bright A_FireBullets(4,2.3,1,5,"SamsaraDoomguyStrBulletPuffBallisticThruGhost")
        DCHG K 1 Bright
        TNT1 A 0 A_GunFlash("FlashStr")
        DCHG L 2 Bright A_FireBullets(4,2.3,1,5,"SamsaraDoomguyStrBulletPuffBallisticThruGhost")
        DCHG M 1 Bright
      FireStrFinish:
        TNT1 A 0 A_ReFire
      FireStrFinishSound:
        TNT1 A 0 A_PlaySound("doomguy/stronghold/chngdn",CHAN_WEAPON)
        TNT1 A 0 A_TakeInventory("SamsaraDoomguyStrChaingunFiringSoundCheck")
        goto Ready

      FlashStr:
        TNT1 A 1 Bright A_Light1
        TNT1 A 1 Bright A_Light2
        TNT1 A 1 Bright A_Light1
        goto LightDone

      // Powered
      ReadyStrPowered:
        DCHG H 1 A_WeaponReady
        goto Ready

      DeselectStartStrPowered:
        TNT1 A 0 A_Lower
        goto DeselectStartStrContinue

      SelectStrPowered:
        TNT1 A 0 A_Raise
        goto SelectStrContinue

      FireStrPowered:
        TNT1 A 0 A_TakeInventory("SamsaraDoomguyStrChaingunPoweredAttackAmount") // reset
      FireStrPoweredLoop:
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrChaingunPoweredAttackAmount",0,"FireStrFinish")
        TNT1 A 0 A_GiveInventory("SamsaraDoomguyStrChaingunPoweredAttackAmount")
        TNT1 A 0 A_GunFlash("FlashStr")
        DCHG J 2 Bright A_FireBullets(0.75,0.75,-1,20,"SamsaraDoomguyStrElectricalPuff",0)
        DCHG K 1 Bright
        TNT1 A 0 A_GunFlash("FlashStr")
        DCHG L 2 Bright A_FireBullets(0.75,0.75,-1,20,"SamsaraDoomguyStrElectricalPuff")
        DCHG M 1 Bright
        loop

      AltFire:
        TNT1 A 0 A_PlaySound("doomguy/stronghold/chnglp",CHAN_WEAPON,1,true)
        TNT1 A 0 A_GiveInventory("SamsaraDoomguyStrChaingunFiringSoundCheck")
        TNT1 A 0 A_TakeInventory("SamsaraDoomguyStrChaingunPoweredAttackAmount") // reset
      AltFireLoop:
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrChaingunPoweredAttackAmount",0,"AltFireFinish")
        TNT1 A 0 A_GiveInventory("SamsaraDoomguyStrChaingunPoweredAttackAmount")
        TNT1 A 0 A_GunFlash
        DCHG J 2 Bright A_FireBullets(7.5,3,-1,16,"SamsaraDoomguyStrRepPuffNoDamage")
        DCHG K 1 Bright
        TNT1 A 0 A_GunFlash
        DCHG L 2 Bright A_FireBullets(7.5,3,-1,16,"SamsaraDoomguyStrRepPuffNoDamage")
        DCHG M 1 Bright
        loop

      AltFireFinish:
        TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"AltFireFinishReFire")
        TNT1 A 0 A_CheckReload
        goto FireStrFinishSound

      AltFireFinishReFire:
        TNT1 A 0 A_JumpIfInventory("Doom64Mode",1,2)
        TNT1 A 0 A_JumpIfInventory("DoomClassMode",1,2)
        TNT1 A 0 A_CheckReload
        goto FireStrFinishSound
        TNT1 A 0 A_ReFire
        goto FireStrFinishSound

      // Doom 64 (fires slightly faster and has recoil visual)
      Ready64:
        64CG A 1 A_WeaponReady(WRF_NOSECONDARY)
        goto Ready

      DeselectStart64:
        64CG A 1 A_Lower
        goto Deselect

      Select64:
        64CG A 1 A_Raise
        goto Select

      Fire64:
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrChaingunFiringSoundCheck",1,"Fire64StopSound")
      Fire64Start:
        TNT1 A 0 A_GunFlash("Flash64")
        TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"Fire64Smooth")
      Fire64Vanilla:
        64CG A 4 A_GiveInventory("SamsaraDoomguy64ChaingunAttackHandler")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"Fire64Vanilla2Attack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"Fire64Vanilla2Attack")
        goto Fire64Vanilla2

      Fire64Vanilla2Attack:
        TNT1 A 0 A_GunFlash("Flash64_2")
      Fire64Vanilla2:
        64CG C 3 A_GiveInventory("SamsaraDoomguy64ChaingunAttackHandler")
        goto FireFinish

      Fire64Smooth:
        64CG A 2 A_GiveInventory("SamsaraDoomguy64ChaingunAttackHandler")
		64CG B 2
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"Fire64Smooth2Attack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"Fire64Smooth2Attack")
        goto Fire64Smooth2

      Fire64Smooth2Attack:
        TNT1 A 0 A_GunFlash("Flash64_2")
      Fire64Smooth2:
        64CG C 2 A_GiveInventory("SamsaraDoomguy64ChaingunAttackHandler")
		64CG D 1
        goto FireFinish

      Fire64StopSound:
        TNT1 A 0 A_PlaySound("silence",CHAN_WEAPON)
        TNT1 A 0 A_StopSound(CHAN_WEAPON)
        TNT1 A 0 A_TakeInventory("SamsaraDoomguyStrChaingunFiringSoundCheck")
        goto Fire64Start

      Flash64:
        64CM A 3 Bright A_Light1
        goto LightDone

      Flash64_2:
        64CM B 2 Bright A_Light2
        goto LightDone

      Spawn:
        WCGN A -1
        stop
    }
}

actor SamsaraDoomguyChaingunAttackHandler : Trigger
{
    States
    {
      // Ammo check stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupTakeAmmo")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"PickupTakeAmmo")
        stop

      // Ammo taking stuff
      PickupTakeAmmo:
        TNT1 A 0 A_TakeInventory("Clip",1,TIF_NOTAKEINFINITE)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("doomguy/chngun",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SamsaraImprovedDoomguyBalance",1,"PickupAttackImproved")
      // Vanilla
      PickupAttackVanilla:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackVanillaCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackVanillaCoop")
      PickupAttackVanillaDM:
        TNT1 A 0 A_FireBullets(5.6,0,1,5,"DoomBulletPuff2ThruGhost",FBF_NOFLASH)
        stop

      PickupAttackVanillaCoop:
        TNT1 A 0 A_FireBullets(5.6,0,1,5,"DoomBulletPuff2ThruGhost_Coop",FBF_NOFLASH)
        stop

      // Improved
      PickupAttackImproved:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackImprovedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackImprovedCoop")
      PickupAttackImprovedDM:
        TNT1 A 0 A_FireBullets(5.6,0,1,random(8,10)*2,"DoomBulletPuff2ThruGhost",FBF_NORANDOM|FBF_NOFLASH)
        stop

      PickupAttackImprovedCoop:
        TNT1 A 0 A_FireBullets(5.6,0,1,random(8,10)*2,"DoomBulletPuff2ThruGhost_Coop",FBF_NORANDOM|FBF_NOFLASH)
        stop
    }
}

actor SamsaraDoomguy64ChaingunAttackHandler : Trigger
{
    States
    {
      // Ammo check stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupTakeAmmo")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"PickupTakeAmmo")
        stop

      // Ammo taking stuff
      PickupTakeAmmo:
        TNT1 A 0 A_TakeInventory("Clip",1,TIF_NOTAKEINFINITE)
        goto PickupPlaySound

      // Sound stuff
      PickupPlaySound:
        TNT1 A 0 A_JumpIfInventory("Doom64ChaingunSoundChannelCount",0,"PickupPlaySound7")
        TNT1 A 0 A_JumpIfInventory("Doom64ChaingunSoundChannelCount",2,"PickupPlaySound6")
        TNT1 A 0 A_JumpIfInventory("Doom64ChaingunSoundChannelCount",1,"PickupPlaySound4")
      PickupPlaySound1:
        TNT1 A 0 A_PlaySound("doom64guy/chngun",CHAN_WEAPON)
        TNT1 A 0 A_GiveInventory("Doom64ChaingunSoundChannelCount")
        goto PickupAttack

      PickupPlaySound4:
        TNT1 A 0 A_PlaySound("doom64guy/chngun")
        TNT1 A 0 A_GiveInventory("Doom64ChaingunSoundChannelCount")
        goto PickupAttack

      PickupPlaySound6:
        TNT1 A 0 A_PlaySound("doom64guy/chngun",CHAN_6)
        TNT1 A 0 A_GiveInventory("Doom64ChaingunSoundChannelCount")
        goto PickupAttack

      PickupPlaySound7:
        TNT1 A 0 A_PlaySound("doom64guy/chngun",CHAN_7)
        TNT1 A 0 A_TakeInventory("Doom64ChaingunSoundChannelCount")
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SamsaraImprovedDoomguyBalance",1,"PickupAttackImproved")
      // Vanilla
      PickupAttackVanilla:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackVanillaCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackVanillaCoop")
      PickupAttackVanillaDM:
        TNT1 A 0 A_FireBullets(5.6,0,1,random(1,4)*4,"Doom64BulletPuffThruGhost",FBF_NORANDOM|FBF_NOFLASH)
        goto PickupFinish

      PickupAttackVanillaCoop:
        TNT1 A 0 A_FireBullets(5.6,0,1,random(1,4)*4,"Doom64BulletPuffThruGhost_Coop",FBF_NORANDOM|FBF_NOFLASH)
        goto PickupFinish

      // Improved
      PickupAttackImproved:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackImprovedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackImprovedCoop")
      PickupAttackImprovedDM:
        TNT1 A 0 A_FireBullets(5.6,0,1,random(6,9)*2,"Doom64BulletPuffThruGhost",FBF_NORANDOM|FBF_NOFLASH)
        goto PickupFinish

      PickupAttackImprovedCoop:
        TNT1 A 0 A_FireBullets(5.6,0,1,random(6,9)*2,"Doom64BulletPuffThruGhost_Coop",FBF_NORANDOM|FBF_NOFLASH)
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 ACS_NamedExecuteAlways("SamsaraRecoil", 0, 1, 0, 5)
        stop
    }
}
