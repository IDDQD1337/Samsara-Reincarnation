actor DGStrHasAutoShotgun : Boolean {}
actor SamsaraDoomguyStrAutoShotgunPoweredAttackAmount : Counter { Inventory.MaxAmount 4 }

// Weapon

actor "Automatic Shotgun" : Weapon
{
    Inventory.PickupMessage "You got the automatic shotgun!"
    Inventory.RestrictedTo "DoomguyPlayer"
    Weapon.SlotNumber 3
    Weapon.SelectionOrder 700
    Weapon.AmmoType1 "DoomguyStrASGClip"
    Weapon.AmmoType2 "Shell"
    Weapon.AmmoUse1 1
    Obituary "%k blasted %o away with the auto shotgun."
    Tag "Auto Shotgun"
    +AMMO_OPTIONAL
    +NOALERT
    +UNDROPPABLE
    States
    {
      Spawn:
        WASG A -1
        stop

      Ready:
        TNT1 A 0 A_JumpIfInventory("DoomguyStrASGClip",0,"ReadyNonReloadable")
        TNT1 A 0 A_JumpIfInventory("Shell",1,"ReadyReloadable")
      ReadyNonReloadable:
        ASGG A 1 A_WeaponReady(WRF_NOSECONDARY)
        goto Ready

      ReadyReloadable:
        ASGG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
        goto Ready

      // Unpowered
      Deselect:
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPower",1,"DeselectPowered")
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPowerPerma",1,"DeselectPowered")
      DeselectContinue:
        TNT1 A 0 A_Lower
        ASGG A 1 A_Lower
        goto Deselect

      Select:
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPower",1,"SelectPowered")
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPowerPerma",1,"SelectPowered")
      SelectContinue:
        TNT1 A 0 A_Raise
        ASGG A 1 A_Raise
        goto Select

      Fire:
        TNT1 A 0 A_JumpIfInventory("DoomguyStrASGClip",1,"FireActual")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireActual")
        TNT1 A 0 A_JumpIfInventory("Shell",1,"Reload")
        ASGG A 2 A_PlaySound("doomguy/stronghold/asgout")
        goto Ready

      FireActual:
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPower",1,"FirePowered")
        ASGG A 1 A_JumpIfInventory("SamsaraDoomguyStrWeaponPowerPerma",1,"FirePowered")
        TNT1 A 0 A_GunFlash
        TNT1 A 0 A_PlaySound("doomguy/stronghold/asgfir",CHAN_WEAPON)
        TNT1 A 0 A_TakeInventory("DoomguyStrASGClip",1,TIF_NOTAKEINFINITE)
        ASGG L 2 Bright A_FireBullets(3.1,2.6,7,5,"SamsaraDoomguyStrBulletPuffBallisticThruGhost",0)
        ASGG M 3 Bright
        ASGG N 2 Bright
        ASGG A 7 A_PlaySound("doomguy/stronghold/asgld1")
      FireFinish:
        TNT1 A 0 A_ReFire
        goto Ready

      Flash:
        TNT1 A 4 Bright A_Light1
        TNT1 A 3 Bright A_Light2
        goto LightDone

      AltFire:
      Reload:
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPower",1,"ReloadPowered")
        ASGG B 3 A_JumpIfInventory("SamsaraDoomguyStrWeaponPowerPerma",1,"ReloadPowered")
        ASGG CDEF 3
        ASGG G 3 A_PlaySound("doomguy/stronghold/asgout")
        ASGG HIJ 3
        ASGG K 15
        ASGG JIH 3
      Reloading:
        TNT1 A 0 A_JumpIfInventory("DoomguyStrASGClip",0,"ReloadFinish")
        TNT1 A 0 A_JumpIfInventory("Shell",1,"ReloadingAmmoCalc")
      ReloadFinish:
        ASGG G 3 A_PlaySound("doomguy/stronghold/asgin")
        ASGG FEDCB 3
        ASGG A 8 A_PlaySound("doomguy/stronghold/asgld1")
        goto Ready

      ReloadingAmmoCalc:
        TNT1 A 0 A_TakeInventory("Shell",1)
        TNT1 A 0 A_GiveInventory("DoomguyStrASGClip")
        goto Reloading

      // Powered
      DeselectPowered:
        TNT1 A 0 A_Lower
        goto DeselectContinue

      SelectPowered:
        TNT1 A 0 A_Raise
        goto SelectContinue

      FirePowered:
        TNT1 A 0 A_TakeInventory("SamsaraDoomguyStrAutoShotgunPoweredAttackAmount") // reset
      FirePoweredLoop:
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrAutoShotgunPoweredAttackAmount",0,"FireFinish")
        TNT1 A 0 A_GiveInventory("SamsaraDoomguyStrAutoShotgunPoweredAttackAmount")
        TNT1 A 0 A_GunFlash("FlashPowered")
        TNT1 A 0 A_PlaySound("doomguy/stronghold/asgfir",CHAN_WEAPON)
        TNT1 A 0 A_GiveInventory("SamsaraDoomguyStrAutoShotgunPoweredAttackHandler")
        ASGG LMN 1 Bright
        ASGG A 1 A_PlaySound("doomguy/stronghold/asgld1")
        loop

      FlashPowered:
        TNT1 A 2 Bright A_Light1
        TNT1 A 2 Bright A_Light2
        goto LightDone

      ReloadPowered:
        ASGG BCDEF 2
        ASGG G 3 A_PlaySound("doomguy/stronghold/asgout")
        ASGG HIJ 2
        ASGG K 10
        ASGG JIH 2
        goto Reloading
    }
}

// Powered attack handler

actor SamsaraDoomguyStrAutoShotgunPoweredAttackHandler : Trigger
{
    States
    {
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrAutoShotgunPoweredAttackAmount",0,"PickupTakeAmmo")
      PickupContinue:
        TNT1 A 0 A_FireBullets(8.6,5.1,8,5,"SamsaraDoomguyStrElectricalPuff",FBF_NOFLASH) //Yes, I nerfed its accuracy. However, you get a chain-shotgun.
        stop

      PickupTakeAmmo:
        TNT1 A 0 A_TakeInventory("DoomguyStrASGClip",1,TIF_NOTAKEINFINITE)
        goto PickupContinue
    }
}

// Ammo

actor DoomguyStrASGClip : Ammo
{
    Inventory.MaxAmount 20
    Tag "Auto Shotgun Magazine"
    +IGNORESKILL
    +UNTOSSABLE
}
