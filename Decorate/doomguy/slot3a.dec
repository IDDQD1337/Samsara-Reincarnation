actor SamsaraDoomguyStrSuperShotgunSecondaryPoweredAttackAmount : Counter { Inventory.MaxAmount 2 }

actor "Super Shotgun" : SuperShotgun
{
    Weapon.AmmoType2 "Shell"
    Weapon.AmmoGive 0
    Weapon.AmmoUse2 4
    Weapon.SlotNumber 3
    +INVENTORY.UNDROPPABLE
    AttackSound ""
	Tag "Super Shotgun"
    Obituary "$SAMSARA_DOOMGUY_OB_SLOT3"
	Inventory.RestrictedTo "DoomguyPlayer"
    States
    {
      Spawn:
        WSSG A -1
        stop

      // Original
      Ready:
        TNT1 A 0 A_JumpIfInventory("DoomClassMode",2,"ReadyStr")
		TNT1 A 0 A_JumpIfInventory("DoomClassMode",1,"Ready64")
        DSSG A 1 A_WeaponReady(WRF_NOSECONDARY)
        loop
	
      Deselect:
        TNT1 A 0 A_JumpIfInventory("DoomClassMode",2,"DeselectStr")
		TNT1 A 0 A_JumpIfInventory("DoomClassMode",1,"Deselect64")
        DSSG A 1 A_Lower
        loop

      Select:
        TNT1 A 0 A_JumpIfInventory("DoomClassMode",2,"SelectStr")
		TNT1 A 0 A_JumpIfInventory("DoomClassMode",1,"Select64")
        DSSG A 1 A_Raise
        loop

      Fire:
        TNT1 A 0 A_JumpIfInventory("DoomClassMode",2,"FireStr")
        DSSG A 3 A_JumpIfInventory("DoomClassMode",1,"Fire64")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireAttack")
        TNT1 A 0 A_JumpIfInventory("Shell",2,"FireAttack")
        goto FireContinue

      FireAttack:
        TNT1 A 0 A_GunFlash
      FireContinue:
        TNT1 A 0 A_GiveInventory("SamsaraDoomguySuperShotgunAttackHandler")
        TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"FireSmooth")
      FireVanilla:
        DSSG A 7 Bright
        DSG2 B 7
        DSG2 C 7 A_CheckReload
        DSG2 D 7 A_PlaySound("doomguy/sshoto",CHAN_WEAPON)
        DSG2 E 7
        DSG2 F 7 A_PlaySound("doomguy/sshotl",CHAN_WEAPON)
        DSG2 G 6
        TNT1 A 0 A_PlaySound("doomguy/sshotc",CHAN_WEAPON)
        DSG2 H 6 A_ReFire
        DSSG A 5 A_ReFire
        goto Ready

      FireSmooth:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasDoubleFiringSpeed",1,"FireSmoothRage")
        DSSG A 2 Bright A_ZoomFactor(0.985,ZOOM_INSTANT)
        DSSG A 4 Bright A_ZoomFactor(1,ZOOM_INSTANT)
        DSSG A 3
        DSSG B 2
        DSSG C 3
        DSSG D 4 A_CheckReload
        DSSG E 4
        DSSG F 2 A_PlaySound("doomguy/sshoto",CHAN_WEAPON)
        DSSG GHIJ 2
        DSSG K 3
        DSSG L 3 A_PlaySound("doomguy/sshotl",CHAN_WEAPON)
        DSSG MNOP 2
        DSSG Q 1
        DSSG Q 1 A_PlaySound("doomguy/sshotc",CHAN_WEAPON)
        DSSG R 3 A_ReFire
        DSSG S 3
        DSSG T 3 A_ReFire
        DSSG A 2
        goto Ready

      FireSmoothRage:
        DSSG A 6 Bright
        DSSG A 4
        DSSG B 2
        DSSG C 4
        DSSG D 4 A_CheckReload
        DSSG E 4
        DSSG F 2 A_PlaySound("doomguy/sshoto",CHAN_WEAPON)
        DSSG GHI 2
        DSSG J 4
        DSSG K 2
        DSSG L 4 A_PlaySound("doomguy/sshotl",CHAN_WEAPON)
        DSSG MN 2
        DSSG O 4
        DSSG P 2
        DSSG Q 2 A_PlaySound("doomguy/sshotc",CHAN_WEAPON)
        DSSG R 2 A_ReFire
        DSSG S 4
        DSSG T 4 A_ReFire
        DSSG A 2
        goto Ready

      Flash:
        DSSF A 3 Bright A_Light1
        DSSF B 3 Bright A_Light2
        goto LightDone

      // Doom 64 (has recoil visual and kickback)
      Ready64:
		TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"Ready64Smooth")
      Ready64Vanilla:
        64DB A 1 A_WeaponReady(WRF_NOSECONDARY)
        goto Ready

      Ready64Smooth:
        64DB D 1 A_WeaponReady(WRF_NOSECONDARY)
        goto Ready

      Deselect64:
		TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"Deselect64Smooth")
      Deselect64Vanilla:
        64DB A 1 A_Lower
        goto Deselect

      Deselect64Smooth:
        64DB D 1 A_Lower
        goto Deselect

      Select64:
		TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"Select64Smooth")
      Select64Vanilla:
        64DB A 1 A_Raise
        goto Select

      Select64Smooth:
        64DB D 1 A_Raise
        goto Select

      Fire64:
        TNT1 A 0 A_GunFlash("Flash64")
        TNT1 A 0 A_GiveInventory("SamsaraDoomguy64SuperShotgunAttackHandler")
		TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"Fire64Smooth")
	  Fire64Vanilla:
        64DB A 5
        64DB C 10
        64DB C 23 A_CheckReload
        64DB B 6 A_PlaySound("doom64guy/sshoto",CHAN_WEAPON)
        64DB A 6 A_PlaySound("doom64guy/sshotc",CHAN_WEAPON)
        64DB A 5 A_ReFire
        goto Ready

      Fire64Smooth:
        64DB X 5 Bright
		64DB EF 3
		64DB GH 2
		64DB I 2 A_CheckReload
		64DB HJ 2
		64DB K 3 A_PlaySound("doom64guy/sshoto",CHAN_WEAPON)
		64DB L 4
        64DB M 3
		64DB N 4 A_PlaySound("doom64guy/sshotl",CHAN_WEAPON)
		64DB O 3
        64DB P 4 A_PlaySound("doom64guy/sshotc",CHAN_WEAPON)
		64DB QST 2
		64DB UW 1
        64DB D 5 A_ReFire
        goto Ready

      Flash64:
        64DM A 2 Bright A_Light1
        64DM A 2 Bright A_Light2
        goto LightDone		

      // Stronghold
      // Unpowered
      ReadyStr:
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPower",1,"ReadyStrPowered")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"ReadyStrPowered")
        DSG2 I 1 A_WeaponReady(WRF_NOSECONDARY)
        goto Ready

      DeselectStr:
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPower",1,"DeselectStrPowered")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"DeselectStrPowered")
      DeselectStrContinue:
        TNT1 A 0 A_Lower
        DSG2 I 1 A_Lower
        goto Deselect

      SelectStr:
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPower",1,"SelectStrPowered")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"SelectStrPowered")
      SelectStrContinue:
        TNT1 A 0 A_Raise
        DSG2 I 1 A_Raise
        goto Select

      FireStr:
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPower",1,"FireStrPowered")
        DSG2 I 3 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"FireStrPowered")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStrAttack")
        TNT1 A 0 A_JumpIfInventory("Shell",2,"FireStrAttack")
        goto FireStrContinue

      FireStrAttack:
        TNT1 A 0 A_GunFlash("FlashStr")
      FireStrContinue:
        DSG2 U 2 Bright A_GiveInventory("SamsaraDoomguyStrSuperShotgunAttackHandler")
        DSG2 V 3 Bright
        DSG2 W 2 Bright
        DSG2 J 3
        DSG2 K 4
        DSG2 L 4 A_CheckReload
        DSG2 K 3
        DSG2 M 3 A_PlaySound("doomguy/stronghold/sshoto",CHAN_WEAPON)
        DSG2 N 3
        DSG2 OP 4
        DSG2 Q 3 A_PlaySound("doomguy/stronghold/sshotl",CHAN_WEAPON)
        DSG2 RS 3
        DSG2 T 4
        TNT1 A 0 A_PlaySound("doomguy/stronghold/sshotc",CHAN_WEAPON)
        DSG2 M 6 A_ReFire
        DSG2 I 5 A_ReFire
        goto Ready

      FlashStr:
        TNT1 A 2 Bright A_Light1
        TNT1 A 3 Bright A_Light2
      FlashStrFinish:
        TNT1 A 2 Bright A_Light1
        goto LightDone

      // Powered
      ReadyStrPowered:
        DSG2 I 1 A_WeaponReady
        goto Ready

      DeselectStrPowered:
        TNT1 A 0 A_Lower
        goto DeselectStrContinue

      SelectStrPowered:
        TNT1 A 0 A_Raise
        goto SelectStrContinue

      FireStrPowered:
        TNT1 A 0 A_TakeInventory("SamsaraDoomguyStrSuperShotgunSecondaryPoweredAttackAmount") // reset
      FireStrPoweredLoop:
        DSG2 I 2 A_JumpIfInventory("SamsaraDoomguyStrSuperShotgunSecondaryPoweredAttackAmount",0,"FireStrPoweredContinue")
        TNT1 A 0 A_GiveInventory("SamsaraDoomguyStrSuperShotgunSecondaryPoweredAttackAmount")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStrPoweredLoopAttack")
        TNT1 A 0 A_JumpIfInventory("Shell",2,"FireStrPoweredLoopAttack")
        goto FireStrPoweredLoopContinue

      FireStrPoweredLoopAttack:
        TNT1 A 0 A_GunFlash("FlashStrPowered")
      FireStrPoweredLoopContinue:
        TNT1 A 0 A_GiveInventory("SamsaraDoomguyStrSuperShotgunPoweredPrimaryAttackHandler")
        DSG2 UVW 2 Bright
        goto FireStrPoweredLoop

      FireStrPoweredContinue:
        DSG2 JK 2
        DSG2 L 2 A_CheckReload
        DSG2 K 2
        DSG2 M 2 A_PlaySound("doomguy/stronghold/sshoto",CHAN_WEAPON)
        DSG2 NOP 2
        DSG2 Q 2 A_PlaySound("doomguy/stronghold/sshotl",CHAN_WEAPON)
        DSG2 RST 2
        TNT1 A 0 A_PlaySound("doomguy/stronghold/sshotc",CHAN_WEAPON)
        DSG2 MI 2 A_ReFire
        goto Ready

      AltFire:
        TNT1 A 0 A_TakeInventory("SamsaraDoomguyStrSuperShotgunSecondaryPoweredAttackAmount") // reset
      AltFireLoop:
        DSG2 I 2 A_JumpIfInventory("SamsaraDoomguyStrSuperShotgunSecondaryPoweredAttackAmount",0,"AltFireContinue")
        TNT1 A 0 A_GiveInventory("SamsaraDoomguyStrSuperShotgunSecondaryPoweredAttackAmount")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"AltFireLoopAttack")
        TNT1 A 0 A_JumpIfInventory("Shell",4,"AltFireLoopAttack")
        goto AltFireLoopContinue

      AltFireLoopAttack:
        TNT1 A 0 A_GunFlash("FlashStrPowered")
      AltFireLoopContinue:
        TNT1 A 0 A_GiveInventory("SamsaraDoomguyStrSuperShotgunPoweredSecondaryAttackHandler")
        DSG2 UVW 2 Bright
        goto AltFireLoop

      AltFireContinue:
        DSG2 JK 2
        DSG2 L 2 A_CheckReload
        DSG2 K 2
        DSG2 M 2 A_PlaySound("doomguy/stronghold/sshoto",CHAN_WEAPON)
        DSG2 NOP 2
        DSG2 Q 2 A_PlaySound("doomguy/stronghold/sshotl",CHAN_WEAPON)
        DSG2 RST 2
        TNT1 A 0 A_PlaySound("doomguy/stronghold/sshotc",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPower",1,"AltFireFinishReFire")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"AltFireFinishReFire")
        TNT1 A 0 A_CheckReload
      AltFireFinish:
        DSG2 M 2
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPower",1,"AltFireFinish2ReFire")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"AltFireFinish2ReFire")
        TNT1 A 0 A_CheckReload
      AltFireFinish2:
        DSG2 I 2
        goto Ready

      AltFireFinishReFire:
        TNT1 A 0 A_JumpIfInventory("DoomClassMode",2,2)
        TNT1 A 0 A_CheckReload
        goto AltFireFinish
        TNT1 A 0 A_ReFire
        goto AltFireFinish

      AltFireFinish2ReFire:
        TNT1 A 0 A_JumpIfInventory("DoomClassMode",2,2)
        TNT1 A 0 A_CheckReload
        goto AltFireFinish2
        TNT1 A 0 A_ReFire
        goto AltFireFinish2

      FlashStrPowered:
        TNT1 A 2 Bright A_Light1
        TNT1 A 2 Bright A_Light2
        goto FlashStrFinish
    }
}

actor SamsaraDoomguySuperShotgunAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Shell",2,1)
        stop
        TNT1 A 0 A_TakeInventory("Shell",2)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("doomguy/sshotf",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SamsaraImprovedDoomguyBalance",1,"PickupAttackImproved")
      // Vanilla
      PickupAttackVanilla:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackVanillaCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackVanillaCoop")
      PickupAttackVanillaDM:
        TNT1 A 0 A_FireBullets(11.2,7.1,20,5,"DoomBulletPuff2ThruGhost",FBF_NOFLASH)
        stop

      PickupAttackVanillaCoop:
        TNT1 A 0 A_FireBullets(11.2,7.1,20,5,"DoomBulletPuff2ThruGhost_Coop",FBF_NOFLASH)
        stop

      // Improved
      PickupAttackImproved:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackImprovedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackImprovedCoop")
      PickupAttackImprovedDM:
        TNT1 A 0 A_FireBullets(11.2,7.1,10,5,"DoomBulletPuff2ThruGhost",FBF_NOFLASH)
        TNT1 A 0 A_FireBullets(11.2,7.1,10,7,"DoomBulletPuff2ThruGhost",FBF_NOFLASH)
        stop

      PickupAttackImprovedCoop:
        TNT1 A 0 A_FireBullets(11.2,7.1,10,5,"DoomBulletPuff2ThruGhost_Coop",FBF_NOFLASH)
        TNT1 A 0 A_FireBullets(11.2,7.1,10,7,"DoomBulletPuff2ThruGhost_Coop",FBF_NOFLASH)
        stop
    }
}

actor SamsaraDoomguy64SuperShotgunAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Shell",2,1)
        stop
        TNT1 A 0 A_TakeInventory("Shell",2)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("doom64guy/sshotf",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SamsaraImprovedDoomguyBalance",1,"PickupAttackImproved")
      // Vanilla
      PickupAttackVanilla:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackVanillaCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackVanillaCoop")
      PickupAttackVanillaDM:
        TNT1 A 0 A_FireBullets(11.2,7.1,20,5,"Doom64BulletPuffThruGhost",FBF_NOFLASH)
        goto PickupFinish

      PickupAttackVanillaCoop:
        TNT1 A 0 A_FireBullets(11.2,7.1,20,5,"Doom64BulletPuffThruGhost_Coop",FBF_NOFLASH)
        goto PickupFinish

      // Improved
      PickupAttackImproved:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackImprovedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackImprovedCoop")
      PickupAttackImprovedDM:
        TNT1 A 0 A_FireBullets(11.2,7.1,10,5,"Doom64BulletPuffThruGhost",FBF_NOFLASH)
        TNT1 A 0 A_FireBullets(11.2,7.1,10,7,"Doom64BulletPuffThruGhost",FBF_NOFLASH)
        goto PickupFinish

      PickupAttackImprovedCoop:
        TNT1 A 0 A_FireBullets(11.2,7.1,10,5,"Doom64BulletPuffThruGhost_Coop",FBF_NOFLASH)
        TNT1 A 0 A_FireBullets(11.2,7.1,10,7,"Doom64BulletPuffThruGhost_Coop",FBF_NOFLASH)
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 A_Recoil(2)
        TNT1 A 0 ACS_NamedExecuteAlways("SamsaraRecoil", 0, 2, 0, 20)
        stop
    }
}

// Unpowered attack handler

actor SamsaraDoomguyStrSuperShotgunAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Shell",2,1)
        stop
        TNT1 A 0 A_TakeInventory("Shell",2)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("doomguy/stronghold/sshotf",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SamsaraImprovedDoomguyBalance",1,"PickupAttackImproved")
      // Vanilla
      PickupAttackVanilla:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackVanillaCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackVanillaCoop")
      PickupAttackVanillaDM:
        TNT1 A 0 A_FireBullets(11.2,7.1,20,5,"SamsaraDoomguyStrBulletPuffBallisticThruGhost",FBF_NOFLASH)
        stop

      PickupAttackVanillaCoop:
        TNT1 A 0 A_FireBullets(11.2,7.1,20,5,"SamsaraDoomguyStrBulletPuffBallisticThruGhostCoop",FBF_NOFLASH)
        stop

      // Improved
      PickupAttackImproved:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackImprovedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackImprovedCoop")
      PickupAttackImprovedDM:
        TNT1 A 0 A_FireBullets(11.2,7.1,10,5,"SamsaraDoomguyStrBulletPuffBallisticThruGhost",FBF_NOFLASH)
        TNT1 A 0 A_FireBullets(11.2,7.1,10,7,"SamsaraDoomguyStrBulletPuffBallisticThruGhost",FBF_NOFLASH)
        stop

      PickupAttackImprovedCoop:
        TNT1 A 0 A_FireBullets(11.2,7.1,10,5,"SamsaraDoomguyStrBulletPuffBallisticThruGhostCoop",FBF_NOFLASH)
        TNT1 A 0 A_FireBullets(11.2,7.1,10,7,"SamsaraDoomguyStrBulletPuffBallisticThruGhostCoop",FBF_NOFLASH)
        stop
    }
}

// Powered attack handlers

actor SamsaraDoomguyStrSuperShotgunPoweredPrimaryAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Shell",2,1)
        stop
        TNT1 A 0 A_TakeInventory("Shell",2)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("doomguy/stronghold/sshotf",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireBullets(14.6,9.4,20,12,"SamsaraDoomguyStrElectricalPuff",FBF_NOFLASH)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireBullets(14.6,9.4,20,12,"SamsaraDoomguyStrElectricalPuffCoop",FBF_NOFLASH)
        stop
    }
}

actor SamsaraDoomguyStrSuperShotgunPoweredSecondaryAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Shell",4,1)
        stop
        TNT1 A 0 A_TakeInventory("Shell",4)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("doomguy/stronghold/sshotf",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireBullets(14.6,9.4,20,3,"SamsaraDoomguyStrRepPuffNoDamage",FBF_NOFLASH)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireBullets(14.6,9.4,20,3,"SamsaraDoomguyStrRepPuffNoDamageCoop",FBF_NOFLASH)
        stop
    }
}
