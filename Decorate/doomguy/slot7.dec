actor DGHasBFG9000 : Boolean {}

Actor "B.F.G. 9000" : BFG9000
{
    //Weapon.SlotNumber 7
	Weapon.AmmoGive 0
    DamageType "BFG"
    Obituary "$SAMSARA_DOOMGUY_OB_SLOT7_1"
	Inventory.RestrictedTo "DoomguyPlayer"
    +INVENTORY.UNDROPPABLE
	Tag "BFG9000"
    States
    {
      // Classic
      Ready:
		TNT1 A 0 A_JumpIfInventory("Doom64Mode",1,"Ready64")
        DBFG A 1 A_WeaponReady
        loop

      Deselect:
		TNT1 A 0 A_JumpIfInventory("Doom64Mode",1,"Deselect64")
        DBFG A 1 A_Lower
        loop

      Select:
		TNT1 A 0 A_JumpIfInventory("Doom64Mode",1,"Select64")
        DBFG A 1 A_Raise
        loop

      Fire:
		TNT1 A 0 A_JumpIfInventory("Doom64Mode",1,"Fire64")
        TNT1 A 0 A_PlaySound("doomguy/bfgf",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"FireSmooth")
      FireVanilla:
        DBFG A 20
        DBFG B 10 Bright A_GunFlash
        DBFG B 10 Bright A_GiveInventory("SamsaraDoomguyBFG9000AttackHandler")
        DBFG B 20 A_ReFire
        goto Ready

      FireSmooth:
        DBFG A 1 Offset(-1,33) A_JumpIfInventory("SamsaraHasDoubleFiringSpeed",1,"FireSmoothRage")
        DBFG A 1 Offset(0,33)
        DBFG A 1 Offset(1,33)
        DBFG A 1 Offset(0,32)
        DBFG A 1 Offset(-1,32)
        DBFG A 1 Offset(0,32)
        DBFG A 1 Offset(1,31)
        DBFG A 1 Offset(0,31)
        DBFG A 1 Offset(-1,31)
        DBFG A 1 Offset(0,30)
        DBFG A 10 Offset(0,30)
        DBFG B 10 Bright A_GunFlash
        TNT1 A 0 A_GiveInventory("SamsaraDoomguyBFG9000AttackHandler")
        DBFG B 1 Bright Offset(0,36) A_ZoomFactor(0.99,ZOOM_INSTANT)
        DBFG B 2 Bright Offset(0,40)
        DBFG C 2 Bright Offset(0,32)
        DBFG C 5 Bright Offset(0,32) A_ZoomFactor(0.995,ZOOM_INSTANT)
        TNT1 A 0 A_ZoomFactor(1,ZOOM_INSTANT)
        DBFG C 3 Bright Offset(0,26) A_ReFire
        DBFG C 3 Bright
        DBFG B 4 Bright Offset(0,32)
        DBFG A 10
        goto Ready

      FireSmoothRage:
        DBFG A 2 Offset(-1,33)
        DBFG A 2 Offset(1,33)
        DBFG A 2 Offset(-1,32)
        DBFG A 2 Offset(1,31)
        DBFG A 2 Offset(-1,31)
        DBFG A 10 Offset(0,30)
        DBFG B 10 Bright A_GunFlash
        TNT1 A 0 A_GiveInventory("SamsaraDoomguyBFG9000AttackHandler")
        DBFG B 2 Bright Offset(0,36) A_ZoomFactor(0.99,ZOOM_INSTANT)
        DBFG B 2 Bright Offset(0,40)
        DBFG C 2 Bright Offset(0,32)
        DBFG C 4 Bright Offset(0,32) A_ZoomFactor(0.995,ZOOM_INSTANT)
        TNT1 A 0 A_ZoomFactor(1,ZOOM_INSTANT)
        DBFG C 4 Bright Offset(0,26) A_ReFire
        DBFG C 2 Bright
        DBFG B 4 Bright Offset(0,32)
        DBFG A 10
        goto Ready

      Flash:
        DBFF A 11 bright A_Light1
        DBFF B 6 bright A_Light2
        goto LightDone

      // Doom 64 (fires slightly slower, has blend effect on projectile impact)
      Ready64:
        64BF A 1 A_WeaponReady
        goto Ready

      Deselect64:
        64BF A 1 A_Lower
        goto Deselect

      Select64:
        64BF A 1 A_Raise
        goto Select

      Fire64:
        64BF A 20 A_PlaySound("doom64guy/bfgf",CHAN_WEAPON)
        64BF A 10 A_GunFlash("Flash64")
        64BF B 5
        64BF B 10 A_GiveInventory("SamsaraDoomguyBFG900064AttackHandler")
        64BF B 25 A_ReFire
        goto Ready

      Flash64:
        64BM A 11 Bright A_Light1
        64BM B 5 Bright
        64BM C 5 Bright A_Light2
        goto LightDone

      Spawn:
        WBFG A -1
        stop
    }
}

actor SamsaraDoomguyBFG9000AttackHandler : Trigger
{
    States
    {
      // Ammo check stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupTakeAmmo")
        TNT1 A 0 A_JumpIfInventory("Cell",40,"PickupTakeAmmo")
        stop

      // Ammo taking stuff
      PickupTakeAmmo:
        TNT1 A 0 A_TakeInventory("Cell",40,TIF_NOTAKEINFINITE)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("DoomNoBFGAim",1,"PickupAttackNoAim")
        TNT1 A 0 A_JumpIfInventory("SamsaraImprovedDoomguyBalance",1,"PickupAttackImproved")
      // Vanilla
      PickupAttackVanilla:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackVanillaCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackVanillaCoop")
      PickupAttackVanillaDM:
        TNT1 A 0 A_FireCustomMissile("DoomBFGBallVanilla",0,false)
        stop

      PickupAttackVanillaCoop:
        TNT1 A 0 A_FireCustomMissile("DoomBFGBallVanilla2",0,false)
        stop

      // Improved
      PickupAttackImproved:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackImprovedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackImprovedCoop")
      PickupAttackImprovedDM:
        TNT1 A 0 A_FireCustomMissile("DoomBFGBall",0,false)
        stop

      PickupAttackImprovedCoop:
        TNT1 A 0 A_FireCustomMissile("DoomBFGBall2",0,false)
        stop

      // No aiming attack stuff
      PickupAttackNoAim:
        TNT1 A 0 A_JumpIfInventory("SamsaraImprovedDoomguyBalance",1,"PickupAttackNoAimImproved")
      // Vanilla
      PickupAttackNoAimVanilla:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackNoAimVanillaCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackNoAimVanillaCoop")
      PickupAttackNoAimVanillaDM:
        TNT1 A 0 A_SpawnItemEx("DoomBFGBallVanilla",0,0,32,25)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,1)
        stop
        TNT1 A 0 A_SpawnItemEx("DoomBFGBallVanilla",0,0,32,25,0,0,-15)
        TNT1 A 0 A_SpawnItemEx("DoomBFGBallVanilla",0,0,32,25,0,0,15)
        stop

      PickupAttackNoAimVanillaCoop:
        TNT1 A 0 A_SpawnItemEx("DoomBFGBallVanilla2",0,0,32,25)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,1)
        stop
        TNT1 A 0 A_SpawnItemEx("DoomBFGBallVanilla2",0,0,32,25,0,0,-15)
        TNT1 A 0 A_SpawnItemEx("DoomBFGBallVanilla2",0,0,32,25,0,0,15)
        stop

      // Improved
      PickupAttackNoAimImproved:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackNoAimImprovedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackNoAimImprovedCoop")
      PickupAttackNoAimImprovedDM:
        TNT1 A 0 A_SpawnItemEx("DoomBFGBall",0,0,32,25)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,1)
        stop
        TNT1 A 0 A_SpawnItemEx("DoomBFGBall",0,0,32,25,0,0,-15)
        TNT1 A 0 A_SpawnItemEx("DoomBFGBall",0,0,32,25,0,0,15)
        stop

      PickupAttackNoAimImprovedCoop:
        TNT1 A 0 A_SpawnItemEx("DoomBFGBall",0,0,32,25)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,1)
        stop
        TNT1 A 0 A_SpawnItemEx("DoomBFGBall2",0,0,32,25,0,0,-15)
        TNT1 A 0 A_SpawnItemEx("DoomBFGBall2",0,0,32,25,0,0,15)
        stop
    }
}

actor SamsaraDoomguyBFG900064AttackHandler : Trigger
{
    States
    {
      // Ammo check stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupTakeAmmo")
        TNT1 A 0 A_JumpIfInventory("Cell",40,"PickupTakeAmmo")
        stop

      // Ammo taking stuff
      PickupTakeAmmo:
        TNT1 A 0 A_TakeInventory("Cell",40,TIF_NOTAKEINFINITE)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("DoomNoBFGAim",1,"PickupAttackNoAim")
        TNT1 A 0 A_JumpIfInventory("SamsaraImprovedDoomguyBalance",1,"PickupAttackImproved")
      // Vanilla
      PickupAttackVanilla:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackVanillaCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackVanillaCoop")
      PickupAttackVanillaDM:
        TNT1 A 0 A_FireCustomMissile("Doom64BFGBallVanilla",0,false)
        stop

      PickupAttackVanillaCoop:
        TNT1 A 0 A_FireCustomMissile("Doom64BFGBallVanilla2",0,false)
        stop

      // Improved
      PickupAttackImproved:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackImprovedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackImprovedCoop")
      PickupAttackImprovedDM:
        TNT1 A 0 A_FireCustomMissile("Doom64BFGBall",0,false)
        stop

      PickupAttackImprovedCoop:
        TNT1 A 0 A_FireCustomMissile("Doom64BFGBall2",0,false)
        stop

      // No aiming attack stuff
      PickupAttackNoAim:
        TNT1 A 0 A_JumpIfInventory("SamsaraImprovedDoomguyBalance",1,"PickupAttackNoAimImproved")
      // Vanilla
      PickupAttackNoAimVanilla:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackNoAimVanillaCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackNoAimVanillaCoop")
      PickupAttackNoAimVanillaDM:
        TNT1 A 0 A_SpawnItemEx("Doom64BFGBallVanilla",0,0,32,25)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,1)
        stop
        TNT1 A 0 A_SpawnItemEx("Doom64BFGBallVanilla",0,0,32,25,0,0,-15)
        TNT1 A 0 A_SpawnItemEx("Doom64BFGBallVanilla",0,0,32,25,0,0,15)
        stop

      PickupAttackNoAimVanillaCoop:
        TNT1 A 0 A_SpawnItemEx("Doom64BFGBallVanilla2",0,0,32,25)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,1)
        stop
        TNT1 A 0 A_SpawnItemEx("Doom64BFGBallVanilla2",0,0,32,25,0,0,-15)
        TNT1 A 0 A_SpawnItemEx("Doom64BFGBallVanilla2",0,0,32,25,0,0,15)
        stop

      // Improved
      PickupAttackNoAimImproved:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackNoAimImprovedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackNoAimImprovedCoop")
      PickupAttackNoAimImprovedDM:
        TNT1 A 0 A_SpawnItemEx("Doom64BFGBall",0,0,32,25)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,1)
        stop
        TNT1 A 0 A_SpawnItemEx("Doom64BFGBall",0,0,32,25,0,0,-15)
        TNT1 A 0 A_SpawnItemEx("Doom64BFGBall",0,0,32,25,0,0,15)
        stop

      PickupAttackNoAimImprovedCoop:
        TNT1 A 0 A_SpawnItemEx("Doom64BFGBall",0,0,32,25)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,1)
        stop
        TNT1 A 0 A_SpawnItemEx("Doom64BFGBall2",0,0,32,25,0,0,-15)
        TNT1 A 0 A_SpawnItemEx("Doom64BFGBall2",0,0,32,25,0,0,15)
        stop
    }
}

actor DoomBFGBall : BFGBall
{
    DamageType "DoomDamageType"
    Decal BFGLightning
    +BRIGHT
    +DONTREFLECT
	Damage (800)
    DeathSound "doomguy/bfgx"
    Obituary "$SAMSARA_DOOMGUY_OB_SLOT7_2"
    States
    {
      Spawn:
        DBFS AB 4 bright
        loop
        
      Death:
        DBFE AB 8 bright
        DBFE C 8 bright A_BFGSpray("DoomBFGSplash")
        DBFE DEF 8 bright
        stop
    }
}

actor DoomBFGBall2 : DoomBFGBall
{
    Species "Player"
    +THRUSPECIES
    States
    {
      Death:
        DBFE AB 8 bright
        DBFE C 8 bright A_BFGSpray("DoomBFGSplashCoop")
        DBFE DEF 8 bright
        stop
    }
}

actor DoomBFGBallVanilla : DoomBFGBall { Damage 100 }

actor DoomBFGBallVanilla2 : DoomBFGBallVanilla
{
    Species "Player"
    +THRUSPECIES
    States
    {
      Death:
        DBFE AB 8 bright
        DBFE C 8 bright A_BFGSpray("DoomBFGSplashCoop")
        DBFE DEF 8 bright
        stop
    }
}

actor DoomBFGSplash
{
    +NOBLOCKMAP
    +CLIENTSIDEONLY
    +NOGRAVITY
    RenderStyle Add
    Alpha 0.75
    States
    {
      Spawn:
        DBFP ABCD 8 bright
        stop
    }
}

actor DoomBFGSplashCoop : DoomBFGSplash { +MTHRUSPECIES }

actor Doom64BFGColorFlash : Trigger
{
    States
    {
      Pickup:
        TNT1 A 0 A_SetBlend("Green",0.45,20)
        stop
    }
}

actor Doom64BFGBall : BFGBall
{
    Renderstyle Normal
    scale 0.75
    Decal BFGLightning
    +BRIGHT
    +DONTREFLECT
    +BLOODLESSIMPACT
    DeathSound "doom64guy/bfgx"
    Obituary "%k \cpvaporized\c- %o \cpwith the B.F.G. 9000.\c-"
    States
    {
      Spawn:
        64BB AB 2 bright
        loop
        
      Death:
        64BB C 8 bright A_GiveToTarget("Doom64BFGColorFlash")
        64BB D 7 bright A_FadeOut(0.142857)
        64BB E 0 bright A_FadeOut(0.142857)
        64BB E 3 bright A_BFGSpray("Doom64BFGSplash")
        64BB F 3 bright A_FadeOut(0.142857)
        64BB G 3 bright A_FadeOut(0.142857)
        64BB H 3 bright A_FadeOut(0.142857)
        stop
    }
}

actor Doom64BFGBall2 : Doom64BFGBall
{
    Species "Player"
    +THRUSPECIES
    States
    {
      Death:
        64BB C 8 bright A_GiveToTarget("Doom64BFGColorFlash")
        64BB D 7 bright A_FadeOut(0.142857)
        64BB E 0 bright A_FadeOut(0.142857)
        64BB E 3 bright A_BFGSpray("Doom64BFGSplashCoop")
        64BB F 3 bright A_FadeOut(0.142857)
        64BB G 3 bright A_FadeOut(0.142857)
        64BB H 3 bright A_FadeOut(0.142857)
        stop
    }
}

actor Doom64BFGBallVanilla : Doom64BFGBall { Damage 100 }

actor Doom64BFGBallVanilla2 : Doom64BFGBallVanilla
{
    Species "Player"
    +THRUSPECIES
    States
    {
      Death:
        64BB C 8 bright A_GiveToTarget("Doom64BFGColorFlash")
        64BB D 7 bright A_FadeOut(0.142857)
        64BB E 0 bright A_FadeOut(0.142857)
        64BB E 3 bright A_BFGSpray("Doom64BFGSplashCoop")
        64BB F 3 bright A_FadeOut(0.142857)
        64BB G 3 bright A_FadeOut(0.142857)
        64BB H 3 bright A_FadeOut(0.142857)
        stop
    }
}

actor Doom64BFGSplash
{
    +NOBLOCKMAP
    +CLIENTSIDEONLY
    +NOGRAVITY
    RenderStyle Normal
    States
    {
      Spawn:
        64B2 ABCDEF 3 bright
        stop
    }
}

actor Doom64BFGSplashCoop : Doom64BFGSplash { +MTHRUSPECIES }
