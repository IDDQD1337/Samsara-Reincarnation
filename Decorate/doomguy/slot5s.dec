actor DGHasGrenadeLauncher : Boolean {}

ACTOR " GrenadeLauncher " : Weapon
{
	Radius 20
	Height 16
	Weapon.Selectionorder 2500
	+WEAPON.NOAUTOFIRE
	+WEAPON.NOAUTOAIM
	Weapon.AmmoUse 1
	Weapon.AmmoGive 0
	Weapon.AmmoType "RocketAmmo"
	Weapon.Kickback 100
	Inventory.PickupMessage "$PICKUP_GRENADELAUNCHER"
	Inventory.RestrictedTo "DoomguyPlayer"
	Tag "Grenade Launcher"
	States
	{
		Spawn:
			GLAU A -1
			stop
		Ready:
			GRLG A 1 A_WeaponReady
			loop
		Deselect:
			GRLG A 1 A_Lower
			loop
		Select: 
			GRLG A 1 A_Raise
			loop
		Fire: 
			GRLG B 8 A_GunFlash
			GRLG B 12 A_GiveInventory("SamsaraDoomguyGrenadeLauncherAttackHandler")
			TNT1 A 0 A_ReFire
			goto Ready
		Flash:
			GRLF A 3 bright A_Light1
			GRLF B 4 bright
			GRLF C 4 bright A_Light2
			GRLF D 4 bright A_Light2
			goto LightDone
	}
}

actor SamsaraDoomguyGrenadeLauncherAttackHandler : Trigger
{
    States
    {
      // Ammo check stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupTakeAmmo")
        TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,"PickupTakeAmmo")
        stop

      // Ammo taking stuff
      PickupTakeAmmo:
        TNT1 A 0 A_TakeInventory("RocketAmmo",1,TIF_NOTAKEINFINITE)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SamsaraImprovedDoomguyBalance",1,"PickupAttackImproved")
      // Vanilla
      PickupAttackVanilla:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackVanillaCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackVanillaCoop")
      PickupAttackVanillaDM:
        TNT1 A 0 A_FireCustomMissile("DoomGrenadeVanilla",0,false,0,0,0,6.328125 )
        stop

      PickupAttackVanillaCoop:
        TNT1 A 0 A_FireCustomMissile("DoomGrenadeVanillaCoop",0,false,0,0,0,6.328125 )
        stop

      // Improved
      PickupAttackImproved:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackImprovedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackImprovedCoop")
      PickupAttackImprovedDM:
        TNT1 A 0 A_FireCustomMissile("DoomGrenade",0,false,0,0,0,6.328125 )
        stop

      PickupAttackImprovedCoop:
        TNT1 A 0 A_FireCustomMissile("DoomGrenadeCoop",0,false,0,0,0,6.328125 )
        stop
    }
}

actor DoomGrenade // does not inherit from Grenade for source port equality, uses zdoom's physics
{
    Projectile
    Radius 8
    Height 8
    Speed 25
    Damage (100)
    DamageType "DoomDamageType"
    ReactionTime 87 // zan value, didn't use A_Countdown in zdoom
    Gravity 0.25
    BounceType "Doom" // replaces +DOOMBOUNCE in zan, plays sounds as intended
    SeeSound "weapons/grenlf"
    BounceSound "weapons/grbnce" // no longer uses +USESTBOUNCESOUND from zan
    DeathSound "weapons/grenlx"
    Decal "Scorch"
    Obituary "$OB_GRENADE"
    +DEHEXPLOSION
    +EXPLODEONDEATH
    +GRENADETRAIL
    -NOGRAVITY
    +RANDOMIZE
    States
    {
      Spawn:
        SGRN A 1 Bright A_Countdown
        loop

      Death:
        TNT1 A 0 A_ChangeFlag("NOGRAVITY",true)
        TNT1 A 0 A_Explode
      DeathFinish:
        MISL B 8 Bright
        MISL C 6 Bright
        MISL D 4 Bright
        stop
    }
}

actor DoomGrenadeCoop : DoomGrenade
{
    Species "Player"
    +THRUSPECIES
}

actor DoomGrenadeVanilla : DoomGrenade { Damage 20 }

actor DoomGrenadeVanillaCoop : DoomGrenadeVanilla
{
    Species "Player"
    +THRUSPECIES
}

actor SamsaraDoomguyStrGrenade // does not inherit from Grenade for source port equality, uses zdoom's physics
{
    Projectile
    Radius 8
    Height 8
    Speed 25
    Damage 20
    DamageType "DoomDamageType"
    ReactionTime 87 // zan value, didn't use A_Countdown in zdoom
    Gravity 0.25
    BounceType "Doom" // replaces +DOOMBOUNCE in zan, plays sounds as intended
    SeeSound "doomguy/stronghold/grenlf"
    BounceSound "doomguy/stronghold/grbnce" // no longer uses +USESTBOUNCESOUND from zan
    DeathSound "doomguy/stronghold/grenlx"
    Decal "Scorch"
    Obituary "%k played grenade catch with %o."
    //SuicideObituary "%o tripped %p own grenade."
    +EXPLODEONDEATH
    +GRENADETRAIL
    -NOGRAVITY
    +RANDOMIZE
    States
    {
      Spawn:
        DGRN A 1 Bright Light("NEWGRENADE") A_Countdown
        loop

      Death:
        TNT1 A 0 A_ChangeFlag("NOGRAVITY",true)
        TNT1 A 0 A_SetTranslucent(0.67,1)
        TNT1 A 0 A_Explode
      DeathFinish:
        DMSL B 8 Bright Light("ROCKET_X1")
        DMSL C 6 Bright Light("ROCKET_X2")
        DMSL D 4 Bright Light("ROCKET_X3")
        stop
    }
}

actor SamsaraDoomguyStrPowerClusterGrenade : SamsaraDoomguyStrGrenade
{
    Speed 32
    Damage 10
    Scale 1.25
    +FORCERADIUSDMG
    States
    {
      Spawn:
        DGRN A 1 Bright Light("POWERGRENADE") A_Countdown
        loop

      Death:
        TNT1 A 0 A_ChangeFlag("NOGRAVITY",true)
        TNT1 A 0 A_SetTranslucent(0.67,1)
        TNT1 A 0 A_CustomMissile("SamsaraDoomguyStrPowerTinyGrenade",0,0,0,CMF_AIMDIRECTION|CMF_TRACKOWNER,random(60,80))
        TNT1 A 0 A_CustomMissile("SamsaraDoomguyStrPowerTinyGrenade",0,0,72,CMF_AIMDIRECTION|CMF_TRACKOWNER,random(60,80))
        TNT1 A 0 A_CustomMissile("SamsaraDoomguyStrPowerTinyGrenade",0,0,144,CMF_AIMDIRECTION|CMF_TRACKOWNER,random(60,80))
        TNT1 A 0 A_CustomMissile("SamsaraDoomguyStrPowerTinyGrenade",0,0,216,CMF_AIMDIRECTION|CMF_TRACKOWNER,random(60,80))
        TNT1 A 0 A_CustomMissile("SamsaraDoomguyStrPowerTinyGrenade",0,0,288,CMF_AIMDIRECTION|CMF_TRACKOWNER,random(60,80))
        TNT1 A 0 A_Explode(32,192,0)
        TNT1 A 0 A_ChangeFlag("FORCERADIUSDMG",false)
        TNT1 A 0 A_Explode(32,128)
      DeathFinish:
        DMSL B 8 Bright Light("POWERHOMROCKET_X1")
        DMSL C 6 Bright Light("POWERHOMROCKET_X2")
        DMSL D 4 Bright Light("POWERHOMROCKET_X3")
        stop
    }
}

actor SamsaraDoomguyStrPowerTinyGrenade : SamsaraDoomguyStrPowerClusterGrenade
{
    Radius 4
    Height 4
    Speed 8
    Damage 5
    Gravity 0.75
    BounceFactor 0.3
    Scale 0.75
    SeeSound ""
    BounceSound "doomguy/stronghold/tinygrbnce"
    DeathSound "doomguy/stronghold/tinygrenlx"
    States
    {
      Spawn:
        DGRN A 1 Bright NoDelay Light("POWERGRENADETINY") ThrustThingZ(0,random(0,8),random(0,1),1)
        DGRN A 1 Bright Light("POWERGRENADETINY") A_Countdown
        wait

      Death:
        TNT1 A 0 A_ChangeFlag("NOGRAVITY",true)
        TNT1 A 0 A_SetTranslucent(0.67,1)
        TNT1 A 0 A_Explode(32,128,0)
        TNT1 A 0 A_ChangeFlag("FORCERADIUSDMG",false)
        TNT1 A 0 A_Explode(32,96)
      DeathFinish:
        DMSL B 8 Bright Light("POWERGRENADETINY_X1")
        DMSL C 6 Bright Light("POWERGRENADETINY_X2")
        DMSL D 4 Bright Light("POWERGRENADETINY_X3")
        stop
    }
}
