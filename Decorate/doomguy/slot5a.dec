actor DGHasRocketLauncher : Boolean {}

actor "Rocket Launcher" : RocketLauncher
{
    Weapon.SlotNumber 5
	Weapon.AmmoGive 0
    +ALT_USES_BOTH
    +UNDROPPABLE
	Inventory.RestrictedTo "DoomguyPlayer"
	Tag "Rocket Launcher"
    States
    {
      Spawn:
        WDRL A -1
        stop

      // Classic
      Ready:
        TNT1 A 0 A_JumpIfInventory("DoomClassMode",2,"ReadyStr")
		TNT1 A 0 A_JumpIfInventory("Doom64Mode",1,"Ready64")
        DMIS A 1 A_WeaponReady(WRF_NOSECONDARY)
        loop

      Deselect:
        TNT1 A 0 A_JumpIfInventory("DoomClassMode",2,"DeselectStr")
		TNT1 A 0 A_JumpIfInventory("Doom64Mode",1,"Deselect64")
        DMIS A 1 A_Lower
        loop

      Select:
        TNT1 A 0 A_JumpIfInventory("DoomClassMode",2,"SelectStr")
		TNT1 A 0 A_JumpIfInventory("Doom64Mode",1,"Select64")
        DMIS A 1 A_Raise
        loop

      Fire:
        TNT1 A 0 A_JumpIfInventory("DoomClassMode",2,"FireStr")
		TNT1 A 0 A_JumpIfInventory("Doom64Mode",1,"Fire64")
        TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"FireSmooth")
      FireVanilla:
        DMIS B 8 A_GunFlash
        DMIS B 12 A_GiveInventory("SamsaraDoomguyRocketLauncherAttackHandler")
        // 20 frames total
      FireFinish:
        TNT1 A 0 A_ReFire
        goto Ready

      Flash:
        DMSF B 3 Bright A_Light1
        DMSF C 4 Bright
        DMSF FG 4 Bright A_Light2
        goto LightDone

      FireSmooth:
        DMIS A 2
        DMIS A 4 Bright A_GunFlash("FlashSmooth")
        DMIS B 2 Bright
        TNT1 A 0 A_GiveInventory("SamsaraDoomguyRocketLauncherAttackHandler")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasDoubleFiringSpeed",1,"FireSmoothRage")
        DMIS DC 3 Bright
        DMIS BE 2
        DMIS FG 1
        goto FireFinish

      FireSmoothRage:
        DMIS DC 2 Bright
        DMIS BEFG 2
        goto FireFinish

      FlashSmooth:
        DMSF A 3 Bright A_Light1
        DMSF B 2 Bright
        DMSF C 2 Bright A_Light2
        DMSF DE 3 Bright
        goto LightDone

      // Doom 64 (has recoil visual and kickback)
      Ready64:
        64ML A 1 A_WeaponReady(WRF_NOSECONDARY)
        goto Ready

      Deselect64:
        64ML A 1 A_Lower
        goto Deselect

      Select64:
        64ML A 1 A_Raise
        goto Select

      Fire64:
		TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"Fire64Smooth")
	  Fire64Vanilla:
        64ML B 8 A_GunFlash("Flash64")
        64ML B 13 A_GiveInventory("SamsaraDoomguy64RocketLauncherAttackHandler")
        goto FireFinish

      Flash64:
        64MM A 2 Bright A_Light1
        64MM B 4 Bright
        64MM CD 4 Bright A_Light2
        goto LightDone

      Fire64Smooth:
        64ML C 2 A_GunFlash("Flash64Smooth")
        64ML DEF 2
        64ML F 3 A_GiveInventory("SamsaraDoomguy64RocketLauncherAttackHandler")
        64ML GH 3
        64ML I 4
        goto FireFinish

      Flash64Smooth:
        64MM E 8 Bright A_Light1
        64MM F 2 Bright
        64MM GH 2 Bright A_Light2
        goto LightDone

      // Stronghold
      // Unpowered
      ReadyStr:
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPower",1,"ReadyStrPowered")
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPowerPerma",1,"ReadyStrPowered")
        DMIS H 1 A_WeaponReady(WRF_NOSECONDARY)
        goto Ready

      DeselectStr:
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPower",1,"DeselectStrPowered")
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPowerPerma",1,"DeselectStrPowered")
      DeselectStrContinue:
        TNT1 A 0 A_Lower
        DMIS H 1 A_Lower
        goto Deselect

      SelectStr:
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPower",1,"SelectStrPowered")
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPowerPerma",1,"SelectStrPowered")
      SelectStrContinue:
        TNT1 A 0 A_Raise
        DMIS H 1 A_Raise
        goto Select

      FireStr:
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPower",1,"FireStrPowered")
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPowerPerma",1,"FireStrPowered")
        DMSF H 4 Bright A_GunFlash("FlashStr")
        TNT1 A 0 A_GiveInventory("SamsaraDoomguyStrRocketLauncherAttackHandler")
        DMSF IJK 5 Bright
        goto FireFinish

      FlashStr:
        TNT1 A 4 A_Light1
        TNT1 A 5 A_Light2
        TNT1 A 5 A_Light1
        goto LightDone

      // Powered
      ReadyStrPowered:
        DMIS H 1 A_WeaponReady
        goto Ready

      DeselectStrPowered:
        TNT1 A 0 A_Lower
        goto DeselectStrContinue

      SelectStrPowered:
        TNT1 A 0 A_Raise
        goto SelectStrContinue

      FireStrPowered:
        DMSF H 4 Bright A_GunFlash("FlashStrPowered")
        DMSF I 2 Bright A_GiveInventory("SamsaraDoomguyStrRocketLauncherPoweredPrimaryAttackHandler")
        DMSF JK 4 Bright
        goto FireFinish

      AltFire:
        DMSF H 4 Bright A_GunFlash("FlashStrPowered")
        DMSF I 2 Bright A_GiveInventory("SamsaraDoomguyStrRocketLauncherPoweredSecondaryAttackHandler1")
        DMSF H 4 Bright A_GunFlash("FlashStrPowered")
        DMSF I 2 Bright A_GiveInventory("SamsaraDoomguyStrRocketLauncherPoweredSecondaryAttackHandler2")
        DMSF H 4 Bright A_GunFlash("FlashStrPowered")
        DMSF I 2 Bright A_GiveInventory("SamsaraDoomguyStrRocketLauncherPoweredSecondaryAttackHandler3")
        DMSF JK 4 Bright
      AltFireFinish:
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPower",1,"AltFireFinishReFire")
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPowerPerma",1,"AltFireFinishReFire")
        TNT1 A 0 A_CheckReload
        goto Ready

      AltFireFinishReFire:
        TNT1 A 0 A_JumpIfInventory("DoomClassMode",2,2)
        TNT1 A 0 A_CheckReload
        goto Ready
        TNT1 A 0 A_ReFire
        goto Ready

      FlashStrPowered:
        TNT1 A 4 A_Light1
        TNT1 A 2 A_Light2
        TNT1 A 4 A_Light1
        goto LightDone
    }
}

actor SamsaraDoomguyRocketLauncherAttackHandler : Trigger
{
    States
    {
      // Ammo check stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupTakeAmmo")
        TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,"PickupTakeAmmo")
        stop

      // Ammo taking stuff
      PickupTakeAmmo:
        TNT1 A 0 A_TakeInventory("RocketAmmo",1,TIF_NOTAKEINFINITE)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SamsaraImprovedDoomguyBalance",1,"PickupAttackImproved")
      // Vanilla
      PickupAttackVanilla:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackVanillaCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackVanillaCoop")
      PickupAttackVanillaDM:
        TNT1 A 0 A_FireCustomMissile("DoomRocketVanilla",0,false)
        stop

      PickupAttackVanillaCoop:
        TNT1 A 0 A_FireCustomMissile("DoomRocketVanilla2",0,false)
        stop

      // Improved
      PickupAttackImproved:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackImprovedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackImprovedCoop")
      PickupAttackImprovedDM:
        TNT1 A 0 A_FireCustomMissile("DoomRocket",0,false)
        stop

      PickupAttackImprovedCoop:
        TNT1 A 0 A_FireCustomMissile("DoomRocket2",0,false)
        stop
    }
}

actor DoomRocket : Rocket
{
    Decal Scorch
    SeeSound "doomguy/rocklf"
    DeathSound "doomguy/rocklx"
	Damage (100)
    Damagetype "DoomDamageType"
    Obituary "$SAMSARA_DOOMGUY_OB_SLOT5"
    States
    {
      Spawn:
        DMSL A 1 bright
        Loop

      Death:
        DMSL B 8 bright A_Explode
        DMSL C 6 bright
        DMSL D 4 bright
        Stop
    }
}

actor DoomRocket2 : DoomRocket { +THRUSPECIES Species "Player" }

actor DoomRocketVanilla : DoomRocket { Damage 20 }

actor DoomRocketVanilla2 : DoomRocketVanilla { +THRUSPECIES Species "Player" }

actor SamsaraDoomguy64RocketLauncherAttackHandler : Trigger
{
    States
    {
      // Ammo check stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupTakeAmmo")
        TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,"PickupTakeAmmo")
        stop

      // Ammo taking stuff
      PickupTakeAmmo:
        TNT1 A 0 A_TakeInventory("RocketAmmo",1,TIF_NOTAKEINFINITE)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SamsaraImprovedDoomguyBalance",1,"PickupAttackImproved")
      // Vanilla
      PickupAttackVanilla:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackVanillaCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackVanillaCoop")
      PickupAttackVanillaDM:
        TNT1 A 0 A_FireCustomMissile("Doom64RocketVanilla",0,false)
        goto PickupFinish

      PickupAttackVanillaCoop:
        TNT1 A 0 A_FireCustomMissile("Doom64RocketVanilla2",0,false)
        goto PickupFinish

      // Improved
      PickupAttackImproved:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackImprovedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackImprovedCoop")
      PickupAttackImprovedDM:
        TNT1 A 0 A_FireCustomMissile("Doom64Rocket",0,false)
        goto PickupFinish

      PickupAttackImprovedCoop:
        TNT1 A 0 A_FireCustomMissile("Doom64Rocket2",0,false)
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 A_Recoil(2)
        TNT1 A 0 ACS_NamedExecuteAlways("SamsaraRecoil", 0, 2, 0, 20)
        stop
    }
}

actor Doom64Rocket : Rocket
{
    Decal Scorch
    SeeSound "doom64guy/rocklf"
    DeathSound "doom64guy/rocklx"
    -DEHEXPLOSION
    -ROCKETTRAIL
    +BLOODLESSIMPACT
	Damage (100)
    DamageType "DoomDamageType"
    Scale 0.75
    Obituary "%k splattered %o's brains with the Rocket Launcher."
    States
    {
      Spawn:
        64MI AA 2 bright A_SpawnItemEx("Doom64RocketSmokeTrail")
        Loop

      Death:
        64MI B 8 bright A_Explode
        64MI C 6 bright A_FadeOut(0.2)
        64MI D 3 bright A_FadeOut(0.2)
        64MI EF 3 bright A_FadeOut(0.2)
        Stop
    }
}

actor Doom64Rocket2 : Doom64Rocket { +THRUSPECIES Species "Player" }

actor Doom64RocketVanilla : Doom64Rocket { Damage 20 }

actor Doom64RocketVanilla2 : Doom64RocketVanilla { +THRUSPECIES Species "Player" }

actor Doom64RocketSmokeTrail : RocketSmokeTrail
{
    +CLIENTSIDEONLY
    RenderStyle Translucent
    Alpha 0.5
    VSpeed 0
    Scale 0.75
    States
    {
    Spawn:
        64RS ABCDE 2 bright
        stop
    }
}

// Unpowered attack handler

actor SamsaraDoomguyStrRocketLauncherAttackHandler : Trigger
{
    States
    {
      // Ammo check stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupTakeAmmo")
        TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,"PickupTakeAmmo")
        stop

      // Ammo taking stuff
      PickupTakeAmmo:
        TNT1 A 0 A_TakeInventory("RocketAmmo",1,TIF_NOTAKEINFINITE)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrRocket",0,false)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrRocketCoop",0,false)
        stop
    }
}

// Powered attack handlers

actor SamsaraDoomguyStrRocketLauncherPoweredPrimaryAttackHandler : Trigger
{
    States
    {
      // Ammo check stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupTakeAmmo")
        TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,"PickupTakeAmmo")
        stop

      // Ammo taking stuff
      PickupTakeAmmo:
        TNT1 A 0 A_TakeInventory("RocketAmmo",1,TIF_NOTAKEINFINITE)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrPowerRocket",0,false)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrPowerRocketCoop",0,false)
        stop
    }
}

actor SamsaraDoomguyStrRocketLauncherPoweredSecondaryAttackHandler1 : Trigger
{
    States
    {
      // Ammo check stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupTakeAmmo")
        TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,"PickupTakeAmmo")
        stop

      // Ammo taking stuff
      PickupTakeAmmo:
        TNT1 A 0 A_TakeInventory("RocketAmmo",1,TIF_NOTAKEINFINITE)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrPowerRocket",random(-5,5)/10.0,false,0,0,0,random(-6,6)/10.0)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrPowerRocketCoop",random(-5,5)/10.0,false,0,0,0,random(-6,6)/10.0)
        stop
    }
}

actor SamsaraDoomguyStrRocketLauncherPoweredSecondaryAttackHandler2 : Trigger
{
    States
    {
      // Ammo check stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupTakeAmmo")
        TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,"PickupTakeAmmo")
        stop

      // Ammo taking stuff
      PickupTakeAmmo:
        TNT1 A 0 A_TakeInventory("RocketAmmo",1,TIF_NOTAKEINFINITE)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrPowerRocket",random(-10,10)/10.0,false,0,0,0,random(-8,8)/10.0)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrPowerRocketCoop",random(-10,10)/10.0,false,0,0,0,random(-8,8)/10.0)
        stop
    }
}

actor SamsaraDoomguyStrRocketLauncherPoweredSecondaryAttackHandler3 : Trigger
{
    States
    {
      // Ammo check stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupTakeAmmo")
        TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,"PickupTakeAmmo")
        stop

      // Ammo taking stuff
      PickupTakeAmmo:
        TNT1 A 0 A_TakeInventory("RocketAmmo",1,TIF_NOTAKEINFINITE)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrPowerRocket",random(-15,15)/10.0,false,0,0,0,random(-10,10)/10.0)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrPowerRocketCoop",random(-15,15)/10.0,false,0,0,0,random(-10,10)/10.0)
        stop
    }
}

// Rocket

actor SamsaraDoomguyStrRocket : Rocket
{
    DamageType "DoomDamageType"
    SeeSound "doomguy/stronghold/rocklf"
    DeathSound "doomguy/stronghold/rocklx"
    Decal "Scorch"
    Obituary "$SAMSARA_DOOMGUY_OB_SLOT5"
    -DEHEXPLOSION
    States
    {
      Spawn:
        DMSL E -1 Bright Light("ROCKET")
        stop

      Death:
        TNT1 A 0 A_SetTranslucent(0.67,1)
        DMSL B 8 Bright Light("ROCKET_X1") A_Explode
        DMSL C 6 Bright Light("ROCKET_X2")
        DMSL D 4 Bright Light("ROCKET_X3")
        stop
    }
}

actor SamsaraDoomguyStrRocketCoop : SamsaraDoomguyStrRocket
{
    Species "Player"
    +THRUSPECIES
}

// Powered rocket

actor SamsaraDoomguyStrPowerRocket : SamsaraDoomguyStrRocket
{
    Speed 45
    Damage (96)
    Scale 1.5
    +FORCERADIUSDMG
    +SEEKERMISSILE
    States
    {
      Spawn:
        DMSL F 1 Bright Light("POWERROCKET") A_SeekerMissile(2,10)
        loop

      Death:
        TNT1 A 0 A_SetTranslucent(0.67,1)
        TNT1 A 0 A_Explode(96,192,0)
        TNT1 A 0 A_ChangeFlag("FORCERADIUSDMG",false)
        DMSL B 8 Bright Light("POWERROCKET_X1") A_Explode(96,128)
        DMSL C 6 Bright Light("POWERROCKET_X2")
        DMSL D 4 Bright Light("POWERROCKET_X3")
        stop
    }
}

actor SamsaraDoomguyStrPowerRocketCoop : SamsaraDoomguyStrPowerRocket
{
    Species "Player"
    +THRUSPECIES
}
