actor " Chainsaw " : Chainsaw replaces Chainsaw
{
    Weapon.Kickback 0
    Weapon.SelectionOrder 2200
    Weapon.SlotNumber 1
    Weapon.SlotPriority 0
    Decal BulletChip
    Weapon.UpSound "WHAT UPSOUND"
    Weapon.ReadySound "WHAT READYSOUND"
    DamageType "Chainsaw"
	Tag "Chainsaw"
    +INVENTORY.UNDROPPABLE
    Obituary "$SAMSARA_DOOMGUY_OB_SLOT1"
	Inventory.RestrictedTo "DoomguyPlayer"
    States
    {
      // Classic
      Ready:
		TNT1 A 0 A_JumpIfInventory("Doom64Mode",1,"Ready64")
		TNT1 A 0 A_PlaySound("doomguy/sawidle",CHAN_WEAPON)
        DSAW CD 4 A_WeaponReady
        loop

      Deselect:
		TNT1 A 0 A_JumpIfInventory("Doom64Mode",1,"Deselect64")
        DSAW C 1 A_Lower
        loop

      Select:
		TNT1 A 0 A_JumpIfInventory("Doom64Mode",1,"Select64")
		TNT1 A 0 A_PlaySound("doomguy/sawup",CHAN_WEAPON)
      SelectLoop:
		TNT1 A 0 A_JumpIfInventory("Doom64Mode",1,"SelectLoop64")
        DSAW C 1 A_Raise
        loop

      Fire:
        TNT1 A 0 A_JumpIfInventory("Doom64Mode",1,"Fire64")
        TNT1 A 0 A_JumpIfInventory("PowerStrength",1,"FireBerserk")
        DSAW AB 4 A_GiveInventory("SamsaraDoomguyChainsawAttackHandler")
      FireFinish:
        TNT1 A 0 A_ReFire
        goto Ready

      FireBerserk:
        DSAW AB 3 A_GiveInventory("SamsaraDoomguyChainsawAttackHandler")
        goto FireFinish

      // Doom 64 (fires twice as fast compared to unberserked classic chainsaw)
      Ready64:
		TNT1 A 0 A_PlaySound("doom64guy/sawidle",CHAN_WEAPON)
        64SW CD 5 A_WeaponReady 
        goto Ready

      Deselect64:
        64SW C 1 A_Lower
        goto Deselect

      Select64:
        TNT1 A 0 A_PlaySound("doom64guy/sawup",CHAN_7)
        goto SelectLoop

      SelectLoop64:
        64SW C 1 A_Raise
        goto SelectLoop

      Fire64:
        64SW AB 2 A_GiveInventory("SamsaraDoomguyChainsaw64AttackHandler")
        goto FireFinish

      Spawn:
        WSAW A -1
        stop
    }
}

actor SamsaraDoomguyChainsawAttackHandler : Trigger
{
    States
    {
      Pickup:
        TNT1 A 0 A_JumpIfInventory("PowerStrength",1,"PickupBerserk")
      // Normal
      PickupNormal:
        TNT1 A 0 A_JumpIfInventory("SamsaraImprovedDoomguyBalance",1,"PickupNormalImproved")
      // Normal (vanilla)
      PickupNormalVanilla:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupNormalVanillaCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupNormalVanillaCoop")
      PickupNormalVanillaDM:
        TNT1 A 0 A_Saw("doomguy/sawfull","doomguy/sawhit",2,"DoomBulletPuff2")
        stop

      PickupNormalVanillaCoop:
        TNT1 A 0 A_Saw("doomguy/sawfull","doomguy/sawhit",2,"DoomBulletPuff2_Coop")
        stop

      // Normal (improved)
      PickupNormalImproved:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupNormalImprovedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupNormalImprovedCoop")
      PickupNormalImprovedDM:
        TNT1 A 0 A_Saw("doomguy/sawfull","doomguy/sawhit",random(1,2)*10,"DoomBulletPuff2",SF_NORANDOM)
        stop

      PickupNormalImprovedCoop:
        TNT1 A 0 A_Saw("doomguy/sawfull","doomguy/sawhit",random(1,2)*10,"DoomBulletPuff2_Coop",SF_NORANDOM)
        stop

      // Berserk
      PickupBerserk:
        TNT1 A 0 A_JumpIfInventory("SamsaraImprovedDoomguyBalance",1,"PickupBerserkImproved")
      // Berserk (vanilla)
      PickupBerserkVanilla:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupBerserkVanillaCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupBerserkVanillaCoop")
      PickupBerserkVanillaDM:
        TNT1 A 0 A_Saw("doomguy/sawfull","doomguy/sawhit",7,"DoomBulletPuff2")
        stop

      PickupBerserkVanillaCoop:
        TNT1 A 0 A_Saw("doomguy/sawfull","doomguy/sawhit",7,"DoomBulletPuff2_Coop")
        stop

      // Berserk (improved)
      PickupBerserkImproved:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupBerserkImprovedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupBerserkImprovedCoop")
      PickupBerserkImprovedDM:
        TNT1 A 0 A_Saw("doomguy/sawfull","doomguy/sawhit",random(4,7)*10,"DoomBulletPuff2",SF_NORANDOM)
        stop

      PickupBerserkImprovedCoop:
        TNT1 A 0 A_Saw("doomguy/sawfull","doomguy/sawhit",random(4,7)*10,"DoomBulletPuff2_Coop",SF_NORANDOM)
        stop
    }
}

actor SamsaraDoomguyChainsaw64AttackHandler : Trigger
{
    States
    {
      Pickup:
        TNT1 A 0 A_JumpIfInventory("PowerStrength",1,"PickupBerserk")
      // Normal
      PickupNormal:
        TNT1 A 0 A_JumpIfInventory("SamsaraImprovedDoomguyBalance",1,"PickupNormalImproved")
      // Normal (vanilla)
      PickupNormalVanilla:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupNormalVanillaCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupNormalVanillaCoop")
      PickupNormalVanillaDM:
        TNT1 A 0 A_Saw("doom64guy/sawfull","doom64guy/sawhit",2,"Doom64BulletPuff")
        stop

      PickupNormalVanillaCoop:
        TNT1 A 0 A_Saw("doom64guy/sawfull","doom64guy/sawhit",2,"Doom64BulletPuff_Coop")
        stop

      // Normal (improved)
      PickupNormalImproved:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupNormalImprovedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupNormalImprovedCoop")
      PickupNormalImprovedDM:
        TNT1 A 0 A_Saw("doom64guy/sawfull","doom64guy/sawhit",random(1,3)*5,"Doom64BulletPuff",SF_NORANDOM)
        stop

      PickupNormalImprovedCoop:
        TNT1 A 0 A_Saw("doom64guy/sawfull","doom64guy/sawhit",random(1,3)*5,"Doom64BulletPuff_Coop",SF_NORANDOM)
        stop

      // Berserk
      PickupBerserk:
        TNT1 A 0 A_JumpIfInventory("SamsaraImprovedDoomguyBalance",1,"PickupBerserkImproved")
      // Berserk (vanilla)
      PickupBerserkVanilla:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupBerserkVanillaCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupBerserkVanillaCoop")
      PickupBerserkVanillaDM:
        TNT1 A 0 A_Saw("doom64guy/sawfull","doom64guy/sawhit",7,"Doom64BulletPuff")
        stop

      PickupBerserkVanillaCoop:
        TNT1 A 0 A_Saw("doom64guy/sawfull","doom64guy/sawhit",7,"Doom64BulletPuff_Coop")
        stop

      // Berserk (improved)
      PickupBerserkImproved:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupBerserkImprovedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupBerserkImprovedCoop")
      PickupBerserkImprovedDM:
        TNT1 A 0 A_Saw("doom64guy/sawfull","doom64guy/sawhit",random(3,6)*5,"Doom64BulletPuff",SF_NORANDOM)
        stop

      PickupBerserkImprovedCoop:
        TNT1 A 0 A_Saw("doom64guy/sawfull","doom64guy/sawhit",random(3,6)*5,"Doom64BulletPuff_Coop",SF_NORANDOM)
        stop
    }
}
