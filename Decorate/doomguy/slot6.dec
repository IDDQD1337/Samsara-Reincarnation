actor DGHasPlasmaRifle : Boolean {}
actor SamsaraDoomguyStrPlasmaRifleReadySoundCheck : Boolean {}
actor SamsaraDoom64PlasmaRifleSoundActive : Boolean {}

actor "Plasma Rifle" : PlasmaRifle
{
    Weapon.SlotNumber 6
    +INVENTORY.UNDROPPABLE
	Tag "Plasma Rifle"
	Inventory.RestrictedTo "DoomguyPlayer"
    States
    {
      Spawn:
        WPLS A -1
        stop

      // Classic
      Ready:
		TNT1 A 0 A_JumpIfInventory("Doom64Mode",1,"Ready64")
        TNT1 A 0 A_JumpIfInventory("SamsaraDoom64PlasmaRifleSoundActive",1,"ReadyStopSound")
      ReadyContinue:
        DPLS A 1 A_WeaponReady
        goto Ready

      ReadyStopSound:
        TNT1 A 0 A_PlaySound("silence",CHAN_6)
        TNT1 A 0 A_StopSound(CHAN_6)
        goto ReadyContinue

      Deselect:
        TNT1 A 0 A_JumpIfInventory("SamsaraDoom64PlasmaRifleSoundActive",1,"DeselectStopSound")
      DeselectContinue:
		TNT1 A 0 A_JumpIfInventory("Doom64Mode",1,"Deselect64")
        DPLS A 1 A_Lower
        goto Deselect

      DeselectStopSound:
        TNT1 A 0 A_PlaySound("silence",CHAN_6)
        TNT1 A 0 A_StopSound(CHAN_6)
        goto DeselectContinue

      Select:
		TNT1 A 0 A_JumpIfInventory("Doom64Mode",1,"Select64")
        TNT1 A 0 A_JumpIfInventory("SamsaraDoom64PlasmaRifleSoundActive",1,"SelectStopSound")
      SelectContinue:
        DPLS A 1 A_Raise
        goto Select

      SelectStopSound:
        TNT1 A 0 A_PlaySound("silence",CHAN_6)
        TNT1 A 0 A_StopSound(CHAN_6)
        goto SelectContinue

      Fire:
		TNT1 A 0 A_JumpIfInventory("Doom64Mode",1,"Fire64")
        TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"FireSmooth")
        TNT1 A 0 A_GunFlash
        DPLS A 3 Bright A_GiveInventory("SamsaraDoomguyPlasmaRifleAttackHandler")
        DPLS H 20 A_ReFire
        goto Ready

      Flash:
	    TNT1 A 0 A_Jump(256,1,2)
	    DPLF A 3 Bright A_Light1
        goto LightDone
	    DPLF B 3 Bright A_Light1
        goto LightDone

      FireSmooth:
        TNT1 A 0 A_GunFlash("FlashSmooth")
        DPLS A 3 Bright A_GiveInventory("SamsaraDoomguyPlasmaRifleAttackHandler")
        DPLS A 1 A_ReFire
        DPLS BC 1
        DPLS D 1 A_JumpIfInventory("SamsaraHasDoubleFiringSpeed",1,"FireSmoothRage")
        DPLS EF 1
        DPLS GF 3
        DPLS EDCB 2
        goto Ready

      FireSmoothRage:
        DPLS DFGFDCB 2
        goto Ready

	  FlashSmooth:
		TNT1 A 0 A_Light1
        TNT1 A 0 A_JumpIfInventory("SamsaraHasDoubleFiringSpeed",1,"FlashSmoothRage")
		DPLF CABD 1 Bright
		goto LightDone

	  FlashSmoothRage:
		DPLF AD 1 Bright
		goto LightDone

      // Doom 64 (fires slower, but is more damaging)
      Ready64:
        TNT1 A 0 A_PlaySound("doom64guy/plasmaidle",CHAN_6,1,true)
        TNT1 A 0 A_GiveInventory("SamsaraDoom64PlasmaRifleSoundActive")
        64PR BCD 2 A_WeaponReady
        goto Ready

      Deselect64:
        64PR A 1 A_Lower
        goto Deselect

      Select64:
        TNT1 A 0 A_PlaySound("doom64guy/plasmaidle",CHAN_6,1,true)
        TNT1 A 0 A_GiveInventory("SamsaraDoom64PlasmaRifleSoundActive")
        64PR A 1 A_Raise
        goto Select

      Fire64:
		TNT1 A 0 A_JumpIfInventory("SamsaraImprovedDoomguyBalance",1,"Fire64ImprovedBalance")
      Fire64VanillaBalance:
		TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"Fire64VanillaBalanceSmooth")
      Fire64VanillaBalanceVanilla:
        TNT1 A 0 A_GunFlash("Flash64")
        64PR A 5 A_GiveInventory("SamsaraDoomguy64PlasmaRifleAttackHandler")
      Fire64Finish:
        TNT1 A 0 A_ReFire
        goto Ready

      Fire64ImprovedBalance:
		TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"Fire64ImprovedBalanceSmooth")
      Fire64ImprovedBalanceVanilla:
        TNT1 A 0 A_GunFlash("Flash64Improved")
        64PR A 4 A_GiveInventory("SamsaraDoomguy64PlasmaRifleAttackHandler")
        64PR A 20 A_ReFire
        goto Ready

      Flash64:
        64RF A 4 Bright A_Light1
        goto LightDone

      Flash64Improved:
        TNT1 A 0 A_Light1
        64RF A 3 Bright A_JumpIfInventory("SamsaraHasDoubleFiringSpeed",1,"Flash64ImprovedRage")
        goto LightDone

      Flash64ImprovedRage:
        64RF A 2 Bright
        goto LightDone

      Fire64VanillaBalanceSmooth:
        TNT1 A 0 A_GunFlash("Flash64Smooth")
        64PR I 2 A_GiveInventory("SamsaraDoomguy64PlasmaRifleAttackHandler")
		64PR J 3
        goto Fire64Finish

      Fire64ImprovedBalanceSmooth:
        64PR A 0 A_GunFlash("Flash64ImprovedSmooth")
        64PR I 2 A_GiveInventory("SamsaraDoomguy64PlasmaRifleAttackHandler")
		64PR J 2
        64PR E 5 A_ReFire
        64PR FGH 5
        goto Ready

      Flash64Smooth:
		TNT1 A 4 Bright A_Light1
		goto LightDone

      Flash64ImprovedSmooth:
        TNT1 A 0 A_Light1
        TNT1 A 3 Bright A_JumpIfInventory("SamsaraHasDoubleFiringSpeed",1,"Flash64ImprovedSmoothRage")
        goto LightDone

      Flash64ImprovedSmoothRage:
        TNT1 A 2 Bright
        goto LightDone
    }
}

actor SamsaraDoomguyPlasmaRifleAttackHandler : Trigger
{
    States
    {
      // Ammo check stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupTakeAmmo")
        TNT1 A 0 A_JumpIfInventory("Cell",1,"PickupTakeAmmo")
        stop

      // Ammo taking stuff
      PickupTakeAmmo:
        TNT1 A 0 A_TakeInventory("Cell",1,TIF_NOTAKEINFINITE)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("DoomPlasmaBall",0,false)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("DoomPlasmaBall2",0,false)
        stop
    }
}

actor SamsaraDoomguy64PlasmaRifleAttackHandler : Trigger
{
    States
    {
      // Ammo check stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupTakeAmmo")
        TNT1 A 0 A_JumpIfInventory("Cell",1,"PickupTakeAmmo")
        stop

      // Ammo taking stuff
      PickupTakeAmmo:
        TNT1 A 0 A_TakeInventory("Cell",1,TIF_NOTAKEINFINITE)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("Doom64PlasmaBall",0,false)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("Doom64PlasmaBall2",0,false)
        stop
    }
}

actor DoomPlasmaBall : PlasmaBall
{
    Decal DoomPlasmaScorch
    SeeSound "doomguy/plasmaf"
    DeathSound "doomguy/plasmax"
    Obituary "$SAMSARA_DOOMGUY_OB_SLOT6"
    DamageType "DoomDamageType"
    States
    {
      Spawn:
        DPLM AB 6 bright
        loop

      Death:
        DPLE ABCDE 4 bright
        stop
    }
}

actor DoomPlasmaBall2 : DoomPlasmaBall { +THRUSPECIES Species "Player" }


actor Doom64PlasmaBall : PlasmaBall
{
    Decal Doom64PlasmaScorch
    RenderStyle Normal
    Alpha 1
	Speed 32
    Scale 0.75
    +BLOODLESSIMPACT
    SeeSound "doom64guy/plasmaf"
    DeathSound "doom64guy/plasmax"
    Obituary "%k \cpfried\c- %o \cpwith the Plasma Gun.\c-"
    DamageType "DoomPlasma"
    Damage 6 // slightly higher damage to make up for drastically slower ROF
    States
    {
      Spawn:
        64PB AB 3 bright
        loop

      Death:
        64PB CDEFGH 2 bright
        stop
    }
}

actor Doom64PlasmaBall2 : Doom64PlasmaBall { +THRUSPECIES Species "Player" }

actor SamsaraDoomguyStrPlasmaBall : PlasmaBall
{
    DamageType "DoomDamageType"
    SeeSound "doomguy/stronghold/plasmaf"
    DeathSound "doomguy/stronghold/plasmax"
    Decal "DoomStrPlasmaScorch"
    States
    {
      Spawn:
        DPLM CD 6 Bright Light("PLASMABALL")
        loop

      Death:
        DPLE F 4 Bright Light("PLASMA_X1")
        DPLE GH 4 Bright Light("PLASMA_X2")
        DPLE I 4 Bright Light("PLASMA_X3")
        DPLE J 4 Bright Light("PLASMA_X4")
        stop
    }
}

actor SamsaraDoomguyStrPowerPlasmaBall : SamsaraDoomguyStrPlasmaBall
{
    Radius 22.75
    Height 14
    Speed 30
    Damage (40)
    DamageType "DoomDamageTypeNoSelfDamage"
    Alpha 1
    Scale 0.8
    +FORCERADIUSDMG
    States
    {
      Spawn:
        DPLE F 2 Bright Light("POWERPLASMA_X1") A_SpawnItemEx("SamsaraDoomguyStrPowerPlasmaBallTrail",random(-1,1),random(-1,1),random(-1,1),random(-1,1),random(-1,1),random(-1,1),random(0,359))
        loop

      Death:
        TNT1 A 0 A_GiveToTarget("SamsaraDoomguyNoSelfDamage")
        TNT1 A 0 A_Explode(40,80)
        TNT1 A 0 A_TakeFromTarget("PowerSamsaraDoomguyNoSelfDamage")
      DeathFinish:
        DPLE F 4 Bright Light("POWERPLASMA_X1")
        DPLE GH 4 Bright Light("POWERPLASMA_X2")
        DPLE I 4 Bright Light("POWERPLASMA_X3")
        DPLE J 4 Bright Light("POWERPLASMA_X4")
        stop
    }
}

actor SamsaraDoomguyStrPowerPlasmaBall2 : SamsaraDoomguyStrPowerPlasmaBall
{
    Speed 60
    Damage (60)
    DamageType "DoomDamageType"
    -FORCERADIUSDMG
    +SEEKERMISSILE
    States
    {
      Spawn:
        DPLE F 1 Bright Light("POWERPLASMA_X1")
      SpawnLoop:
        TNT1 A 0 A_SeekerMissile(2,10,SMF_LOOK,50,10)
        DPLE F 1 Bright Light("POWERPLASMA_X1") A_SpawnItemEx("SamsaraDoomguyStrPowerPlasmaBallTrail")
        loop

      Death:
        goto DeathFinish
    }
}

actor SamsaraDoomguyStrPowerPlasmaBallTrail : SamsaraDoomguyStrElectricalPuff
{
    SeeSound ""
    AttackSound ""
    Decal ""
    +CLIENTSIDEONLY
    +NOTONAUTOMAP
}
