actor DGStrHasLandMines : Boolean {}

actor "Land Mine Layer" : Weapon
{
    Inventory.PickupMessage "You got some land mines!"
    Inventory.RestrictedTo "DoomguyPlayer"
    Weapon.SlotNumber 5
    Weapon.AmmoType "DoomguyStrMines"
    //Weapon.AmmoGive 5
    Weapon.AmmoUse 1
    Tag "Land Mines"
    +ALT_USES_BOTH
    +NOALERT
    +UNDROPPABLE
    States
    {
      Spawn:
        M_N3 A -1
        stop

      Ready:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReadyAmmo")
        TNT1 A 0 A_JumpIfInventory("DoomguyStrMines",1,"ReadyAmmo")
      ReadyNoAmmo:
        TNT1 A 1 A_WeaponReady
        goto Ready

      ReadyAmmo:
        LAYR A 1 A_WeaponReady
        goto Ready

      // Unpowered
      Deselect:
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPower",1,"DeselectPowered")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"DeselectPowered")
      DeselectContinue:
        TNT1 A 0 A_Lower
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"DeselectAmmo")
        TNT1 A 0 A_JumpIfInventory("DoomguyStrMines",1,"DeselectAmmo")
      DeselectNoAmmo:
        TNT1 A 1 A_Lower
        goto Deselect

      DeselectAmmo:
        LAYR A 1 A_Lower
        goto Deselect

      Select:
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPower",1,"SelectPowered")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"SelectPowered")
      SelectContinue:
        TNT1 A 0 A_Raise
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"SelectAmmo")
        TNT1 A 0 A_JumpIfInventory("DoomguyStrMines",1,"SelectAmmo")
      SelectNoAmmo:
        TNT1 A 1 A_Raise
        goto Select

      SelectAmmo:
        LAYR A 1 A_Raise
        goto Select

      Fire:
        LAYR B 8 A_PlaySound("doomguy/stronghold/minebeep",CHAN_WEAPON)
        LAYD ABCDE 1
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPower",1,"FirePowered")
        TNT1 A 5 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"FirePowered")
        TNT1 A 9 A_GiveInventory("SamsaraDoomguyStrLandMineLayerPrimaryAttackHandler")
      FireFinish:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireFinishAmmo")
        TNT1 A 0 A_JumpIfInventory("DoomguyStrMines",1,"FireFinishAmmo")
      FireFinishNoAmmo:
        TNT1 AAAAA 1
        TNT1 A 4
        goto Ready

      FireFinishAmmo:
        LAYU ABCDE 1
        LAYR A 4
        goto Ready

      AltFire:
        LAYR B 8 A_PlaySound("doomguy/stronghold/minebeep",CHAN_WEAPON)
        LAYD ABCDE 1
        TNT1 A 0 A_JumpIfInventory("SamsaraDoomguyStrWeaponPower",1,"AltFirePowered")
        TNT1 A 10 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"AltFirePowered")
        LAYT A 1 A_PlaySound("doomguy/stronghold/minethrow",CHAN_WEAPON)
        LAYT BCDEFGH 1
        LAYT I 1 A_GiveInventory("SamsaraDoomguyStrLandMineLayerSecondaryAttackHandler")
        LAYT JKLMNOPQR 1
        TNT1 A 10
      AltFireFinish:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"AltFireFinishAmmo")
        TNT1 A 0 A_JumpIfInventory("DoomguyStrMines",1,"AltFireFinishAmmo")
      AltFireFinishNoAmmo:
        TNT1 AAAAA 1
        TNT1 A 6
        goto Ready

      AltFireFinishAmmo:
        LAYU ABCDE 1
        LAYR A 6
        goto Ready

      // Powered
      DeselectPowered:
        TNT1 A 0 A_Lower
        goto DeselectContinue

      SelectPowered:
        TNT1 A 0 A_Raise
        goto SelectContinue

      FirePowered:
        TNT1 A 3
        TNT1 A 6 A_GiveInventory("SamsaraDoomguyStrLandMineLayerPoweredPrimaryAttackHandler")
      FirePoweredFinish:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FirePoweredFinishAmmo")
        TNT1 A 0 A_JumpIfInventory("DoomguyStrMines",1,"FirePoweredFinishAmmo")
      FirePoweredFinishNoAmmo:
        TNT1 AAAAA 1
        TNT1 A 2
        goto Ready

      FirePoweredFinishAmmo:
        LAYU ABCDE 1
        LAYR A 2
        goto Ready

      AltFirePowered:
        TNT1 A 5
        LAYT A 1 A_PlaySound("doomguy/stronghold/minethrow",CHAN_WEAPON)
        LAYT BCDEFGH 1
        LAYT I 1 A_GiveInventory("SamsaraDoomguyStrLandMineLayerPoweredSecondaryAttackHandler")
        LAYT JKLMNOPQR 1
        TNT1 A 5
        goto AltFireFinish
    }
}

// Unpowered attack handlers

actor SamsaraDoomguyStrLandMineLayerPrimaryAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("DoomguyStrMines",1,1)
        stop
        TNT1 A 0 A_TakeInventory("DoomguyStrMines",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrLandmine3",0,false)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrLandmine3Coop",0,false)
        stop
    }
}

actor SamsaraDoomguyStrLandMineLayerSecondaryAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("DoomguyStrMines",1,1)
        stop
        TNT1 A 0 A_TakeInventory("DoomguyStrMines",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrLandmine2",0,false,0,8)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrLandmine2Coop",0,false,0,8)
        stop
    }
}

// Powered attack handlers

actor SamsaraDoomguyStrLandMineLayerPoweredPrimaryAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("DoomguyStrMines",1,1)
        stop
        TNT1 A 0 A_TakeInventory("DoomguyStrMines",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrPowerLandmine3A",0,false)
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrPowerLandmine3B",-7,false)
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrPowerLandmine3B",7,false)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrPowerLandmine3ACoop",0,false)
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrPowerLandmine3BCoop",-7,false)
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrPowerLandmine3BCoop",7,false)
        stop
    }
}

actor SamsaraDoomguyStrLandMineLayerPoweredSecondaryAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("DoomguyStrMines",1,1)
        stop
        TNT1 A 0 A_TakeInventory("DoomguyStrMines",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrPowerLandmine2A",0,false,0,8)
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrPowerLandmine2B",-7,false,0,8)
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrPowerLandmine2B",7,false,0,8)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrPowerLandmine2ACoop",0,false,0,8)
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrPowerLandmine2BCoop",-7,false,0,8)
        TNT1 A 0 A_FireCustomMissile("SamsaraDoomguyStrPowerLandmine2BCoop",7,false,0,8)
        stop
    }
}

// Active land mine

actor SamsaraDoomguyStrLandmine
{
    Projectile
    Health 20
    Radius 8
    Height 5
    Damage 10
    DamageType "DoomDamageType"
    Mass 1000000
    Decal "Scorch"
    Obituary "%k killed %o with a land mine."
    Tag "Land Mine"
    Species "Landmine"
    -BLOODSPLATTER
    +MOVEWITHSECTOR
    +NEVERTARGET
    -NOBLOCKMAP
    +NOBLOOD
    -NOGRAVITY
    //+NOTARGETSWITCH // don't replace ownership with whoever attacked it // only applied in co-op
    +SHOOTABLE
    +THRUSPECIES
    States
    {
      Spawn:
        M_N3 A 0 NoDelay A_PlaySound("doomguy/stronghold/tink",CHAN_VOICE)
      SpawnLoop:
        M_N3 A 5
        M_N3 B 5 Light("LandMineSpawnB")
        loop

      Death:
        TNT1 A 0 A_ChangeFlag("NOBLOCKMAP",true)
        TNT1 A 0 A_UnSetShootable
      DeathStart:
        TNT1 A 0 A_PlaySound("doomguy/stronghold/mineexpl",CHAN_VOICE)
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_NoGravity
        TNT1 A 0 A_SetDamageType("DoomDamageType")
        TNT1 A 0 A_Jump(256,"DeathCheck")
      DeathCheck:
        TNT1 A 0 A_Explode(384,160,0) // adjust accordingly
      DeathFinish:
        DMSL B 8 Bright Light("ROCKET_X1")
        DMSL C 6 Bright Light("ROCKET_X2")
        DMSL D 4 Bright Light("ROCKET_X3")
        stop
    }
}

actor SamsaraDoomguyStrLandmineCoop : SamsaraDoomguyStrLandmine
{
    Species "Player"
    +NOBLOCKMAP
    +NOTARGETSWITCH
    -SHOOTABLE
    States
    {
      Death:
        goto DeathStart

      DeathCheck:
        TNT1 A 0 A_Explode(384,160,0)
        goto DeathFinish
    }
}

// Thrown land mine

actor SamsaraDoomguyStrLandmine2 : SamsaraDoomguyStrLandmine
{
    Speed 15
    BounceType "Doom"
    BounceSound "doomguy/stronghold/tink"
    Decal ""
    +NOBLOCKMAP
    -SHOOTABLE
    States
    {
      Spawn:
        goto SpawnLoop

      Death:
        TNT1 A 0 A_StopSound
        TNT1 A 0 A_SpawnItemEx("SamsaraDoomguyStrLandmine",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
        stop
    }
}

actor SamsaraDoomguyStrLandmine2Coop : SamsaraDoomguyStrLandmine2
{
    Species "Player"
    +NOTARGETSWITCH
    States
    {
      Death:
        TNT1 A 0 A_StopSound
        TNT1 A 0 A_SpawnItemEx("SamsaraDoomguyStrLandmineCoop",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
        stop
    }
}

// Dropped land mine

actor SamsaraDoomguyStrLandmine3 : SamsaraDoomguyStrLandmine2 { Speed 2 }

actor SamsaraDoomguyStrLandmine3Coop : SamsaraDoomguyStrLandmine3
{
    Species "Player"
    +NOTARGETSWITCH
    States
    {
      Death:
        TNT1 A 0 A_StopSound
        TNT1 A 0 A_SpawnItemEx("SamsaraDoomguyStrLandmineCoop",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
        stop
    }
}

// Active powered land mine

actor SamsaraDoomguyStrPowerLandmineA : SamsaraDoomguyStrLandmine
{
    Damage 20
    States
    {
      DeathCheck:
        TNT1 A 0 A_Explode(768,320,0) // adjust accordingly
        goto DeathFinish
    }
}

actor SamsaraDoomguyStrPowerLandmineACoop : SamsaraDoomguyStrPowerLandmineA
{
    Species "Player"
    +NOBLOCKMAP
    +NOTARGETSWITCH
    -SHOOTABLE
    States
    {
      Death:
        goto DeathStart

      DeathCheck:
        TNT1 A 0 A_Explode(768,320,0)
        goto DeathFinish
    }
}

actor SamsaraDoomguyStrPowerLandmineB : SamsaraDoomguyStrPowerLandmineA
{
    States
    {
      DeathCheck:
        TNT1 A 0 A_Explode(384,160,0) // adjust accordingly
        goto DeathFinish
    }
}

actor SamsaraDoomguyStrPowerLandmineBCoop : SamsaraDoomguyStrPowerLandmineB
{
    Species "Player"
    +NOBLOCKMAP
    +NOTARGETSWITCH
    -SHOOTABLE
    States
    {
      Death:
        goto DeathStart

      DeathCheck:
        TNT1 A 0 A_Explode(384,160,0)
        goto DeathFinish
    }
}

// Thrown powered land mine

actor SamsaraDoomguyStrPowerLandmine2A : SamsaraDoomguyStrPowerLandmineA
{
    Speed 15
    Damage 10
    BounceType "Doom"
    BounceSound "doomguy/stronghold/tink"
    Decal ""
    +NOBLOCKMAP
    -SHOOTABLE
    States
    {
      Spawn:
        goto SpawnLoop

      Death:
        TNT1 A 0 A_StopSound
        TNT1 A 0 A_SpawnItemEx("SamsaraDoomguyStrPowerLandmineA",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
        stop
    }
}

actor SamsaraDoomguyStrPowerLandmine2ACoop : SamsaraDoomguyStrPowerLandmine2A
{
    Species "Player"
    +NOTARGETSWITCH
    States
    {
      Death:
        TNT1 A 0 A_StopSound
        TNT1 A 0 A_SpawnItemEx("SamsaraDoomguyStrPowerLandmineACoop",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
        stop
    }
}

actor SamsaraDoomguyStrPowerLandmine2B : SamsaraDoomguyStrPowerLandmine2A
{
    States
    {
      Death:
        TNT1 A 0 A_StopSound
        TNT1 A 0 A_SpawnItemEx("SamsaraDoomguyStrPowerLandmineB",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
        stop
    }
}

actor SamsaraDoomguyStrPowerLandmine2BCoop : SamsaraDoomguyStrPowerLandmine2B
{
    Species "Player"
    +NOTARGETSWITCH
    States
    {
      Death:
        TNT1 A 0 A_StopSound
        TNT1 A 0 A_SpawnItemEx("SamsaraDoomguyStrPowerLandmineBCoop",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
        stop
    }
}

// Dropped powered land mine

actor SamsaraDoomguyStrPowerLandmine3A : SamsaraDoomguyStrPowerLandmine2A { Speed 5 }

actor SamsaraDoomguyStrPowerLandmine3ACoop : SamsaraDoomguyStrPowerLandmine3A
{
    Species "Player"
    +NOTARGETSWITCH
    States
    {
      Death:
        TNT1 A 0 A_StopSound
        TNT1 A 0 A_SpawnItemEx("SamsaraDoomguyStrPowerLandmineACoop",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
        stop
    }
}

actor SamsaraDoomguyStrPowerLandmine3B : SamsaraDoomguyStrPowerLandmine2B { Speed 5 }

actor SamsaraDoomguyStrPowerLandmine3BCoop : SamsaraDoomguyStrPowerLandmine3B
{
    Species "Player"
    +NOTARGETSWITCH
    States
    {
      Death:
        TNT1 A 0 A_StopSound
        TNT1 A 0 A_SpawnItemEx("SamsaraDoomguyStrPowerLandmineBCoop",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
        stop
    }
}

// Ammo

actor DoomguyStrMines : Ammo
{
    Inventory.PickupMessage "Picked up some additional land mines."
    Inventory.Icon "AMSMS0"
    Inventory.Amount 2
    Inventory.MaxAmount 20
    Ammo.BackpackAmount 5
    Ammo.BackpackMaxAmount 40
    Tag "Land Mines"
    /*States
    {
      Spawn:
        M_N3 A -1
        stop
    }*/
}
