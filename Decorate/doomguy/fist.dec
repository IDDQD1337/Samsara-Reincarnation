actor " Fist " : Weapon
{
    Weapon.SlotNumber 0
    Weapon.SelectionOrder 3700
    Weapon.Kickback 100
    +INVENTORY.UNDROPPABLE
    +WEAPON.WIMPY_WEAPON
    +WEAPON.MELEEWEAPON
    +WEAPON.NOALERT
    Obituary "$SAMSARA_DOOMGUY_OB_FIST"
	Inventory.RestrictedTo "DoomguyPlayer"
    States
    {
	  Ready:
		TNT1 A 0 A_JumpIfInventory("Doom64Mode", 1, "Ready64")
        DPUN A 1 A_WeaponReady
        loop

      Ready64:
        64FS A 1 A_WeaponReady
        Goto Ready

      Deselect:
		TNT1 A 0 A_JumpIfInventory("Doom64Mode", 1, "Deselect64")
        DPUN A 1 A_Lower
        loop

      Deselect64:
        64FS A 1 A_Lower
        loop

      Select: 
		TNT1 A 0 A_JumpIfInventory("Doom64Mode", 1, "Select64")
        DPUN A 1 A_Raise
        loop
		
      Select64: 
        64FS A 1 A_Raise
        loop		

      Fire:
		TNT1 A 0 A_JumpIfInventory("Doom64Mode", 1, "Fire64")
        TNT1 A 0 A_JumpIfInventory("VanillaDoom", 1, "VanillaFire")
        TNT1 A 0 A_JumpIfInventory("RuneDoubleFiringSpeed", 1, "Rage")
        DPUN LBCD 1
        goto Fire2

     Fire64:
        64FS E 0 A_JumpIfInventory("PunchDrunkZerk", 1, "StartBerserk64")
        64FS E 0 A_JumpIfInventory("PowerChainsawBerserk", 1, "StartBerserk64")
        64FS B 5
        Goto Fire2
		
      StartBerserk64:
        64FS B 4
        Goto Fire2		

      VanillaFire:
        DPUN M 4
        goto Fire2

      Rage:
        DPUN LC 2
        goto Fire2

      Fire2:
		TNT1 A 0 A_JumpIfInventory("Doom64Mode", 1, "Fire64_2")
        DPUN E 0 A_JumpIfInventory("PunchDrunkZerk", 1, "FirePDBerserk")
        DPUN E 0 A_JumpIfInventory("PowerChainsawBerserk", 1, "FireBerserk")
        DPUN E 0 A_CustomPunch(random(1, 10)*2, 1, 0, "DoomPunchPuff")
        goto FireEnd
		
      Fire64_2:
        64FS E 0 A_JumpIfInventory("PunchDrunkZerk", 1, "FirePDBerserk64")
        64FS E 0 A_JumpIfInventory("PowerChainsawBerserk", 1, "FireBerserk64")
        64FS E 0 A_CustomPunch(random(1, 10)*2, 1, 0, "Doom64PunchPuff")
        goto FireEnd64		

      FireBerserk:
        DPUN E 0 A_CustomPunch(random(10, 20)*12, 1, 0, "DoomPunchPuff2") // Technically should be random(1, 10)*20, but oh well.
        goto FireEnd

      FirePDBerserk:
        DPUN E 0 A_CustomPunch(random(10, 20)*4, 1, 0, "DoomPunchPuff2") // Punchdrunk triples fist damage already.
        goto FireEnd
		
      FireBerserk64:
        64FS E 0 A_CustomPunch(random(10, 20)*12, 1, 0, "Doom64PunchPuff2")
        goto FireEndBerserk

      FirePDBerserk64:
        64FS E 0 A_CustomPunch(random(10, 20)*4, 1, 0, "Doom64PunchPuff2")
        goto FireEndBerserk		

      FireEnd:
        DPUN E 0 A_JumpIfInventory("VanillaDoom", 1, "VanillaEnd")
        DPUN E 0 A_JumpIfInventory("RuneDoubleFiringSpeed", 1, "RageEnd")
        DPUN E 1
        DPUN FGHI 2
        DPUN JKL 1
        DPUN A 5 A_ReFire
        goto Ready
		
      FireEnd64:
        64FS C 5
        64FS D 5
        64FS C 5
        64FS B 5 A_ReFire
        goto Ready
		
      FireEndBerserk:
        64FS C 4
        64FS D 5
        64FS C 4
        64FS B 5 A_ReFire
        goto Ready		
    
      VanillaEnd:
        DPUN N 4
        DPUN O 5
        DPUN N 4
        DPUN M 5 A_ReFire
        Goto Ready

      RageEnd:
        DPUN EFGHIJL 2
        DPUN A 4 A_ReFire
        goto Ready
    }
}

actor DoomPunchPuff : BulletPuff
{
    DamageType "DoomDamageType"
    States
    {
      Spawn:
      Crash:
        TNT1 A 0 A_PlaySound("doomguy/wallpunch2")
        DPFF CD 4
        Stop

      Melee:
        TNT1 A 0 A_PlaySound("doomguy/punch")
        stop
    }
}

actor DoomPunchPuff2 : BulletPuff
{
    DamageType "DoomDamageType"
    States
    {
      Spawn:
      Crash:
        TNT1 A 0 A_PlaySound("doomguy/wallpunch")
        DPFF CD 4
        stop

      Melee:
        TNT1 A 0 A_PlaySound("doomguy/punch2")
        stop
    }
}
