actor " Fist " : Weapon
{
    Weapon.SlotNumber 0
    Weapon.SelectionOrder 3700
    Weapon.Kickback 100
    +INVENTORY.UNDROPPABLE
    +WEAPON.WIMPY_WEAPON
    +WEAPON.MELEEWEAPON
    +WEAPON.NOALERT
    Obituary "$SAMSARA_DOOMGUY_OB_FIST"
	Inventory.RestrictedTo "DoomguyPlayer"
	Tag "Brass Knuckles"
    States
    {
      // Classic
      Ready:
        TNT1 A 0 A_JumpIfInventory("Doom64Mode",1,"Ready64")
        DPUN A 1 A_WeaponReady
        loop

      Deselect:
        TNT1 A 0 A_JumpIfInventory("DoomClassMode",2,"DeselectStr")
        TNT1 A 0 A_JumpIfInventory("Doom64Mode",1,"Deselect64")
        DPUN A 1 A_Lower
        loop

      Select:
        TNT1 A 0 A_JumpIfInventory("DoomClassMode",2,"SelectStr")
        TNT1 A 0 A_JumpIfInventory("Doom64Mode",1,"Select64")
        DPUN A 1 A_Raise
        loop

      Fire:
        TNT1 A 0 A_JumpIfInventory("DoomClassMode",2,"FireStr")
        TNT1 A 0 A_JumpIfInventory("Doom64Mode",1,"Fire64")
        TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"FireSmooth")
      FireVanilla:
        DPUN M 4
        DPUN N 4 A_GiveInventory("SamsaraDoomguyFistAttackHandler")
        DPUN O 5
        DPUN N 4
        DPUN M 5 A_ReFire
        goto Ready

      FireSmooth:
        DPUN L 1 A_JumpIfInventory("SamsaraHasDoubleFiringSpeed",1,"FireSmoothRage")
        DPUN BCD 1
        DPUN E 2 A_GiveInventory("SamsaraDoomguyFistAttackHandler")
        DPUN FGHI 2
        DPUN JKL 1
        DPUN A 5 A_ReFire
        goto Ready

      FireSmoothRage:
        DPUN LC 2
        DPUN E 2 A_GiveInventory("SamsaraDoomguyFistAttackHandler")
        DPUN FGHIJL 2
        DPUN A 4 A_ReFire
        goto Ready

      // Doom 64 (fires slower without berserk compared to classic fist)
      Ready64:
        64FS A 1 A_WeaponReady
        goto Ready

      Deselect64:
        64FS A 1 A_Lower
        goto Deselect

      Select64:
        64FS A 1 A_Raise
        goto Select

      Fire64:
        TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"Fire64Smooth")
      Fire64Vanilla:
        64FS B 5 A_JumpIfInventory("PowerStrength",1,"Fire64VanillaBerserk")
        64FS C 5 A_GiveInventory("SamsaraDoomguy64FistAttackHandler")
        64FS DC 5
        64FS B 5 A_ReFire
        goto Ready

      Fire64VanillaBerserk:
        64FS B 4
        64FS C 4 A_GiveInventory("SamsaraDoomguy64FistAttackHandler")
        64FS D 5
        64FS C 4
        64FS B 5 A_ReFire
        goto Ready

      Fire64Smooth:
        64FS E 1 A_JumpIfInventory("PowerStrength",1,"Fire64SmoothBerserk")
        64FS FG 1
        64FS B 2
        64FS H 1 A_GiveInventory("SamsaraDoomguy64FistAttackHandler")
        64FS CIDJ 1
        64FS DICHB 2
        64FS G 1 A_ReFire
        64FS FE 1
        64FS A 2
        goto Ready

      Fire64SmoothBerserk:
        64FS EFGB 1
        64FS H 1 A_GiveInventory("SamsaraDoomguy64FistAttackHandler")
        64FS CIDJDI 1
        64FS CHB 2
        64FS G 1 A_ReFire
        64FS FE 1
        64FS A 2
        goto Ready

      // Stronghold
      // Unpowered
      DeselectStr:
        TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"DeselectStrPowered")
      DeselectStrContinue:
        TNT1 A 0 A_Lower
        DPUN A 1 A_Lower
        goto Deselect

      SelectStr:
        TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"SelectStrPowered")
      SelectStrContinue:
        TNT1 A 0 A_Raise
        DPUN A 1 A_Raise
        goto Select

      FireStr:
        TNT1 A 0 A_JumpIfInventory("SamsaraUsingSmoothAnims",1,"FireStrSmooth")
      FireStrVanilla:
        DPUN M 4
        TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"FireStrVanillaPowered")
        TNT1 A 0 A_GiveInventory("SamsaraDoomguyStrFistAttackHandler")
      FireStrVanillaFinish:
        DPUN N 4
        DPUN O 5
        DPUN N 4
        DPUN M 5 A_ReFire
        goto Ready

      FireStrSmooth:
        DPUN L 1 A_JumpIfInventory("SamsaraHasDoubleFiringSpeed",1,"FireStrSmoothRage")
        DPUN BCD 1
        TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"FireStrSmoothPowered")
        TNT1 A 0 A_GiveInventory("SamsaraDoomguyStrFistAttackHandler")
      FireStrSmoothFinish:
        DPUN EFGHI 2
        DPUN JKL 1
        DPUN A 5 A_ReFire
        goto Ready

      FireStrSmoothRage:
        DPUN LC 2
        TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"FireStrSmoothRagePowered")
        TNT1 A 0 A_GiveInventory("SamsaraDoomguyStrFistAttackHandler")
      FireStrSmoothRageFinish:
        DPUN EFGHIJL 2
        DPUN A 4 A_ReFire
        goto Ready

      // Powered
      DeselectStrPowered:
        TNT1 A 0 A_Lower
        goto DeselectStrContinue

      SelectStrPowered:
        TNT1 A 0 A_Raise
        goto SelectStrContinue

      FireStrVanillaPowered:
        TNT1 A 0 A_GiveInventory("SamsaraDoomguyStrFistPoweredAttackHandler")
        goto FireStrVanillaFinish

      FireStrSmoothPowered:
        TNT1 A 0 A_GiveInventory("SamsaraDoomguyStrFistPoweredAttackHandler")
        goto FireStrSmoothFinish

      FireStrSmoothRagePowered:
        TNT1 A 0 A_GiveInventory("SamsaraDoomguyStrFistPoweredAttackHandler")
        goto FireStrSmoothRageFinish
    }
}

// punchdrunk damage values: random(1,10)*8 (normal), random(10,20)*4 (berserk). Punchdrunk triples fist damage already.

actor SamsaraDoomguyFistAttackHandler : Trigger
{
    States
    {
      Pickup:
        TNT1 A 0 A_JumpIfInventory("PowerStrength",1,"PickupBerserk")
      // Normal
      PickupNormal:
        TNT1 A 0 A_JumpIfInventory("SamsaraImprovedDoomguyBalance",1,"PickupNormalImproved")
      // Normal (vanilla)
      PickupNormalVanilla:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupNormalVanillaCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupNormalVanillaCoop")
      PickupNormalVanillaDM:
        TNT1 A 0 A_CustomPunch(random(1,10)*2,true,0,"DoomPunchPuff")
        stop

      PickupNormalVanillaCoop:
        TNT1 A 0 A_CustomPunch(random(1,10)*2,true,0,"DoomPunchPuff_Coop")
        stop

      // Normal (improved)
      PickupNormalImproved:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupNormalImprovedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupNormalImprovedCoop")
      PickupNormalImprovedDM:
        TNT1 A 0 A_CustomPunch(random(2,10)*5,true,0,"DoomPunchPuff")
        stop

      PickupNormalImprovedCoop:
        TNT1 A 0 A_CustomPunch(random(2,10)*5,true,0,"DoomPunchPuff_Coop")
        stop

      // Berserk
      PickupBerserk:
        TNT1 A 0 A_JumpIfInventory("SamsaraImprovedDoomguyBalance",1,"PickupBerserkImproved")
      // Berserk (vanilla)
      PickupBerserkVanilla:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupBerserkVanillaCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupBerserkVanillaCoop")
      PickupBerserkVanillaDM:
        TNT1 A 0 A_CustomPunch(random(1,10)*20,true,0,"DoomPunchPuff2")
        stop

      PickupBerserkVanillaCoop:
        TNT1 A 0 A_CustomPunch(random(1,10)*20,true,0,"DoomPunchPuff2_Coop")
        stop

      // Berserk (improved)
      PickupBerserkImproved:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupBerserkImprovedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupBerserkImprovedCoop")
      PickupBerserkImprovedDM:
        TNT1 A 0 A_CustomPunch(random(20,50)*5,true,0,"DoomPunchPuff2")
        stop

      PickupBerserkImprovedCoop:
        TNT1 A 0 A_CustomPunch(random(20,50)*5,true,0,"DoomPunchPuff2_Coop")
        stop
    }
}

actor SamsaraDoomguy64FistAttackHandler : Trigger
{
    States
    {
      Pickup:
        TNT1 A 0 A_JumpIfInventory("PowerStrength",1,"PickupBerserk")
      // Normal
      PickupNormal:
        TNT1 A 0 A_JumpIfInventory("SamsaraImprovedDoomguyBalance",1,"PickupNormalImproved")
      // Normal (vanilla)
      PickupNormalVanilla:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupNormalVanillaCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupNormalVanillaCoop")
      PickupNormalVanillaDM:
        TNT1 A 0 A_CustomPunch(random(1,10)*2,true,0,"Doom64PunchPuff")
        stop

      PickupNormalVanillaCoop:
        TNT1 A 0 A_CustomPunch(random(1,10)*2,true,0,"Doom64PunchPuff_Coop")
        stop

      // Normal (improved)
      PickupNormalImproved:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupNormalImprovedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupNormalImprovedCoop")
      PickupNormalImprovedDM:
        TNT1 A 0 A_CustomPunch(random(2,10)*5,true,0,"Doom64PunchPuff")
        stop

      PickupNormalImprovedCoop:
        TNT1 A 0 A_CustomPunch(random(2,10)*5,true,0,"Doom64PunchPuff_Coop")
        stop

      // Berserk
      PickupBerserk:
        TNT1 A 0 A_JumpIfInventory("SamsaraImprovedDoomguyBalance",1,"PickupBerserkImproved")
      // Berserk (vanilla)
      PickupBerserkVanilla:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupBerserkVanillaCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupBerserkVanillaCoop")
      PickupBerserkVanillaDM:
        TNT1 A 0 A_CustomPunch(random(1,10)*20,true,0,"Doom64PunchPuff2")
        stop

      PickupBerserkVanillaCoop:
        TNT1 A 0 A_CustomPunch(random(1,10)*20,true,0,"Doom64PunchPuff2_Coop")
        stop

      // Berserk (improved)
      PickupBerserkImproved:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupBerserkImprovedCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupBerserkImprovedCoop")
      PickupBerserkImprovedDM:
        TNT1 A 0 A_CustomPunch(random(20,50)*5,true,0,"Doom64PunchPuff2")
        stop

      PickupBerserkImprovedCoop:
        TNT1 A 0 A_CustomPunch(random(20,50)*5,true,0,"Doom64PunchPuff2_Coop")
        stop
    }
}

actor SamsaraDoomguyStrFistAttackHandler : Trigger
{
    States
    {
      Pickup:
        TNT1 A 0 A_JumpIfInventory("PowerStrength",1,"PickupBerserk")
      // Normal
      PickupNormal:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupNormalSpread") // A_CustomPunch doesn't support spread, multiply damage by 3 to compensate
        TNT1 A 0 A_CustomPunch(random(1,10)*2,true,0,"SamsaraDoomguyStrFistPuff")
        stop

      PickupNormalSpread:
        TNT1 A 0 A_CustomPunch((random(1,10)*2)*3,true,0,"SamsaraDoomguyStrFistPuff")
        stop

      // Berserk
      PickupBerserk:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupBerserkSpread")
        TNT1 A 0 A_CustomPunch((random(1,10)*2)*10,true,0,"SamsaraDoomguyStrFistPuff2")
        stop

      PickupBerserkSpread:
        TNT1 A 0 A_CustomPunch(((random(1,10)*2)*10)*3,true,0,"SamsaraDoomguyStrFistPuff2")
        stop
    }
}

actor SamsaraDoomguyStrFistPoweredAttackHandler : Trigger
{
    States
    {
      Pickup:
        TNT1 A 0 A_JumpIfInventory("PowerStrength",1,"PickupBerserk")
      // Normal
      PickupNormal:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupNormalSpread")
        TNT1 AAAAAAAAAAAAAAAA 0 A_CustomPunch(random(1,10)*2,true,0,"SamsaraDoomguyStrFistPuff")
        stop

      PickupNormalSpread:
        TNT1 AAAAAAAAAAAAAAAA 0 A_CustomPunch((random(1,10)*2)*3,true,0,"SamsaraDoomguyStrFistPuff")
        stop

      // Berserk
      PickupBerserk:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasSpread",1,"PickupBerserkSpread")
        TNT1 AAAAAAAAAAAAAAAA 0 A_CustomPunch((random(1,10)*2)*10,true,0,"SamsaraDoomguyStrFistPuff2")
        stop

      PickupBerserkSpread:
        TNT1 AAAAAAAAAAAAAAAA 0 A_CustomPunch(((random(1,10)*2)*10)*3,true,0,"SamsaraDoomguyStrFistPuff2")
        stop
    }
}

actor DoomPunchPuff : BulletPuff
{
    DamageType "DoomDamageTypeFist"
    States
    {
      Spawn:
      Crash:
        TNT1 A 0 A_PlaySound("doomguy/wallpunch2")
        DPFF CD 4
        stop

      Melee:
        TNT1 A 0 A_PlaySound("doomguy/punch")
        stop
    }
}

actor DoomPunchPuff_Coop : DoomPunchPuff { Species "Player" +THRUSPECIES +MTHRUSPECIES }

actor DoomPunchPuff2 : BulletPuff
{
    DamageType "DoomDamageTypeFist"
    States
    {
      Spawn:
      Crash:
        TNT1 A 0 A_PlaySound("doomguy/wallpunch")
        DPFF CD 4
        stop

      Melee:
        TNT1 A 0 A_PlaySound("doomguy/punch2")
        stop
    }
}

actor DoomPunchPuff2_Coop : DoomPunchPuff2 { Species "Player" +THRUSPECIES +MTHRUSPECIES }

actor Doom64PunchPuff : Doom64BulletPuff
{
    DamageType "DoomFist"
    States
    {
      Spawn:
      Crash:
        TNT1 A 0 A_PlaySound("doom64guy/wallpunch2")
        64PF CDEF 4
        stop

      Melee:
        TNT1 A 0 A_PlaySound("doom64guy/punch")
        stop
    }
}

actor Doom64PunchPuff_Coop : Doom64PunchPuff { Species "Player" +THRUSPECIES +MTHRUSPECIES }

actor Doom64PunchPuff2 : Doom64BulletPuff
{
    DamageType "DoomFist"
    States
    {
      Spawn:
      Crash:
        TNT1 A 0 A_PlaySound("doom64guy/wallpunch")
        64PF CDEF 4
        stop

      Melee:
        TNT1 A 0 A_PlaySound("doom64guy/punch2")
        stop
    }
}

actor Doom64PunchPuff2_Coop : Doom64PunchPuff2 { Species "Player" +THRUSPECIES +MTHRUSPECIES }

// Bullet puff (fist)

actor SamsaraDoomguyStrFistPuff : SamsaraDoomguyStrBulletPuff
{
    DamageType "DoomDamageTypeFist"
    States
    {
      Spawn:
      Crash:
        TNT1 A 0 A_PlaySound("doomguy/wallpunch2")
        DPFF CD 4
        stop

      Melee:
        TNT1 A 0 A_PlaySound("doomguy/punch")
        stop
    }
}

actor SamsaraDoomguyStrFistPuffCoop : SamsaraDoomguyStrFistPuff { +MTHRUSPECIES }

actor SamsaraDoomguyStrFistPuff2 : SamsaraDoomguyStrBulletPuff
{
    DamageType "DoomDamageTypeFist"
    States
    {
      Spawn:
      Crash:
        TNT1 A 0 A_PlaySound("doomguy/wallpunch")
        DPFF CD 4
        stop

      Melee:
        TNT1 A 0 A_PlaySound("doomguy/punch2")
        stop
    }
}

actor SamsaraDoomguyStrFistPuff2Coop : SamsaraDoomguyStrFistPuff2 { +MTHRUSPECIES }
