Actor Q2Phalanx : Q2Weapon
{
	Tag "Phalanx Particle Cannon"
	Inventory.PickupMessage "Phalanx Particle Cannon"
	Weapon.AmmoGive 5
	Weapon.AmmoType RocketAmmo
	Weapon.AmmoUse 2
	+WEAPON.EXPLOSIVE
	Weapon.SlotNumber 5
    Weapon.SlotPriority 1
	Weapon.SelectionOrder 500
	Inventory.RestrictedTo "Bitterman"		
	States
	{
	Spawn:
		Q2WP A -1
		Stop
	Select:
		TNT1 A 0 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_TakeInventory(Q2WeaponIndex)
		TNT1 A 0 A_GiveInventory(Q2WeaponIndex,5)
		PHX0 ABCDEF 3
		Goto Ready+1
	Deselect:
		PHX4 A 0 A_StopSound(CHAN_5)
		PHX4 ABCDE 3
		Goto InstantDeselect
	InstantDeselect:
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
		TNT1 A 1 A_Lower
		Wait
	Ready:
		TNT1 A 0 A_PlaySound("Q2Weapons/PhalanxLoop",CHAN_5,1.0,true)
		TNT1 A 0 A_Jump(32,2)
		PHX2 A 1 A_WeaponReady
		Loop
		PHX2 "ABCDEFGHIJKLMNOPQRSTUVWXYZ[\]" 4 A_WeaponReady
		PHX3 ABCDEFGHIJ 5 A_WeaponReady
		Goto Ready+2
	Fire:
		PHX2 A 4
		PHX1 A 0 A_AlertMonsters
		PHX1 A 0 A_PlaySound("Q2Weapons/PhalanxFire",CHAN_WEAPON)
		PHX1 A 0 A_GunFlash
		PHX1 A 4
		PHX1 B 0 A_TakeInventory("RocketAmmo",2,TIF_NOTAKEINFINITE)
		PHX1 B 4 A_FireCustomMissile("Q2PhalanxMissile",1.5,0,8,0,0,-2.1)
		PHX1 C 4 A_FireCustomMissile("Q2PhalanxMissile",-1.5,0,8,0,0,-0.9)
		PHX1 DEFGHIJKLMN 4
		Goto Ready		
	Flash:
		TNT1 A 1 A_Light2
		TNT1 A 0 A_SetPitch(Pitch-3.0)
		TNT1 AAAAA 1 A_SetPitch(Pitch+0.3)
		TNT1 A 0 A_Light1
		TNT1 AAAAA 1 A_SetPitch(Pitch+0.3)
		Goto LightDone
	}
}

Actor Q2PhalanxMissile : Q2Rocket
{
	Speed 20
	Damage (75)
	-ROCKETTRAIL 
	+BRIGHT
	//DeathSound "Q2Weapons/PhalanxExplode"
	States
	{
	Spawn:
		Q2PX A 0 NoDelay A_PlaySound("Q2Weapons/RocketFly",CHAN_VOICE,0.66,1)
		Q2PX AAAAA 0 A_SpawnItemEx(Q2RocketTrail,0,0,0,-FRandom(0,0.5),0,FRandom(-0.5,0),Random(1,360),128)
		Q2PX A 1 Light(ROCKET) 
		Q2PX AAAAA 0 A_SpawnItemEx(Q2RocketTrail,0,0,0,-FRandom(0,0.5),0,FRandom(-0.5,0),Random(1,360),128)
		Q2PX B 1 Light(ROCKET) 
		Q2PX AAAAA 0 A_SpawnItemEx(Q2RocketTrail,0,0,0,-FRandom(0,0.5),0,FRandom(-0.5,0),Random(1,360),128)
		Q2PX C 1 Light(ROCKET) 
		Loop
	Death:
		TNT1 A 0 A_Explode(225, 120)
		TNT1 A 5 A_SpawnItem(Q2Explosion)
		Stop
	}
}