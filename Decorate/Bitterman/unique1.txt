Actor Q2GrenadeLauncher : Q2Weapon
{
	Tag "Grenade Launcher"
	Inventory.PickupMessage "Grenade Launcher"
	Weapon.AmmoGive 5
	Weapon.AmmoType RocketAmmo
	Weapon.AmmoUse 1
	+WEAPON.EXPLOSIVE
	Weapon.SlotNumber 8
	Inventory.RestrictedTo "Bitterman"		
	States
	{
	Spawn:
		Q2WP A -1
		Stop
	Select:
		TNT1 A 0 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_TakeInventory(Q2WeaponIndex)
		TNT1 A 0 A_GiveInventory(Q2WeaponIndex,9)
		GRL0 ABCDEF 4
		Goto Ready+1
	Deselect:
		GRL1 A 2
		GRL0 STUVW 3
		Goto InstantDeselect
	InstantDeselect:
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
		TNT1 A 1 A_Lower
		Wait
	Ready:
		TNT1 A 0 A_Jump(32,2)
		GRL1 A 1 A_WeaponReady
		Loop
		GRL1 "BCDEFGHIJKLMNOPQRSTUVWXYZ[^]" 5 A_WeaponReady
		GRL2 ABCDEFGHIJKLMN 5 A_WeaponReady
		Goto Ready+1
	Fire:
		TNT1 A 0 A_JumpIfInventory("CoopModeOn", 1, "CoopFire")
		TNT1 A 0 A_JumpIfInventory(PowerSilencer,1,"SilentFire")
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("Q2Weapons/GrenadeLauncherFire",CHAN_WEAPON)
		TNT1 A 0 A_FireCustomMissile(Q2Grenade,0,1,9,0,0,10)
		TNT1 A 0 A_GunFlash
		GRL0 HI 3
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("Q2Weapons/GrenadeLauncherReload",0)
		GRL0 JKLMNOPQR 4
		GRL1 A 2
		Goto Ready
	SilentFire:
		TNT1 A 0 A_PlaySound("Q2Weapons/GrenadeLauncherFire",CHAN_WEAPON,0.25,0,3)
		TNT1 A 0 A_JumpIfInventory("CoopModeOn", 1, "CoopFire")
		TNT1 A 0 A_FireCustomMissile(Q2Grenade,0,1,9,0,0,10)
		TNT1 A 0 A_GunFlash
		GRL0 HI 3
		TNT1 A 0 A_PlaySound("Q2Weapons/GrenadeLauncherReload",0,0.5,0,3)
		GRL0 JKLMNOPQR 4
		GRL1 A 2
		Goto Ready
	CoopFire:
		TNT1 A 0 A_JumpIfInventory(PowerSilencer,1,"SilentCoopFire")
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("Q2Weapons/GrenadeLauncherFire",CHAN_WEAPON)
		TNT1 A 0 A_FireCustomMissile(Q2Grenade_Coop,0,1,9,0,0,10)
		TNT1 A 0 A_GunFlash
		GRL0 HI 3
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("Q2Weapons/GrenadeLauncherReload",0)
		GRL0 JKLMNOPQR 4
		GRL1 A 2
		Goto Ready
	SilentCoopFire:
		TNT1 A 0 A_PlaySound("Q2Weapons/GrenadeLauncherFire",CHAN_WEAPON,0.25,0,3)
		TNT1 A 0 A_FireCustomMissile(Q2Grenade_Coop,0,1,9,0,0,10)
		TNT1 A 0 A_GunFlash
		GRL0 HI 3
		TNT1 A 0 A_PlaySound("Q2Weapons/GrenadeLauncherReload",0,0.5,0,3)
		GRL0 JKLMNOPQR 4
		GRL1 A 2
		Goto Ready
	DFire:
		GRL0 JKLM 4
		GRL0 NOPQR 4 A_WeaponReady
		Goto Ready
	Flash:
		TNT1 A 0 A_JumpIfInventory("SamsaraRecoilIsDisabled", 1, "FlashNoRecoil")
		TNT1 A 1 A_Light2
		TNT1 A 0 A_SetPitch(Pitch-5.0)
		TNT1 AAAAA 1 A_SetPitch(Pitch+0.5)
		TNT1 A 0 A_Light1
		TNT1 AAAAA 1 A_SetPitch(Pitch+0.5)
		Goto LightDone
	FlashNoRecoil:
		TNT1 A 6 A_Light2
		TNT1 A 5 A_Light1
		Goto LightDone
	}
}
Actor Q2Grenade
{
	Projectile
	-NOGRAVITY +FORCERADIUSDMG +MOVEWITHSECTOR
	Speed 25
	Gravity 0.66
	Damage (125)
	Radius 5
	Height 10
	BounceType Doom
	BounceSound "Q2Weapons/GrenadeBounce"
	DamageType "BittermanDamage"
	Var Int User_Angle;
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_Jump(128,2)
		TNT1 A 0 A_SetUserVar(User_Angle,Random(1,6))
		Goto Idle
		TNT1 A 0 A_SetUserVar(User_Angle,Random(-6,-1))
	Idle:
		TNT1 AAA 0 A_SpawnItemEx(Q2GrenadeSmoke,Random2(5),0,Random2(5),0,0,FRandom(0.1,0.5),Random(1,360),0,128)
		TNT1 A 0 A_SetAngle(Angle+User_Angle)
		SGRN A 1 A_SetPitch(Pitch-15)
		Loop
	Death:
		TNT1 A 0 A_PlaySound("Q2Weapons/GrenadeBounce")
		SGRN AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx(Q2GrenadeSmoke,Random2(5),0,Random2(5),0,0,FRandom(0.1,1),Random(1,360),0,128)
		TNT1 A 0 A_Jump(256,"XDeath")
	XDeath:
		TNT1 A 0 A_PlaySound("Q2Weapons/GrenadeExplosion")
		TNT1 A 0 A_SpawnItem(Q2Explosion)
		TNT1 A 5 A_Explode(200, 165)
		Stop
	}
}
Actor Q2Grenade_Coop : Q2Grenade { Species "Player" +THRUSPECIES +DONTHARMCLASS +DONTHARMSPECIES }
Actor Q2GrenadeSmoke : Q2BulletPuffParticle
{
	Scale 1
	States
	{
	Fade:
		"----" A 1 A_FadeOut(0.025)
		Wait
	}
}
