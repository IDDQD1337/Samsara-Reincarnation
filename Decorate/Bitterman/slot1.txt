Actor Q2Machinegun : Q2Weapon
{
	Tag "Machinegun"
	Inventory.PickupMessage "Machinegun"
	Weapon.AmmoType Clip
	Weapon.AmmoUse 1
	Weapon.SlotNumber 1
	Weapon.AmmoGive 50
	Inventory.RestrictedTo "Bitterman"		
	States
	{
	Spawn:
		Q2WP A -1
		Stop
	Select:
		TNT1 A 0 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_TakeInventory(Q2WeaponIndex)
		TNT1 A 0 A_GiveInventory(Q2WeaponIndex,1)
		MGN0 ABCD 5
		Goto Ready+1
	Deselect:
		MGN0 H 3
		MGN1 STUV 3
		Goto InstantDeselect
	InstantDeselect:
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
		TNT1 A 1 A_Lower
		Wait
	Ready:
		TNT1 A 0 A_Jump(6,2)
		MGN0 H 1 A_WeaponReady
		Loop
		MGN0 "IJKLMNOPQRSTUVWXYZ[^]" 5 A_WeaponReady
		MGN1 ABCDEFGHIJKLMNOPQR 5 A_WeaponReady
		Goto Ready+1
	Fire:
		MGN0 F 4 A_GunFlash
		MGN0 E 3 A_GunFlash
		MGN0 F 0 A_JumpIfInventory("Clip",1,3)
		TNT1 A 0 A_PlayWeaponSound("Q2Weapons/NoAmmo")
		TNT1 A 0 A_CheckReload
		MGN0 F 0 A_ReFire
		TNT1 A 0 A_GunFlash("ClimbDown",GFF_NOEXTCHANGE)
		Goto Ready
	Flash:
		TNT1 A 0 A_JumpIfInventory("CoopModeOn", 1, "CoopFlash")
		TNT1 A 0 A_JumpIfInventory(PowerSilencer,1,"SilentFlash")
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("Q2Weapons/MachineGunFire",CHAN_WEAPON)
		TNT1 A 0 A_FireBullets(5,2,1,5.5,"Q2BulletPuff")
//		TNT1 A 0 A_FireCustomMissile(WeaponFlash,0,0)
		TNT1 A 0 A_JumpIfInventory(MachinegunClimbToken,0,"Null")
		TNT1 A 0 A_GiveInventory(MachinegunClimbToken,1)
		TNT1 AAA 1 A_SetPitch(Pitch-0.33)
		Stop
	SilentFlash:
		TNT1 A 0 A_PlaySound("Q2Weapons/MachineGunFire",CHAN_WEAPON,0.25,0,3)
		TNT1 A 0 A_FireBullets(5,2,1,5.5,"Q2BulletPuff")
//		TNT1 A 0 A_FireCustomMissile(WeaponFlash,0,0)
		TNT1 A 0 A_JumpIfInventory(MachinegunClimbToken,0,"Null")
		TNT1 A 0 A_GiveInventory(MachinegunClimbToken,1)
		TNT1 AAA 1 A_SetPitch(Pitch-0.33)
		Stop
	CoopFlash:
		TNT1 A 0 A_JumpIfInventory(PowerSilencer,1,"SilentCoopFlash")
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("Q2Weapons/MachineGunFire",CHAN_WEAPON)
		TNT1 A 0 A_FireCustomMissile("Q2MachineGunBullet", frandom(-5.00, 5.00), 1, 0, 0, 0, frandom(-2.00, 2.00))
//		TNT1 A 0 A_FireCustomMissile(WeaponFlash,0,0)
		TNT1 A 0 A_JumpIfInventory(MachinegunClimbToken,0,"Null")
		TNT1 A 0 A_GiveInventory(MachinegunClimbToken,1)
		TNT1 AAA 1 A_SetPitch(Pitch-0.33)
		Stop
	SilentCoopFlash:
		TNT1 A 0 A_PlaySound("Q2Weapons/MachineGunFire",CHAN_WEAPON,0.25,0,3)
		TNT1 A 0 A_FireCustomMissile("Q2MachineGunBullet", frandom(-5.00, 5.00), 1, 0, 0, 0, frandom(-2.00, 2.00))
//		TNT1 A 0 A_FireCustomMissile(WeaponFlash,0,0)
		TNT1 A 0 A_JumpIfInventory(MachinegunClimbToken,0,"Null")
		TNT1 A 0 A_GiveInventory(MachinegunClimbToken,1)
		TNT1 AAA 1 A_SetPitch(Pitch-0.33)
		Stop
	ClimbDown:
		TNT1 A 0 A_JumpIfInventory(MachinegunClimbToken,1,1)
		Stop
		TNT1 A 0 A_TakeInventory(MachinegunClimbToken,2)
		TNT1 A 1 A_SetPitch(Pitch+1.98)
		Loop
	}
}

Actor Q2MachineGunBullet : Q2BulletCoop { Damage (random(10,12)) }
Actor MachinegunClimbToken : InvBoolean { Inventory.MaxAmount 10 }