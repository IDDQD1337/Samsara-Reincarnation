actor Acolyte2 : SamsaraMonsterReincAcolyte replaces Acolyte
{
	DamageFactor "CorvusCrossbow", 1.1
	DamageFactor "DragonClaw", 1.5
	DamageFactor "WitchMelee", 1.1
	PainChance "MightyBoot", 255
	PainChance "CalebFire", 0
	PainChance "WTMelee", 255
	PainChance "WTMeleeIce", 255
	+NOTDMATCH
	+NOICEDEATH
	+THRUGHOST
	Tag "$TAG_ACOLYTE"
	States
	{
	Death:
		TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
		TNT1 A 0 A_SpawnItemEx("UniversalBloodyHellGoreDeathEffect",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)
		Goto Super::Death

	XDeath:
		TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
		TNT1 A 0 A_SpawnItemEx("UniversalBloodyHellGoreXDeathEffect",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)
		Goto Super::XDeath

	Missile:
		TNT1 A 0 A_ChangeFlag("NOPAIN",0)
		AGRD E 8 A_FaceTarget
		AGRD FE 4 A_ShootGun
		AGRD F 6 A_ShootGun
		Goto See

	Burn:
		TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
		SBRN A 3 bright A_PlaySound("strife/burndeath",CHAN_VOICE)
		SBRN B 3 bright A_SpawnItemEx("FireDroplet", 0, 0, 24, 0, 0, -1)
		SBRN C 3 bright A_Wander
		SBRN D 3 bright A_NoBlocking
		SBRN E 5 bright A_SpawnItemEx("FireDroplet", 0, 0, 24, 0, 0, -1)
		SBRN FGH 5 bright A_Wander
		SBRN I 5 bright A_SpawnItemEx("FireDroplet", 0, 0, 24, 0, 0, -1)
		SBRN JKL 5 bright A_Wander
		SBRN M 5 bright A_SpawnItemEx("FireDroplet", 0, 0, 24, 0, 0, -1)
		SBRN NOPQPQ 5 bright
		SBRN RSTU 7 bright
		SBRN V -1
		Stop

	Disintegrate:
		TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
		DISR A 5 A_PlaySound("misc/disruptordeath", CHAN_VOICE)
		DISR BC 5
		DISR D 5 A_NoBlocking
		DISR EF 5
		DISR GHIJ 4
		MEAT D 700
		Stop

	Pain.MightyBoot:
		TNT1 A 0 A_ChangeFlag("NOPAIN",1)
		TNT1 A 0 A_PlaySound("duke/kickhit",CHAN_BODY)
		AGRD G 2 A_FaceTarget
		TNT1 A 0 A_Recoil(14)
		AGRD G 16 A_Pain
		Goto See

	Pain.Shock:
	Pain.Tesla:
		CBGS A 4 A_Pain
		CBGS A 4
		TNT1 A -1 A_Jump(256,"See")
		Wait

	Death.CalebFire:
	Death.LifeLeech:
		TNT1 A 0 A_Jump(218, 2)
		TNT1 A 0 A_GiveToTarget("CalebDMTaunt1", 1)
		TNT1 A 0 A_SpawnItemEx("BloodBurningCorpse_Generic", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION|SXF_SETMASTER)
		TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
		TNT1 A 5 A_NoBlocking
		Stop
	}
}

actor AcolyteTan2: Acolyte2 replaces AcolyteTan
{
	ConversationID 53
	+MISSILEMORE
	+MISSILEEVENMORE
	DropItem "StrifeAcolyteDrop"
}

actor AcolyteRed2: Acolyte2 replaces AcolyteRed
{
	ConversationID 54
	+MISSILEMORE
	+MISSILEEVENMORE
	Translation "128:143=64:79"
	DropItem "StrifeAcolyteDrop"
}

actor AcolyteGold2: Acolyte2 replaces AcolyteGold
{
	ConversationID 58
	+MISSILEMORE
	+MISSILEEVENMORE
	Translation "128:143=80:95"
}

actor AcolyteGray2: Acolyte2 replaces AcolyteGray
{
	ConversationID 56
	+MISSILEMORE
	+MISSILEEVENMORE
	Translation "128:143=16:31"
}

actor AcolyteDGreen2: Acolyte2 replaces AcolyteDGreen
{
	ConversationID 57
	+MISSILEMORE
	+MISSILEEVENMORE
	Translation "128:143=53:63"
}

actor AcolyteLGreen2: Acolyte2 replaces AcolyteLGreen
{
	Health 60
	ConversationID 59
	Translation "128:143=96:111"
}

actor AcolyteRust2: Acolyte2 replaces AcolyteRust
{
	ConversationID 55
	+MISSILEMORE
	+MISSILEEVENMORE
	Translation "128:143=176:191"
}

actor AcolyteShadow2: Acolyte2 replaces AcolyteShadow
{
	ConversationID 61
	+MISSILEMORE
	//+GHOST
	States
	{
	See:
		AGRD A 6 A_BeShadowyFoe
		Goto Acolyte::See+1

	Pain:
		TNT1 A 0 A_SetShadow
		AGRD O 8 Fast Slow A_Pain
		Goto See
	}
}

actor AcolyteToBe2: Acolyte2 replaces AcolyteToBe
{
	ConversationID 29
	Health 61
	Radius 20
	Height 56
	DeathSound "becoming/death"
	-COUNTKILL
	-ISMONSTER
	States
	{
	Spawn:
		ARMR A -1
		Stop

	Pain:
		ARMR A -1 //A_HideDecepticon // Only inherits from AcolyteToBe. Interesting.
		Stop

	Death:
		Goto XDeath
	}
}

actor AcolyteBlue2 : SamsaraMonsterReincAcolyteBlue replaces AcolyteBlue// - Gives QuestItem7 when all the Acolytes are killed. Interesting.
{
	DamageFactor "CorvusCrossbow", 1.1
	DamageFactor "DragonClaw", 1.5
	DamageFactor "WitchMelee", 1.1
	PainChance "MightyBoot", 255
	PainChance "CalebFire", 0
	PainChance "WTMelee", 255
	PainChance "WTMeleeIce", 255
	+NOTDMATCH
	+NOICEDEATH
	Translation "128:143=144:159"
	+THRUGHOST
	Tag "Acolyte"
	States
	{
	Death:
		TNT1 A 0 A_GiveToTarget("AcolyteBlueDeathCount",1)
		TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
		TNT1 A 0 A_SpawnItemEx("UniversalBloodyHellGoreDeathEffect",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)
		TNT1 A 0 A_JumpIfInTargetInventory("AcolyteBlueDeathCount",5,"GiveQuestItem")
	DeathFinish:
		AGRD G 4
		AGRD H 4 A_Scream
		AGRD I 4
		AGRD J 3
		AGRD K 3 A_NoBlocking
		AGRD L 3
		AGRD M 3 A_AcolyteDie
		AGRD N -1
		Stop

	XDeath:
		TNT1 A 0 A_GiveToTarget("AcolyteBlueDeathCount",1)
		TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
		TNT1 A 0 A_SpawnItemEx("UniversalBloodyHellGoreXDeathEffect",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)
		TNT1 A 0 A_JumpIfInTargetInventory("AcolyteBlueDeathCount",5,"GiveQuestItemXDeath")
	XDeathFinish:
		GIBS A 5 A_NoBlocking
		GIBS BC 5 A_TossGib
		GIBS D 4 A_TossGib
		GIBS E 4 A_XScream
		GIBS F 4 A_TossGib
		GIBS GH 4
		GIBS I 5
		GIBS J 5 A_AcolyteDie
		GIBS K 5
		GIBS L 1400
		Stop

	Burn:
		TNT1 A 0 A_GiveToTarget("AcolyteBlueDeathCount",1)
		TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
		TNT1 A 0 A_JumpIfInTargetInventory("AcolyteBlueDeathCount",5,"GiveQuestItemBurn")
	BurnFinish:
		SBRN A 3 bright A_PlaySound("strife/burndeath",CHAN_VOICE)
		SBRN B 3 bright A_SpawnItemEx("FireDroplet", 0, 0, 24, 0, 0, -1)
		SBRN C 3 bright A_Wander
		SBRN D 3 bright A_NoBlocking
		SBRN E 5 bright A_SpawnItemEx("FireDroplet", 0, 0, 24, 0, 0, -1)
		SBRN FGH 5 bright A_Wander
		SBRN I 5 bright A_SpawnItemEx("FireDroplet", 0, 0, 24, 0, 0, -1)
		SBRN JKL 5 bright A_Wander
		SBRN M 5 bright A_SpawnItemEx("FireDroplet", 0, 0, 24, 0, 0, -1)
		SBRN NOPQPQ 5 bright
		SBRN RSTU 7 bright
		SBRN V 0 A_AcolyteDie
		SBRN V -1
		Stop

	Disintegrate:
		TNT1 A 0 A_GiveToTarget("AcolyteBlueDeathCount",1)
		TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
		TNT1 A 0 A_JumpIfInTargetInventory("AcolyteBlueDeathCount",5,"GiveQuestItemDisintegrate")
	DisintegrateFinish:
		DISR A 5 A_PlaySound("misc/disruptordeath", CHAN_VOICE)
		DISR BC 5
		DISR D 5 A_NoBlocking
		DISR EF 5
		DISR GHIJ 4
		MEAT D 0 A_AcolyteDie
		MEAT D 700
		Stop

	Pain.MightyBoot:
		TNT1 A 0 A_ChangeFlag("NOPAIN",1)
		TNT1 A 0 A_PlaySound("duke/kickhit",CHAN_BODY)
		AGRD G 2 A_FaceTarget
		TNT1 A 0 A_Recoil(14)
		AGRD G 16 A_Pain
		Goto See
	Pain.Shock:
	Pain.Tesla:
		CBGS A 4 A_Pain
		CBGS A 4
		TNT1 A -1 A_Jump(256,"See")
		Wait
	Death.CalebFire:
	Death.LifeLeech:
		TNT1 A 0 A_Jump(218, 2)
		TNT1 A 0 A_GiveToTarget("CalebDMTaunt1", 1)
		TNT1 A 0 A_SpawnItemEx("BloodBurningCorpse_Generic", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION|SXF_SETMASTER)
		TNT1 A 0 A_GiveToTarget("AcolyteBlueDeathCount",1)
		TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
		TNT1 A 0 A_JumpIfInTargetInventory("AcolyteBlueDeathCount",5,"GiveQuestItemLifeLeechBurn")
	LifeLeechFinish:
		TNT1 A 0 A_AcolyteDie
		TNT1 A 5 A_NoBlocking
		Stop

	Missile:
		TNT1 A 0 A_ChangeFlag("NOPAIN",0)
		AGRD E 8 A_FaceTarget
		AGRD FE 4 A_ShootGun
		AGRD F 6 A_ShootGun
		Goto See
	}
}

actor AcolyteBlueDeathCount : Counter { Inventory.MaxAmount 5 }