actor SigilSplinter1: Boolean { ConversationID 196 }
actor SigilSplinter2: Boolean { ConversationID 197 }
actor SigilSplinter3: Boolean { ConversationID 198 }
actor SigilSplinter4: Boolean { ConversationID 199 }
actor SigilSplinter5: Boolean { ConversationID 200 }
actor SigilInventoryCharge : Boolean { Inventory.InterHubAmount 0 }

actor GlobalSigilGiver
{
	+NOINTERACTION
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 ACS_NamedExecuteAlways("SamsaraSpectres", 0, 6, args[0], 1)
		Stop
	}
}

actor GlobalSigil1Giver: GlobalSigilGiver { Args 1 }
actor GlobalSigil2Giver: GlobalSigilGiver { Args 2 }
actor GlobalSigil3Giver: GlobalSigilGiver { Args 3 }
actor GlobalSigil4Giver: GlobalSigilGiver { Args 4 }
actor GlobalSigil5Giver: GlobalSigilGiver { Args 5 }

actor MaxSigil: CustomInventory
{
	+NOINTERACTION
	+AUTOACTIVATE
	Inventory.MaxAmount 0
	States
	{
	Spawn:
	Pickup:
		TNT1 A 0
		TNT1 A 0 ACS_NamedExecuteAlways("SamsaraSpectres", 0, 6, 1, 1)
		TNT1 A 0 ACS_NamedExecuteAlways("SamsaraSpectres", 0, 6, 2, 1)
		TNT1 A 0 ACS_NamedExecuteAlways("SamsaraSpectres", 0, 6, 3, 1)
		TNT1 A 0 ACS_NamedExecuteAlways("SamsaraSpectres", 0, 6, 4, 1)
		TNT1 A 0 ACS_NamedExecuteAlways("SamsaraSpectres", 0, 6, 5, 1)
		Stop
	}
}

actor SigilInventoryWeapon: CustomInventory
{
    Inventory.MaxAmount 1
    Tag "Sigil"
    +INVBAR
    +UNDROPPABLE
    States
    {
      Use:
        TNT1 A 0 A_JumpIfInventory("StrifeClass",1,"UseFail")
        TNT1 A 0 A_JumpIfInventory("SigilInventoryCharge",1,"UseFail")
      UseSuccess:
        TNT1 A 0 A_GiveInventory("SigilInventoryCharge")
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_PlaySound("strife/sigilcharge",CHAN_7)
        TNT1 A 0 A_Light2
        TNT1 A 0 ACS_NamedExecuteWithResult("SamsaraSigil", 1) // gunflash stuff
        TNT1 A 0 ACS_NamedExecuteWithResult("SamsaraSigil", 2) // attack stuff
        TNT1 A 0 ACS_NamedExecuteWithResult("SamsaraSigil", 3) // cooldown stuff
        fail

      UseFail:
        TNT1 A 0
        fail
    }
}

actor SigilInventoryWeapon1: SigilInventoryWeapon { Inventory.Icon "I_SGL1" }
actor SigilInventoryWeapon2: SigilInventoryWeapon { Inventory.Icon "I_SGL2" }
actor SigilInventoryWeapon3: SigilInventoryWeapon { Inventory.Icon "I_SGL3" }
actor SigilInventoryWeapon4: SigilInventoryWeapon { Inventory.Icon "I_SGL4" }
actor SigilInventoryWeapon5: SigilInventoryWeapon { Inventory.Icon "I_SGL5" }

actor SigilInventorySequenceGunFlash1 : Trigger
{
    States
    {
      Pickup:
        TNT1 A 0 A_Light2
        stop
    }
}

actor SigilInventorySequenceGunFlash2 : Trigger
{
    States
    {
      Pickup:
        TNT1 A 0 A_LightInverse
        stop
    }
}

actor SigilInventorySequenceGunFlash3 : Trigger
{
    States
    {
      Pickup:
        TNT1 A 0 A_Light1
        stop
    }
}

actor SigilInventorySequenceGunFlash4 : Trigger
{
    States
    {
      Pickup:
        TNT1 A 0 A_Light0
        stop
    }
}

actor SigilInventorySequenceAttack : Trigger
{
    States
    {
      Pickup:
        TNT1 A 0 A_PlaySound("strife/sigilcharge",CHAN_7)
        TNT1 A 0 A_JumpIfInventory("StrifeSigilPiece",0,"PickupMode5")
        TNT1 A 0 A_JumpIfInventory("StrifeSigilPiece",4,"PickupMode4")
        TNT1 A 0 A_JumpIfInventory("StrifeSigilPiece",3,"PickupMode3")
        TNT1 A 0 A_JumpIfInventory("StrifeSigilPiece",2,"PickupMode2")
      // Lightning Strike
      PickupMode1:
        TNT1 A 0 Thing_Damage(0,2,0)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupMode1Coop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupMode1Coop")
      PickupMode1DM:
        TNT1 A 0 A_FireCustomMissile("StrifeSigilLightningSpot",0,false)
        stop

      PickupMode1Coop:
        TNT1 A 0 A_FireCustomMissile("StrifeSigilLightningSpot_C",0,false)
        stop

      // Lightning Bolt
      PickupMode2:
        TNT1 A 0 Thing_Damage(0,4,0)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupMode2Coop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupMode2Coop")
      PickupMode2DM:
        TNT1 A 0 A_FireCustomMissile("StrifeSpectralLightningH1",0,false)
        stop

      PickupMode2Coop:
        TNT1 A 0 A_FireCustomMissile("StrifeSpectralLightningH1_C",0,false)
        stop

      // Lightning Wall
      PickupMode3:
        TNT1 A 0 Thing_Damage(0,6,0)
        TNT1 A 0 A_TakeInventory("SamsaraStrifeSigilMode3AngleIncrement") // reset
        TNT1 A 0 A_SetAngle(angle-90)
      PickupMode3Loop:
        TNT1 A 0 A_GiveInventory("SamsaraStrifeSigilMode3AngleIncrement")
        TNT1 A 0 A_SetAngle(angle+9)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupMode3LoopCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupMode3LoopCoop")
      PickupMode3LoopDM:
        TNT1 A 0 A_FireCustomMissile("StrifeSpectralLightningBall1",0,false)
        goto PickupFinishMode3

      PickupMode3LoopCoop:
        TNT1 A 0 A_FireCustomMissile("StrifeSpectralLightningBall1_C",0,false)
        goto PickupFinishMode3

      // Homing Lightning
      PickupMode4:
        TNT1 A 0 Thing_Damage(0,8,0)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupMode4Coop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupMode4Coop")
      PickupMode4DM:
        TNT1 A 0 A_FireCustomMissile("StrifeSigilLightningBigV1",-15,false)
        TNT1 A 0 A_FireCustomMissile("StrifeSigilLightningBigV1",-7.5,false)
        TNT1 A 0 A_FireCustomMissile("StrifeSigilLightningBigV1",0,false)
        TNT1 A 0 A_FireCustomMissile("StrifeSigilLightningBigV1",7.5,false)
        TNT1 A 0 A_FireCustomMissile("StrifeSigilLightningBigV1",15,false)
        stop

      PickupMode4Coop:
        TNT1 A 0 A_FireCustomMissile("StrifeSigilLightningBigV1_C",-15,false)
        TNT1 A 0 A_FireCustomMissile("StrifeSigilLightningBigV1_C",-7.5,false)
        TNT1 A 0 A_FireCustomMissile("StrifeSigilLightningBigV1_C",0,false)
        TNT1 A 0 A_FireCustomMissile("StrifeSigilLightningBigV1_C",7.5,false)
        TNT1 A 0 A_FireCustomMissile("StrifeSigilLightningBigV1_C",15,false)
        stop

      // Lightning Storm
      PickupMode5:
        TNT1 A 0 Thing_Damage(0,10,0)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupMode5Coop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupMode5Coop")
      PickupMode5DM:
        TNT1 A 0 A_FireCustomMissile("StrifeSpectralLightningBigBall",0,false)
        stop

      PickupMode5Coop:
        TNT1 A 0 A_FireCustomMissile("StrifeSpectralLightningBigBall_C",0,false)
        stop

      // Finishing stuff
      // Lightning Wall
      PickupFinishMode3:
        TNT1 A 0 A_JumpIfInventory("SamsaraStrifeSigilMode3AngleIncrement",0,1)
        goto PickupMode3Loop
        TNT1 A 0 A_SetAngle(angle-90)
        stop
    }
}

actor SigilInventorySequenceEnd : Trigger
{
    States
    {
      Pickup:
        TNT1 A 0 A_TakeInventory("SigilInventoryCharge")
        stop
    }
}

Actor StrifeSigil1Pickup : CustomInventory replaces Sigil1
{
	Game Strife
	ConversationID 196, 190, 194
	Inventory.Icon "I_SGL1"
	+AUTOACTIVATE
	+INVENTORY.QUIET
	Inventory.MaxAmount 0
	States
	{
	Spawn:
		SIGL A -1
		Stop
	Pickup:
		TNT1 A 0 A_JumpIfInventory("PickupFlashesAreDisabled",1,2)
		TNT1 A 0 A_SetBlend("d7ba45", 0.3, 4)
		TNT1 A 0 ACS_NamedExecuteAlways("SamsaraSpectres", 0, 6, 1, 1)
		TNT1 A -1 A_Jump(256,"PickupSoundNormal")
		Wait
	PickupSoundNormal:
		TNT1 A 0 ACS_NamedExecuteAlways("SamsaraPickupMessages", 0, 92, 0, 0)
		TNT1 A 1 A_PlaySound("weapons/sigilcharge", CHAN_ITEM)
		Stop
	}
}

Actor StrifeSigil2Pickup : StrifeSigil1Pickup replaces Sigil2
{
	ConversationID 197, 191, 195
	Inventory.Icon "I_SGL2"
	States
	{
	Spawn:
		SIGL B -1
		Stop
	Pickup:
		TNT1 A 0 A_JumpIfInventory("PickupFlashesAreDisabled",1,2)
		TNT1 A 0 A_SetBlend("d7ba45", 0.3, 4)
		TNT1 A 0 ACS_NamedExecuteAlways("SamsaraSpectres", 0, 6, 2, 1)
		TNT1 A -1 A_Jump(256,"PickupSoundNormal")
		Wait
	}
}

Actor StrifeSigil3Pickup : StrifeSigil2Pickup replaces Sigil3
{
	ConversationID 198, 192, 196
	Inventory.Icon "I_SGL3"
	States
	{
	Spawn:
		SIGL C -1
		Stop
	Pickup:
		TNT1 A 0 A_JumpIfInventory("PickupFlashesAreDisabled",1,2)
		TNT1 A 0 A_SetBlend("d7ba45", 0.3, 4)
		TNT1 A 0 ACS_NamedExecuteAlways("SamsaraSpectres", 0, 6, 3, 1)
		TNT1 A -1 A_Jump(256,"PickupSoundNormal")
		Wait
	}
}

Actor StrifeSigil4Pickup : StrifeSigil3Pickup replaces Sigil4
{
	ConversationID 199, 193, 197
	Inventory.Icon "I_SGL4"
	States
	{
	Spawn:
		SIGL D -1
		Stop
	Pickup:
		TNT1 A 0 A_JumpIfInventory("PickupFlashesAreDisabled",1,2)
		TNT1 A 0 A_SetBlend("d7ba45", 0.3, 4)
		TNT1 A 0 ACS_NamedExecuteAlways("SamsaraSpectres", 0, 6, 4, 1)
		TNT1 A -1 A_Jump(256,"PickupSoundNormal")
		Wait
	}
}

Actor StrifeSigil5Pickup : StrifeSigil4Pickup replaces Sigil5
{
	ConversationID 200, 194, 198
	Inventory.Icon "I_SGL5"
	States
	{
	Spawn:
		SIGL E -1
		Stop
	Pickup:
		TNT1 A 0 A_JumpIfInventory("PickupFlashesAreDisabled",1,2)
		TNT1 A 0 A_SetBlend("d7ba45", 0.3, 4)
		TNT1 A 0 ACS_NamedExecuteAlways("SamsaraSpectres", 0, 6, 5, 1)
		TNT1 A -1 A_Jump(256,"PickupSoundNormal")
		Wait
	}
}
