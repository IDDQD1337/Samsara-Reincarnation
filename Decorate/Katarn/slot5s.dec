// I.M. Mines: The primary mode has a 3 second delay until detonation, so drop it and get out of the way fast! The secondary mode is a proximity trigger with a motion sensor. It has a 1 second delay after placement before it is armed, but then immediately detonates when there is a moving object in its activation radius. Needless to say, you don't want to hang around after these are placed.

Actor "I.M. Mines" : Weapon
{
  Weapon.SelectionOrder 2000
  Weapon.AmmoUse        1
  Weapon.BobStyle		Inverse
  Weapon.BobSpeed		1.05
  Weapon.BobRangeX	  	1.0
  Weapon.BobRangeY	  	0.55  
  Weapon.AmmoUse2       1
  Weapon.AmmoGive       5
  Weapon.AmmoType       "RocketAmmo"
  Weapon.AmmoType2      "RocketAmmo"
  Weapon.SlotNumber     5
  Weapon.SlotPriority 	1
  Obituary              "%k tripped %o's I.M. mine."
  +NOALERT
  +THRUGHOST
  +WEAPON.NOAUTOAIM
  +WEAPON.NOAUTOFIRE
  +WEAPON.EXPLOSIVE
  +INVENTORY.UNDROPPABLE
  Inventory.RestrictedTo "KatarnPlayer"
  Tag "I.M. Mines"
  States
  {
    Ready:
      TNT1 A 0 A_JumpIfNoAmmo("ReadyAmmoLess")
	  KHAN B 2 A_WeaponReady
      Loop
	  
    ReadyAmmoLess:
	  TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,"Ready")
	  KDET B 1 A_WeaponReady
	  Loop

    Deselect:
      TNT1 A 0 A_JumpIfNoAmmo("DeselectAmmoLess")
	  TNT1 A 0 A_Lower
      KHAN B 1 A_Lower
      Goto Deselect+1
	  
	DeselectAmmoLess:
      KDET A 0 A_Lower
      KDET B 1 A_Lower
	  Loop

    Select:
      TNT1 A 0 A_JumpIfNoAmmo("SelectAmmoLess")
	  TNT1 A 0 A_Raise
      KHAN B 1 A_Raise
      Goto Select+1
	  
	SelectAmmoLess:
      KDET A 0 A_Raise
      KDET B 1 A_Raise
	  Loop
	  
	Fire:
      TNT1 A 0 A_JumpIfInventory("CoopModeOn", 1, "CoopFire")
      KHAN C 0 A_Lower
      KHAN C 1 Offset(0,50)
	  KHAN C 1 Offset(0,75)
      TNT1 AAAAAAAA 1 A_WeaponReady(14)
	  TNT1 A 2 A_WeaponReady(14)
      TNT1 A 5 A_SpawnItemEx("DarkForcesIMMine",0,0,0,0,0,0,0,SXF_SETMASTER|SXF_NOCHECKPOSITION)
	  TNT1 A 0 A_JumpIfNoAmmo("ReadyAmmoLess")
	  KHAN B 1 Offset(0,75)
	  KHAN B 1 Offset(0,50)
	  KHAN BBBBB 1 A_WeaponReady(14)
      TNT1 A 0 A_ReFire
      TNT1 AAAAA 0 A_Lower
	  Goto Ready
	  
	CoopFire:
      KHAN C 0 A_Lower
      KHAN C 1 Offset(0,50)
	  KHAN C 1 Offset(0,75)
      TNT1 AAAAAAAA 1 A_WeaponReady(14)
	  KHAN C 2 A_WeaponReady(14)
      TNT1 A 5 A_SpawnItemEx("DarkForcesIMMineCoop",0,0,0,0,0,0,0,SXF_SETMASTER|SXF_NOCHECKPOSITION)
	  TNT1 A 0 A_JumpIfNoAmmo("ReadyAmmoLess")
	  KHAN B 1 Offset(0,75)
	  KHAN B 1 Offset(0,50)
	  KHAN BBBBB 1 A_WeaponReady(14)
      TNT1 A 0 A_ReFire
      TNT1 AAAAA 0 A_Lower
	  Goto Ready
	  
	AltFire:
      TNT1 A 0 A_JumpIfInventory("CoopModeOn", 1, "CoopAltFire")
      KHAN C 0 A_Lower
      KHAN C 1 Offset(0,50)
	  KHAN C 1 Offset(0,75)
      TNT1 AAAAAAAA 1 A_WeaponReady(14)
	  TNT1 A 2 A_WeaponReady(14)
      TNT1 A 5 A_SpawnItemEx("DarkForcesIMMineProxy",0,0,0,0,0,0,0,SXF_SETMASTER|SXF_NOCHECKPOSITION)
	  TNT1 A 0 A_JumpIfNoAmmo("ReadyAmmoLess")
	  KHAN B 1 Offset(0,75)
	  KHAN B 1 Offset(0,50)
	  KHAN BBBBB 1 A_WeaponReady(14)
      TNT1 A 0 A_ReFire
      TNT1 AAAAA 0 A_Lower
	  Goto Ready
	  
	CoopAltFire:
      KHAN C 0 A_Lower
      KHAN C 1 Offset(0,50)
	  KHAN C 1 Offset(0,75)
      TNT1 AAAAAAAA 1 A_WeaponReady(14)
	  KHAN C 2 A_WeaponReady(14)
      TNT1 A 5 A_SpawnItemEx("DarkForcesIMMineProxyCoop",0,0,0,0,0,0,0,SXF_SETMASTER|SXF_NOCHECKPOSITION)
	  TNT1 A 0 A_JumpIfNoAmmo("ReadyAmmoLess")
	  KHAN B 1 Offset(0,75)
	  KHAN B 1 Offset(0,50)
	  KHAN BBBBB 1 A_WeaponReady(14)
      TNT1 A 0 A_ReFire
      TNT1 AAAAA 0 A_Lower
	  Goto Ready

    Spawn:
      IMIN A -1
      Stop
  }
}

Actor DarkForcesIMMine
{
	Radius      8
	Height      5
	Damage      (0)
	Mass        100000
	Scale		0.75
	PROJECTILE
	-NOGRAVITY
	-NOBLOCKMAP
	-MISSILE
    +MOVEWITHSECTOR
    +NEVERTARGET
	+FORCERADIUSDMG
	Obituary              "%k tripped %o's I.M. mine."
	States
	{
	Spawn:
      PMIN B 0 NoDelay A_PlaySound("katarn/minplce", CHAN_BODY)
	  PMIN B 105
      TNT1 A 0 A_PlaySound("katarn/exmiss",6,1.0,0,ATTN_NORM)
      TNT1 A 0 A_AlertMonsters
	  TNT1 A 0 BRIGHT A_Explode(200,200)
      TNT1 A 5 A_SpawnItemEx("explosion_immine",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
      Stop
  }
}

Actor DarkForcesIMMineCoop : DarkForcesIMMine { +THRUSPECIES +DONTHARMCLASS +DONTHARMSPECIES +MTHRUSPECIES Species "Player" }

Actor DarkForcesIMMineProxy
{
	Radius      8
	Height      5
	Damage      (0)
	Mass        100000
	Scale		0.75
	PROJECTILE
	-NOGRAVITY
	-NOBLOCKMAP
	-MISSILE
    +MOVEWITHSECTOR
    +NEVERTARGET
	+FORCERADIUSDMG
	+LOOKALLAROUND
	+QUICKTORETALIATE
	Obituary              "%k tripped %o's I.M. mine."
	States
	{
	Spawn:
      PMIN B 0 NoDelay A_PlaySound("katarn/minplce", CHAN_BODY)
	  PMIN B 35
	SpawnLoop:
	  PMIN B 0 A_JumpIfCloser(96, "Death")
	  PMIN B 1 A_LookEx(LOF_NOSIGHTCHECK|LOF_NOSOUNDCHECK, 0, 96, 0, 0, "Death")
	  Loop
    Death:
	  PMIN B 35 A_PlaySound("katarn/minbeep",6,1.0,0,ATTN_NORM)
	  TNT1 A 0 A_PlaySound("katarn/exmiss",6,1.0,0,ATTN_NORM)
      TNT1 A 0 A_AlertMonsters
	  TNT1 A 0 BRIGHT A_Explode(200,200)
      TNT1 A 5 A_SpawnItemEx("explosion_immine",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
      Stop
  }
}

Actor DarkForcesIMMineProxyCoop : DarkForcesIMMineProxy { +THRUSPECIES +DONTHARMCLASS +DONTHARMSPECIES +MTHRUSPECIES +NOTARGETSWITCH Species "Player" }

ACTOR explosion_immine
{
  Radius  6
  Height  6
  Scale   1.0
  +NOGRAVITY
  +CLIENTSIDEONLY
  +NOINTERACTION
  States
  {
    Spawn:
      EXML A 0
      EXML AB 2
      EXML CDE 2 BRIGHT
      EXML FGHIJKLM 2
      Stop
  }
}