actor " 18mm Semi " : SamsaraDisruptorWeapon
{
    Weapon.SlotNumber 1
    Weapon.SelectionOrder 3300
    Weapon.KickBack 100
    Tag "18mm Semi"
    +WIMPY_WEAPON
    States
    {
      // 18mm Semi
      Ready:
        TNT1 A 0 A_JumpIfInventory("SamsaraDisruptorHas18mmAuto",1,"ReadyAuto")
        SQ96 A 1 A_WeaponReady
        loop

      Deselect:
        TNT1 A 0 A_Lower
        TNT1 A 0 A_JumpIfInventory("SamsaraDisruptorHas18mmAuto",1,"DeselectAuto")
        SQ96 A 1 A_Lower
        loop

      Select:
        TNT1 A 0 A_Raise
        TNT1 A 0 A_JumpIfInventory("SamsaraDisruptorHas18mmAuto",1,"SelectAuto")
        SQ96 A 1 A_Raise
        loop

      Fire:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto FireStart
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"FireStart")
        goto NoAmmoSwitch

      FireStart:
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_JumpIfInventory("SamsaraDisruptorHas18mmAuto",1,"FireStartAuto")
        TNT1 A 0 A_GunFlash
        TNT1 A 0 A_GiveInventory("SamsaraDisruptor18mmSemiAttackHandler")
        SQ96 C 4 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        SQ96 B 4 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto FireFinish
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireFinish")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"FireFinish")
        goto NoAmmoSwitch

      FireFinish:
        SQ96 A 6 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto Ready

      Flash:
        SQ96 D 4 Bright A_Light2
        goto LightDone

      // 18mm Auto
      ReadyAuto:
        SQ69 K 1 A_WeaponReady
        goto Ready

      DeselectAuto:
        SQ69 K 1 A_Lower
        goto Deselect

      SelectAuto:
        SQ69 K 1 A_Raise
        goto Select

      FireStartAuto:
        TNT1 A 0 A_GunFlash("FlashAuto")
        TNT1 A 0 A_GiveInventory("SamsaraDisruptor18mmAutoAttackHandler")
        SQ69 L 1 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        SQ69 IJ 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto FireFinishAuto
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireFinishAuto")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"FireFinishAuto")
        goto NoAmmoSwitch

      FireFinishAuto:
        SQ69 K 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto Ready

      FlashAuto:
        TNT1 A 0 A_Light2
        TNT1 A 0 A_Jump(256,1,2)
        SQ69 M 1 Bright
        goto LightDone
        SQ69 N 1 Bright
        goto LightDone

      // "No ammo" switch
      NoAmmoSwitch:
        TNT1 A 0 A_ClearReFire
        TNT1 A 0 A_SelectWeapon("Disruptor Fist")
        goto Deselect
    }
}

actor SamsaraDisruptor18mmSemiAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto PickupAttack
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("Disruptor/Pistol",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireBullets(2,1,-1,7,"DisruptorPuff",FBF_NOFLASH)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireBullets(2,1,-1,7,"DisruptorPuffCoop",FBF_NOFLASH)
        stop
    }
}

actor SamsaraDisruptor18mmAutoAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto PickupAttack
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("Disruptor/Pistol",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireBullets(2,1.5,-1,7,"DisruptorPuff",FBF_NOFLASH)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireBullets(2,1.5,-1,7,"DisruptorPuffCoop",FBF_NOFLASH)
        stop
    }
}

actor DisruptorPuff : BulletPuff
{
    VSpeed 0
    RenderStyle "Add"
    Alpha 1
    Scale 0.5
    Decal "BulletChip"
    +NOEXTREMEDEATH
    States
    {
      Spawn:
      Crash:
        SQ96 EFGHIJKLMNOPQRS 4 Bright
        stop

      Melee:
        TNT1 A 1
        stop
    }
}

actor DisruptorPuffCoop : DisruptorPuff { +MTHRUSPECIES }

actor DisruptorSparkPuff
{
    RenderStyle Add
    Scale 0.5
    +ALLOWPARTICLES
    +NOBLOCKMAP
    +NOEXTREMEDEATH
    +NOGRAVITY
    +RANDOMIZE
    States
    {
      Spawn:
        SQ95 GHIJKLMNOPQRSTU 1 Bright
        stop
    }
}
