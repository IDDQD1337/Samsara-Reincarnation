actor PlasmaLanceAngle : Counter { Inventory.MaxAmount 20 }
actor PlasmaLanceTurn : Boolean {}
actor PlasmaLanceTimer : Counter { Inventory.MaxAmount 29 }
actor PlasmaLanceFrame : Counter { Inventory.MaxAmount 3 }

Actor " Disruptor Plasmalance " : Weapon
{
	Weapon.SelectionOrder 600
	Weapon.SlotNumber 6
	Weapon.SlotPriority 8
	weapon.BobStyle normal
	weapon.BobRangeX 1.2
	weapon.BobRangeY 0.5
	weapon.BobSpeed 2.0
	Weapon.AmmoUse 1
	Weapon.AmmoGive 40
	Weapon.AmmoType "Cell"
	Inventory.PickupMessage "You picked up a plasma lance!"
	Obituary "%o was disintegrated by %k's plasma lance."
	Inventory.RestrictedTo "DisruptorPlayer"
	Tag "Plasma Lance"
	States
	{
	Spawn:
		SQ72 Y -1
		Stop

	Ready:
		SQ84 ABCDEF 2 A_WeaponReady
		Loop

	Deselect:
		TNT1 A 0 A_StopSound(CHAN_WEAPON)
		TNT1 A 0 A_StopSound(CHAN_5)
		TNT1 A 0 A_Lower
		SQ84 A 1 A_Lower
		Loop

	Select:
		TNT1 A 0 A_StopSound(CHAN_WEAPON)
		TNT1 A 0 A_StopSound(CHAN_5)
		TNT1 A 0 A_TakeInventory("DisruptorMode", 1)
		TNT1 A 0 A_Raise
		SQ84 A 1 A_Raise
		Loop

	Fire:
		TNT1 A 0 A_PlaySound("Disruptor/LanceFire",CHAN_WEAPON)
		TNT1 A 0 A_TakeInventory("PlasmaLanceFrame",99)
		TNT1 A 0 A_TakeInventory("PlasmaLanceAngle",99)
		TNT1 A 0 A_TakeInventory("PlasmaLanceTurn",99)
		TNT1 A 0 A_TakeInventory("PlasmaLanceTimer",99)
		goto Hold

	Hold:
		TNT1 A 0 A_JumpIfInventory("PlasmaLanceTimer", 28, "StartLoop")
		TNT1 A 0 A_GiveInventory("PlasmaLanceTimer",1)
		TNT1 A 0 A_JumpIfInventory("PlasmaLanceAngle", 20, "LanceDirectionLeft")
		TNT1 A 0 A_JumpIfInventory("PlasmaLanceAngle", 1, "HoldProjectile")
		goto LanceDirectionRight

	AngleAdd:
		TNT1 A 0 A_GiveInventory("PlasmaLanceAngle",1)
		goto HoldProjectile+1
	AngleSub:
		TNT1 A 0 A_TakeInventory("PlasmaLanceAngle",1)
		goto HoldProjectile+1

	HoldProjectile:
		TNT1 A 0 A_JumpIfInventory("PlasmaLanceTurn",1,"AngleAdd")
		goto AngleSub
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GiveInventory("SamsaraDisruptorPlasmaLanceAttackHandler")
		goto SelectFrame

	SelectFrame:
		TNT1 A 0 A_GiveInventory("PlasmaLanceFrame",1)
		TNT1 A 0 A_JumpIfInventory("PlasmaLanceFrame",3,"Frame3")
		TNT1 A 0 A_JumpIfInventory("PlasmaLanceFrame",2,"Frame2")
		TNT1 A 0 A_JumpIfInventory("PlasmaLanceFrame",1,"Frame1")
	Frame1:
		TNT1 A 0 A_TakeInventory("Cell",1,TIF_NOTAKEINFINITE)
		SQ84 G 1 bright
		goto Hold
	Frame2:
		SQ84 H 1 bright
		goto Hold
	Frame3:
		TNT1 A 0 A_TakeInventory("PlasmaLanceFrame",3)
		SQ84 I 1 bright
		goto HoldRefire

	HoldRefire:
		TNT1 A 0 A_Refire("Hold")
		Goto HoldStop

	HoldStop:
		TNT1 A 0 A_StopSound(CHAN_WEAPON)
		TNT1 A 0 A_StopSound(CHAN_5)
		SQ84 A 1
		Goto Ready

	StartLoop:
		TNT1 A 0 A_PlaySound("Disruptor/LanceSpin",CHAN_5,1.0,1)
		Goto Hold+1

	LanceDirectionLeft:
		TNT1 A 0 A_TakeInventory("PlasmaLanceTurn",1)
		Goto HoldProjectile
	LanceDirectionRight:
		TNT1 A 0 A_GiveInventory("PlasmaLanceTurn",1)
		Goto HoldProjectile

	NoAmmoSwitch:
		TNT1 A 0 A_ClearReFire
		TNT1 A 0 A_SelectWeapon(" 18mm Semi ")
		goto Deselect
	}
}

actor SamsaraDisruptorPlasmaLanceAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 // The ammo stuff is in the weapon itself
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        //TNT1 A 0 A_PlaySound("Disruptor/LanceFire",CHAN_WEAPON)
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
		TNT1 A 0 A_FireCustomMissile("DisruptorPlasmaLance_Projectile",0,0)
        stop

      PickupAttackCoop:
		TNT1 A 0 A_FireCustomMissile("DisruptorPlasmaLance_Projectile",0,0)
        stop
    }
}

Actor DisruptorPlasmaLance_Projectile
{
	Radius 4
	Height 8
	DamageType "Disruptoruniversaldamageshit2"
	//DeathSound "Disruptor/GreenL"
	Damage (13)
	Speed 12
	Renderstyle Add
	Alpha 0.5
	Projectile
	+FORCEXYBILLBOARD
	+SKYEXPLODE
	+BLOODSPLATTER
	var int user_angle;
	var int user_pitch;
	States
	{
	Spawn:
		SQ84 JJ 0 A_SetUserVar("user_angle", CallACS("DisruptorPlasmaLanceAngle"))
		SQ84 J 0 A_SetUserVar("user_pitch", CallACS("DisruptorPlasmaLancePitch")/182)
		TNT1 A 2
		SQ84 J 3 Bright
		SQ84 L 16 Bright A_ChangeVelocity(12*cos(user_pitch),(user_angle-10)/2,velz,CVF_REPLACE|CVF_RELATIVE)
		SQ84 L 16 Bright A_ChangeVelocity(12*cos(user_pitch),-(user_angle-10)/2,velz,CVF_REPLACE|CVF_RELATIVE)
		SQ84 L 0 A_ChangeVelocity(12*cos(user_pitch),0,velz,CVF_REPLACE|CVF_RELATIVE)
	Death:
		SQ84 AAA 0 A_SpawnItemEx("DisruptorPlasmaLance_ProjectileDeath",frandom(-4.0,4.0),frandom(-4.0,4.0),frandom(-4.0,4.0),0,0,0,0,SXF_NOCHECKPOSITION)
		stop
	}
}

Actor DisruptorPlasmaLance_ProjectileDeath
{
	Radius 4
	Height 8
	Renderstyle Add
	Alpha 0.5
	+CLIENTSIDEONLY
	+NOINTERACTION
	+FORCEXYBILLBOARD
	States
	{
		Spawn:
			SQ84 AA 0 A_SetScale(frandom(0.5,1.0))
			SQ84 A 0 A_Stop
			SQ84 A 0 A_ChangeVelocity(frandom(0.0,2.0),frandom(0.0,2.0),frandom(0.0,2.0),CVF_REPLACE)
			SQ84 MMNNOOPP 1 Bright A_Fadeout(0.05)
			stop
	}
}