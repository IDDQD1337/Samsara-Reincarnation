Actor " Phase Rifle " : Weapon
{
  Weapon.SelectionOrder 1550
  Weapon.SlotNumber 2  
  weapon.BobStyle normal
  weapon.BobRangeX 1.2
  weapon.BobRangeY 0.5
  weapon.BobSpeed 2.0  
  Weapon.AmmoUse1 2
  Weapon.AmmoGive1 40
  Weapon.AmmoType1 "Clip"
  Weapon.AmmoUse2 0
  Weapon.AmmoGive2 0  
  Weapon.AmmoType2 "DisruptorHiFreq"
  +WEAPON.AMMO_OPTIONAL
  +WEAPON.ALT_AMMO_OPTIONAL
  +WEAPON.NOALERT
  Inventory.PickupMessage "You got a Phase Rifle!"
  Inventory.RestrictedTo "DisruptorPlayer"
  Tag "Phase Rifle"
  States
  {
  Spawn:
    SQ72 R -1
    Stop

  // Normal
  Ready:
	TNT1 A 0 A_JumpIfInventory("DisruptorMode", 1, "ReadyRed")
    SQ69 AAABBBCCCDDDEEE 1 A_WeaponReady
    loop

  Deselect:
	TNT1 A 0 A_JumpIfInventory("DisruptorMode", 1, "DeselectRed")  
	TNT1 A 0 A_Lower
    SQ69 A 1 A_Lower
	loop

  Select:
	TNT1 A 0 A_JumpIfInventory("DisruptorMode", 1, "SelectRed")
	TNT1 A 0 A_Raise
    SQ69 A 1 A_Raise
    loop

  Fire:
	TNT1 A 0 A_JumpIfInventory("DisruptorMode", 1, "FireRed")
    TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart")
    TNT1 A 0 A_JumpIfInventory("Clip",2,"FireStart")
    goto NoAmmoSwitch
  FireStart:
	TNT1 A 0 A_AlertMonsters
  FireContinue:
	TNT1 A 0 A_GiveInventory("SamsaraDisruptorPhaseRifleAttackHandler")
  FireFinish:
    SQ69 F 1 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
	SQ69 FGG 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
	SQ69 HH 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
	SQ69 AABBCCDDEE 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
	goto Ready

  AltFire:
	TNT1 A 0 A_JumpIfInventory("DisruptorMode", 1, 4)
	SQ73 KK 5 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH|WRF_NOSECONDARY)
	SQ73 K 0 A_GiveInventory("DisruptorMode", 1)
	goto ReadyRed
  	SQ69 AA 5 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH|WRF_NOSECONDARY)
	SQ69 A 0 A_TakeInventory("DisruptorMode")
	goto Ready	

  // High Frequency
  ReadyRed:
	SQ73 KKKLLLMMMNNNOOO 1 A_WeaponReady
	loop

  DeselectRed:
	TNT1 A 0 A_Lower
	SQ73 K 1 A_Lower
    loop

  SelectRed:
	TNT1 A 0 A_Raise
	SQ73 K 1 A_Raise
    loop

  FireRed:
	TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
    TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireRedStart")
    TNT1 A 0 A_JumpIfInventory("DisruptorHiFreq",1,"FireRedStart")
    goto NoAmmoSwitchRed
  FireRedStart:
	TNT1 A 0 A_AlertMonsters
  FireRedContinue:
	TNT1 A 0 A_GiveInventory("SamsaraDisruptorPhaseRifleHiFreqAttackHandler")
  FireRedFinish:
	SQ73 P 1 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
	SQ73 QQ 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
	SQ73 RR 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
    SQ73 KLMNO 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
	goto Ready	

  NoAmmoSwitch:
    TNT1 A 0 A_ClearReFire
    TNT1 A 0 A_SelectWeapon(" 18mm Semi ")
    goto Deselect

  NoAmmoSwitchRed:
    TNT1 A 0 A_ClearReFire
    goto AltFire
  }
}

actor SamsaraDisruptorPhaseRifleAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",2,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",2)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("Disruptor/GreenLaser",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("DisruptorGP_1",0,1,0,0)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("DisruptorGP_2",0,1,0,0)
        stop
    }
}

actor SamsaraDisruptorPhaseRifleHiFreqAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("DisruptorHiFreq",1,1)
        stop
        TNT1 A 0 A_TakeInventory("DisruptorHiFreq",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("Disruptor/GreenLaser",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("DisruptorGP_12",0,1,0,0)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("DisruptorGP_22",0,1,0,0)
        stop
    }
}

actor DisruptorHiFreq : Ammo
{
    Inventory.Amount 10
    Inventory.MaxAmount 150
    Ammo.BackpackAmount 10
    Ammo.BackpackMaxAmount 300
    Inventory.Icon "AMSMK0"
    Tag "High Frequency Ammo"
}

Actor DisruptorGP_1 : FastProjectile
{
   Radius 2
   Height 1
   Damage (Random(30,40))
   Speed 63
   -EXTREMEDEATH
   +NOEXTREMEDEATH
   -SKYEXPLODE   
   +THRUGHOST
   +FORCEXYBILLBOARD
   +BLOODSPLATTER   
   Renderstyle Add
   Alpha 1.0
   Obituary "%o ate %k's lemon worms."   
   Projectile
   DamageType "Disruptoruniversaldamageshit2"   
   DeathSound "Disruptor/GreenL"
   AttackSound "Disruptor/GreenL"
   States
   {
   Spawn:
      SQ97 G 1 Bright
      Loop
   Death:
	  SQ98 ABCDEFGHIJKL 3 Bright
	  SQ98 MNO 2 Bright
      stop
   XDeath:
    Crash:
   	  SQ97 HIJKLMNOP 3 Bright 
   	  SQ97 QRS 2 Bright 	  
      stop			  
   }
}   

Actor DisruptorGP_2 : DisruptorGP_1 { Species "Player" +THRUSPECIES }

Actor DisruptorMode : Boolean {}
