Actor " 18mm Semi " : Weapon
{
  weapon.selectionorder 3300
  weapon.kickback 100
  weapon.BobStyle normal
  weapon.BobRangeX 1.2
  weapon.BobRangeY 0.5
  weapon.BobSpeed 2.0
  Weapon.AmmoUse 0
  Weapon.AmmoGive 0
  Weapon.AmmoType ""
  Weapon.SlotNumber 1  
  Decal BulletChip
  +UNDROPPABLE
  +WEAPON.WIMPY_WEAPON
  Inventory.RestrictedTo "DisruptorPlayer"
  Tag "18mm Semi"
  states
  {
  Spawn:
	TNT1 A -1
	Stop
  
  // 18mm Semi
  Ready:
    TNT1 A 0 A_JumpIfInventory("Disruptor_Has18mmAuto",1,"ReadyAuto")
	SQ96 A 1 A_WeaponReady
    loop

  Deselect:
	TNT1 A 0 A_JumpIfInventory("Disruptor_Has18mmAuto",1,"DeselectAuto")
	TNT1 A 0 A_Lower
    SQ96 A 1 A_Lower
    loop

  Select:
	TNT1 A 0 A_JumpIfInventory("Disruptor_Has18mmAuto",1,"SelectAuto")
	TNT1 A 0 A_TakeInventory("DisruptorMode", 1)
	TNT1 A 0 A_Raise
    SQ96 A 1 A_Raise
    loop

  Fire:
    TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
    goto FireStart
    TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart")
    TNT1 A 0 A_JumpIfInventory("Clip",1,"FireStart")
    goto NoAmmoSwitch

  FireStart:
	TNT1 A 0 A_JumpIfInventory("Disruptor_Has18mmAuto",1,"FireAutoStart")
	SQ96 BBB 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
	TNT1 A 0 A_AlertMonsters
	TNT1 A 0 A_PlaySound("Disruptor/Pistol",CHAN_WEAPON)
	SQ96 B 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
	TNT1 A 0 A_GunFlash
	SQ96 CC 1 Bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
  FireContinue:
	TNT1 A 0 A_GiveInventory("SamsaraDisruptor18mmSemiAttackHandler")
    SQ96 BBBBB 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
	SQ96 AAA 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
  FireFinish:
    TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
    goto FireFinishReFire
    TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireFinishReFire")
    TNT1 A 0 A_JumpIfInventory("Clip",1,"FireFinishReFire")
    goto NoAmmoSwitch

  FireFinishReFire:
    TNT1 A 0 A_ReFire
    goto Ready

  Flash:
	TNT1 A 0 A_Light2
	SQ96 D 2 Bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
	TNT1 A 0 A_Light0
	goto Lightdone

  // 18mm Auto
  ReadyAuto:
    SQ69 K 1 A_WeaponReady
    loop

  DeselectAuto:
	TNT1 A 0 A_Lower
	SQ69 K 1 A_Lower
    loop

  SelectAuto:
	TNT1 A 0 A_TakeInventory("DisruptorMode", 1)
	TNT1 A 0 A_Raise
	SQ69 K 1 A_Raise
    loop

  FireAutoStart:
	TNT1 A 0 A_AlertMonsters
	TNT1 A 0 A_GunFlash("FlashAuto")
	TNT1 A 0 A_PlaySound("Disruptor/Pistol",CHAN_WEAPON)
	SQ69 L 1 bright 
  FireAutoContinue:
	TNT1 A 0 A_GiveInventory("SamsaraDisruptor18mmAutoAttackHandler")
	SQ69 I 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
	SQ69 JJ 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
  FireAutoFinish:
    TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
    goto FireAutoFinishReFire
    TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireAutoFinishReFire")
    TNT1 A 0 A_JumpIfInventory("Clip",1,"FireAutoFinishReFire")
    goto NoAmmoSwitch

  FireAutoFinishReFire:
    TNT1 A 0 A_ReFire
	SQ69 KK 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
    goto Ready

  FlashAuto:
	TNT1 A 0 A_Jump(256,"FlashAutoStyle1","FlashAutoStyle2")
	Goto FlashAutoContinue
  FlashAutoStyle1:
	TNT1 A 0 A_Light2
	SQ69 M 2 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
	Goto FlashAutoContinue
  FlashAutoStyle2:
	TNT1 A 0 A_Light2
	SQ69 N 2 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
	Goto FlashAutoContinue
  FlashAutoContinue:
	TNT1 A 0 A_Light0
	Goto LightDone

  NoAmmoSwitch:
    TNT1 A 0 A_ClearReFire
    TNT1 A 0 A_SelectWeapon("Disruptor Fist")
    goto Deselect
  }
}

actor SamsaraDisruptor18mmSemiAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto PickupAttack
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        //TNT1 A 0 A_PlaySound("Disruptor/Pistol",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireBullets(2,1,0,7,"DisruptorPuff")
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireBullets(2,1,0,7,"DisruptorPuffCoop")
        stop
    }
}

actor SamsaraDisruptor18mmAutoAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto PickupAttack
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Clip",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        //TNT1 A 0 A_PlaySound("Disruptor/Pistol",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireBullets(2,1.5,0,7,"DisruptorPuff")
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireBullets(2,1.5,0,7,"DisruptorPuffCoop")
        stop
    }
}

Actor DisruptorBullet : FastProjectile
{
	Speed 320
	Radius 1
	Height 1
	Projectile
	Species "Player"
	Damage (8)
	+THRUSPECIES
	+BLOODSPLATTER
	+NOTIMEFREEZE
	+NOEXTREMEDEATH
	DamageType "DisruptorUniversalDamageShit1"
	states
	{
	Spawn:
		TNT1 A 1
		Loop
	Crash:	
		TNT1 A 1 A_SpawnItemEx("DisruptorSparkPuff")//,0,0,0,0,0,0,0,128,0) this makes puff apprear on bleeding actors....
		Stop
	Death:
		TNT1 A 1 A_SpawnItemEx("DisruptorPuff",0,0,0,0,0,1,0,0)//,0,0,0,0,0,0,0,128,0) this makes puff apprear on bleeding actors....
		Stop
	XDeath:
		TNT1 A 1
		Stop
	}
}

Actor "DisruptorPuff" : BulletPuff	
{  
  +NOBLOCKMAP
  +NOGRAVITY
  +ALLOWPARTICLES
  +RANDOMIZE
  -SKYEXPLODE  
  +NOEXTREMEDEATH
  Scale 0.5
  Renderstyle Add
  Alpha 1.0
  States
  {
  Spawn:
  Crash:
    SQ96 EFGHIJKLMNOPQRS 4 Bright
	Stop
  Melee:
	TNT1 A 1 
	Stop
  }
} 

Actor "DisruptorPuffCoop": "DisruptorPuff" { +MTHRUSPECIES +THRUSPECIES Species "Player" }

Actor "DisruptorSparkPuff"	
{  
  +NOBLOCKMAP
  +NOGRAVITY
  +ALLOWPARTICLES
  +RANDOMIZE
  -SKYEXPLODE  
  +NOEXTREMEDEATH
  Scale 0.5
  Renderstyle Add
  Alpha 1.0
  VSpeed 0
  States
  {
  Spawn:
    SQ95 GHIJKLMNOPQRSTU 1 Bright
	Stop
  }
} 
