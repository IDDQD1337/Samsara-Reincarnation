Actor " AM Blaster " : Weapon
{
  Weapon.SelectionOrder 400
  Weapon.SlotNumber 3
  weapon.BobStyle normal
  weapon.BobRangeX 1.2
  weapon.BobRangeY 0.5
  weapon.BobSpeed 2.0  
  Weapon.AmmoUse 1
  Weapon.AmmoGive 8
  Weapon.AmmoType "Shell"
  Inventory.PickupMessage "AM Blaster"
  Obituary "%o was destroyed by %k's blaster."
  Inventory.RestrictedTo "DisruptorPlayer"
  Tag "AM Blaster"
  States
  {
  Spawn:
    SQ72 V -1
    stop

  Ready:
    SQ69 OOOOPPPPQQQQRRRRSSSSTTTT 1 A_WeaponReady
    loop

  Deselect:
	TNT1 A 0 A_Lower
    SQ69 T 1 A_Lower
	loop

  Select:
	TNT1 A 0 A_TakeInventory("DisruptorMode", 1)
 	TNT1 A 0 A_Raise
    SQ69 T 1 A_Raise
    loop

  Fire:
    TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireStart")
    TNT1 A 0 A_JumpIfInventory("Shell",1,"FireStart")
    goto NoAmmoSwitch

  FireStart:
	TNT1 A 0 A_PlaySound("Disruptor/SSG",CHAN_WEAPON)
	TNT1 A 0 A_AlertMonsters
	TNT1 A 0 A_JumpIfInventory("DisruptorDummyShit",1,"FireAnim2")
	goto FireAnim1

  FireAnim1:
	SQ69 UU 1 Bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
	TNT1 A 0 A_GiveInventory("SamsaraDisruptorAMBlasterPrimaryAttackHandler")
	SQ69 U 1 Bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
	TNT1 A 0 A_Recoil(2)
	TNT1 A 0 A_GiveInventory("DisruptorDummyShit",1)
	SQ69 VVVVVV 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
	SQ69 VVVVV 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
	TNT1 A 0 A_ReFire
	SQ69 VVVV 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
    SQ69 VV 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
	goto Ready

  FireAnim2:
	SQ69 WW 1 Bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
	TNT1 A 0 A_GiveInventory("SamsaraDisruptorAMBlasterPrimaryAttackHandler")
	SQ69 W 1 Bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
	TNT1 A 0 A_Recoil(2)
	TNT1 A 0 A_TakeInventory("DisruptorDummyShit",1)	
	SQ69 XXXXXX 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
	SQ69 XXXXX 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
	TNT1 A 0 A_ReFire
	SQ69 XXXX 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
    SQ69 XX 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
	goto Ready

  AltFire:
    TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"AltFireStart")
    TNT1 A 0 A_JumpIfInventory("Shell",2,"AltFireStart")
    goto NoAmmoSwitch

  AltFireStart:
	TNT1 A 0 A_PlaySoundEx("Disruptor/Charge","soundslot7",0,0)
    SQ69 T 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
    TNT1 A 0 A_GiveInventory("DisruptorCharging",5)
  AltHold:
    TNT1 A 0 A_JumpIfInventory("DisruptorCharging",7,"Chargan2")
    SQ69 T 3 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
    TNT1 A 0 A_GiveInventory("DisruptorCharging",1)
    TNT1 A 0 A_Refire
    TNT1 A 0 A_JumpIfInventory("DisruptorChargeLevel",1,"AltFireContinue")
    TNT1 A 0 A_TakeInventory("DisruptorCharging",7)
    Goto Ready

  AltFireContinue:
    SQ69 T 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
    TNT1 A 0 A_AlertMonsters
    TNT1 A 0 A_TakeInventory("DisruptorCharging",7)
    TNT1 A 0 A_TakeInventory("DisruptorChargeLevel",999)
    TNT1 A 0 A_GiveInventory("SamsaraDisruptorAMBlasterSecondaryAttackHandler")
  AltFireFinish:
	TNT1 A 0 A_Light2
    SQ69 Y 4 Bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
	TNT1 A 0 A_Recoil(5)
	TNT1 A 0 A_Light1
    SQ69 Z 4 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
	TNT1 A 0 A_Light0
    SQ69 Z 15 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
    Goto Ready
	
 Chargan2:
    TNT1 A 0 A_TakeInventory("DisruptorCharging")
    TNT1 A 0 A_GiveInventory("DisruptorChargeLevel")
    TNT1 A 0 A_JumpIfInventory("DisruptorChargeLevel",2,"LolBeep")
    goto LolNoBeep

 LolBeep:
	TNT1 A 0 A_PlaySound("Disruptor/Beep",CHAN_WEAPON)
    SQ69 T 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
    goto AltHold

 LolNoBeep:
    SQ69 T 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
    goto AltHold
	
  NoAmmoSwitch:
    TNT1 A 0 A_ClearReFire
    TNT1 A 0 A_SelectWeapon(" 18mm Semi ")
    goto Deselect
  }
}

actor SamsaraDisruptorAMBlasterPrimaryAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Shell",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Shell",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        //TNT1 A 0 A_PlaySound("Disruptor/SSG",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 AAAAA 0 A_FireCustomMissile("DisruptorBullet3",fRandom(-5.00,5.00),0,0,0,0,fRandom(-4.00,4.00))
		TNT1 A 0 A_FireCustomMissile("DisruptorBullet3",fRandom(-5.00,5.00),1,0,0, 0,fRandom(-4.00,4.00))
        stop

      PickupAttackCoop:
        TNT1 AAAAA 0 A_FireCustomMissile("DisruptorBullet3_1",fRandom(-5.00,5.00),0,0,0,0,fRandom(-4.00,4.00))
		TNT1 A 0 A_FireCustomMissile("DisruptorBullet3_1",fRandom(-5.00,5.00),1,0,0, 0,fRandom(-4.00,4.00))
        stop
    }
}

actor SamsaraDisruptorAMBlasterSecondaryAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Shell",2,1)
        stop
        TNT1 A 0 A_TakeInventory("Shell",2)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("Disruptor/SuperShoot",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 AAAAAAAAAAAA 0 A_FireCustomMissile("DisruptorBullet3",fRandom(-5.50,5.50),0,0,0,0,fRandom(-4.50,4.50))
        stop

      PickupAttackCoop:
        TNT1 AAAAAAAAAAAA 0 A_FireCustomMissile("DisruptorBullet3_1",fRandom(-5.50,5.50),0,0,0,0,fRandom(-4.50,4.50))
        stop
    }
}

actor DisruptorChargeLevel: Counter { Inventory.MaxAmount 999 }
actor DisruptorCharging: Counter { Inventory.MaxAmount 7 }
actor DisruptorDummyShit : Inventory { Inventory.Amount 1 Inventory.Maxamount 1 }

Actor DisruptorBullet3 : FastProjectile
{
	Radius 1
	Height 1
	Projectile
	Speed 600
	RenderStyle Add
	Alpha 1.0
    DamageType "Disruptoruniversaldamageshit2"   
	+BLOODSPLATTER
	+FORCEXYBILLBOARD
	+NOTIMEFREEZE
	+NOEXTREMEDEATH	
	Damage (Random(12,15))
	Scale 0.54
	States
	{
	Spawn:
		TNT1 A 1
		Loop
	Death:
	XDeath:
	Crash:
		SQ97 TUVWXYZ 3 Bright
		SQ98 P 3 Bright
		Stop
	}
}	

Actor DisruptorBullet3_1 : DisruptorBullet3 { Species "Player" +THRUSPECIES }