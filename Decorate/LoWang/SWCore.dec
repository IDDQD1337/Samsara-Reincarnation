//Core: ////////////////////////////////////////////////////////////////////////
Actor SWBoolean : Inventory
{
	Inventory.Amount 1
	Inventory.MaxAmount 1
	+INVENTORY.IGNORESKILL
	+INVENTORY.UNDROPPABLE
	-INVENTORY.INVBAR
}

Actor SWCounter : Inventory
{
	Inventory.Amount 1
	Inventory.MaxAmount 255
	+INVENTORY.IGNORESKILL
	+INVENTORY.UNDROPPABLE
	-INVENTORY.INVBAR
}

Actor SWACSTrigger : CustomInventory
{
	+INVENTORY.ALWAYSPICKUP
	+INVENTORY.AUTOACTIVATE
	+INVENTORY.IGNORESKILL
	+INVENTORY.UNDROPPABLE
	-INVENTORY.INVBAR
	Inventory.PickupMessage "kek"
	states
	{
	Spawn:
		TNT1 A -1
		stop
	Use:
		TNT1 A 1
		stop
	}
}

Actor SWLoWangBloody : SWBoolean { }

Actor SWPuffSmoke
{
	+NOBLOCKMAP
	+NOGRAVITY
	+RANDOMIZE
	VSpeed 1
	Mass 5
	scale 0.4
	renderstyle translucent
	alpha 0.5
	states
	{
	Spawn:
		WMI2 ABCDE 5
		stop
	}
}

Actor SWPuff
{
	+NOBLOCKMAP
	+NOGRAVITY
	+RANDOMIZE
	+ALLOWPARTICLES
	scale 0.35
	states
	{
	Spawn:
		WMI1 A 0
		WMI1 A 0 A_SpawnItemEx("SWPuffSmoke",0,0,0,0,0,random(1,2) * 0.4,0,SXF_CLIENTSIDE)
		WMI1 ABC 3 bright
		stop
	}
}

Actor BulletPuffSW : SWPuff
{
	+NOBLOCKMAP
	+NOGRAVITY
	+RANDOMIZE
	+ALLOWPARTICLES
	scale 0.35
	VSpeed 0
	DamageType "SWBullet"
	Renderstyle Translucent
	Alpha 0.75	
	States
	{
		Spawn:
		Crash:
		TNT1 A 0 A_PlaySound("SWBullet/Ricochet")		
		WMI1 A 0 A_SpawnItemEx("SWPuffSmoke",0,0,0,0,0,random(1,2) * 0.4,0,SXF_CLIENTSIDE)
		WMI1 ABC 3 bright
			Stop
		Melee:
			TNT1 A 1
			Stop
	}
}

Actor BulletPuffSWCoop : BulletPuffSW { +MTHRUSPECIES +THRUSPECIES Species "Player" }

//Casing effects written from scratch by Ghastly_dragon, based on the casing effects in KDiZD,
//Coded by TheDarkArchon.

Actor SWShellCasingSpawner {
	Height 1
	Radius 1
	+NOCLIP
	+MISSILE
	+NOBLOCKMAP
	+NOGRAVITY
	+DROPOFF
	+NOTELEPORT
	+CLIENTSIDEONLY
	States {
		Spawn:
		TNT1 A 0
		TNT1 A 1 A_CustomMissile("SWShellCasing",0,0,random(0,360),2,random(100,80))
		Stop
	}
}

Actor SWShellCasing
{
	Height 2
	Radius 2
	Speed 4
	Scale 0.15	
	BounceFactor 0.4
	Gravity 0.23
	Projectile
	+DOOMBOUNCE
	+DROPOFF
	+NOTELEPORT
	-NOGRAVITY
	+CLIENTSIDEONLY
    SeeSound "Weapons/ShellCasing"
  States
  {
  Spawn:
    CASS ABCDEFGH 1
    Loop
  Death:
    TNT1 A 0 A_Jump(128, "D", "F", "H")
    CASS B 10 A_FadeOut(0.03)
    Goto Death+1
	D:
    CASS D 10 A_FadeOut(0.03)
    Loop
	F:
    CASS F 10 A_FadeOut(0.03)
    Loop
	H:
    CASS H 10 A_FadeOut(0.03)
    Loop
  }
}

Actor SWBulletCasingSpawner {
	+NOCLIP
	+MISSILE
	+NOBLOCKMAP
	+NOGRAVITY
	+DROPOFF
	+NOTELEPORT
	+CLIENTSIDEONLY
	States {
		Spawn:
			TNT1 A 0
			TNT1 A 1 A_CustomMissile("SWBulletCasing",0,0,random(-80,-100),2,random(45,80))
		Stop
	}
}

Actor SWBulletCasing {
	Scale 0.15
	Radius 2
	Height 2
	Speed 3
	Gravity 0.20
	+DOOMBOUNCE
	+MISSILE
	+DROPOFF
	+NOTELEPORT
	+CLIENTSIDEONLY
	bouncefactor 0.5
	SeeSound "Weapons/BulletCasing"
	States {
		Spawn:
			BCAS ABCDEF 2
			Goto Spawn+1
		Death:
			TNT1 A 0 A_Jump(128, "B", "C", "D", "E", "F")
			BCAS A 16 A_FadeOut(0.03)
			Goto Death+1
		B:
			BCAS B 16 A_FadeOut(0.03)
			Loop
		C:
			BCAS C 16 A_FadeOut(0.03)
			Loop
		D:
			BCAS D 16 A_FadeOut(0.03)
			Loop
		E:
			BCAS E 16 A_FadeOut(0.03)
			Loop
		F:
			BCAS F 16 A_FadeOut(0.03)
			Loop	
	}
}

// Lo Wang
Actor FriendlyFatAss
{
	Health 100
	Radius 40
	Height 50
	Speed 6
	PainChance 100
	Scale 0.53
	MONSTER
	+FLOORCLIP
	+FRIENDLY
	-SOLID
	Species "Player"	
	MeleeSound "SW/Land"
	Obituary "%o was somehow killed by a fatass."
	states
	{
	Spawn:
		SAL0 AB 8 A_Look
		loop
	See:
		SAL0 AABBCCDD 4 A_Chase
		loop
    Melee:
        TNT1 A 0 A_ChangeFlag("NOPAIN",0)
        SAL0 EF 9 A_FaceTarget
        SAL0 G 8 A_CustomMeleeAttack(2 * (random(6,9)))
        Goto See
	Pain:
		SAL0 A 4 A_Pain
		goto See
	Death:
		SAL0 H 2 A_Scream
		SAL0 IJKLM 2
		SAL0 N -1
		stop
	XDeath:
		TNT1 A 5 A_SpawnItemEx("SWHumanGibSpawner",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
		stop
	}
}