actor "Explosive Shotgun" : DukeWeapon
{
	Weapon.SlotNumber 3
	Weapon.SelectionOrder 1300
	Weapon.AmmoUse 3
	Weapon.AmmoGive 15
	Decal DoomImpScorch
	Weapon.AmmoType "Shell"
	DamageType "DukeExplosive"
	+NOALERT
	+WEAPON.EXPLOSIVE
	+INVENTORY.UNDROPPABLE
	Obituary "$SAMSARA_DUKE_OB_SLOT3"
	Inventory.Pickupmessage "You got the Explosive Shotgun!"
	Inventory.PickupSound "duke/xshotgunpump"
	Weapon.UpSound "duke/xshotgunpump"
	Scale 0.4
	Tag "Explosive Shotgun"
	States
	{
	  Spawn:
		DXSP A -1
		Stop
		
	  Ready:
		TNT1 A 0 A_JumpIfInventory("DBSHOT",1,"ReadyA")
		TNT1 A 0 A_JumpIfInventory("DukeReady",1,"IntroTaunt")
		TNT1 A 0 A_JumpIfInventory("DukeWaiting",15,"WaitingTaunt")
		TNT1 A 0 A_JumpIfInventory("DukeGiveMoney",1,"GiveMoney")
		TNT1 A 0 A_JumpIfInventory("DukeStomp", 1, "Stomp")
		DKXS A 1 A_WeaponReady
		Loop

	  Deselect:
		DKXS A 0 A_GunFlash("Flash",GFF_NOEXTCHANGE)
		Goto Super::Deselect

	  Select:
		DKXS A 0 A_GunFlash("Flash",GFF_NOEXTCHANGE)
		DKXS A 0 A_JumpIfInventory("DukeReady",1,"IntroTaunt1")
		Goto Super::Select

	  Fire:
		TNT1 A 0 A_JumpIfInventory("DukeStomp", 1, "Stomp")
		TNT1 A 0 A_GiveInventory("DBSHOT",60)
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_JumpIfInventory("DB1",1,"AltFireA1")
		TNT1 A 0 A_JumpIfInventory("DB2",1,"AltFireA2")
		TNT1 A 0 A_JumpIfInventory("DB3",1,"AltFireA3")
		TNT1 A 0 A_JumpIfInventory("DB4",1,"AltFireA4")
		TNT1 A 0 A_JumpIfInventory("DB5",1,"AltFireA5")
		TNT1 A 0 A_JumpIfInventory("DB6",1,"AltFireA6")
		TNT1 A 0 A_JumpIfInventory("DB7",1,"AltFireA7")
		TNT1 A 0 A_JumpIfInventory("DB8",1,"AltFireA8")
		TNT1 A 0 A_JumpIfInventory("DB9",1,"AltFireA9")
		TNT1 A 0 A_JumpIfInventory("DB10",1,"AltFireA10")
		TNT1 A 0 A_JumpIfInventory("DB11",1,"AltFireA11")
		TNT1 A 0 A_JumpIfInventory("DB12",1,"AltFireA12")
		TNT1 A 0 A_JumpIfInventory("DB13",1,"AltFireA13")
		TNT1 A 0 A_JumpIfInventory("DB14",1,"AltFireA14")
		TNT1 A 0 A_JumpIfInventory("DB15",1,"AltFireA15")
		TNT1 A 0 A_JumpIfInventory("DB16",1,"AltFireA16")
		TNT1 A 0 A_JumpIfInventory("DB17",1,"AltFireA17")
		TNT1 A 0 A_JumpIfInventory("DB18",1,"AltFireA18")
		Goto ReadyA

	  Flash:
		TNT1 A 0 A_JumpIfInventory("DBSHOT",1,"FlashA")
		stop
	  FlashA:
		TNT1 A 0 A_TakeInventory("DukeWaiting",15)
		TNT1 A 0 A_AlertMonsters
		DKXS A 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_TakeInventory("Shell",3,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_PlayWeaponSound("duke/xshotgunfire")
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"SPFire")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopFire")
		TNT1 A 0 A_FireBullets(6,5,7,6,"DukeExplosiveShotgunPuff", FBF_NORANDOM)
		Goto ShottyAnim
	  CoopFire:
		TNT1 A 0 A_FireBullets(6,5,7,6,"DukeExplosiveShotgunPuffCoop", FBF_NORANDOM)
		//DKXS A 0 A_FireCustomMissile("DukeExplosiveShotgunBullet",random(-100,100)/100.00,1,0,1,0,random(-100,100)/100.00)
		//DKXS AA 0 A_FireCustomMissile("DukeExplosiveShotgunBullet",random(-200,200)/100.00,0,0,1,0,random(-200,200)/100.00)
		//DKXS AAAA 0 A_FireCustomMissile("DukeExplosiveShotgunBullet",random(-500,500)/100.00,0,0,1,0,random(-400,400)/100.00)
		Goto ShottyAnim
	  SPFire:
		TNT1 A 0 A_FireBullets(6,5,7,6,"DukeExplosiveShotgunPuffSP", FBF_NORANDOM)
		Goto ShottyAnim
	  ShottyAnim:
		TNT1 A 0 A_Light1
		DKXS B 1 Bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_Light0
		DKXS B 1 Bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		DKXS CDEA 1 BRIGHT A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		DKXS F 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		DKXS A 0 A_CheckReload
		DKXS GHI 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		DKXS J 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		DKXS K 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		DKXS E 0 A_PlaySound("duke/xshotgunpump",CHAN_5)
		DKXS LMN 3 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_FireCustomMissile("ShellCasingSpawner2",0,0,5)
		DKXS NML 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		DKXS K 3 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		DKXS JIHG 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		DKXS FA 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_TakeInventory("DBSHOT",99)
		DKXS A 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		stop

	  AltFire:
		TNT1 A 0 A_JumpIfInventory("DukeClassMode", 2, "NoAltFireForDuke64")
		Goto Super::AltFire

	  AltFlash:
		DKXS A 2
		stop

	  NoAltFireForDuke64:
		TNT1 A 0
		goto Ready

	  TauntWeaponSound:
		TNT1 A 0 A_PlayWeaponSound("duke/xshotgunpump")
		Goto Select

	  IntroTaunt:
		DKXS A 0
		Goto Super::IntroTaunt

	  WaitingTaunt:
		DKXS A 0
		Goto Super::WaitingTaunt

	  GiveMoney:
		DKXS A 0
		Goto Super::GiveMoney
	}
}

actor DukeExplosiveShotgunBullet : FastProjectile
{
	Damage (6)
	Radius 1
	Height 1
	Speed 320
	Projectile
	Obituary "$SAMSARA_DUKE_OB_SLOT3"
	DamageType "DukeExplosive"
	Renderstyle "Normal"
	+BLOODSPLATTER
	+NOTIMEFREEZE
	+THRUSPECIES
	Species "Player"
	Renderstyle Add
	Alpha 0.9
	Scale 0.5
	Decal DoomImpScorch
	+FORCERADIUSDMG
	States
	{
	  Spawn:
		TNT1 A 1
		loop

	  XDeath:
	  Death:
		TNT1 A 1 A_PlaySound("duke/xshotgunxplod")
		TNT1 A 0 A_Explode(25,64) // Seven x 25 = 175 damage. 175+42=217. Seven x 35 = 245 damage. 245+21 = 266 damage total.
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("ExplosiveFireSpark",0,0,Random(-200,200),2,Random(-200,200))
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("ExplosiveFireSpark2",0,0,Random(-100,100),2,Random(-100,100))
		DKSX ABCDEFGH 2 BRIGHT
		DKSX IJKLMNOPQ 1 BRIGHT
		stop
	}
}

ACTOR DukeExplosiveShotgunPuff : BulletPuff
{
	Renderstyle Add
	Alpha 0.9
	DamageType "DukeExplosive"
	Scale 0.5
	VSpeed 0
	-SOLID
	+PUFFONACTORS
	+ALWAYSPUFF
	+PUFFGETSOWNER
	+FORCERADIUSDMG
	States
	{
	Spawn:
	Melee:
	Crash:
	   TNT1 A 0
	   TNT1 A 1 A_PlaySound("duke/xshotgunxplod")
	   TNT1 A 0 A_Explode((19),64) // Seven x 20 = 140 damage. 140+35 = 175 damage total.
	   TNT1 AAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("ExplosiveFireSpark",0,0,Random(-200,200),2,Random(-200,200))
	   TNT1 AAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("ExplosiveFireSpark2",0,0,Random(-100,100),2,Random(-100,100))
	   DKSX ABCDEFGH 2 BRIGHT
	   DKSX IJKLMNOPQ 1 BRIGHT
	   stop
   }
}

actor DukeExplosiveShotgunPuffSP : DukeExplosiveShotgunPuff
{
	States
	{
	Spawn:
	Melee:
	Crash:
	   TNT1 A 0
	   TNT1 A 1 A_PlaySound("duke/xshotgunxplod")
	   TNT1 A 0 A_Explode((25),64)
	   TNT1 AAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("ExplosiveFireSpark",0,0,Random(-200,200),2,Random(-200,200))
	   TNT1 AAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("ExplosiveFireSpark2",0,0,Random(-100,100),2,Random(-100,100))
	   DKSX ABCDEFGH 2 BRIGHT
	   DKSX IJKLMNOPQ 1 BRIGHT
	   stop
   }
}

Actor DukeExplosiveShotgunPuffCoop : DukeExplosiveShotgunPuff
{
	Species "Player"
	+THRUSPECIES
	+MTHRUSPECIES
	+DONTHARMSPECIES
	+DONTHARMCLASS
}

actor ExplosiveFireSpark
{
	health 5
	radius 1
	height 1
	Speed 10
	Scale 0.2
	Gravity 0.7
	PROJECTILE
	+NOBLOCKMAP
	+DONTSPLASH
	+RANDOMIZE
	-NOGRAVITY
	-SOLID
	+WINDTHRUST
	+CLIENTSIDEONLY
	States
	{
	  Spawn:
		DXSK A 1 BRIGHT A_FadeOut(0.01)
		loop
	}
}

actor ExplosiveFireSpark2 : ExplosiveFireSpark
{
	Speed 7
	Gravity 0.75
	scale 0.35
}
