actor "Explosive Shotgun" : DukeWeapon
{
	Weapon.SlotNumber 3
	Weapon.SelectionOrder 1300
	Weapon.AmmoUse 3
	Weapon.AmmoGive 15
	Decal DoomImpScorch
	Weapon.AmmoType "Shell"
	DamageType "DukeExplosive"
	+NOALERT
	+WEAPON.EXPLOSIVE
	+INVENTORY.UNDROPPABLE
	Obituary "$SAMSARA_DUKE_OB_SLOT3"
	Inventory.Pickupmessage "You got the Explosive Shotgun!"
	Inventory.PickupSound "duke/xshotgunpump"
	Weapon.UpSound ""
	Scale 0.4
	Tag "Explosive Shotgun"
	States
	{
	  Spawn:
		DXSP A -1
		Stop
		
	  Ready:
		TNT1 A 0 A_JumpIfInventory("DBSHOT",1,"ReadyA")
		TNT1 A 0 A_JumpIfInventory("DukeReady",1,"IntroTaunt")
		TNT1 A 0 A_JumpIfInventory("DukeWaiting",15,"WaitingTaunt")
		TNT1 A 0 A_JumpIfInventory("DukeGiveMoney",1,"GiveMoney")
		TNT1 A 0 A_JumpIfInventory("DukeStomp", 1, "Stomp")
		TNT1 A 0 A_JumpIfInventory("DukeClassMode", 2, 2)
		DKXS A 1 A_WeaponReady
		Loop
		D6GL A 1 A_WeaponReady
		Loop

	  Deselect:
		TNT1 A 0 A_JumpIfInventory("DukeClassMode", 2, "Deselect64")
		DKXS A 0 A_GunFlash("Flash",GFF_NOEXTCHANGE)
		Goto Super::Deselect
		
	  Deselect64:
		D6GL A 0 A_GunFlash("Flash",GFF_NOEXTCHANGE)
		Goto Super::Deselect

	  Select:
		DKXS A 0 A_GunFlash("Flash",GFF_NOEXTCHANGE)
		DKXS A 0 A_JumpIfInventory("DukeReady",1,"IntroTaunt1")
		DKXS A 0 A_JumpIfInventory("DukeClassMode", 2, "Select64")
		DKXS A 0 A_PlayWeaponSound("duke/xshotgunpump")
		Goto Super::Select
		
	  Select64:
		TNT1 A 0 A_PlayWeaponSound("duke64/wepselect")
		D6GL A 0
		Goto Super::Select

	  Fire:
		TNT1 A 0 A_JumpIfInventory("DukeStomp", 1, "Stomp")
		TNT1 A 0 A_GiveInventory("DBSHOT",60)
		TNT1 A 0 A_GunFlash
		Goto ReadyA

	  Flash:
		TNT1 A 0 A_JumpIfInventory("DBSHOT",1,"FlashA")
		stop
	  FlashA:
		TNT1 A 0 A_TakeInventory("DukeWaiting",15)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_JumpIfInventory("DukeClassMode",2,"FlashA64")
		DKXS A 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_TakeInventory("Shell",3,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_PlayWeaponSound("duke/xshotgunfire")
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"SPFire")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopFire")
		TNT1 A 0 A_FireBullets(6,5,7,6,"DukeExplosiveShotgunPuff", FBF_NORANDOM)
		Goto ShottyAnim
	  CoopFire:
		TNT1 A 0 A_FireBullets(6,5,7,6,"DukeExplosiveShotgunPuffCoop", FBF_NORANDOM)
		//DKXS A 0 A_FireCustomMissile("DukeExplosiveShotgunBullet",random(-100,100)/100.00,1,0,1,0,random(-100,100)/100.00)
		//DKXS AA 0 A_FireCustomMissile("DukeExplosiveShotgunBullet",random(-200,200)/100.00,0,0,1,0,random(-200,200)/100.00)
		//DKXS AAAA 0 A_FireCustomMissile("DukeExplosiveShotgunBullet",random(-500,500)/100.00,0,0,1,0,random(-400,400)/100.00)
		Goto ShottyAnim
	  SPFire:
		TNT1 A 0 A_FireBullets(6,5,7,6,"DukeExplosiveShotgunPuffSP", FBF_NORANDOM)
		Goto ShottyAnim
	  ShottyAnim:
		TNT1 A 0 A_Light1
		DKXS B 1 Bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_Light0
		DKXS B 1 Bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		DKXS CDEA 1 BRIGHT A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		DKXS F 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		DKXS A 0 A_CheckReload
		DKXS GHI 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		DKXS J 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		DKXS K 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		DKXS E 0 A_PlaySound("duke/xshotgunpump",CHAN_5)
		DKXS LMN 3 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_FireCustomMissile("ShellCasingSpawner2",0,0,5)
		DKXS NML 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		DKXS K 3 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		DKXS JIHG 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		DKXS FA 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_TakeInventory("DBSHOT",99)
		DKXS A 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		stop
		
	  FlashA64:
		TNT1 A 0 A_Light1
		D6GL A 3 Bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_Light0
		TNT1 A 0 A_PlayWeaponSound("duke64/grenadefire")
		TNT1 A 0 ACS_NamedExecuteAlways("SamsaraRecoil", 0, 6, 3, 17)
		D6GL B 1 A_FireCustomMissile("Duke64Grenade",0,1,4,16)
		D6GL CD 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		D6GL E 3 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		D6GL A 15 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_TakeInventory("DBSHOT",99)
		D6GL A 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		stop

	  AltFire:
		TNT1 A 0 A_JumpIfInventory("DukeClassMode", 2, "NoAltFireForDuke64")
		Goto Super::AltFire

	  AltFlash:
		DKXS A 2
		stop

	  NoAltFireForDuke64:
		TNT1 A 0
		goto Ready

	  TauntWeaponSound:
		TNT1 A 0 A_JumpIfInventory("DukeClassMode", 2, "TauntWeaponSound64")
		TNT1 A 0 A_PlayWeaponSound("duke/xshotgunpump")
		Goto Select
		
	  TauntWeaponSound64:
		TNT1 A 0 A_PlayWeaponSound("duke64/wepselect")
		Goto Select

	  IntroTaunt:
		DKXS A 0 A_JumpIfInventory("DukeClassMode", 2, "IntroTaunt64")
		DKXS A 0
		Goto Super::IntroTaunt
	  IntroTaunt64:
		D6GL A 0
		Goto Super::IntroTaunt

	  WaitingTaunt:
		DKXS A 0 A_JumpIfInventory("DukeClassMode", 2, "WaitingTaunt64")
		DKXS A 0
		Goto Super::WaitingTaunt
	  WaitingTaunt64:
		D6GL A 0
		Goto Super::WaitingTaunt

	  GiveMoney:
		DKXS A 0 A_JumpIfInventory("DukeClassMode", 2, "GiveMoney64")
		DKXS A 0
		Goto Super::GiveMoney
	  GiveMoney64:
		D6GL A 0
		Goto Super::GiveMoney
	}
}

actor DukeExplosiveShotgunBullet : FastProjectile
{
	Damage (6)
	Radius 1
	Height 1
	Speed 320
	Projectile
	Obituary "$SAMSARA_DUKE_OB_SLOT3"
	DamageType "DukeExplosive"
	Renderstyle "Normal"
	+BLOODSPLATTER
	+NOTIMEFREEZE
	+THRUSPECIES
	Species "Player"
	Renderstyle Add
	Alpha 0.9
	Scale 0.5
	Decal DoomImpScorch
	+FORCERADIUSDMG
	States
	{
	  Spawn:
		TNT1 A 1
		loop

	  XDeath:
	  Death:
		TNT1 A 1 A_PlaySound("duke/xshotgunxplod")
		TNT1 A 0 A_Explode(25,64) // Seven x 25 = 175 damage. 175+42=217. Seven x 35 = 245 damage. 245+21 = 266 damage total.
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("ExplosiveFireSpark",0,0,Random(-200,200),2,Random(-200,200))
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("ExplosiveFireSpark2",0,0,Random(-100,100),2,Random(-100,100))
		DKSX ABCDEFGH 2 BRIGHT
		DKSX IJKLMNOPQ 1 BRIGHT
		stop
	}
}

ACTOR DukeExplosiveShotgunPuff : BulletPuff
{
	Renderstyle Add
	Alpha 0.9
	DamageType "DukeExplosive"
	Scale 0.5
	VSpeed 0
	-SOLID
	+PUFFONACTORS
	+ALWAYSPUFF
	+PUFFGETSOWNER
	+FORCERADIUSDMG
	States
	{
	Spawn:
	Melee:
	Crash:
	   TNT1 A 0
	   TNT1 A 1 A_PlaySound("duke/xshotgunxplod")
	   TNT1 A 0 A_Explode((19),64) // Seven x 20 = 140 damage. 140+35 = 175 damage total.
	   TNT1 AAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("ExplosiveFireSpark",0,0,Random(-200,200),2,Random(-200,200))
	   TNT1 AAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("ExplosiveFireSpark2",0,0,Random(-100,100),2,Random(-100,100))
	   DKSX ABCDEFGH 2 BRIGHT
	   DKSX IJKLMNOPQ 1 BRIGHT
	   stop
   }
}

actor DukeExplosiveShotgunPuffSP : DukeExplosiveShotgunPuff
{
	States
	{
	Spawn:
	Melee:
	Crash:
	   TNT1 A 0
	   TNT1 A 1 A_PlaySound("duke/xshotgunxplod")
	   TNT1 A 0 A_Explode((25),64)
	   TNT1 AAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("ExplosiveFireSpark",0,0,Random(-200,200),2,Random(-200,200))
	   TNT1 AAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("ExplosiveFireSpark2",0,0,Random(-100,100),2,Random(-100,100))
	   DKSX ABCDEFGH 2 BRIGHT
	   DKSX IJKLMNOPQ 1 BRIGHT
	   stop
   }
}

Actor DukeExplosiveShotgunPuffCoop : DukeExplosiveShotgunPuffSP
{
	Species "Player"
	+THRUSPECIES
	+MTHRUSPECIES
	+DONTHARMSPECIES
	+DONTHARMCLASS
}

actor ExplosiveFireSpark
{
	health 5
	radius 1
	height 1
	Speed 10
	Scale 0.2
	Gravity 0.7
	PROJECTILE
	+NOBLOCKMAP
	+DONTSPLASH
	+RANDOMIZE
	-NOGRAVITY
	-SOLID
	+WINDTHRUST
	+CLIENTSIDEONLY
	States
	{
	  Spawn:
		DXSK A 1 BRIGHT A_FadeOut(0.01)
		loop
	}
}

actor ExplosiveFireSpark2 : ExplosiveFireSpark
{
	Speed 7
	Gravity 0.75
	scale 0.35
}

actor Duke64RocketPuff
{
	VSpeed 1
	Scale 0.5
	+NOINTERACTION
	+CLIENTSIDEONLY
	-SOLID
	RenderStyle Translucent
	Alpha 0.788
    States
    {
      Spawn:
        TNT1 A 2
        D6PF AA 1 A_SetScale(scalex+0.05,scaley+0.05)
		D6PF BBCCDDEEFFGGHHII 2 A_SetScale(scalex+0.05,scaley+0.05)
        Stop
    }
}

actor MLExplosion
{
	+NOINTERACTION
	Scale 0.7
	States
    {
      Spawn:
        DKSX A 2 BRIGHT
		DKSX B 3 BRIGHT
		DKSX CD 2 BRIGHT
		DKSX E 3 BRIGHT
		DKSX FG 2 BRIGHT
		DKSX H 3 BRIGHT
		DKSX IJ 2 BRIGHT
		DKSX K 3 BRIGHT
		DKSX LM 2 BRIGHT
		DKSX N 3 BRIGHT
		DKSX OP 2 BRIGHT
		DKSX Q 3 BRIGHT
        Stop
    }
}

actor Duke64Grenade
{
    Radius 6
    Height 6
    Speed 24
    
    Projectile
    -NOGRAVITY
    DamageType "DukeExplosive"
    Gravity 0.67
    Scale 0.3
    BounceFactor 0.5
    
    BounceType "Hexen"
    +CANBOUNCEWATER
    +RANDOMIZE
    +DEHEXPLOSION
    +MOVEWITHSECTOR
	+USEBOUNCESTATE
	+FORCERADIUSDMG
	+NOEXPLODEFLOOR
	-BOUNCEONFLOORS
    
    ReactionTime 75
    
    BounceSound "duke64/grenadebounce"
    
    Obituary "$SAMSARA_RANGER_OB_SLOT3_1"
    
    States
    {
	Spawn:
        D6GR A 0
        D6GR A 0 A_Recoil(-1)  // to stop the "freeze in midair" bug from happening
        D6GR A 0 A_PlaySound("quakeweps/blank", CHAN_VOICE)
		Goto SpawnLoop
        
    //Bounce.Floor:
	Bounce.Ceiling:
	Bounce.Wall:
    SpawnLoop:
		D6GR A 1 A_JumpIfInventory("Duke64GrenadeTicCounter",6,"SpawnLoop2")
		//D6GR A 0 A_SpawnItemEx("Duke64RocketPuff",0,0,0,0,0,0,0,160)
        D6GR A 0 A_Countdown
		D6GR A 0 A_GiveInventory("Duke64GrenadeTicCounter",1)
		D6GR A 0 A_CheckFloor("FloorLoop")
		Loop
	SpawnLoop2:
		D6GR B 1 A_JumpIfInventory("Duke64GrenadeTicCounter",12,"SpawnLoop3")
		D6GR B 0 A_SpawnItemEx("Duke64RocketPuff",0,0,4,0,0,0,0,160)
        D6GR B 0 A_Countdown
		D6GR B 0 A_GiveInventory("Duke64GrenadeTicCounter",1)
		D6GR B 0 A_CheckFloor("FloorLoop2")
		Loop
	SpawnLoop3:
		D6GR C 1 A_JumpIfInventory("Duke64GrenadeTicCounter",18,"SpawnLoop4")
		D6GR C 0 A_SpawnItemEx("Duke64RocketPuff",0,0,4,0,0,0,0,160)
        D6GR C 0 A_Countdown
		D6GR C 0 A_GiveInventory("Duke64GrenadeTicCounter",1)
		D6GR C 0 A_CheckFloor("FloorLoop3")
		Loop
	SpawnLoop4:
		D6GR D 1 A_JumpIfInventory("Duke64GrenadeTicCounter",24,"SpawnLoop5")
		D6GR D 0 A_SpawnItemEx("Duke64RocketPuff",0,0,4,0,0,0,0,160)
        D6GR D 0 A_Countdown
		D6GR D 0 A_GiveInventory("Duke64GrenadeTicCounter",1)
		D6GR D 0 A_CheckFloor("FloorLoop4")
		Loop
	SpawnLoop5:
		D6GR E 1 A_JumpIfInventory("Duke64GrenadeTicCounter",30,"SpawnLoop6")
		D6GR E 0 A_SpawnItemEx("Duke64RocketPuff",0,0,4,0,0,0,0,160)
        D6GR E 0 A_Countdown
		D6GR E 0 A_GiveInventory("Duke64GrenadeTicCounter",1)
		D6GR E 0 A_CheckFloor("FloorLoop5")
		Loop
	SpawnLoop6:
		D6GR F 1 A_JumpIfInventory("Duke64GrenadeTicCounter",36,"SpawnLoopReset")
		D6GR F 0 A_SpawnItemEx("Duke64RocketPuff",0,0,4,0,0,0,0,160)
        D6GR F 0 A_Countdown
		D6GR F 0 A_GiveInventory("Duke64GrenadeTicCounter",1)
		D6GR F 0 A_CheckFloor("FloorLoop6")
		Loop
	SpawnLoopReset:
		TNT1 A 0 A_TakeInventory("Duke64GrenadeTicCounter",36)
		Goto SpawnLoop
	FloorLoop:
		D6GR G 1 A_JumpIfInventory("Duke64GrenadeTicCounter",6,"FloorLoop2")
		D6GR G 0 A_SpawnItemEx("Duke64RocketPuff",0,0,4,0,0,0,0,160)
        D6GR G 0 A_Countdown
		D6GR G 0 A_GiveInventory("Duke64GrenadeTicCounter",1)
		Loop
	FloorLoop2:
		D6GR H 1 A_JumpIfInventory("Duke64GrenadeTicCounter",12,"FloorLoop3")
		D6GR H 0 A_SpawnItemEx("Duke64RocketPuff",0,0,4,0,0,0,0,160)
        D6GR H 0 A_Countdown
		D6GR H 0 A_GiveInventory("Duke64GrenadeTicCounter",1)
		Loop
	FloorLoop3:
		D6GR I 1 A_JumpIfInventory("Duke64GrenadeTicCounter",18,"FloorLoop4")
		D6GR I 0 A_SpawnItemEx("Duke64RocketPuff",0,0,4,0,0,0,0,160)
        D6GR I 0 A_Countdown
		D6GR I 0 A_GiveInventory("Duke64GrenadeTicCounter",1)
		Loop
	FloorLoop4:
		D6GR J 1 A_JumpIfInventory("Duke64GrenadeTicCounter",24,"FloorLoop5")
		D6GR J 0 A_SpawnItemEx("Duke64RocketPuff",0,0,4,0,0,0,0,160)
        D6GR J 0 A_Countdown
		D6GR J 0 A_GiveInventory("Duke64GrenadeTicCounter",1)
		Loop
	FloorLoop5:
		D6GR K 1 A_JumpIfInventory("Duke64GrenadeTicCounter",30,"FloorLoop6")
		D6GR K 0 A_SpawnItemEx("Duke64RocketPuff",0,0,4,0,0,0,0,160)
        D6GR K 0 A_Countdown
		D6GR K 0 A_GiveInventory("Duke64GrenadeTicCounter",1)
		Loop
	FloorLoop6:
		D6GR L 1 A_JumpIfInventory("Duke64GrenadeTicCounter",36,"FloorLoopReset")
		D6GR L 0 A_SpawnItemEx("Duke64RocketPuff",0,0,4,0,0,0,0,160)
        D6GR L 0 A_Countdown
		D6GR L 0 A_GiveInventory("Duke64GrenadeTicCounter",1)
		Loop
	FloorLoopReset:
		TNT1 A 0 A_ChangeVelocity(-1,-1,0,CVF_RELATIVE)
		TNT1 A 0 A_TakeInventory("Duke64GrenadeTicCounter",36)
		Goto FloorLoop
        
      Bounce.Actor.Creature:
	  Death:
        MISL B 0 A_Explode(200,180)
        TNT1 A 0 A_PlaySound("duke64/explosion", 6, 1.0, 0, ATTN_NONE)
		TNT1 A 0 A_SpawnItemEx("SmallExplosionFireRing",0,0,0,0,0,0,0,160)
		TNT1 A 0 A_SpawnItemEx("SmallExplosionFireBall",0,0,0,0,0,0,0,160)
		TNT1 A 0 A_SpawnItemEx("Duke64SmallExplosionSmoke")
		TNT1 AAAAAA 0 A_SpawnItemEx("PipebombFrag64",random(-1,1),random(-1,1),random(4,8),random(-4,4),random(-4,4),random(5,10),random(-180,180),160)
        TNT1 A 0 A_SpawnItemEx("PipebombFrag64_2",random(-1,1),random(-1,1),random(4,8),random(-4,4),random(-4,4),random(5,10),random(-180,180),160)
		TNT1 A 1 A_SpawnItemEx("MLExplosion",0,0,0,0,0,0,0,160)
        Stop
    }
}

ACTOR Duke64GrenadeTicCounter : Inventory { Inventory.MaxAmount 36 }