actor RPG : DukeWeapon
{
	Weapon.SlotNumber 5
	Weapon.SelectionOrder 2500
	Weapon.AmmoUse 1
	Weapon.AmmoGive 5
	Weapon.AmmoType "RocketAmmo"
	//Obituary "%k sent %o home in a box."
	Inventory.Pickupmessage "You got the RPG!"
	Inventory.PickupSound "duke/weaponget"
	Weapon.upsound ""
//  Scale 0.75
	+NOALERT
	+WEAPON.EXPLOSIVE
	+INVENTORY.UNDROPPABLE
	Tag "RPG"
	States
	{
	  Spawn:
		DRPG A -1
		Stop

	  Ready:
		TNT1 A 0 A_JumpIfInventory("DBSHOT",1,"ReadyA")
		TNT1 A 0 A_JumpIfInventory("DukeReady",1,"IntroTaunt")
		TNT1 A 0 A_JumpIfInventory("DukeWaiting",15,"WaitingTaunt")
		TNT1 A 0 A_JumpIfInventory("DukeGiveMoney",1,"GiveMoney")
		TNT1 A 0 A_JumpIfInventory("DukeStomp", 1, "Stomp")
		TNT1 A 0 A_JumpIfInventory("DukeClassMode", 2, 4)
		TNT1 A 0 A_JumpIfInventory("DukeLABToken", 1, 2)
		DKRL A 1 A_WeaponReady
		Loop
		DKCL A 1 A_WeaponReady
		Loop
		D6ML A 0 A_JumpIfInventory("Duke64UsingHeatSeeking",1,2)
		D6ML A 1 A_WeaponReady
		Loop
		D6ML B 1 A_WeaponReady
		Loop

	  Deselect:
		TNT1 A 0 A_GunFlash("Flash",GFF_NOEXTCHANGE)
		DKRL A 0 A_JumpIfInventory("DukeClassMode", 2, "Deselect64")
		DKRL A 0 A_JumpIfInventory("DukeLABToken", 1, "DeselectLAB")
		Goto Super::Deselect
	  Deselect64:
		D6ML A 0 A_JumpIfInventory("Duke64UsingHeatSeeking", 1, "Deselect64Homing")
		D6ML A 0
		Goto Super::Deselect
	  Deselect64Homing:
		D6ML B 0
		Goto Super::Deselect
	  DeselectLAB:
		DKCL A 0
		Goto Super::Deselect

	  Select:
		TNT1 A 0 A_JumpIfInventory("DukeReady",1,"IntroTaunt1")
		TNT1 A 0 A_GunFlash("Flash",GFF_NOEXTCHANGE)
		DKRL A 0 A_JumpIfInventory("DukeClassMode", 2, "Select64")
		DKRL A 0 A_PlayWeaponSound("duke/weaponget")
		DKRL A 0 A_JumpIfInventory("DukeLABToken", 1, "SelectLAB")
		Goto Super::Select
	  Select64:
		DKRL A 0 A_PlayWeaponSound("duke64/wepselect")
		D6ML A 0 A_JumpIfInventory("Duke64UsingHeatSeeking", 1, "Select64Homing")
		D6ML A 0
		Goto Super::Select
	  Select64Homing:
		D6ML B 0
		Goto Super::Select
	  SelectLAB:
		DKCL A 0
		Goto Super::Select

	  Fire:
		TNT1 A 0 A_JumpIfInventory("DukeStomp", 1, "Stomp")
		TNT1 A 0 A_GiveInventory("DBSHOT",60)
		TNT1 A 0 A_GunFlash
		Goto ReadyA

	  Flash:
		TNT1 A 0 A_JumpIfInventory("DBSHOT",1,"FlashA")
		stop
	  FlashA:
		TNT1 A 0 A_TakeInventory("DukeWaiting",15)
		TNT1 A 0 A_TakeInventory("RocketAmmo",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_JumpIfInventory("DukeClassMode",2,"FlashA64")
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_Light1
		TNT1 A 0 A_JumpIfInventory("DukeLABToken", 1, 33)
		TNT1 A 0 A_PlayWeaponSound("duke/rocketfire")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,2)
		TNT1 A 0 A_FireCustomMissile("SamsaraDukeRocket",0,0,4,2)
		Goto FlashAEnd
		TNT1 A 0 A_FireCustomMissile("SamsaraDukeRocketCoop",0,0,4,2)
		Goto FlashAEnd
	  FlashAEnd:
		DKRL B 1 BRIGHT A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_Light1
		DKRL B 1 BRIGHT A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_Light1
		DKRL C 1 BRIGHT A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_Light1
		DKRL D 1 BRIGHT A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_Light0
		DKRL EFG 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		DKRL H 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		DKRL IJKLMNOP 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		DKRL Q 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		DKRL PONM 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_TakeInventory("DBSHOT",99)
		DKRL LA 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		Stop
	  FlashALAB:
		TNT1 A 0 A_PlayWeaponSound("duke/rocketfire2")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,2)
		TNT1 A 0 A_FireCustomMissile("SamsaraDukeRocketBeach",0,0,4,2)
		Goto FlashALABEnd
		TNT1 A 0 A_FireCustomMissile("SamsaraDukeRocketBeachCoop",0,0,4,2)
		Goto FlashALABEnd
	  FlashALABEnd:
		DKCL A 1 BRIGHT A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_Light1
		DKCL B 1 BRIGHT A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_Light1
		DKCL C 1 BRIGHT A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_Light1
		DKCL D 1 BRIGHT A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_Light0
		DKCL EFG 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		DKCL H 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		DKCL IJKLMNOP 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		DKCL Q 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		DKCL PONM 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_TakeInventory("DBSHOT",99)
		DKCL LA 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		Stop
	  FlashA64:
		D6ML D 2
		D6ML EF 1
		TNT1 A 0 A_AlertMonsters
		D6ML A 0 A_JumpIfInventory("Duke64UsingHeatSeeking", 1, "FlashA64Homing")
		TNT1 A 0 A_PlayWeaponSound("duke64/missilefire")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,2)
		TNT1 A 0 A_FireCustomMissile("Duke64Rocket",0,0,4,2)
		Goto FlashA64End
		TNT1 A 0 A_FireCustomMissile("Duke64RocketCoop",0,0,4,2)
		Goto FlashA64End
	  FlashA64End:
		D6ML G 1 Bright ACS_NamedExecuteAlways("SamsaraRecoil", 0, 6, 3, 17)
		TNT1 A 0 A_Light1
		D6ML FE 1 Bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_Light0
		D6ML D 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		D6ML C 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		D6MB ABC 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		D6MB D 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		D6MB EFG 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		D6MB H 6 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		D6MB IJK 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		D6MB L 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		D6MB MNOP 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		D6ML C 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_TakeInventory("DBSHOT",99)
		D6ML A 4 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		Stop
	  FlashA64Homing:
		TNT1 A 0 A_PlayWeaponSound("duke64/missilefire")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,2)
		TNT1 A 0 A_FireCustomMissile("Duke64HeatSeekingRocket",0,0,4,2)
		Goto FlashA64End
		TNT1 A 0 A_FireCustomMissile("Duke64HeatSeekingRocketCoop",0,0,4,2)
		Goto FlashA64End
	  FlashA64HomingEnd:
		D6ML G 1 Bright ACS_NamedExecuteAlways("SamsaraRecoil", 0, 6, 3, 17)
		TNT1 A 0 A_Light1
		D6ML FE 1 Bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_Light0
		D6ML D 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		D6ML C 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		D6MB ABC 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		D6MB D 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		D6MB EFG 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		D6MB H 6 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		D6MB IJK 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		D6MB L 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		D6MB MNOP 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		D6ML C 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_TakeInventory("DBSHOT",99)
		D6ML B 4 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		Stop

	  AltFire:
		TNT1 A 0 A_JumpIfInventory("DukeClassMode", 2, "Duke64SwitchAmmo")
		Goto Super::Altfire

	  AltFlash:
		DKRL A 2 A_JumpIfInventory("DukeLABToken", 1, 1)
		Stop
		DKCL A 2
		Stop

	  Duke64SwitchAmmo:
		TNT1 A 0 A_JumpIfInventory("DBSHOT",1,"Ready")
		TNT1 A 0 A_JumpIfInventory("Duke64UsingHeatSeeking",1,"Duke64SwitchAmmo2")
		TNT1 A 0 A_GiveInventory("Duke64UsingHeatSeeking",1)
		Goto Duke64SwitchAmmoAnim
	  Duke64SwitchAmmo2:
		TNT1 A 0 A_TakeInventory("Duke64UsingHeatSeeking",1)
		Goto Duke64SwitchAmmoAnim2
	  Duke64SwitchAmmoAnim:
		TNT1 A 0 A_PlayWeaponSound("duke64/wepselect")
		D6ML A 5
		TNT1 A 0
		D6ML A 10
		goto Ready
	  Duke64SwitchAmmoAnim2:
		TNT1 A 0 A_PlayWeaponSound("duke64/wepselect")
		D6ML B 5
		TNT1 A 0
		D6ML B 10
		goto Ready

	  TauntWeaponSound:
		TNT1 A 0 A_JumpIfInventory("DukeClassMode", 2, "TauntWeaponSound64")
		TNT1 A 0 A_PlayWeaponSound("duke/weaponget")
		Goto Select
		
	  TauntWeaponSound64:
		TNT1 A 0 A_PlayWeaponSound("duke64/wepselect")
		Goto Select

	  IntroTaunt:
		DKRL A 0 A_JumpIfInventory("DukeClassMode", 2, "IntroTaunt64")
		DKRL A 0 A_JumpIfInventory("DukeLABToken", 1, "IntroTauntLAB")
		Goto Super::IntroTaunt
	  IntroTaunt64:
		D6ML A 0 A_JumpIfInventory("Duke64UsingHeatSeeking", 1, "IntroTaunt64Homing")
		D6ML A 0
		Goto Super::IntroTaunt
	  IntroTaunt64Homing:
		D6ML B 0
		Goto Super::IntroTaunt
	  IntroTauntLAB:
		DKCL A 0
		Goto Super::IntroTaunt

	  WaitingTaunt:
		DKRL A 0 A_JumpIfInventory("DukeClassMode", 2, "WaitingTaunt64")
		DKRL A 0 A_JumpIfInventory("DukeLABToken", 1, "WaitingTauntLAB")
		Goto Super::WaitingTaunt
	  WaitingTaunt64:
		D6ML A 0 A_JumpIfInventory("Duke64UsingHeatSeeking", 1, "WaitingTaunt64Homing")
		D6ML A 0
		Goto Super::WaitingTaunt
	  WaitingTaunt64Homing:
		D6ML A 0
		Goto Super::WaitingTaunt
	  WaitingTauntLAB:
		DKCL A 0
		Goto Super::WaitingTaunt

	  GiveMoney:
		DKRL A 0 A_JumpIfInventory("DukeClassMode", 2, "GiveMoney64")
		DKRL A 0 A_JumpIfInventory("DukeLABToken", 1, "GiveMoneyLAB")
		Goto Super::GiveMoney
	  GiveMoney64:
		D6ML A 0 A_JumpIfInventory("Duke64UsingHeatSeeking", 1, "GiveMoney64Homing")
		D6ML A 0
		Goto Super::GiveMoney
	  GiveMoney64Homing:
		D6ML A 0
		Goto Super::GiveMoney
	  GiveMoneyLAB:
		DKCL A 0
		Goto Super::GiveMoney
	}
}

actor SamsaraDukeRocket
{
	Radius 12
	Height 8
	Speed 30
	Damage (0)
	Projectile
	Obituary "$SAMSARA_DUKE_OB_SLOT5"
	Scale 0.55
	Decal Scorch
	DamageType "DukeExplosive"
	+FORCERADIUSDMG
	States
	{
		Spawn:
			RPGX A 5 bright	//Original Duke behaviour
		SpawnLoop:
			RPGX A 1 bright A_SpawnItemEx("SamsaraDukeRocketPuff",0,0,0,0,0,1.77,0,160)
			Loop

		XDeath:
		Crash:
			TNT1 A 0 A_Explode(140, 178, 0)
			
		Death:
			TNT1 A 0 A_Explode(140, 178)//(128, 128)
			TNT1 A 0 A_PlaySound("duke/explosion", 6, 1.0, 0, ATTN_NONE)
			TNT1 A 1 A_SpawnItemEx("RPGExplosion_2",0,0,0,0,0,0,0,160)
			stop
	}
}

actor SamsaraDukeRocketCoop : SamsaraDukeRocket {
	+THRUSPECIES
	+DONTHARMCLASS
	+DONTHARMSPECIES
	Species "Player"
}

actor SamsaraDukeRocketBeach : SamsaraDukeRocket
{
	Radius 12
	Height 8
	Speed 30
	Projectile
	Obituary "$SAMSARA_DUKE_OB_SLOT5"
	Scale 0.55
	Decal Scorch
	DamageType "DukeExplosive"
	+FORCERADIUSDMG
	States
	{
	  Spawn:
		RPGX W 5 bright	//Original Duke behaviour
		Loop
	}
}

actor SamsaraDukeRocketBeachCoop : SamsaraDukeRocketBeach {
	+THRUSPECIES
	+DONTHARMCLASS
	+DONTHARMSPECIES
	Species "Player"
	States {
	 XDeath:
	 Crash:
		TNT1 A 0 A_Explode(140, 178, 0)
	  Death:
		TNT1 A 0 A_Explode(140, 178)//(128, 128)
		TNT1 A 0 A_PlaySound("duke/explosion", 6, 1.0, 0, ATTN_NONE)
		TNT1 A 1 A_SpawnItemEx("RPGExplosion_2",0,0,0,0,0,0,0,160)
		stop
	}	
}

actor RPGExplosion
{
	//+CLIENTSIDEONLY - Adding this makes the Pipebomb's explosions invisible WELL OKAY THEN
	+NOINTERACTION
	Scale 0.7
	States
	{
	  Spawn:
		RPGX BCDEFGHIJKLMNOPQRSTUV 2 BRIGHT
		Stop
	}
}

actor RPGExplosion_2 : RPGExplosion { +CLIENTSIDEONLY } //WELP

actor SamsaraDukeRocketPuff
{
	VSpeed 1
	Scale 0.55
	+NOINTERACTION
	+CLIENTSIDEONLY
	-SOLID
	RenderStyle Translucent
	Alpha 0.788
	States
	{
	  Spawn:
		TNT1 A 2
		DKPF ABCD 4
		Stop
	}
}


actor Duke64Rocket : SamsaraDukeRocket
{
	States
    {
      Spawn:
        MISX A 1 bright A_SpawnItemEx("Duke64RocketPuff",0,0,0,0,0,0,0,160)
        Loop

      Death:
        TNT1 A 0 A_Explode(128, 128)
        TNT1 A 0 A_PlaySound("duke64/explosion", 6, 1.0, 0, ATTN_NONE)
        TNT1 A 1 A_SpawnItemEx("MLExplosion",0,0,0,0,0,0,0,160)
		TNT1 A 0 A_SpawnItemEx("MissileFireRing",0,0,0,0,0,0,0,160)
		TNT1 A 0 A_SpawnItemEx("MissileFireBall",0,0,0,0,0,0,0,160)
        Stop
    }
}

actor Duke64RocketCoop : Duke64Rocket
{
	+THRUSPECIES
	+DONTHARMCLASS
	+DONTHARMSPECIES
	Species "Player"
}

actor Duke64HeatSeekingRocket : Duke64Rocket
{
	+SEEKERMISSILE
	+SCREENSEEKER
	States
    {
      Spawn:
        MISX A 1 bright A_SeekerMissile(0,2,SMF_PRECISE|SMF_LOOK)
		TNT1 A 0 A_JumpIfTracerCloser(4096,"NoWeave")
		TNT1 A 0 A_Weave(3, 3, 1.5, 1.5)
		TNT1 A 0 A_SpawnItemEx("Duke64RocketPuff",0,0,0,0,0,0,0,160)
        Loop
	  NoWeave:
		MISX A 1 bright A_SeekerMissile(0,2,SMF_PRECISE|SMF_LOOK)
		TNT1 A 0 A_SpawnItemEx("Duke64RocketPuff",0,0,0,0,0,0,0,160)
        Loop
    }
}

actor Duke64HeatSeekingRocketCoop : Duke64HeatSeekingRocket
{
	+THRUSPECIES
	+DONTHARMCLASS
	+DONTHARMSPECIES
	Species "Player"
}

Actor MissileFireRing : Duke64FireRing { Mass 1 }
Actor MissileFireBall : Duke64FireBall { Mass 1 }

Actor Duke64UsingHeatSeeking : Boolean { }