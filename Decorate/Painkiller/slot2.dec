actor Painkiller_Shotgun : PainkillerWeapon
{
	Inventory.PickupMessage "Shotgun"
	Inventory.PickupSound "Painkiller/Shotgun/Pickup"
	Weapon.SlotNumber 2
	Weapon.SelectionOrder 900
	Weapon.AmmoType1 "Shell"
	Weapon.AmmoGive1 20
	Weapon.AmmoUse1 2
	Weapon.AmmoType2 "Painkiller_FreezerAmmo"
	Weapon.AmmoUse2 1
	Obituary "%o was blasted by %k's shotgun."
	Tag "Shotgun/Freezer"
	States
	{
		Spawn:
			PK99 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("Painkiller_Shotgun_Icon",0,0,40)
			PK99 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("Painkiller_Shotgun_Icon2",0,0,40)
			loop

		Ready:
			PK00 ABCDEFGHI 10 A_WeaponReady
			loop

		Select:
			TNT1 A 0 A_TakeInventory("Painkiller_HoldingStake")
			TNT1 A 0 A_GiveInventory("Painkiller_HoldingShotgun")
			TNT1 A 0 A_TakeInventory("Painkiller_HoldingBolt")
			TNT1 A 0 A_TakeInventory("Painkiller_HoldingRifle")
			TNT1 A 0 A_TakeInventory("Painkiller_HoldingChaingun")
			TNT1 A 0 A_TakeInventory("Painkiller_HoldingElectro")
			TNT1 A 0 A_TakeInventory("Painkiller_HoldingDemon")
			goto Super::Select

		Fire:
			PK01 A 0 A_SpawnItemEx("Painkiller_Shotgun_FireSmoke",4,4,38,0,-0.1,0.1,0,SXF_NOCHECKPOSITION)
			PK01 A 0 A_SpawnItemEx("Painkiller_Shotgun_FireSmoke",4,4,38,0,0.1,0.1,0,SXF_NOCHECKPOSITION)
			PK01 A 0 A_GunFlash
			PK01 A 2 A_GiveInventory("SamsaraPainkillerShotgunPrimaryAttackHandler")
			PK01 BCDEFGHJKM 2
			PK01 NP 1
			goto Ready

		Flash:
			PK03 ABCDE 1 Bright
			stop

		AltFire:
			PK02 A 3 A_GiveInventory("SamsaraPainkillerShotgunSecondaryAttackHandler")
			PK02 BC 3 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			PK02 DEFGHIJKLMNOP 3 A_WeaponReady(WRF_NOSECONDARY|WRF_NOSWITCH)
			goto Ready
	}
}

actor SamsaraPainkillerShotgunPrimaryAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Shell",2,1)
        stop
        TNT1 A 0 A_TakeInventory("Shell",2)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("Painkiller/Shotgun/Fire",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasPermaWeaponLevel2",1,"PickupAttackMod")
        TNT1 A 0 A_JumpIfInventory("Painkiller_WeaponModifier_Token",1,"PickupAttackMod")
      // Normal
      PickupAttackNormal:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackNormalCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackNormalCoop")
      PickupAttackNormalDM:
        TNT1 A 0 A_FireBullets(8,8,15,8,"Painkiller_Shotgun_Puff",FBF_NORANDOM|FBF_NOFLASH) // unused dm state was 24 bullet count with 4 damage, adjust if needed or remove this comment
        goto PickupFinish

      PickupAttackNormalCoop:
        TNT1 A 0 A_FireBullets(8,8,15,8,"Painkiller_Shotgun_PuffCoop",FBF_NORANDOM|FBF_NOFLASH)
        goto PickupFinish

      // Weapon Modifier
      PickupAttackMod:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackModCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackModCoop")
      PickupAttackModDM:
        TNT1 A 0 A_FireBullets(2.5,2.5,24,8,"Painkiller_Shotgun_Puff",FBF_NORANDOM|FBF_NOFLASH) // unused dm state was 5 damage, adjust if needed or remove this comment
        goto PickupFinish

      PickupAttackModCoop:
        TNT1 A 0 A_FireBullets(2.5,2.5,24,8,"Painkiller_Shotgun_PuffCoop",FBF_NORANDOM|FBF_NOFLASH)
        goto PickupFinish

      // Finishing stuff
      PickupFinish:
        TNT1 A 0 A_Quake(4,5,0,4,"")
        stop
    }
}

actor SamsaraPainkillerShotgunSecondaryAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Painkiller_FreezerAmmo",1,1)
        stop
        TNT1 A 0 A_TakeInventory("Painkiller_FreezerAmmo",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("Painkiller/Freezer/Fire",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("Painkiller_Shotgun_IceShot",0,false,-5)
        TNT1 A 0 A_FireCustomMissile("Painkiller_Shotgun_IceShot",0,false,5)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("Painkiller_Shotgun_IceShotCoop",0,false,-5)
        TNT1 A 0 A_FireCustomMissile("Painkiller_Shotgun_IceShotCoop",0,false,5)
        stop
    }
}

actor Painkiller_Shotgun_Icon : Painkiller_Weapon_Icon
{
	States
	{
		Spawn:
			PK07 R 2
			stop
	}
}

actor Painkiller_Shotgun_Icon2 : Painkiller_Weapon_Icon
{
	States
	{
		Spawn:
			PK07 S 2
			stop
	}
}

Actor Painkiller_Shotgun_Puff : PainkillerPuff
{
	+HITTRACER
	+ALWAYSPUFF
	+PUFFONACTORS
	+PUFFGETSOWNER
	+BLOODLESSIMPACT
	-SKYEXPLODE
	+FORCEXYBILLBOARD
	Decal "PainkillerBulletHole"
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 ACS_NamedExecuteAlways("Painkiller_ShotgunBlast")
			TNT1 A 0 ACS_NamedExecuteWithResult("Painkiller_BloodGenerator_Offsets")
			TNT1 A 0 A_GiveInventory("Painkiller_BloodGenerator",1,AAPTR_TRACER)
			TNT1 A 0 A_SpawnItemEx("Painkiller_ShotgunSmoke",0,0,0,0,0,frandom(0.5,1.0),0,SXF_NOCHECKPOSITION)
			TNT1 A 2 A_GiveInventory("Painkiller_DamageCredit",1,AAPTR_TRACER)
			stop

		Melee:
		Crash:
			TNT1 A 0 A_PlaySound("Painkiller/Ricochet",CHAN_BODY,0.33)
			TNT1 A 0 A_SpawnItemEx("Painkiller_ShotgunSmoke",0,0,0,0,0,frandom(0.5,1.0),0,SXF_NOCHECKPOSITION)
			TNT1 A 0 A_SpawnItemEx("Painkiller_ChaingunHitWallEffect1",0,0,0,0,0,frandom(2.5,3.5),0,SXF_NOCHECKPOSITION)
			TNT1 A 0 A_SpawnItemEx("Painkiller_ChaingunHitWallEffectSpawner",0,0,0,0,0,0,0,SXF_CLIENTSIDE|SXF_NOCHECKPOSITION)
			TNT1 A 1 A_GiveInventory("Painkiller_DamageCredit",1,AAPTR_TRACER)
			Stop
	}
}

Actor Painkiller_Shotgun_PuffCoop : Painkiller_Shotgun_Puff { +MTHRUSPECIES }

Actor Painkiller_Shotgun_IceShot
{
	Projectile
	Speed 32
	Damage (0)
	ExplosionDamage 0
	Scale 0.4
	DamageType "PainkillerIce"
	Alpha 0.8
	RenderStyle Add
	Height 16
	Radius 8
	+FORCERADIUSDMG
	+FORCEPAIN
	+FORCEXYBILLBOARD
	States
	{
		Spawn:
			PK00 J 0 Bright
			PK00 J 0 A_SetPitch(-CallACS("Painkiller_ShooterPitch")/182)
		SpawnLoop:
			//PK00 J 0 A_SpawnItemEx("Painkiller_PainkillerLaserSegment",-40,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)
			PK00 J 0 A_SpawnItemEx("Painkiller_Shotgun_IceParticle",-8,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
			PK00 J 2 Bright Light("PAINKILLER_ICESHOT_X1") A_SpawnItemEx("Painkiller_Trail",-8,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)
			loop

		Death:
			PK00 J 0 A_GiveInventory("Painkiller_DamageCredit",1,AAPTR_TRACER)
			PK00 J 0 A_Explode(-1,16,0,false,16)
			PK00 J 2 Bright Light("PAINKILLER_ICESHOT_X1") A_FadeOut(0.1) PK00 J 0 A_SetScale(scalex-0.05)
			PK00 J 2 Bright Light("PAINKILLER_ICESHOT_X2") A_FadeOut(0.1) PK00 J 0 A_SetScale(scalex-0.05)
			PK00 J 2 Bright Light("PAINKILLER_ICESHOT_X3") A_FadeOut(0.1) PK00 J 0 A_SetScale(scalex-0.05)
			PK00 J 2 Bright Light("PAINKILLER_ICESHOT_X4") A_FadeOut(0.1) PK00 J 0 A_SetScale(scalex-0.05)
			PK00 H 2 Bright Light("PAINKILLER_ICESHOT_X5") A_FadeOut(0.1) PK00 H 0 A_SetScale(scalex-0.05)
			PK00 H 2 Bright A_FadeOut(0.1) PK00 H 0 A_SetScale(scalex-0.05)
			PK00 H 2 Bright A_FadeOut(0.1) PK00 H 0 A_SetScale(scalex-0.05)
			PK00 H 2 Bright A_FadeOut(0.1) PK00 H 0 A_SetScale(scalex-0.05)
			stop
	}
}

Actor Painkiller_Shotgun_IceShotCoop : Painkiller_Shotgun_IceShot { +THRUSPECIES Species "Player" }

Actor Painkiller_Shotgun_IceParticle
{
	Scale 0.4
	Alpha 0.8
	RenderStyle Add
	+NOINTERACTION
	+NOGRAVITY
	+FORCEXYBILLBOARD
	States
	{
		Spawn:
			PK00 J 2 Bright A_FadeOut(0.1) PK00 J 0 A_SetScale(scalex-0.05)
			PK00 J 2 Bright A_FadeOut(0.1) PK00 J 0 A_SetScale(scalex-0.05)
			PK00 J 2 Bright A_FadeOut(0.1) PK00 J 0 A_SetScale(scalex-0.05)
			PK00 J 2 Bright A_FadeOut(0.1) PK00 J 0 A_SetScale(scalex-0.05)
			PK00 H 2 Bright A_FadeOut(0.1) PK00 H 0 A_SetScale(scalex-0.05)
			PK00 H 2 Bright A_FadeOut(0.1) PK00 H 0 A_SetScale(scalex-0.05)
			PK00 H 2 Bright A_FadeOut(0.1) PK00 H 0 A_SetScale(scalex-0.05)
			PK00 H 2 Bright A_FadeOut(0.1) PK00 H 0 A_SetScale(scalex-0.05)
			stop
	}
}

Actor Painkiller_Shotgun_FireSmoke : Painkiller_Smoke
{
	Scale 0.05
	States
	{
		Spawn:
			PK00 D 0
			PK00 DDDDDDDDDDDDDDDD 3 A_FadeOut(0.05)
			stop
	}
}

Actor Painkiller_IceChunk : IceChunk { Gravity 0.25 Translation "0:255=%[0,0,0]:[1.6,2.0,2.0]" +FORCEXYBILLBOARD }

Actor Painkiller_IceShatter : Trigger
{
	States
	{
		Pickup:
			TNT1 A 0 A_JumpIfInventory("Painkiller_NoBloodFlag",1,"Finish")
			TNT1 A 0 ACS_NamedExecuteWithResult("Painkiller_GibCreator", 1)
		Finish:	
			TNT1 A 0 A_PlaySound("Painkiller/Freeze/Gib")
			TNT1 A 0 ACS_NamedExecuteWithResult("Samsara_KillCount")
			TNT1 A 0 ACS_NamedExecuteWithResult("Painkiller_MonsterKill")
			TNT1 A 0 A_KillMaster
			TNT1 A 0 A_Die
			TNT1 A 0 ACS_NamedExecuteWithResult("Painkiller_FreezeShatter")
			stop
	}
}

Actor Painkiller_IceShatter2 : Trigger
{
	States
	{
		Pickup:
			TNT1 A 0 A_JumpIfInventory("Painkiller_NoBloodFlag",1,"Finish")
		Finish:	
			TNT1 A 0 A_PlaySound("Painkiller/Freeze/Gib")
			TNT1 A 0 ACS_NamedExecuteWithResult("Samsara_KillCount")
			TNT1 A 0 ACS_NamedExecuteWithResult("Painkiller_MonsterKill2")
			TNT1 A 0 A_KillMaster
			TNT1 A 0 A_Die
			TNT1 A 0 ACS_NamedExecuteWithResult("Painkiller_FreezeShatter")
			stop
	}
}

Actor Painkiller_ShotgunSmoke
{
	Radius 2
	Height 4
	RenderStyle Translucent
	Scale 0.1
	Alpha 0.33
	+NOINTERACTION
	+NOTRIGGER
	+THRUACTORS
	+CLIENTSIDEONLY
	+FORCEXYBILLBOARD
	States
	{
		Spawn:
			PK00 E 0
			PK00 E 0 A_FadeOut(0.033)	PK06 E 2 A_SetScale(scalex+0.01)
			PK00 E 0 A_FadeOut(0.033)	PK06 E 2 A_SetScale(scalex+0.01)
			PK00 E 0 A_FadeOut(0.033)	PK06 E 2 A_SetScale(scalex+0.01)
			PK00 E 0 A_FadeOut(0.033)	PK06 E 2 A_SetScale(scalex+0.01)
			PK00 E 0 A_FadeOut(0.033)	PK06 E 2 A_SetScale(scalex+0.01)
			PK00 F 0 A_FadeOut(0.033)	PK06 F 2 A_SetScale(scalex+0.01)
			PK00 F 0 A_FadeOut(0.033)	PK06 F 2 A_SetScale(scalex+0.01)
			PK00 F 0 A_FadeOut(0.033)	PK06 F 2 A_SetScale(scalex+0.01)
			PK00 F 0 A_FadeOut(0.033)	PK06 F 2 A_SetScale(scalex+0.01)
			PK00 F 0 A_FadeOut(0.033)	PK06 F 2 A_SetScale(scalex+0.01)
			stop
	}
}
