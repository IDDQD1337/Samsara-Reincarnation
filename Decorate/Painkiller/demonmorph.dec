actor SamsaraPainkillerDemonMorphLoopSoundCheck : Boolean {}

actor Painkiller_DemonMorph : PainkillerWeapon
{
	Inventory.PickupMessage "Huh?"
    Weapon.UpSound ""
	//Weapon.SlotNumber 7
	Weapon.SelectionOrder 100
	Obituary "%o had %p soul claimed by %k."
	Tag "Demon Morph"
	+BFG
	+CHEATNOTWEAPON
	States
	{
		Ready:
			TNT1 A 0 ACS_NamedExecuteWithResult("Painkiller_DemonModeActive")
			TNT1 A 0 A_GiveInventory("Painkiller_BlockDemonMorph")
		ReadyLoop:
            TNT1 A 0 A_JumpIfInventory("PainkillerDemonMorphActive",1,1)
            goto Remove
			TNT1 A 0 A_GiveInventory("SamsaraPainkillerDemonMorphLoopSoundStarter")
			TNT1 A 5 A_WeaponReady(WRF_DISABLESWITCH)
			TNT1 A 0 A_JumpIfInventory("SPModeOn",1,1)
			loop
			TNT1 A 0 A_GiveInventory("PainkillerDemonMorphTimerFreezer")
			loop

        Deselect:
            TNT1 A 0 A_GiveInventory("SamsaraPainkillerDemonMorphLoopSoundStopper")
            goto Super::Deselect

		Select:
			TNT1 A 0 A_TakeInventory("Painkiller_HoldingStake")
			TNT1 A 0 A_TakeInventory("Painkiller_HoldingShotgun")
			TNT1 A 0 A_TakeInventory("Painkiller_HoldingBolt")
			TNT1 A 0 A_TakeInventory("Painkiller_HoldingRifle")
			TNT1 A 0 A_TakeInventory("Painkiller_HoldingChaingun")
			TNT1 A 0 A_TakeInventory("Painkiller_HoldingElectro")
			TNT1 A 0 A_GiveInventory("Painkiller_HoldingDemon")
			TNT1 A 0 A_PlaySound("Painkiller/DemonMorph/Start",CHAN_6|CHAN_NOPAUSE)
            goto Super::Select

		Fire:
            TNT1 A 0 A_JumpIfInventory("PainkillerDemonMorphActive",1,1)
            goto Remove
			TNT1 A 0 A_GiveInventory("SamsaraPainkillerDemonMorphAttackHandler")
			TNT1 A 1 A_ZoomFactor(1.04)
			TNT1 A 1 A_ZoomFactor(1.08)
			TNT1 A 1 A_ZoomFactor(1.12)
			TNT1 A 1 A_ZoomFactor(1.16)
			TNT1 A 1 A_ZoomFactor(1.2)
			TNT1 A 1 A_ZoomFactor(1.24)
			TNT1 A 1 A_ZoomFactor(1.28)
			TNT1 A 1 A_ZoomFactor(1.32)
			TNT1 A 1 A_ZoomFactor(1.36)
			TNT1 A 1 A_ZoomFactor(1.4)
			TNT1 A 1 A_ZoomFactor(1.36)
			TNT1 A 1 A_ZoomFactor(1.32)
			TNT1 A 1 A_ZoomFactor(1.28)
			TNT1 A 1 A_ZoomFactor(1.24)
			TNT1 A 1 A_ZoomFactor(1.2)
			TNT1 A 1 A_ZoomFactor(1.16)
			TNT1 A 1 A_ZoomFactor(1.12)
			TNT1 A 1 A_ZoomFactor(1.08)
			TNT1 A 1 A_ZoomFactor(1.04)
			TNT1 A 0 A_ZoomFactor(1)
			goto ReadyLoop

		Remove:
			TNT1 A 0 A_TakeInventory("Painkiller_BlockDemonMorph")
			TNT1 A 0 A_TakeInventory("PainkillerDemonMorphScreen")
			TNT1 A 0 A_TakeInventory("PainkillerDemonMorphTimerFreezer")
			//TNT1 A 0 A_TakeInventory("PainkillerDemonMorphSlower")
			TNT1 A 0 A_TakeInventory("PainkillerDemonMorphInvulnerability")
            TNT1 A 0 A_GiveInventory("SamsaraPainkillerDemonMorphLoopSoundStopper")
			TNT1 A 0 A_PlaySound("Painkiller/DemonMorph/Stop",CHAN_7)
			TNT1 A 0 A_TakeInventory("Painkiller_DemonMorph")
            goto Super::Deselect
	}
}

actor SamsaraPainkillerDemonMorphAttackHandler : Trigger
{
    States
    {
      // Attack stuff
      Pickup:
        TNT1 A 0 A_PlaySound("Painkiller/DemonMorph/Fire",CHAN_WEAPON|CHAN_NOPAUSE)
        TNT1 A 0 A_PlaySound("Painkiller/DemonMorph/Loop",CHAN_7|CHAN_NOPAUSE,1.0,true)
        TNT1 A 0 A_GiveInventory("SamsaraPainkillerDemonMorphLoopSoundCheck")
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupCoop")
      PickupDM:
        TNT1 AAAAA 0 A_RailAttack(1000,0,0,None,None,RGF_SILENT|RGF_NOPIERCING,0,"Painkiller_DemonMorphBlaster",0,0,2048)
        stop

      PickupCoop:
        TNT1 AAAAA 0 A_RailAttack(1000,0,0,None,None,RGF_SILENT|RGF_NOPIERCING,0,"Painkiller_DemonMorphBlasterCoop",0,0,2048)
        stop
    }
}

actor SamsaraPainkillerDemonMorphLoopSoundStarter : Trigger
{
    States
    {
      // Sound starting stuff
      Pickup:
        TNT1 A 0 A_PlaySound("Painkiller/DemonMorph/Loop",CHAN_7|CHAN_NOPAUSE,1.0,true)
        TNT1 A 0 A_GiveInventory("SamsaraPainkillerDemonMorphLoopSoundCheck")
        stop
    }
}

actor SamsaraPainkillerDemonMorphLoopSoundStopper : Trigger
{
    States
    {
      // Sound stopping stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraPainkillerDemonMorphLoopSoundCheck",1,1)
        stop
        TNT1 A 0 A_PlaySound("silence",CHAN_7)
        TNT1 A 0 A_StopSound(CHAN_7)
        TNT1 A 0 A_TakeInventory("SamsaraPainkillerDemonMorphLoopSoundCheck")
        stop
    }
}

actor PainkillerDemonMorphActive : Powerup {}

Actor PainkillerDemonMorphScreen : PowerLightAmp
{
    Powerup.Duration 0x7FFFFFFF
    Powerup.Colormap 1.0, 1.0, 1.0
}

Actor PainkillerDemonMorphTimerFreezer : PowerTimeFreezer { Powerup.Duration 24 }

Actor PainkillerDemonMorphSlower : PowerSpeed
{
    Inventory.Icon ""
	Speed 0.5
    Powerup.Duration 0x7FFFFFFF
    //-NOTELEPORTFREEZE // doesn't exist in zandronum yet
    +NOTRAIL
}

Actor PainkillerDemonMorphInvulnerability : PowerProtection
{
    Powerup.Duration 0x7FFFFFFF
    DamageFactor "Normal", 0
}

Actor Painkiller_DemonMorphBlaster
{
	+NOINTERACTION
	+PUFFONACTORS
	+ALWAYSPUFF
	+HITTRACER
    +NOTIMEFREEZE
	DamageType "PainkillerDemon"
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_RadiusGive("Painkiller_DemonMorphBlast",24,RGF_MONSTERS|RGF_NOSIGHT,24)
			stop

		Death:
			TNT1 A 1 A_GiveInventory("Painkiller_DamageCredit",1,AAPTR_TRACER)
			stop
	}
}

Actor Painkiller_DemonMorphBlasterCoop : Painkiller_DemonMorphBlaster { +MTHRUSPECIES }

Actor Painkiller_DemonMorphBlast : Trigger
{
	States
	{
		Pickup:
			TNT1 A 0 ACS_NamedExecuteAlways("Painkiller_DemonMorphBlast", 0, 0)
			stop
	}
}
