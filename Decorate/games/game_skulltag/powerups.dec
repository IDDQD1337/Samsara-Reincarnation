actor Guardsphere : CustomInventory 5037
//actor Guardsphere2: CustomInventory replaces Guardsphere
{
	Game Doom
	SpawnID 0
	+COUNTITEM
	+NOGRAVITY
	Inventory.MaxAmount 0
	Inventory.PickupMessage "$PICKUP_GUARDSPHERE"
	Inventory.PickupSound "nosoundforyoubitch"
	Inventory.PickupAnnouncerEntry "guardsphere"
	States
	{
	Spawn:
		GARD ABCD 6 bright
		Loop

	Pickup:
		TNT1 A 0 A_JumpIfInventory("DoomClassMode",2,"PickupDoomStr")
		TNT1 A 1 A_JumpIfInventory("CorvusClass", 1, "PickupHeretic")
		TNT1 A 0 A_PlaySound("misc/p_pkup",CHAN_ITEM|CHAN_MAYBE_LOCAL)
		TNT1 A 1 A_GiveInventory("PowerQuarterDamage")
		TNT1 A 1 A_GiveInventory("PowerGuardsphereColor")
		Stop

	PickupDoomStr:
		TNT1 A 0 A_PlaySound("misc/p_pkup",CHAN_ITEM|CHAN_MAYBE_LOCAL)
		TNT1 A 0 A_GiveInventory("PowerQuarterDamage")
		TNT1 A 0 A_GiveInventory("PowerGuardsphereColorStr")
		Stop

	PickupHeretic:
		TNT1 A 0 A_JumpIfInventory("GuardsphereHeretic", 0, "PickupFail")
		TNT1 A 0 A_PlaySound("heretic/itemget",CHAN_ITEM|CHAN_MAYBE_LOCAL)
		TNT1 A 1 A_GiveInventory("GuardsphereHeretic")
		Stop

	PickupFail:
		GARD A 1 bright
		Fail
	}
}

actor GuardsphereHeretic : PowerupGiver
{
	Inventory.RestrictedTo "CorvusPlayer"
	Inventory.MaxAmount 16
	Inventory.UseSound "heretic/itemuse"
	+Inventory.UNDROPPABLE
	+Inventory.HUBPOWER
	Inventory.Icon "ARTISTP5"
	Powerup.Type QuarterDamage
	Powerup.Color GreenMap
	Tag "Guardsphere"
}

actor PowerGuardsphereColor : Powerup
{
	Powerup.Color GreenMap
	Powerup.Duration -25
}

actor PowerGuardsphereColorStr : Powerup
{
	Powerup.Color "FF AA 00", 0.1
	Powerup.Duration -25
}

actor PowerQuadDamage : PowerDamage
{
	Powerup.Duration -25
	DamageFactor "normal", 4
}

actor PowerQuarterDamage : PowerProtection
{
	Powerup.Duration -25
	DamageFactor "normal", 0.25
}

actor PowerTurbo : PowerSpeed
{
	Powerup.Duration -25
	Speed 1.4
}

actor PowerTranslucency : PowerInvisibility
{
	+CANTSEEK
	Powerup.Strength 80
	Powerup.Mode Translucent
}

actor RuneIcon : Boolean {}
