ACTOR PeasantB : SamsaraMonsterReincStrifeHumanoid replaces Peasant
{
	Health 31
	PainChance 200
	Speed 8
	Radius 20
	Height 56
	Monster
	+FRIENDLY
	-COUNTKILL
	+NOSPLASHALERT
	+FLOORCLIP
	+JUSTHIT
	MinMissileChance 150
	MaxStepHeight 16
	MaxDropoffHeight 32
	SeeSound "peasant/sight"
	AttackSound "peasant/attack"
	PainSound "peasant/pain"
	DeathSound "peasant/death"
	CrushPainSound "misc/pcrush"
	HitObituary "$OB_PEASANT"
	Tag "Peasant"
	States
	{
	Spawn:
		PEAS A 10 A_Look2
		Loop
	See:
		PEAS AABBCCDD 5 A_Wander
		Goto Spawn
	Melee:
		PEAS E 10 A_FaceTarget
		PEAS F 8 A_CustomMeleeAttack(2*random[PeasantAttack](1,5)+2)
		PEAS E 8
		Goto See
	Pain:
		PEAS O 3
		PEAS O 3 A_Pain
		Goto Melee
	Pain.MightyBoot:
		TNT1 A 0 A_ChangeFlag("NOPAIN",1)
		TNT1 A 0 A_PlaySound("duke/kickhit",CHAN_BODY)
		PEAS O 2 A_FaceTarget
		TNT1 A 0 A_Recoil(14)
		PEAS O 16 A_Pain
		Goto See
	Wound:
		PEAS G 5
		PEAS H 10 A_GetHurt
		PEAS I 6
		Goto Wound+1
	Death:
		TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
		PEAS G 5 A_SpawnItemEx("UniversalBloodyHellGoreDeathEffect",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)
		PEAS H 5 A_Scream
		PEAS I 6
		PEAS J 5 A_NoBlocking
		PEAS K 5
		PEAS L 6
		PEAS M 8
		PEAS N -1
		Stop
	Death.CalebFire:
	Death.LifeLeech:
		TNT1 A 0 A_Jump(218, 2)
		TNT1 A 0 A_GiveToTarget("CalebDMTaunt1", 1)
		TNT1 A 0 A_SpawnItemEx("BloodBurningCorpse_Generic", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION|SXF_SETMASTER)
		TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
		TNT1 A 0 A_Stop
		TNT1 A 5 A_NoBlocking
		TNT1 A -1 ACS_NamedExecuteWithResult("SamsaraBossDeathToggler")
		Stop
	XDeath:
	XDeath.PunchDagger:
	XDeath.ElectricBolt:
	XDeath.PoisonBolt:
	XDeath.StrifeExplosive:
	XDeath.StrifeSigil:
		TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
		TNT1 A 0 A_SpawnItemEx("UniversalBloodyHellGoreXDeathEffect",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)
		GIBS M 5 A_TossGib
		GIBS N 5 A_XScream
		GIBS O 5 A_NoBlocking
		GIBS PQRS 4 A_TossGib
		GIBS U 5
		GIBS V -1
		Stop
	Burn:
	Death.PhosFire:
	Death.PhosFireCoop:
		TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
		SBRN A 3 bright A_PlaySound("human/imonfire",CHAN_VOICE)
		SBRN B 3 bright A_SpawnItemEx("FireDroplet", 0, 0, 24, 0, 0, -1)
		SBRN C 3 bright A_Wander
		SBRN D 3 bright A_NoBlocking
		SBRN E 5 bright A_SpawnItemEx("FireDroplet", 0, 0, 24, 0, 0, -1)
		SBRN FGH 5 bright A_Wander
		SBRN I 5 bright A_SpawnItemEx("FireDroplet", 0, 0, 24, 0, 0, -1)
		SBRN JKL 5 bright A_Wander
		SBRN M 5 bright A_SpawnItemEx("FireDroplet", 0, 0, 24, 0, 0, -1)
		SBRN NOPQPQ 5 bright
		SBRN RSTU 7 bright
		SBRN V -1
		Stop
	Disintegrate:
		TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
		DISR A 5 A_PlaySound("misc/disruptordeath", CHAN_VOICE)
		DISR BC 5
		DISR D 5 A_NoBlocking
		DISR EF 5
		DISR GHIJ 4
		TNT1 A -1
		Stop
	}
}

// Conversation-Specific NPCs

ACTOR Peasant1B : PeasantB replaces Peasant1 	// Person (MAP07) and Security Complex (MAP19)
{
	ConversationID 6
	Speed 4
}

ACTOR Peasant2B : PeasantB replaces Peasant2 	// Arion, the Gubnuh's Secretary.
{												// The Extremely Worried Technician (just a thought!)
	ConversationID 7
	Speed 5
}

ACTOR Peasant3B : PeasantB replaces Peasant3 	// Some random asshole the ZDoom Wiki doesn't document.
{
	ConversationID 6
	Speed 5
}

ACTOR Peasant4B : PeasantB replaces Peasant4 	// Harris, the fucker what makes your game unwinnable.
{												// Tevick, the friendly Shadow Armor merchant.
	Translation 0
	Speed 7
	ConversationID 9
}

ACTOR Peasant5B : PeasantB replaces Peasant5 	// Derwin. Tear his ear off for the Gubnuh.
{												// Also Harris in the demo version.
	Speed 7
	ConversationID 10
	Translation 0
}

ACTOR Peasant6B : PeasantB replaces Peasant6 	// Worner, your mole in the warehouse.
{
	Translation 0
	Speed 7
	ConversationID 11
}

ACTOR Peasant7B : PeasantB replaces Peasant7 	// Warden Montag. Kill him in the Prison for a prison ID.
{
	ConversationID 12
	Translation 2
}

ACTOR Peasant8B : PeasantB replaces Peasant8 	// Da Judge. Kill him to free prisoners.
{
	ConversationID 13
	Translation 2
}

ACTOR Peasant9B : PeasantB replaces Peasant9 	// Beldin. Kill him, take his ring to be a part of the Rebellion.
{
	ConversationID 14
	Translation 2
}

ACTOR Peasant10B : PeasantB replaces Peasant10 	// Rowan. Give him Beldin's ring to get the Communicator.
{
	ConversationID 15
	Translation 1
}

ACTOR Peasant11B : PeasantB replaces Peasant11 	// Geoff the increasingly-irrelevant doorman.
{
	ConversationID 16
	Translation 1
}

ACTOR Peasant12B : PeasantB replaces Peasant12 	// A nameless key-giving technician.
{
	ConversationID 17
	Translation 1
}

ACTOR Peasant13B : PeasantB replaces Peasant13 	// Irale, the order's "fixer"
{												// Richter, the front's leader (special not-possessed-by-an-alien edition)
	ConversationID 18
	Translation "32:47=208:223", "48:63=208:223", "128:143=48:63" // 3
}

ACTOR Peasant14B : PeasantB replaces Peasant14	// Weran, the Rat King.
{
	ConversationID 19
	Translation "32:47=208:223", "48:63=208:223", "128:143=48:63" // 3
}

ACTOR Peasant15B : PeasantB replaces Peasant15	// Random nameless computer technician.
{
	ConversationID 20
	Translation "32:47=208:223", "48:63=208:223", "128:143=48:63" // 3
}

ACTOR Peasant16B : PeasantB replaces Peasant16	// Random Sewer Gate Guard.
{												// The Programmer! Or someone who thinks he is, anyway.
	ConversationID 21
	Translation "32:63=0:31", "80:95=16:31", "128:143=96:111", "192:223=32:63" // 5
}

ACTOR Peasant17B : PeasantB replaces Peasant17	// Some random asshole the ZDoom Wiki doesn't document.
{
	ConversationID 22
	Translation "32:63=0:31", "80:95=16:31", "128:143=96:111", "192:223=32:63" // 5
}

ACTOR Peasant18B : PeasantB replaces Peasant18	// Some random asshole the ZDoom Wiki doesn't document.
{
	ConversationID 23
	Translation "32:63=0:31", "80:95=16:31", "128:143=96:111", "192:223=32:63" // 5
}

ACTOR Peasant19B : PeasantB replaces Peasant19	// The Keymaster. Gives you a key, natch.
{												// Another Fake Programmer, too.
	ConversationID 24
	Translation "32:63=0:31", "80:95=128:143", "128:143=80:95", "192:223=160:191" // 4
}

ACTOR Peasant20B : PeasantB replaces Peasant20	// Some random asshole the ZDoom Wiki doesn't document.
{
	ConversationID 25
	Translation "32:63=0:31", "80:95=128:143", "128:143=80:95", "192:223=160:191" // 4
}

ACTOR Peasant21B : PeasantB replaces Peasant21	// Some random asshole the ZDoom Wiki doesn't document.
{
	ConversationID 26
	Translation "32:63=0:31", "80:95=128:143", "128:143=80:95", "192:223=160:191" // 4
}

ACTOR Peasant22B : PeasantB replaces Peasant22	// Gubnuh Morel. Presumably supposed to be fatter than this.
{												// And one last Fake Programmer for the road...
	ConversationID 27
	Translation "32:63=0:31", "80:95=64:79", "128:143=144:159", "192:192=1:1", "193:223=1:31" // 6
}