// Other enemies
ACTOR Macil2B : SamsaraMonsterReincMacil replaces Macil2
{
	Game Strife
	ConversationID 50, 49, 50
	Health 1500 // Why the fuck was his health <100?
	Radius 20
	Height 56
	Speed 8
	Painchance 200
	Monster
	-SPECTRAL // Dude's just a human... Well, a possessed human, of course, but still.
	+NOTDMATCH
	+NOICEDEATH
	+NOSPLASHALERT
	-NODAMAGE
	+NEVERRESPAWN
	+BOSS
	+DONTRIP
	+COUNTKILL
	DamageFactor "Fire", 0.5
	DamageFactor "Spectral", 1
	DamageFactor "SpectralLow", 1
	DamageFactor "DukeSpectral", 1
	DamageFactor "ZorchSpectral", 1
	DamageFactor "SpectralNoQuad", 1
	MinMissileChance 150
	SeeSound "macil/sight"
	PainSound "macil/pain"
	ActiveSound "macil/active"
	DeathSound "macil/slop"
	CrushPainSound "misc/pcrush"
	Tag "$TAG_MACIL2"
	Obituary "$OB_MACIL"
	DropItem "None"
	MaxStepHeight 16
	MaxDropoffHeight 32
	States
	{
	See:
		LEAD AABBCCDD 3 A_FastChase // Why, yes, I do enjoy making enemies gigantic bags of dicks.
		Loop
	Missile:
		LEAD E 4 A_FaceTarget
		LEAD F 4 bright A_ShootGun
		LEAD F 0 A_ChangeFlag("NOPAIN",0)
		LEAD E 2 A_SentinelRefire
		Loop
	Pain.MightyBoot:
		TNT1 A 0 A_ChangeFlag("NOPAIN",1)
		TNT1 A 0 A_PlaySound("duke/kickhit",CHAN_BODY)
		LEAD Y 2 A_FaceTarget
		TNT1 A 0 A_Recoil(14)
		LEAD Y 16 A_Pain
		Goto See
	XDeath:
	Death:
		LEAD G 5 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
		LEAD G 5
		LEAD H 5 A_Scream
		LEAD IJ 4
		LEAD K 3
		LEAD L 3 A_NoBlocking
		LEAD MNOPQRSTUV 3
		LEAD W 4 Bright A_SpawnItemEx("AlienSpectre4", 0, 0, 0, 0, 0, random[spectrespawn](0,255)*0.0078125, 0, SXF_NOCHECKPOSITION)
		LEAD X -1
		Stop
	Burn:
		TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
		SBRN A 3 bright A_PlaySound("human/imonfire",CHAN_VOICE)
		SBRN B 3 bright A_SpawnItemEx("FireDroplet", 0, 0, 24, 0, 0, -1)
		SBRN C 3 bright A_Wander
		SBRN D 3 bright A_NoBlocking
		SBRN E 5 bright A_SpawnItemEx("FireDroplet", 0, 0, 24, 0, 0, -1)
		SBRN FGH 5 bright A_Wander
		SBRN I 5 bright A_SpawnItemEx("FireDroplet", 0, 0, 24, 0, 0, -1)
		SBRN JKL 5 bright A_Wander
		SBRN M 5 bright A_SpawnItemEx("FireDroplet", 0, 0, 24, 0, 0, -1)
		SBRN NOPQPQ 5 bright
		SBRN RSTU 7 bright
		SBRN V -1 A_SpawnItemEx("AlienSpectre4", 0, 0, 0, 0, 0, random[spectrespawn](0,255)*0.0078125, 0, SXF_NOCHECKPOSITION)
		Stop
	Disintegrate:
		TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
		DISR A 5 A_PlaySound("misc/disruptordeath", CHAN_VOICE)
		DISR BC 5
		DISR D 5 A_NoBlocking
		DISR EF 5
		DISR GHIJ 4
		TNT1 A -1 A_SpawnItemEx("AlienSpectre4", 0, 0, 0, 0, 0, random[spectrespawn](0,255)*0.0078125, 0, SXF_NOCHECKPOSITION)
		Stop
	}
}

ACTOR StrifeBishop2 : SamsaraMonsterReincStrifeBishop replaces StrifeBishop
{
	Game Strife
	ConversationID 64,-1,-1
	Health 1000
	Painchance 128
	Speed 8
	Radius 40
	Height 56
	Mass 500
	Monster
	+NOBLOOD
	+NOTDMATCH
	+FLOORCLIP
	+INCOMBAT
	+NOICEDEATH
	+NEVERRESPAWN
	+DONTRIP
	Tag "Bishop"
	DamageFactor "Fire", 0.5
	MinMissileChance 150
	MaxDropoffHeight 32
	SeeSound "bishop/sight"
	PainSound "bishop/pain"
	DeathSound "bishop/death"
	ActiveSound "bishop/active"
	DropItem "StrifeBishopDrop"
	Obituary "$OB_STFBISHOP"
	States
	{
	Spawn:
		MLDR A 10 A_Look
		Loop
	See:
		TNT1 A 0 A_GiveInventory("SamsaraBossSightTaunt")
	See2:
		MLDR AABBCCDD 3 A_Chase
		Loop
	Missile:
		MLDR E 3 A_FaceTarget
		MLDR F 2 Bright A_CustomMissile("BishopMissile", 64, 0, 0, CMF_AIMOFFSET)
		Goto See2
	Pain:
		MLDR D 1 A_Pain
		Goto See2
	Death:
	Disintegrate:
		TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
		MLDR G 3 Bright
		MLDR H 5 Bright A_Scream
		MLDR I 4 Bright A_TossGib
		MLDR J 4 Bright A_Explode(64,64,1,1)
		MLDR KL 3 Bright
		MLDR M 4 Bright A_NoBlocking
		MLDR N 4 Bright
		MLDR O 4 Bright A_TossGib
		MLDR P 4 Bright
		MLDR Q 4 Bright A_TossGib
		MLDR R 4 Bright
		MLDR S 4 Bright A_TossGib
		MLDR T 4 Bright
		MLDR U 4 Bright A_TossGib
		MLDR V 4 Bright A_SpawnItemEx("AlienSpectre2", 0, 0, 0, 0, 0, random[spectrespawn](0,255)*0.0078125, 0, SXF_NOCHECKPOSITION)
		Stop
	}
}

ACTOR Loremaster2 : SamsaraMonsterReincLoremaster replaces Loremaster
{
	Game Strife
	ConversationID 66, 63, 64
	Health 1500	
	Speed 10
	Radius 15	
	Height 56
	FloatSpeed 5
	Monster
	+FLOAT
	+NOBLOOD
	+NOGRAVITY
	+NOTDMATCH
	+FLOORCLIP
	+NOBLOCKMONST
	+INCOMBAT
	+LOOKALLAROUND
	+NOICEDEATH
	+NEVERRESPAWN
	+DONTRIP
	+BOSS
	DamageFactor "Fire", 0.5
	MinMissileChance 150
	Tag "$TAG_PRIEST"
	SeeSound "loremaster/sight"
	AttackSound "loremaster/attack"
	PainSound "loremaster/pain"
	DeathSound "loremaster/death"
	ActiveSound "loremaster/active"
	Obituary "$OB_LOREMASTER"
	DropItem "Junk"
	States
	{
	Spawn:
		PRST A 10 A_Look
		PRST B 10 A_SentinelBob
		Loop
	See:
		TNT1 A 0 A_GiveInventory("SamsaraBossSightTaunt")
	See2:
		PRST A 4 A_Chase
		PRST A 4 A_SentinelBob
		PRST B 4 A_Chase
		PRST B 4 A_SentinelBob
		PRST C 4 A_Chase
		PRST C 4 A_SentinelBob
		PRST D 4 A_Chase
		PRST D 4 A_SentinelBob
		Loop
	Melee:
		PRST E 4 A_FaceTarget
		PRST F 4 A_CustomMeleeAttack((random[SpectreMelee](0, 255)&9)*5)
		PRST E 4 A_SentinelBob
		Goto See2
	Missile:
		PRST E 4 A_FaceTarget
		PRST F 4 A_CustomMissile("LoreShot", 32, 0)
		PRST E 4 A_SentinelBob
		Goto See2
	Death:
	Disintegrate:
		TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
		PDED A 6
		PDED B 6 A_Scream
		PDED C 6
		PDED D 6 A_Fall
		PDED E 6
		PDED FGHIJIJIJKL 5
		PDED MNOP 4
		PDED Q 4 A_SpawnItemEx("AlienSpectre5",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		PDED RS 4
		PDED T -1
		Stop
	}
}

ACTOR Programmer2 : SamsaraMonsterReincProgrammer replaces Programmer
{
	Game Strife
	ConversationID 95, -1, -1
	Health 2500 // WHY YES I AM INTENT ON INCREASING THE HEALTH OF EVERY FUCKING ENEMY I CAN
	PainChance 50
	Speed 26
	FloatSpeed 5
	Radius 45
	Height  60
	Mass 800
	Damage 4
	Monster
	+NOGRAVITY
	+FLOAT
	+NOBLOOD
	+NOTDMATCH
	+DONTMORPH
	+NOBLOCKMONST
	+LOOKALLAROUND
	+NOICEDEATH
	+NOTARGETSWITCH
	+BOSS
	+DONTRIP
	DamageFactor "Fire", 0.5
	MinMissileChance 150
	AttackSound "programmer/attack"
	PainSound "programmer/pain"
	DeathSound "programmer/death"
	ActiveSound "programmer/active"
	Obituary "$OB_PROGRAMMER"
	Tag "Programmer"
	DropItem "None"
	States
	{
	Spawn:
		PRGR A 5 A_Look
		PRGR A 1 A_SentinelBob
		Loop
	See:
		TNT1 A 0 A_GiveInventory("SamsaraBossSightTaunt")
	See2:
		PRGR A 160 A_SentinelBob
		PRGR BCD 5 A_SentinelBob
		PRGR EF 2 A_SentinelBob
		PRGR EF 3 A_Chase
		Goto See2+4
	Melee:
		PRGR E 2 A_SentinelBob
		PRGR F 3 A_SentinelBob
		PRGR E 3 A_FaceTarget
		PRGR F 4 A_ProgrammerMelee
		Goto See2+4
	Missile:
		PRGR G 5 A_FaceTarget
		PRGR H 5 A_SentinelBob
		PRGR I 5 bright A_FaceTarget
		PRGR J 5 bright A_SpotLightning
		Goto See2+4
	Pain:
		PRGR K 5 A_Pain
		PRGR L 5 A_SentinelBob
		Goto See2+4
	Death:
	Disintegrate:
		TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
		PRGR L 7 bright A_TossGib
		PRGR M 7 bright A_Scream
		PRGR N 7 bright A_TossGib
		PRGR O 7 bright A_NoBlocking
		PRGR P 7 bright A_TossGib
		PRGR Q 7 bright A_SpawnProgrammerBase
		PRGR R 7 bright
		PRGR S 6 bright
		PRGR TUVW 5 bright
		PRGR X 32 bright
		TNT1 A 0 A_GiveToTarget("Sigil",1)
		PRGR X 0 A_SpawnItemEx("GlobalSigil1Giver")
		PRGR X -1 bright A_ProgrammerDeath
		Stop
	}
}

// KneelingGuy not needed. He's just holographic and does nothing except block the way.

actor ExplosiveBarrel2_2 : ExplosiveBarrel2 replaces ExplosiveBarrel2
{
	ConversationID 204
    Tag "Explosive Barrel"
	+DONTRIP
	States
	{
	Death:
		TNT1 A 0 A_Scream
		BART BCD 2 bright
		BART E 2 bright A_NoBlocking
		TNT1 A 0 A_SetDamageType("Explosive")
	DeathContinue:
		TNT1 A 0 A_Explode(64,64,XF_HURTSOURCE,true)
		TNT1 A 0 A_ChangeFlag("BLOODLESSIMPACT",false)
		BART FGHIJ 2 bright
		BART K 3 bright
		BART L -1
		Stop
	Death.ZorchPistol:
	Death.Zorch:
	Death.ZorchKamikaze:
	Death.PhasingZorch:
	Death.ZorchSpectral:
		TNT1 A 0 A_Scream
		BART BCD 2 bright
		BART E 2 bright A_NoBlocking
		TNT1 A 0 A_SetDamageType("Zorch")
		TNT1 A 0 A_ChangeFlag("BLOODLESSIMPACT",true)
		Goto DeathContinue
	}
}
