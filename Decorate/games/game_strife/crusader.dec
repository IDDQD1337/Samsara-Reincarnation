actor Crusader2 : SamsaraMonsterReincCrusader replaces Crusader
{
	Game Strife
	ConversationID 63,-1,-1
	Speed 8
	Radius 40
	Height 56
	Mass 400
	Health 400
	Painchance 128
	Monster
	+FLOORCLIP
	+DONTMORPH
	+MISSILEMORE
	+INCOMBAT
	+NOICEDEATH
	+NOBLOOD
	MinMissileChance 120
	MaxDropoffHeight 32
	DropItem "StrifeEnergyPod2"
	SeeSound "crusader/sight"
	PainSound "crusader/pain"
	DeathSound "crusader/death"
	ActiveSound "crusader/active"
	Obituary "$OB_CRUSADER"
	States
	{
	Spawn:
		ROB2 Q 10 A_Look
		Loop
	See:
		ROB2 AABBCCDD 3 A_Chase
		Loop
	Missile:
		TNT1 A 0 A_ChangeFlag("NOPAIN",0)
		ROB2 E 3 A_FaceTarget
		ROB2 F 2 bright A_CrusaderChoose
		ROB2 E 2 bright A_CrusaderSweepLeft
		ROB2 F 3 bright A_CrusaderSweepLeft
		ROB2 E 2 bright A_CrusaderSweepLeft
		ROB2 F 2 bright A_CrusaderSweepLeft
		ROB2 E 2 bright A_CrusaderSweepRight
		ROB2 F 2 bright A_CrusaderSweepRight
		ROB2 E 2 bright A_CrusaderSweepRight
		ROB2 F 2 A_CrusaderRefire
		Loop
	Pain:
		ROB2 D 1 Slow A_Pain
		Goto See
	Pain.MightyBoot:
		TNT1 A 0 A_ChangeFlag("NOPAIN",1)
		TNT1 A 0 A_PlaySound("duke/kickhit",CHAN_BODY)
		ROB2 E 2 A_FaceTarget
		TNT1 A 0 A_Recoil(10)
		ROB2 E 16 A_Pain
		Goto See
	Death:
	Disintegrate:
		TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
		ROB2 G 3 A_Scream
		ROB2 H 5 A_TossGib
		ROB2 I 4 Bright A_TossGib
		ROB2 J 4 Bright A_Explode(64,64,1,1)
		ROB2 K 4 Bright A_Fall
		ROB2 L 4 A_Explode(64,64,1,1)
		ROB2 MN 4 A_TossGib
		ROB2 O 4 A_Explode(64,64,1,1)
		ROB2 P -1 A_CrusaderDeath
		Stop

		// SamsaraMonsterReincCrusader states
		XDeath.PunchDagger:
		XDeath.ElectricBolt:
		XDeath.PoisonBolt:
		XDeath.StrifeExplosive:
		XDeath.StrifeSigil:
			TNT1 A -1 A_Jump(256,"Death")
			Wait
	}
}

