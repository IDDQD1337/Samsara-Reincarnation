actor "AlienPlasmaRifle" : InstantSwitchWeapon
{
    Weapon.SelectionOrder 1500
    Weapon.SlotNumber 5
	Weapon.BobStyle InverseAlpha
	Weapon.BobRangeX 0.2
	Weapon.BobRangeY 0.2
	Weapon.BobSpeed 4.0	
    Inventory.PickUpSound "C7/AmmoPickup"
    Weapon.AmmoUse 2
    Weapon.AmmoGive 5
    Weapon.AmmoType "RocketAmmo"
	+WEAPON.ALT_AMMO_OPTIONAL
	+WEAPON.NOALERT
	Weapon.AmmoType2 "C7ProxyMineAmmo"
	Weapon.AmmoUse2 1
	Weapon.AmmoGive2 0
    Inventory.PickupMessage "You got the Alien Plasma Rifle!"
	Tag "Alien Plasma Rifle"	
    Weapon.YAdjust 11
	Inventory.RestrictedTo "Corridor7Player"	
    States
    {
      Spawn:
        7RL2 A -1
        Stop
      Ready:
        TNT1 A 0 SetPlayerProperty(0,1,2)
        C7RL A 1 A_WeaponReady
	  Ready2:
	    C7RL BCDEFGHA 1 A_WeaponReady
        Loop
      Fire:
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopFire")
		TNT1 A 0 A_AlertMonsters
        C7RL I 4 BRIGHT A_FireCustomMissile("C7Plasma")
        C7RL JK 4 BRIGHT
        C7RL L 4 
		C7RL L 0 A_Refire
        Goto Ready
      CoopFire:
		TNT1 A 0 A_AlertMonsters
        C7RL I 4 BRIGHT A_FireCustomMissile("C7Plasma2")
        C7RL JK 4 BRIGHT
        C7RL L 4 
		C7RL L 0 A_Refire
        Goto Ready
      AltFire: 
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"AltCoopFire")	  
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"AltCoopFire")		
		C7RL A 0 A_JumpIfNoAmmo("Ready")
		C7RL A 5 A_SpawnItemEx("C7ProxyMine",0,0,0,0,0,0,0,SXF_SETMASTER)
		C7RL A 5 A_TakeInventory("C7ProxyMineAmmo", 1)
		C7RL A 5 A_Print("Mine placed")
		Goto Ready
      AltCoopFire:	
		C7RL A 0 A_JumpIfNoAmmo("Ready")
		C7RL A 5 A_SpawnItemEx("C7CoopProxyMine",0,0,0,0,0,0,0,SXF_SETMASTER)
		C7RL A 5 A_TakeInventory("C7ProxyMineAmmo", 1)
		C7RL A 5 A_Print("Mine placed")
		Goto Ready	  
    }
}

actor C7Plasma
{
    Radius 8
    Height 6
    Speed 20
    Damage (Random(70,52))
    Projectile
    +FORCERADIUSDMG
    Decal Scorch
    SeeSound "C7/AlienPlasmaRifle"
    DeathSound "C7/Explosion"
    Obituary "%o was deep fried by %k's Alien Plasma Rifle."
    States
    {
      Spawn:
        7MIS ABCDEFGH 2 Bright
        Loop
      Death:
        7MIS I 8 Bright A_Explode(Random(134,151),96,0)
        7MIS J 6 Bright
        7MIS K 4 Bright
        Stop
    }
}

actor C7Plasma2 : C7Plasma {
	+THRUSPECIES
	Species "Player" 
	Damage (Random(65,80))
	States {
      Death:
        7MIS I 8 Bright A_Explode(Random(129,151),123,0)
        7MIS J 6 Bright
        7MIS K 4 Bright
        Stop
	}	
}