actor "AlienDisintegrator" : Corridor7WeaponBase
{
	Weapon.SelectionOrder 1000
	Weapon.SlotNumber 7
	Weapon.BobStyle InverseAlpha
	Weapon.BobRangeX 0.2
	Weapon.BobRangeY 0.2
	Weapon.BobSpeed 4.0	
	Inventory.PickUpSound "C7/AmmoPickup"
	Weapon.AmmoUse 40
	Weapon.AmmoGive 200
	Weapon.AmmoType "Cell"
	+WEAPON.ALT_AMMO_OPTIONAL
	+WEAPON.BFG
	+WEAPON.NOALERT
	-WEAPON.CHEATNOTWEAPON
	Weapon.AmmoType2 "C7ProxyMineAmmo"
	Weapon.AmmoUse2 1
	Weapon.AmmoGive2 0
	Inventory.PickupMessage "You got the Alien Disintegrator!"
	Weapon.YAdjust 12
	Tag "Alien Disintegrator"
	Inventory.RestrictedTo "Corridor7Player"
	States
	{
	  Spawn:
		7DN2 A -1
		Stop

	  Ready:
		C7DN A 1 A_WeaponReady
		loop

	  Fire:
		TNT1 A 0 A_JumpIfInventory("SPModeOn", 1, "SPFire")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn", 1, "CoopFire")
		C7DN A 2 A_AlertMonsters
		C7DN B 5 BRIGHT A_FireCustomMissile("C7DisintDM")
		CYDN C 0 A_SetBlend("Red",0.7, 4)
		C7DN CD 4 BRIGHT
		C7DN A 20
		Goto Ready

	  SPFire:
		C7DN A 2 A_AlertMonsters
		C7DN B 5 BRIGHT A_FireCustomMissile("C7Disint")
		CYDN C 0 A_SetBlend("Red",0.7, 4)
		C7DN CD 4 BRIGHT
		C7DN A 20
		Goto Ready

	  CoopFire:
	  	C7DN A 2 A_AlertMonsters
		C7DN B 5 BRIGHT A_FireCustomMissile("C7Disint2")
		CYDN C 0 A_SetBlend("Red",0.7, 4)
		C7DN CD 4 BRIGHT
		C7DN A 20
		Goto Ready
	}
}

actor C7Disint
{
	Radius 2
	Height 2
	Speed 12
	Damage 0
	DamageType "C7Disintegrate"
	Projectile
	SeeSound "C7/AlienDisintegrator"
	Obituary "%o existence was undone by %k's disintegrator."
	+DONTBLAST
	+DONTREFLECT
	+DONTSPLASH
	+NODAMAGETHRUST
	+NOTIMEFREEZE
	+NOTONAUTOMAP
	+NOTRIGGER
	+SKYEXPLODE
	States
	{
	  Spawn:
		TNT1 A 2 
		Goto Death

	  Death:
		TNT1 A 2 A_BFGSpray("BlankPuffC7",70,10)
		TNT1 A 2
		Stop
	}
}

Actor C7DisintDM : C7Disint
{
	States
	{
	  Death:
		TNT1 A 2 A_BFGSpray("BlankPuffC7",20,10)
		TNT1 A 2
		Stop
	}
}

actor C7Disint2 : C7Disint
{
 Species "Player"
 +THRUSPECIES
 +DONTHARMCLASS
 +DONTHARMSPECIES
 +MTHRUSPECIES
 +DONTSPLASH
 states
 {
	   Death:
		TNT1 A 2 A_BFGSpray("BlankPuffC7_2",100,16)
		TNT1 A 2
		Stop
  }	
}

Actor BlankPuffC7_2 : BlankPuffC7 { Species "Player" +THRUSPECIES +DONTHARMCLASS +DONTHARMSPECIES +MTHRUSPECIES +DONTSPLASH DamageType "C7Disintegrate" }