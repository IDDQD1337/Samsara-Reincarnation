actor SamsaraCorridor7AlienAssaultCannonAttackSilentSound : Boolean {}

actor "AlienAssaultCannon" : Corridor7WeaponBase
{
    Inventory.PickupMessage "You got the Alien Assault-Cannon!"
    Weapon.SlotNumber 6
    Weapon.SelectionOrder 800
    Weapon.YAdjust 20
    Weapon.AmmoType1 "Cell"
    Weapon.AmmoGive1 75
    Weapon.AmmoUse1 2
    Obituary "%k's assault cannon ripped %o to dust."
    Tag "Alien Assault Cannon"
    States
    {
      Spawn:
        7AL2 A -1
        stop

      Ready:
        C7AL F 1 A_WeaponReady
        loop

      Fire:
        TNT1 A 0 A_AlertMonsters
        C7AL F 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Cell",2,1)
        goto HoldStart
        TNT1 A 0 A_GunFlash
        goto HoldStart

      Hold:
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_GunFlash
      HoldStart:
        TNT1 A 0 A_TakeInventory("SamsaraCorridor7AlienAssaultCannonAttackSilentSound")
        TNT1 A 0 A_GiveInventory("SamsaraCorridor7AlienAssaultCannonAttackHandler")
        C7AL A 3 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        C7AL B 4 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Cell",2,1)
        goto Hold2Start
        TNT1 A 0 A_GunFlash
        goto Hold2Start

      Hold2Start:
        TNT1 A 0 A_GiveInventory("SamsaraCorridor7AlienAssaultCannonAttackSilentSound")
        TNT1 A 0 A_GiveInventory("SamsaraCorridor7AlienAssaultCannonAttackHandler")
        C7AL C 3 Bright A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        C7AL B 4 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        TNT1 A 0 A_ReFire
        C7AL D 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        C7AL E 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        C7AL D 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
        goto Ready

      AltFire:
        C7AL F 0
        goto Super::AltFire
    }
}

actor SamsaraCorridor7AlienAssaultCannonAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("Cell",2,1)
        stop
        TNT1 A 0 A_TakeInventory("Cell",2)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SamsaraCorridor7AlienAssaultCannonAttackSilentSound",1,2)
        TNT1 A 0 A_PlaySound("C7/AlienAssaultCannon",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireBullets(6,6,4,random(6,11),"BlankPuffThruGhost",FBF_NORANDOM|FBF_NOFLASH)
        TNT1 A 0 A_FireBullets(6,6,4,random(4,6),"BlankPuffThruGhostBloodless",FBF_NORANDOM|FBF_NOFLASH,256)
        TNT1 A 0 A_FireBullets(6,6,2,random(1,8),"BlankPuffThruGhostBloodless",FBF_NORANDOM|FBF_NOFLASH,64)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireBullets(6,6,4,random(6,11),"BlankPuffThruGhostCoop",FBF_NORANDOM|FBF_NOFLASH)
        TNT1 A 0 A_FireBullets(6,6,4,random(3,6),"BlankPuffThruGhostBloodlessCoop",FBF_NORANDOM|FBF_NOFLASH,256)
        TNT1 A 0 A_FireBullets(6,6,2,random(1,8),"BlankPuffThruGhostBloodlessCoop",FBF_NORANDOM|FBF_NOFLASH,64)
        stop
    }
}
