actor C7MineDMToken : Counter { Inventory.MaxAmount 20 }

actor C7ProxyMineAmmo : Ammo
{
    Inventory.Amount 5
    Inventory.MaxAmount 30
    Ammo.BackpackMaxAmount 60 // was 30
    Tag "Proximity Mines"
}

actor C7ProxyMinePickup : C7ProxyMineAmmo
{
    Inventory.PickupMessage "You got a package of Proximity Mines!"
    Inventory.Amount 5
    Ammo.BackpackAmount 1
    Inventory.Icon "7MN2A0"
    Inventory.RestrictedTo "Corridor7Player"
    States
    {
      Spawn:
        7MN2 A -1
        stop
    }
}

actor SamsaraCorridor7ProximityMineAttackHandler : Trigger
{
    States
    {
      // Ammo checking/taking stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("C7ProxyMineAmmo",1,1)
        stop
        TNT1 A 0 A_TakeInventory("C7ProxyMineAmmo",1)
        goto PickupAttack

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 ACS_NamedExecuteWithResult("C7MinePlacer", 1)
        stop

      PickupAttackCoop:
        TNT1 A 0 ACS_NamedExecuteWithResult("C7MinePlacer")
        stop
    }
}

actor C7ProxyMine
{
    Monster
    Health 1
    Radius 6
    Height 6
    Mass 5000
    MeleeRange 48
    -COUNTKILL
    +DROPOFF
    +FRIENDLY
    +MOVEWITHSECTOR
    +NOBLOCKMAP
    +NOBLOCKMONST
    +NOBLOOD
    +NOTARGET
    +SHOOTABLE
    -SOLID
    +THRUACTORS
    +VULNERABLE
    States
    {
      Spawn:
        C7MN B 35 NoDelay Light("C7MINEONFLOORSHIT_") A_PlaySound("C7MinePlace")
      Active:
        TNT1 A 0 A_CopyFriendliness(AAPTR_MASTER)
        TNT1 A 0 A_ChangeFlag("THRUACTORS",false)
        TNT1 A 0 A_ChangeFlag("VULNERABLE",true)
        TNT1 A 0 A_ChangeFlag("NOBLOCKMAP",false)
      ActiveLoop:
        C7MN BCDEFGHA 2 Light("C7MINEONFLOORSHIT_") A_LookEx(LOF_NOSOUNDCHECK,0,48)
        TNT1 A 0 A_PlaySound("C7MineBeep",CHAN_VOICE)
        C7MN HGFEDC 2 Light("C7MINEONFLOORSHIT_") A_LookEx(LOF_NOSOUNDCHECK,0,48)
        loop

      See:
        C7MN I 2 Light("C7EXPLOSION1__") A_Die
      Death:
        TNT1 A 0 A_PlaySound("C7/Explosion",CHAN_VOICE)
        TNT1 A 0 A_CustomMissile("C7MineExplosion",0)
      DeathFinish:
        C7MN I 2 Bright Light("C7EXPLOSION1__")
        C7MN J 2 Bright Light("C7EXPLOSION1__") A_SetShootable
        C7MN K 2 Bright Light("C7EXPLOSION1__")
        C7MN LMN 2 Bright Light("C7EXPLOSION2__")
        C7MN O 1 Light("C7EXPLOSION3__")
        C7MN O 1 Light("C7EXPLOSION3__") A_FadeOut(1.0/35.0)
        wait
    }
}

actor C7ProxyMineCoop : C7ProxyMine
{
    -SHOOTABLE
    States
    {
      Spawn:
        C7MN B 35 NoDelay Light("C7MINEONFLOORSHIT_") A_PlaySound("C7MinePlace")
      Active:
        TNT1 A 0 A_CopyFriendliness(AAPTR_MASTER)
        TNT1 A 0 A_ChangeFlag("THRUACTORS",false)
        TNT1 A 0 A_ChangeFlag("VULNERABLE",true)
        TNT1 A 0 A_ChangeFlag("NOBLOCKMAP",false)
      ActiveLoop:
        C7MN BCDEFGHA 2 Light("C7MINEONFLOORSHIT_") A_LookEx(LOF_NOSOUNDCHECK,0,48)
        TNT1 A 0 A_PlaySound("C7MineBeep",CHAN_VOICE)
        C7MN HGFEDC 2 Light("C7MINEONFLOORSHIT_") A_LookEx(LOF_NOSOUNDCHECK,0,48)
        loop

      See:
        C7MN I 2 Light("C7EXPLOSION1__") A_Die
      Death:
        TNT1 A 0 A_PlaySound("C7/Explosion",CHAN_VOICE)
        TNT1 A 0 A_CustomMissile("C7MineExplosionCoop",0)
        goto DeathFinish
    }
}

actor C7MineExplosion
{
    Projectile
    Radius 1
    Height 1
    DeathSound "C7/Explosion"
    Decal "Scorch"
    Obituary "%o didn't see %k's proximity mine."
    +FORCERADIUSDMG
    States
    {
      Spawn:
        TNT1 A 1 A_Die
      Death:
        TNT1 A 0 A_TransferPointer(AAPTR_TARGET,AAPTR_DEFAULT,AAPTR_MASTER,AAPTR_TARGET)
        TNT1 A 0 A_TakeFromTarget("C7MineDMToken",1)
        TNT1 A 1 A_Explode(random(40,64),120)
        stop
    }
}

actor C7MineExplosionCoop : C7MineExplosion
{
    States
    {
      Spawn:
        TNT1 A 1 A_Die
      Death:
        TNT1 A 0 A_TransferPointer(AAPTR_TARGET,AAPTR_DEFAULT,AAPTR_MASTER,AAPTR_TARGET)
        TNT1 A 1 A_Explode(128,160,0)
        stop
    }
}    

/*Actor C7CoopProxyMine: C7ProxyMine
{
    Species "Player"
    +THRUSPECIES
    +DONTHARMCLASS
    +DONTHARMSPECIES
    +MTHRUSPECIES
    States
    {
    Spawn:
        C7MN B 0 NoDelay A_PlaySound("C7MinePlace", CHAN_BODY)
        C7MN B 35
    Active:
        C7MN B 0 A_ClearTarget    
    ActiveLoop:
        C7MN B 2 A_LookEx (LOF_NOSOUNDCHECK, 0, 32)
        C7MN C 2 A_LookEx (LOF_NOSOUNDCHECK, 0, 32)
        C7MN D 2 A_LookEx (LOF_NOSOUNDCHECK, 0, 32)
        C7MN E 2 A_LookEx (LOF_NOSOUNDCHECK, 0, 32)
        C7MN F 2 A_LookEx (LOF_NOSOUNDCHECK, 0, 32)
        C7MN G 2 A_LookEx (LOF_NOSOUNDCHECK, 0, 32)
        C7MN H 2 A_LookEx (LOF_NOSOUNDCHECK, 0, 32)
        C7MN A 2 A_LookEx (LOF_NOSOUNDCHECK, 0, 32)
        C7MN A 0 A_PlaySound("C7MineBeep")
        C7MN H 2 A_LookEx (LOF_NOSOUNDCHECK, 0, 32)
        C7MN G 2 A_LookEx (LOF_NOSOUNDCHECK, 0, 32)
        C7MN F 2 A_LookEx (LOF_NOSOUNDCHECK, 0, 32)
        C7MN E 2 A_LookEx (LOF_NOSOUNDCHECK, 0, 32)
        C7MN D 2 A_LookEx (LOF_NOSOUNDCHECK, 0, 32)
        C7MN C 2 A_LookEx (LOF_NOSOUNDCHECK, 0, 32)
        Loop    
    See:
        C7MN I 2 A_Die
    Death:    
        C7MN I 0 A_Playsound("C7/Explosion")
        C7MN I 2 BRIGHT A_CustomMissile("C7CoopMineExplosion",0,0,0,CMF_TRACKOWNER)
        C7MN J 2 BRIGHT A_SetShootable
        C7MN KLMN 2 BRIGHT
        C7MN O 1
        C7MN O 1 A_FadeOut (1.0 / 35.0)
        Wait
    }
}

Actor C7CoopMineExplosion : C7MineExplosion
{
    Species "Player"
    +THRUSPECIES
    +MTHRUSPECIES
    +DONTHARMCLASS
    +DONTHARMSPECIES
    DamageType "C7MineBoom"
}*/
