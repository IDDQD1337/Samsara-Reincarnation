#library "Greed.acs"
#include "zcommon.acs"

Script 704 (void)
{
	// Collect current both Armor and Health values of the player
	int oldarmor = CheckInventory("Armor");
	int oldhealth = GetActorProperty(0, APROP_Health);
  
	While(CheckInventory("ReversoPillGain")) // While on item effect
	{
		// Calculate damage taken on Armor and Health
		int armordamage = oldarmor - CheckInventory("Armor");
		int healthdamage = oldhealth - GetActorProperty(0, APROP_Health);
		if (healthdamage) // If player sustained damage on Health ...
		{		
			if (armordamage == 0) // ... and if the player has no armor, gives both HP and Armor on same proportions
			{
				GiveInventory("ArmorBonus", healthdamage/2);
				GiveInventory("HealthBonus", healthdamage + (healthdamage/2));
			}
			else // ... but if the player has armor, gives both HP and Armors on different proportions, based on armor's protection percentage
				{
					GiveInventory("HealthBonus", 2*healthdamage);
					GiveInventory("ArmorBonus", 2*armordamage);		
				}
		}
		else // ... Else, if the player had no damage over his/her Health, but had on Armor, then give stuff
			{
				GiveInventory("HealthBonus", armordamage/2);
				GiveInventory("ArmorBonus", armordamage + (armordamage/2));		
			}
	
	oldarmor = CheckInventory("Armor");
	oldhealth = GetActorProperty(0, APROP_Health);

    Delay(1);
  }
}